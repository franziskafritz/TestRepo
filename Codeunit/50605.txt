OBJECT Codeunit 50605 IWS Process Item Import Log
{
  OBJECT-PROPERTIES
  {
    Date=11.11.14;
    Time=12:01:13;
    Modified=Yes;
    Version List=HME1790;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            RequestIDL@1000000000 : Integer;
          BEGIN
            IF "Parameter String" <> '' THEN BEGIN
              IF EVALUATE(RequestIDL,"Parameter String") THEN
                Code(RequestIDL);
            END ELSE BEGIN
              Code(RequestIDL);
            END;
          END;

  }
  CODE
  {

    PROCEDURE Code@1000000000(RequestIDV@1000000008 : Integer);
    VAR
      IILGlobalDataL@1000000000 : Record 50600;
      IILGlobalData2L@1000000007 : Record 50600;
      IILAppDomDataL@1000000001 : Record 50601;
      IILLanguageSDataL@1000000002 : Record 50602;
      IILGlobalDataTmpL@1000000003 : TEMPORARY Record 50600;
      IILGlobalDataHandlingL@1000000004 : Codeunit 50602;
      IILAppDomDataHandlingL@1000000005 : Codeunit 50603;
      ILLLanguageDataHandlingL@1000000006 : Codeunit 50604;
      CommitCounterL@1000000009 : Integer;
    BEGIN
      IF RequestIDV <>  0 THEN
        IILGlobalDataL.SETRANGE("Request ID",RequestIDV);

      CommitCounterL := 0;

      IF IILGlobalDataL.FINDLAST THEN BEGIN
        REPEAT
          IILGlobalData2L.RESET;
          CommitCounterL += 1;
          IILGlobalData2L := IILGlobalDataL;
          RequestIDV := IILGlobalDataL."Request ID";
          IILGlobalDataTmpL.SETRANGE("Item No.",IILGlobalDataL."Item No.");
          IF IILGlobalDataTmpL.ISEMPTY THEN BEGIN
            EvaluateEntry(IILGlobalData2L);
            IF ProcessItemUpdate(IILGlobalData2L) THEN BEGIN
              IILGlobalDataTmpL.INIT;
              IILGlobalDataTmpL."Request ID" := IILGlobalDataL."Request ID";
              IILGlobalDataTmpL."Item No." := IILGlobalDataL."Item No.";
              IILGlobalDataTmpL.INSERT;
            END;
            IILGlobalData2L.FIND;
          END;
          IILGlobalData2L.RESET;
          IILGlobalData2L.SETRANGE("Item No.",IILGlobalDataL."Item No.");
          IILGlobalData2L.SETFILTER("Request ID",'<=%1',RequestIDV);
          IF IILGlobalData2L.FINDSET(TRUE,TRUE) THEN
            REPEAT
              ArchiveandDeleteData(IILGlobalData2L);
            UNTIL IILGlobalData2L.NEXT = 0;
          IF CommitCounterL = 500 THEN BEGIN
            COMMIT;
            CommitCounterL := 0;
          END;
        UNTIL IILGlobalDataL.NEXT(-1) = 0;
      END;
    END;

    PROCEDURE EvaluateEntry@1000000001(VAR IILGlobalDataR@1000000003 : Record 50600);
    VAR
      ItemL@1000000007 : Record 27;
      IILAppDomDataL@1000000005 : Record 50601;
      IILLanguageDataL@1000000004 : Record 50602;
      IILGlobalDataHandlingL@1000000002 : Codeunit 50602;
      IILAppDomDataHandlingL@1000000001 : Codeunit 50603;
      ILLLanguageDataHandlingL@1000000000 : Codeunit 50604;
      ErrorMessageL@1000000006 : Text[250];
      SkipProcessingL@1000000008 : Boolean;
    BEGIN
      IILGlobalDataHandlingL.EvaluateData(IILGlobalDataR,ErrorMessageL);
      IF ErrorMessageL <> '' THEN BEGIN
        IILGlobalDataR."Import Error Text" := ErrorMessageL;
        IILGlobalDataR."Import Status" := IILGlobalDataR."Import Status"::Error;
        IILGlobalDataR.MODIFY;
        //Check if item is existing already, if not then dont evaluate the related data
        IF NOT ItemL.GET(IILGlobalDataR."Item No.") THEN
          SkipProcessingL := TRUE;
      END;

      IILAppDomDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");
      IF IILAppDomDataL.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          IF NOT SkipProcessingL THEN BEGIN
            ErrorMessageL := '';
            IILAppDomDataHandlingL.EvaluateData(IILAppDomDataL,ErrorMessageL);
          END;
          IF (ErrorMessageL <> '') OR SkipProcessingL THEN BEGIN
            IILAppDomDataL."Import Error Text" := ErrorMessageL;
            IILAppDomDataL."Import Status" := IILAppDomDataL."Import Status"::Error;
            IILAppDomDataL.MODIFY;
          END;
        UNTIL IILAppDomDataL.NEXT = 0;
      END;

      IILLanguageDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");
      IF IILLanguageDataL.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          IF NOT SkipProcessingL THEN BEGIN
            ErrorMessageL := '';
            ILLLanguageDataHandlingL.EvaluateData(IILLanguageDataL,ErrorMessageL);
          END;
          IF (ErrorMessageL <> '') OR SkipProcessingL THEN BEGIN
            IILLanguageDataL."Import Error Text" := ErrorMessageL;
            IILLanguageDataL."Import Status" := IILLanguageDataL."Import Status"::Error;
            IILLanguageDataL.MODIFY;
          END;
        UNTIL IILLanguageDataL.NEXT = 0;
      END;
    END;

    PROCEDURE ArchiveandDeleteData@1000000003(VAR IILGlobalDataR@1000000005 : Record 50600);
    VAR
      IILAppDomDataL@1000000004 : Record 50601;
      IILLanguageDataL@1000000003 : Record 50602;
      IILGlobalDataArchiveL@1000000000 : Record 50603;
      IILAppDomDataArchiveL@1000000001 : Record 50604;
      IILLanguageDataArchiveL@1000000002 : Record 50605;
      ErrorMessageL@1000000006 : Text[250];
    BEGIN
      IILGlobalDataArchiveL.TRANSFERFIELDS(IILGlobalDataR);
      IILGlobalDataArchiveL.INSERT;
      IILGlobalDataR.DELETE;

      //H1790 11.11.14 ARI +++++++++++++++++++++++++++++++++++
      {
      IILAppDomDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");
      IF IILAppDomDataL.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          IILAppDomDataArchiveL.TRANSFERFIELDS(IILAppDomDataL);
          IILAppDomDataArchiveL.INSERT;
          IILAppDomDataL.DELETE;
        UNTIL IILAppDomDataL.NEXT = 0;
      END;

      IILLanguageDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");
      IF IILLanguageDataL.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          IILLanguageDataArchiveL.TRANSFERFIELDS(IILLanguageDataL);
          IILLanguageDataArchiveL.INSERT;
          IILLanguageDataL.DELETE;
        UNTIL IILLanguageDataL.NEXT = 0;
      END;
      }
      //H1790 11.11.14 ARI -----------------------------------
    END;

    PROCEDURE ProcessItemUpdate@1000000004(VAR IILGlobalDataR@1000000000 : Record 50600) : Boolean;
    VAR
      IILImportHandlerL@1000000003 : Codeunit 50600;
      IILAppDomDataL@1000000001 : Record 50601;
      IILLanguageDataL@1000000002 : Record 50602;
    BEGIN

      IILAppDomDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");
      IILLanguageDataL.SETRANGE("Attached to Request ID",IILGlobalDataR."Request ID");

      CLEARLASTERROR;
      IILImportHandlerL.SetRequestID(IILGlobalDataR."Request ID");
      COMMIT;
      IF NOT IILImportHandlerL.RUN THEN BEGIN
        IILGlobalDataR."Import Status" := IILGlobalDataR."Import Status"::Error;
        IILGlobalDataR."Import Error Text" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(IILGlobalDataR."Import Error Text"));
        IILGlobalDataR."Modified on" := CURRENTDATETIME;
        IILGlobalDataR.MODIFY;

        //Set Related Records to Error as well
        IILAppDomDataL.FINDSET(TRUE,FALSE);
        REPEAT
          IILAppDomDataL."Import Status" := IILAppDomDataL."Import Status"::Error;
          IILAppDomDataL."Import Error Text" := IILAppDomDataL."Import Error Text" + ' ' +
                                                  COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(IILAppDomDataL."Import Error Text"));
          IILAppDomDataL."Modified on" := CURRENTDATETIME;
          IILAppDomDataL.MODIFY;
        UNTIL IILAppDomDataL.NEXT = 0;

        IF IILLanguageDataL.FINDSET(TRUE,FALSE) THEN
          REPEAT
            IILLanguageDataL."Import Status" := IILLanguageDataL."Import Status"::Error;
            IILLanguageDataL."Import Error Text" := IILLanguageDataL."Import Error Text" + ' ' +
                                                      COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(IILLanguageDataL."Import Error Text"));
            IILLanguageDataL."Modified on" := CURRENTDATETIME;
            IILLanguageDataL.MODIFY;
          UNTIL IILLanguageDataL.NEXT = 0;

        EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________
      H1422       19.09.14  ARI       Created
      H1790       11.11.14  ARI       Item WS Performance Improvement : Dont archive Ap Domain and Languagse specfic Data (Quickfix)
    }
    END.
  }
}

