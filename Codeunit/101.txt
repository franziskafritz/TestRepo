OBJECT Codeunit 101 Cust. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=13.02.07;
    Time=12:00:00;
    Version List=NAVW15.00,OPP6.00,GOB1.00;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CustEntryApplID@1000 : Code[20];
      Text001@1000000000 : TextConst 'DEU=Die Darlehensnummer mu· die gleiche sein beim Ausgleich.;ENU=Loan number must be the same.';

    PROCEDURE SetApplId@1(VAR CustLedgEntry@1000 : Record 21;ApplyingCustLedgEntry@1003 : Record 21;AppliedAmount@1004 : Decimal;PmtDiscAmount@1002 : Decimal;AppliesToID@1001 : Code[20]);
    BEGIN
      CustLedgEntry.LOCKTABLE;
      IF CustLedgEntry.FIND('-') THEN BEGIN
        //A/gob-bth/101212/v1.00
        IF CustLedgEntry.Darlehen <>  ApplyingCustLedgEntry.Darlehen THEN
          ERROR(Text001);
        //E/gob-bth/101212/v1.00
        // Make Applies-to ID
        IF CustLedgEntry."Applies-to ID" <> '' THEN
          CustEntryApplID := ''
        ELSE BEGIN
          CustEntryApplID := AppliesToID;
          IF CustEntryApplID = '' THEN BEGIN
            CustEntryApplID := USERID;
            IF CustEntryApplID = '' THEN
              CustEntryApplID := '***';
          END;
        END;

        // Set Applies-to ID
        REPEAT
          CustLedgEntry.TESTFIELD(Open,TRUE);
          CustLedgEntry."Applies-to ID" := CustEntryApplID;
          IF CustLedgEntry."Applies-to ID" = '' THEN BEGIN
            CustLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
            CustLedgEntry."Accepted Payment Tolerance" := 0;
          END;
          // Set Amount to Apply
          IF ((CustLedgEntry."Amount to Apply" <> 0) AND (CustEntryApplID = '')) OR
            (CustEntryApplID = '')
          THEN
            CustLedgEntry."Amount to Apply" := 0
          ELSE
            IF CustLedgEntry."Amount to Apply" = 0 THEN BEGIN
              CustLedgEntry.CALCFIELDS("Remaining Amount");
              CustLedgEntry."Amount to Apply" := CustLedgEntry."Remaining Amount"
            END;

          IF CustLedgEntry."Entry No." = ApplyingCustLedgEntry."Entry No." THEN
            CustLedgEntry."Applying Entry" := ApplyingCustLedgEntry."Applying Entry";
          CustLedgEntry.MODIFY;
        UNTIL CustLedgEntry.NEXT = 0;
      END;
    END;

    PROCEDURE "++++ OPplus ++++"@5157803();
    BEGIN
    END;

    PROCEDURE SetReopenID@5157802(VAR CustLedgEntry@1000 : Record 21;AppliesToID@1001 : Code[20]);
    BEGIN
      // gbedv EA -------------------------------------------------- BEGIN
      CustLedgEntry.LOCKTABLE;
      IF CustLedgEntry.FIND('-') THEN BEGIN
        // Make Reopen-to ID
        IF CustLedgEntry."Reopen-to ID" <> '' THEN
          CustEntryApplID := ''
        ELSE BEGIN
          CustEntryApplID := AppliesToID;
          IF CustEntryApplID = '' THEN BEGIN
            CustEntryApplID := USERID;
            IF CustEntryApplID = '' THEN
              CustEntryApplID := '***';
          END;
        END;

        // Set Reopen-to ID
        REPEAT
          CustLedgEntry.TESTFIELD(Open,FALSE);
          CustLedgEntry."Reopen-to ID" := CustEntryApplID;
          CustLedgEntry.MODIFY;
        UNTIL CustLedgEntry.NEXT = 0;
      END;
      // gbedv EA -------------------------------------------------- END
    END;

    BEGIN
    {
      -----------------------------------------------------
      (c) gbedv, OPplus, All rights reserved

      No.  Date       changed
      -----------------------------------------------------
      EA   29.04.10   Extended Application
                      - New Function
      -----------------------------------------------------
      --------------------------------------------------------------------------------------------------------------------
                                         GOB Software und Systeme GmbH
                                                      -
                                                    Home 24

       Version   Datum        Berater   Beschreibung
      --------------------------------------------------------------------------------------------------------------------
       1.00      10.12.12     gob-bth   P0484 PrÅfung der Darlehensnummer bei Ausgleich
    }
    END.
  }
}

