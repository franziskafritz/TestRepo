OBJECT Codeunit 50269 DPD Label Management
{
  OBJECT-PROPERTIES
  {
    Date=21.11.14;
    Time=12:00:00;
    Modified=Yes;
    Version List=GOB,HME1677;
  }
  PROPERTIES
  {
    OnRun=VAR
            a@1000000000 : Record 38;
            DPDRoutes@1000000001 : Record 50242;
          BEGIN
          END;

  }
  CODE
  {
    VAR
      ISOIES7064MOD3736Array@1000000000 : ARRAY [36] OF Code[1];
      GOBText001@1000000001 : TextConst 'DEU=<The package weights more than 31,5 kg.>;ENU=The package weights more than 31,5 kg';
      DPDLabelMgt@1000000002 : Codeunit 50269;

    PROCEDURE GetCheckDigit@1000000002(p_InputString@1000000000 : Code[28]) : Code[1];
    VAR
      i@1000000001 : Integer;
      m@1000000002 : Integer;
      cd@1000000003 : Integer;
      WrongSTRLENError@1000000004 : TextConst 'ENU=String Lenght of Input String %1 must be 14 oder 28 for Check Digit Calculation';
    BEGIN
      FillISOIES7064MOD3736;
      i := 0;
      m := 36;
      cd := 36;

      FOR i := 1 TO STRLEN(p_InputString) DO BEGIN
        cd += GetNumValueFromISO7064Array(COPYSTR(p_InputString,i,1));
        IF cd > m THEN
          cd -= m;
        cd *= 2;
        IF cd > m THEN
          cd := cd - m - 1;
        IF i = STRLEN(p_InputString) THEN BEGIN
          cd := m + 1 - cd;
          IF cd = m THEN
            EXIT(GetTextValueFromISO7064Array(0))
          ELSE
            EXIT(GetTextValueFromISO7064Array(cd));
        END;
      END;
    END;

    PROCEDURE FillISOIES7064MOD3736@1000000001();
    BEGIN
      ISOIES7064MOD3736Array[1] := '0';
      ISOIES7064MOD3736Array[2] := '1';
      ISOIES7064MOD3736Array[3] := '2';
      ISOIES7064MOD3736Array[4] := '3';
      ISOIES7064MOD3736Array[5] := '4';
      ISOIES7064MOD3736Array[6] := '5';
      ISOIES7064MOD3736Array[7] := '6';
      ISOIES7064MOD3736Array[8] := '7';
      ISOIES7064MOD3736Array[9] := '8';
      ISOIES7064MOD3736Array[10] := '9';
      ISOIES7064MOD3736Array[11] := 'A';
      ISOIES7064MOD3736Array[12] := 'B';
      ISOIES7064MOD3736Array[13] := 'C';
      ISOIES7064MOD3736Array[14] := 'D';
      ISOIES7064MOD3736Array[15] := 'E';
      ISOIES7064MOD3736Array[16] := 'F';
      ISOIES7064MOD3736Array[17] := 'G';
      ISOIES7064MOD3736Array[18] := 'H';
      ISOIES7064MOD3736Array[19] := 'I';
      ISOIES7064MOD3736Array[20] := 'J';
      ISOIES7064MOD3736Array[21] := 'K';
      ISOIES7064MOD3736Array[22] := 'L';
      ISOIES7064MOD3736Array[23] := 'M';
      ISOIES7064MOD3736Array[24] := 'N';
      ISOIES7064MOD3736Array[25] := 'O';
      ISOIES7064MOD3736Array[26] := 'P';
      ISOIES7064MOD3736Array[27] := 'Q';
      ISOIES7064MOD3736Array[28] := 'R';
      ISOIES7064MOD3736Array[29] := 'S';
      ISOIES7064MOD3736Array[30] := 'T';
      ISOIES7064MOD3736Array[31] := 'U';
      ISOIES7064MOD3736Array[32] := 'V';
      ISOIES7064MOD3736Array[33] := 'W';
      ISOIES7064MOD3736Array[34] := 'X';
      ISOIES7064MOD3736Array[35] := 'Y';
      ISOIES7064MOD3736Array[36] := 'Z';
    END;

    PROCEDURE GetNumValueFromISO7064Array@1000000004(p_Input@1000000000 : Code[1]) : Integer;
    VAR
      WrongInputError@1000000001 : TextConst 'ENU=You have to input a Value to calculate the check degit.';
      i@1000000002 : Integer;
    BEGIN
      IF p_Input <> '' THEN BEGIN
        FOR i := 1 TO 36 DO BEGIN
          IF ISOIES7064MOD3736Array[i] = p_Input THEN
            EXIT(i - 1);
        END;
      END;
      ERROR(WrongInputError);
    END;

    PROCEDURE GetTextValueFromISO7064Array@1000000000(p_Index@1000000000 : Integer) : Code[1];
    VAR
      WrongIndexError@1000000001 : TextConst 'ENU=Wrong Index %1 for check digit calculation';
      i@1000000002 : Integer;
    BEGIN
      IF p_Index + 1 <= 36 THEN
        EXIT(ISOIES7064MOD3736Array[p_Index + 1]);
      ERROR(WrongIndexError,p_Index);
    END;

    PROCEDURE GetTrackingNo@1000000003(PurchaseHeaderV@1000000000 : Record 38;PurchaseLineV@1000000001 : Record 39) TrackingCodeO : Code[15];
    VAR
      DPDSetupL@1000000002 : Record 50221;
      VendorL@1000000003 : Record 23;
      ParcelNoL@1000000004 : Text[30];
      VendNoFreeParcelNoError@1000000005 : TextConst 'ENU=PO: %5. There is no free Parcel No for Vendor %1.\Start No. %2\End No. %3\Last Used No. %4.';
      NoFreeParcelNoError@1000000006 : TextConst 'ENU=There is no free Parcel No for home24 in DPD Setup.\Start No. %1\End No. %2\Last Used No. %3.';
      StartNoL@1000000007 : BigInteger;
      EndNoL@1000000008 : BigInteger;
      LastNoL@1000000009 : BigInteger;
      WarningNoL@1000000010 : BigInteger;
      FPCGeneralSetupL@1000000011 : Record 50055;
      ParcelStatusDHL@1000000012 : Record 50021;
      DHLSetupL@1000000013 : Record 50022;
      CUParcelStatusHistoryMgmtL@1000000014 : Codeunit 80011;
      RecRefL@1000000015 : RecordRef;
      SalesLineL@1000000016 : Record 37;
      IDSIdentcodeL@1000000017 : Record 50158;
    BEGIN
      //H1677 05.11.14 DMA +++++++++++++++++++++++++++++++++++++++++++++++++++++
      ParcelNoL := '';
      DPDSetupL.GET;
      DPDSetupL.TESTFIELD("Start Parcel No.");
      DPDSetupL.TESTFIELD("End Parcel No.");

      VendorL.GET(PurchaseHeaderV."Buy-from Vendor No.");
      IF (VendorL."DPD Start Parcel No." <> '') AND (VendorL."DPD End Parcel No." <> '') THEN BEGIN
        EVALUATE(StartNoL,VendorL."DPD Start Parcel No.");
        EVALUATE(EndNoL,VendorL."DPD End Parcel No.");
        IF VendorL."DPD Last Used Parcel No." <> '' THEN
          EVALUATE(LastNoL,VendorL."DPD Last Used Parcel No.");
        IF VendorL."DPD Warning at Parcel No." <> '' THEN
          EVALUATE(WarningNoL,VendorL."DPD Warning at Parcel No.");
        IF EndNoL - LastNoL <= 0 THEN
          ERROR(VendNoFreeParcelNoError,
            VendorL."No.",
            VendorL."DPD Start Parcel No.",
            VendorL."DPD End Parcel No.",
            VendorL."DPD Last Used Parcel No.",
            PurchaseHeaderV."No.");
        ParcelNoL := VendorL."DPD Last Used Parcel No.";
        IF ParcelNoL = '' THEN
          ParcelNoL := VendorL."DPD Start Parcel No.";
        ParcelNoL := INCSTR(ParcelNoL);
        VendorL."DPD Last Used Parcel No." := ParcelNoL;
        VendorL.MODIFY;
        IF (WarningNoL <> 0) AND (LastNoL + 1 >= WarningNoL) THEN
          SendWarningMailVendor(VendorL);
      END ELSE BEGIN
        EVALUATE(StartNoL,DPDSetupL."Start Parcel No.");
        EVALUATE(EndNoL,DPDSetupL."End Parcel No.");
        EVALUATE(LastNoL,DPDSetupL."Last Used Parcel No.");
        EVALUATE(WarningNoL,DPDSetupL."Warning at Parcel No.");
        IF EndNoL - LastNoL <= 0 THEN
          ERROR(NoFreeParcelNoError,DPDSetupL."Start Parcel No.",DPDSetupL."End Parcel No.",DPDSetupL."Last Used Parcel No.");
        ParcelNoL := DPDSetupL."Last Used Parcel No.";
        IF ParcelNoL = '' THEN
          ParcelNoL := DPDSetupL."Start Parcel No.";
        ParcelNoL := INCSTR(ParcelNoL);
        DPDSetupL."Last Used Parcel No." := ParcelNoL;
        DPDSetupL.MODIFY;
        IF (WarningNoL <> 0) AND (LastNoL >= WarningNoL) THEN
          SendWarningMailHome24(DPDSetupL);
      END;
      //r_TrackingCode := ParcelNo + GetCheckDigit(ParcelNo);
      TrackingCodeO := ParcelNoL;

      //ParcelNo := ParcelNo + GetCheckDigit(ParcelNo);
      FPCGeneralSetupL.GET;
      CASE FPCGeneralSetupL."Active Parcel Status History" OF
        FPCGeneralSetupL."Active Parcel Status History"::"Use Only Old DHL Parcel Status History",
        FPCGeneralSetupL."Active Parcel Status History"::"Fill Both But Show And Use Old",
        FPCGeneralSetupL."Active Parcel Status History"::"Fill Both But Show And Use New" :
          BEGIN
            DHLSetupL.GET;
            CLEAR(ParcelStatusDHL);
            ParcelStatusDHL."DHL Shipment Code" := ParcelNoL;
            ParcelStatusDHL."Document Line No." := PurchaseLineV."Sales Order Line No.";
            ParcelStatusDHL."Document No." := PurchaseLineV."Sales Order No.";
            ParcelStatusDHL."Document Type" := ParcelStatusDHL."Document Type"::Order;
            ParcelStatusDHL."Time Stamp" := CURRENTDATETIME;
            ParcelStatusDHL."Piece-Code (Identifier)" := '';
            ParcelStatusDHL."Purchase Order Code" := PurchaseLineV."Document No.";
            ParcelStatusDHL."PO Line No." := PurchaseLineV."Line No.";
            ParcelStatusDHL."Current Status" := TRUE;
            ParcelStatusDHL."Status Event" := DHLSetupL."Status Code Label Send";
            ParcelStatusDHL."Status Code" := DHLSetupL."RIC Code Label Send";
            IF ParcelStatusDHL.INSERT(TRUE) THEN;
          END;
      END;
      CASE FPCGeneralSetupL."Active Parcel Status History" OF
        FPCGeneralSetupL."Active Parcel Status History"::"Fill Both But Show And Use Old",
        FPCGeneralSetupL."Active Parcel Status History"::"Fill Both But Show And Use New",
        FPCGeneralSetupL."Active Parcel Status History"::"Use Only New Parcel Status History":
          BEGIN
            DPDSetupL.TESTFIELD("Parcel Status Code DS");
            DPDSetupL.TESTFIELD("Parcel Status Code Sub DS");
            CLEAR(CUParcelStatusHistoryMgmtL);
            SalesLineL.GET(SalesLineL."Document Type"::Order,PurchaseLineV."Sales Order No.",PurchaseLineV."Sales Order Line No.");
            RecRefL.GETTABLE(SalesLineL);
            CUParcelStatusHistoryMgmtL.FillHistoryByDocLine(
              RecRefL,                                                     // Rec
              0,                                                          // p_Command
              1,                                                          // p_InfoDirection::  Incoming,Outgoing
              7,                             // p_UsedInterface::,EDI,DHL,DocData,Drop Shipment,Rhenus,UPS,DPD,WHSLF,Importer //P1283
              'DPD',                                                      // p_ShippingAgentCode
              UPPERCASE(ParcelNoL),  // p_TrackingCode
              '',                                                         // p_PieceCode
              DPDSetupL."Parcel Status Code DS",                          // p_StatusCode
              DPDSetupL."Parcel Status Code Sub DS",                             // p_StatusRICCode
              '',                                                         // p_Comment
              0);                                                         // p_HandleStatusDescription
          END;
      END;
      //E/

      // Eintrag in der Identcode-Tabelle (fÅr EDI)
      // S/P1245
      IDSIdentcodeL.INIT;
      IDSIdentcodeL.Identcode := ParcelNoL;
      IDSIdentcodeL."Purchase Order No." := PurchaseLineV."Document No.";
      IDSIdentcodeL."Shipping Agent" := DPDSetupL."Shipping Agent DPD";
      IF IDSIdentcodeL.INSERT THEN;
      // E/P1245
      //H1677 05.11.14 DMA -----------------------------------------------------
    END;

    PROCEDURE GetRoute@1000000007(p_PurchaseHeader@1000000000 : Record 38;VAR p_DPDRoutes_Var@1000000009 : Record 50242);
    VAR
      PostCodeSearchString@1000000001 : Code[20];
      DPDRoutes@1000000002 : Record 50242;
      DestinationCountryFilter@1000000003 : Code[10];
      CountryRegion@1000000004 : Record 9;
      CompanyInformation@1000000005 : Record 79;
      DPDCountries@1000000006 : Record 50243;
      BeginPostCodeFilter@1000000007 : Code[10];
      RouteFound@1000000008 : Boolean;
    BEGIN
      DestinationCountryFilter := '';
      BeginPostCodeFilter := '';

      PostCodeSearchString := DELCHR(p_PurchaseHeader."Ship-to Post Code",'=',' '); //P1285

      // S/P1285
      //WHILE STRLEN(PostCodeSearchString) < 5 DO
      //  PostCodeSearchString := INSSTR(PostCodeSearchString,'0',1);

      //DestinationCountryFilter := '';
      // E/P1285
      IF CountryRegion.GET(p_PurchaseHeader."Ship-to Country/Region Code") THEN BEGIN
        CountryRegion.TESTFIELD("DPD Country Code");
        DPDCountries.GET(CountryRegion."DPD Country Code");
        DestinationCountryFilter := DPDCountries."ISO-Alpha2CountryCode";
      END ELSE BEGIN
        CompanyInformation.GET;
        CountryRegion.GET(CompanyInformation."Country/Region Code");
        CountryRegion.TESTFIELD("DPD Country Code");
        DPDCountries.GET(CountryRegion."DPD Country Code");
        DestinationCountryFilter := DPDCountries."ISO-Alpha2CountryCode";
      END;

      BeginPostCodeFilter := COPYSTR(PostCodeSearchString,1,1) + '*';

      RouteFound := FALSE;
      DPDRoutes.RESET;
      DPDRoutes.SETCURRENTKEY(DestinationCountry,BeginPostCode,EndPostCode);
      DPDRoutes.SETRANGE(DestinationCountry,DestinationCountryFilter);
      DPDRoutes.SETFILTER(BeginPostCode,BeginPostCodeFilter);
      DPDRoutes.SETRANGE(ServiceCodes,'');
      IF DPDRoutes.FIND('-') THEN BEGIN
        REPEAT
          IF ((DPDRoutes.BeginPostCode <= PostCodeSearchString) AND (DPDRoutes.EndPostCode >= PostCodeSearchString)) OR
            ((DPDRoutes.BeginPostCode = PostCodeSearchString) AND (DPDRoutes.EndPostCode = ''))
          THEN BEGIN
            RouteFound := TRUE;

            p_DPDRoutes_Var := DPDRoutes;
          END;
        UNTIL (DPDRoutes.NEXT = 0) OR RouteFound;
      END;
    END;

    PROCEDURE CreateDPDBarcode128@1000000005(PurchaseLineV@1000000002 : Record 39;ItemV@1000000011 : Record 27;ParcelCounterV@1000000016 : Integer) DPDBarcodeO : Text[30];
    VAR
      PurchaseHeaderL@1000000001 : Record 38;
      CountryRegionL@1000000005 : Record 9;
      CompanyInformationL@1000000007 : Record 79;
      DPDSetupL@1000000010 : Record 50221;
      PostCodeLengthL@1000000003 : Integer;
      PostCodeL@1000000004 : Text[10];
      TrackingNoL@1000000012 : Text[15];
      NumberOfParcelsL@1000000014 : Integer;
      TotalWeightL@1000000015 : Decimal;
    BEGIN
      //H1677 05.11.14 DMA ++++++++++++++++++++++++++++++++++++++++
      PurchaseHeaderL.GET(PurchaseLineV."Document Type",PurchaseLineV."Document No.");
      PurchaseHeaderL.TESTFIELD("Ship-to Post Code");
      PostCodeL := DELCHR(PurchaseHeaderL."Ship-to Post Code",'=',' ');//P1285
      DPDSetupL.GET;
      DPDSetupL.TESTFIELD("Service Code Small Parcel");
      DPDSetupL.TESTFIELD("Service Code Medium Parcel");

      //Identification Char
      DPDBarcodeO := '%';

      //Post Code:
      IF STRLEN(PostCodeL) < 7 THEN BEGIN
        REPEAT
          PostCodeLengthL := PostCodeLengthL + 1;
          PostCodeL := '0' + PostCodeL;
        UNTIL STRLEN(PostCodeL) >=7
      END;
      DPDBarcodeO := DPDBarcodeO + PostCodeL;

      //Tracking No.:
      //S,gob-Fuchs,18.09.14,P1277
      //Check if tracking no for a order exists
      GetExistingIdentCodeForPL(PurchaseLineV,ParcelCounterV,TrackingNoL);
      IF TrackingNoL = '' THEN
      //E,P1277
        TrackingNoL := GetTrackingNo(PurchaseHeaderL,PurchaseLineV);
      DPDBarcodeO := DPDBarcodeO + TrackingNoL;

      //Service Code:
      //S,gob-Fuchs,17.09.14,P1277
      ItemV.CALCFIELDS("Parcels Number");
      IF ItemV."Parcels Number" = 0 THEN
        NumberOfParcelsL := 1
      ELSE
        NumberOfParcelsL := ItemV."Parcels Number";
      TotalWeightL := ItemV."Gross Weight" / NumberOfParcelsL;
      IF TotalWeightL = 0 THEN
        TotalWeightL := 0.1;
      //E,gob-Fuchs,17.09.14,P1277

      IF TotalWeightL > DPDSetupL."Max Weight per Parcel" THEN //P1277
        ERROR(GOBText001)
      ELSE
        IF TotalWeightL <= DPDSetupL."Max. Weight Small Parcel" THEN //P1277
          DPDBarcodeO := DPDBarcodeO + DPDSetupL."Service Code Small Parcel"
        ELSE
          DPDBarcodeO := DPDBarcodeO + DPDSetupL."Service Code Medium Parcel";

      //Country Code:
      IF PurchaseHeaderL."Ship-to Country/Region Code" <>'' THEN BEGIN
        CountryRegionL.GET(PurchaseHeaderL."Ship-to Country/Region Code");
        CountryRegionL.TESTFIELD("DPD Country Code");
        DPDBarcodeO := DPDBarcodeO + CountryRegionL."DPD Country Code";
      END ELSE BEGIN
        CompanyInformationL.GET;
        CountryRegionL.GET(CompanyInformationL."Country/Region Code");
        CountryRegionL.TESTFIELD("DPD Country Code");
        DPDBarcodeO := DPDBarcodeO + CountryRegionL."DPD Country Code";
      END;

      //H1677 05.11.14 DMA ----------------------------------------
    END;

    PROCEDURE CreateParcelStatusRecords@1000000006(p_PurchaseLine@1000000000 : Record 39;p_TrackingNo@1000000002 : Code[30];p_CreationDateTime@1000000007 : DateTime);
    VAR
      DPDSetup@1000000001 : Record 50221;
      FPCGeneralSetup@1000000003 : Record 50055;
      ParcelStatusHistoryMgmt@1000000004 : Codeunit 80011;
      RecRef@1000000005 : RecordRef;
      DHLParcelsStatusHistory@1000000006 : Record 50021;
      SalesLine@1000000008 : Record 37;
      rIDSIdentcode@1000000009 : Record 50158;
    BEGIN
      DPDSetup.GET;
      DPDSetup.TESTFIELD("Shipping Agent DPD");
      DPDSetup.TESTFIELD("Parcel Status Code DS");
      FPCGeneralSetup.GET;

      IF  FPCGeneralSetup."Active Parcel Status History" IN
        [FPCGeneralSetup."Active Parcel Status History"::"Use Only New Parcel Status History",
         FPCGeneralSetup."Active Parcel Status History"::"Fill Both But Show And Use Old",
         FPCGeneralSetup."Active Parcel Status History"::"Fill Both But Show And Use New"]
      THEN BEGIN
        SalesLine.GET(SalesLine."Document Type"::Order,p_PurchaseLine."Sales Order No.",p_PurchaseLine."Sales Order Line No.");
        RecRef.GETTABLE(SalesLine);
        ParcelStatusHistoryMgmt.SetDateTimeGlobal(p_CreationDateTime);
        ParcelStatusHistoryMgmt.FillHistoryByDocLine(
          RecRef,
          1,
          0,
          7,
          DPDSetup."Shipping Agent DPD",
          p_TrackingNo,
          '',
          DPDSetup."Parcel Status Code DS",
          DPDSetup."Parcel Status Code Sub DS",
          '',
          0);
      END;

      IF  FPCGeneralSetup."Active Parcel Status History" IN
        [FPCGeneralSetup."Active Parcel Status History"::"Use Only Old DHL Parcel Status History",
         FPCGeneralSetup."Active Parcel Status History"::"Fill Both But Show And Use Old",
         FPCGeneralSetup."Active Parcel Status History"::"Fill Both But Show And Use New"]
      THEN BEGIN
        DHLParcelsStatusHistory.INIT;
        DHLParcelsStatusHistory."Entry No." := 0;
        DHLParcelsStatusHistory."DHL Shipment Code" := p_TrackingNo;
        DHLParcelsStatusHistory."Time Stamp" := p_CreationDateTime;
        DHLParcelsStatusHistory."Status Event" := DPDSetup."Parcel Status Code DS";
        DHLParcelsStatusHistory."Status Code" := DPDSetup."Parcel Status Code Sub DS";
        DHLParcelsStatusHistory."Document No." := SalesLine."Document No.";
        DHLParcelsStatusHistory."Document Type" := DHLParcelsStatusHistory."Document Type"::Order;
        DHLParcelsStatusHistory."Document Line No." := SalesLine."Line No.";
        DHLParcelsStatusHistory."Import Time Stamp" := p_CreationDateTime;
        DHLParcelsStatusHistory."Shipment through" := DHLParcelsStatusHistory."Shipment through"::DocData;
        DHLParcelsStatusHistory."Shipping Agent" := DPDSetup."Shipping Agent DPD";
        DHLParcelsStatusHistory.INSERT(TRUE);
      END;

      // Eintrag in der Identcode-Tabelle (fÅr EDI)
      // S/P1245
      rIDSIdentcode.INIT;
      rIDSIdentcode.Identcode := p_TrackingNo;
      rIDSIdentcode."Purchase Order No." := p_PurchaseLine."Document No.";
      rIDSIdentcode."Shipping Agent" := DPDSetup."Shipping Agent DPD";
      IF rIDSIdentcode.INSERT THEN;
      // E/P1245
    END;

    PROCEDURE SendWarningMailHome24@1000000013(p_DPDSetup@1000000000 : Record 50221);
    VAR
      MailIT@1000000004 : OCX "{C8C4804C-89BA-4291-AB58-4C6050B65175}:'ExpandIT.MailIT2'";
      MailITSetup@1000000003 : Record 75402;
      JobID@1000000002 : Text[1024];
      Port@1000000001 : Integer;
      MaxTrackNoReachedText@1000000005 : TextConst 'ENU=The maximum tracking number for home24 has almost been reached.';
      CheckTrackNoResetText@1000000006 : TextConst 'ENU=Please check manually if you can reset the number range in the DPD Setup';
      LastNo@1000000007 : BigInteger;
      EndNo@1000000008 : BigInteger;
    BEGIN
      MailITSetup.GET;
      //Testfield MailIT Mail Data
      MailITSetup.TESTFIELD("User Name (Direct)");
      MailITSetup.TESTFIELD("Password (Direct)");
      p_DPDSetup.TESTFIELD("Warning E-Mail Address");
      JobID := MailIT.CreateGUID();
      MailIT.SetService('SMTP');
      MailIT.SetFrom('dpd_warning@home24.de');
      MailIT.AddRecipient(p_DPDSetup."Warning E-Mail Address",p_DPDSetup."Warning E-Mail Address");
      MailIT.SetSubject('DPD Tracking Warning');
      MailIT.AppendHTML('<font face="Arial","sans-serif">');
      MailIT.AppendHTML('<table width=590 border=0>');
      MailIT.AppendHTML(MaxTrackNoReachedText + '<br />');
      MailIT.AppendHTML('Start No.: ' + FORMAT(p_DPDSetup."Start Parcel No.") + '<br />');
      MailIT.AppendHTML('End No.: ' + FORMAT(p_DPDSetup."End Parcel No.") + '<br />');
      MailIT.AppendHTML('Last Used No.: ' + FORMAT(p_DPDSetup."Last Used Parcel No.") +'<br />');
      IF p_DPDSetup."End Parcel No." <> '' THEN
        EVALUATE(EndNo,p_DPDSetup."End Parcel No.");
      IF p_DPDSetup."Last Used Parcel No." <> '' THEN
        EVALUATE(LastNo,p_DPDSetup."Last Used Parcel No.");
      MailIT.AppendHTML('Remaining Nos.: ' + FORMAT(EndNo - LastNo) +'<br />');
      MailIT.AppendHTML(CheckTrackNoResetText + '<br />');
      MailIT.SetServer(MailITSetup."Mail Server");
      EVALUATE(Port,MailITSetup."Server Port");
      MailIT.SetPort(Port);
      MailIT.SetLogin(MailITSetup."User Name (Direct)");
      MailIT.SetPassword(MailITSetup."Password (Direct)");
      MailIT.Send();
    END;

    PROCEDURE SendWarningMailVendor@1000000020(p_Vendor@1000000000 : Record 23);
    VAR
      MailIT@1000000004 : OCX "{C8C4804C-89BA-4291-AB58-4C6050B65175}:'ExpandIT.MailIT2'";
      MailITSetup@1000000003 : Record 75402;
      JobID@1000000002 : Text[1024];
      Port@1000000001 : Integer;
      MaxTrackNoReachedText@1000000005 : TextConst 'ENU=The maximum tracking number for Vendor %1 has almost been reached.';
      CheckTrackNoResetText@1000000006 : TextConst 'ENU=Please check manually if you can reset the number range in the Vendor Card';
      LastNo@1000000007 : BigInteger;
      EndNo@1000000008 : BigInteger;
      DPDSetup@1000000009 : Record 50221;
    BEGIN
      MailITSetup.GET;
      //Testfield MailIT Mail Data
      MailITSetup.TESTFIELD("User Name (Direct)");
      MailITSetup.TESTFIELD("Password (Direct)");
      DPDSetup.GET;
      DPDSetup.TESTFIELD("Warning E-Mail Address");
      JobID := MailIT.CreateGUID();
      MailIT.SetService('SMTP');
      MailIT.SetFrom('dpd_warning@home24.de');
      MailIT.AddRecipient(DPDSetup."Warning E-Mail Address",DPDSetup."Warning E-Mail Address");
      MailIT.SetSubject('DPD Tracking Warning');
      MailIT.AppendHTML('<font face="Arial","sans-serif">');
      MailIT.AppendHTML('<table width=590 border=0>');
      MailIT.AppendHTML(STRSUBSTNO(MaxTrackNoReachedText,p_Vendor."No.") + '<br />');
      MailIT.AppendHTML('Start No.: ' + FORMAT(p_Vendor."DPD Start Parcel No.") + '<br />');
      MailIT.AppendHTML('End No.: ' + FORMAT(p_Vendor."DPD End Parcel No.") + '<br />');
      MailIT.AppendHTML('Last Used No.: ' + FORMAT(p_Vendor."DPD Last Used Parcel No.") +'<br />');
      IF p_Vendor."DPD End Parcel No." <> '' THEN
        EVALUATE(EndNo,p_Vendor."DPD End Parcel No.");
      IF p_Vendor."DPD Last Used Parcel No." <> '' THEN
        EVALUATE(LastNo,p_Vendor."DPD Last Used Parcel No.");
      MailIT.AppendHTML('Remaining Nos.: ' + FORMAT(EndNo - LastNo) +'<br />');
      MailIT.AppendHTML(CheckTrackNoResetText + '<br />');
      MailIT.SetServer(MailITSetup."Mail Server");
      EVALUATE(Port,MailITSetup."Server Port");
      MailIT.SetPort(Port);
      MailIT.SetLogin(MailITSetup."User Name (Direct)");
      MailIT.SetPassword(MailITSetup."Password (Direct)");
      MailIT.Send();
    END;

    PROCEDURE CutAlgoTEMP@1000000010();
    VAR
      StringOk@1000000000 : Boolean;
      StringToCreate@1000000001 : Text[1024];
      NoOfLoops@1000000002 : Integer;
      FPCManagement@1000000003 : Codeunit 50003;
    BEGIN
      NoOfLoops := 0;
      StringOk := FALSE;
      REPEAT
        StringToCreate := 'BC_Overflow_1';
        StringToCreate := StringToCreate + 'Sender Name 1';
        StringToCreate := StringToCreate + 'Sender Name 2';
        StringToCreate := StringToCreate + 'Sender Adresse';
        StringToCreate := StringToCreate + 'Sender Adresse 2';
        IF NoOfLoops = 0 THEN
          StringToCreate := StringToCreate + 'Sender Contact';
        IF NoOfLoops <= 1 THEN
          StringToCreate := StringToCreate + 'Sender Phone';
        // alle check einbauen

        IF STRLEN(StringToCreate) <= 152 THEN
          StringOk := TRUE
        ELSE
          NoOfLoops +=1;
      UNTIL StringOk OR (NoOfLoops > 10); //Max anzahl loops wenn vorhanden

      IF StringOk THEN
        IF NoOfLoops = 0 THEN
          FPCManagement.ReplaceString(StringToCreate,'BC_Overflow_1','0')
        ELSE
          FPCManagement.ReplaceString(StringToCreate,'BC_Overflow_1','1')
    END;

    PROCEDURE GetCarrierForDropshipOrder@1000000008(p_PurchaseHeader@1000000000 : Record 38;p_SalesHeader@1000000008 : Record 36) : Code[10];
    VAR
      PurchaseLine@1000000001 : Record 39;
      ItemShippingRelation@1000000002 : Record 50116;
      ShippingAgentPrioTEMP@1000000003 : TEMPORARY Record 50223;
      Purchasing@1000000004 : Record 5721;
      FPCGeneralSetup@1000000005 : Record 50055;
      ShippingAgentPrioritization@1000000006 : Record 50223;
      ShippingAgentTemP@1000000007 : Code[10];
      FPCInterfaceSetup@1000000009 : Record 50014;
      DPDSetup@1000000010 : Record 50221;
    BEGIN
      FPCGeneralSetup.GET;
      FPCGeneralSetup.TESTFIELD("Interface Code Dropshipment");
      DPDSetup.GET;
      DPDSetup.TESTFIELD("Shipping Agent DPD");
      ShippingAgentPrioTEMP.DELETEALL;
      PurchaseLine.RESET;
      PurchaseLine.SETRANGE("Document Type",p_PurchaseHeader."Document Type");
      PurchaseLine.SETRANGE("Document No.",p_PurchaseHeader."No.");
      PurchaseLine.SETRANGE(Type,PurchaseLine.Type::Item);
      IF PurchaseLine.FINDSET THEN BEGIN
        REPEAT
          ShippingAgentTemP := '';
          IF ItemShippingRelation.GET(PurchaseLine."No.",p_SalesHeader."Website No.",PurchaseLine."Purchasing Code") THEN
            ShippingAgentTemP := ItemShippingRelation."Shipping Agent";
          IF ShippingAgentTemP = '' THEN BEGIN
            FPCInterfaceSetup.GET(FPCGeneralSetup."Interface Code Dropshipment");
            FPCInterfaceSetup.TESTFIELD("Default Shipping Agent");
            ShippingAgentTemP := FPCInterfaceSetup."Default Shipping Agent";
          END;
          IF ShippingAgentTemP IN [DPDSetup."Shipping Agent DPD",FPCGeneralSetup."Ship. Agent Code DHL"] THEN BEGIN
            IF ShippingAgentPrioritization.GET(
              0,
              FPCGeneralSetup."Interface Code Dropshipment",
              PurchaseLine."Purchasing Code",
              ShippingAgentTemP)
            THEN BEGIN
              IF NOT ShippingAgentPrioTEMP.GET(
                 0,
                 FPCGeneralSetup."Interface Code Dropshipment",
                 PurchaseLine."Purchasing Code",
                 ShippingAgentTemP)
              THEN BEGIN
                ShippingAgentPrioTEMP.INIT;
                ShippingAgentPrioTEMP.TRANSFERFIELDS(ShippingAgentPrioritization);
                ShippingAgentPrioTEMP.INSERT;
              END;
            END;
          END;
        UNTIL PurchaseLine.NEXT = 0;
        IF NOT ShippingAgentPrioTEMP.ISEMPTY THEN BEGIN
          ShippingAgentPrioTEMP.SETCURRENTKEY(Priority);
          ShippingAgentPrioTEMP.FINDFIRST;
          EXIT(ShippingAgentPrioTEMP."Shipping Agent");
        END ELSE BEGIN
          FPCInterfaceSetup.GET(FPCGeneralSetup."Interface Code Dropshipment");
          FPCInterfaceSetup.TESTFIELD("Default Shipping Agent");
          EXIT(FPCInterfaceSetup."Default Shipping Agent");
        END;
      END;
    END;

    PROCEDURE "- P1277 Functions ---"@1000000016();
    BEGIN
    END;

    PROCEDURE GetExistingIdentCodeForPL@1000000015(PurchaseLineV@1000000000 : Record 39;ParcelNoV@1000000001 : Integer;VAR IdentCodeClearTextR@1000000004 : Text[30]) IdentCodeO : Code[20];
    VAR
      ParcelStatusHistoryL@1000000005 : Record 80013;
    BEGIN
      //H1677 05.11.14 DMA +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      //S,gob-Fuchs,18.09.14,P1277
      IdentCodeClearTextR := '';
      ParcelStatusHistoryL.RESET;
      ParcelStatusHistoryL.SETCURRENTKEY("Purchase Doc. No.","Purchase Doc. Line No.","Purchase Doc. Type");
      ParcelStatusHistoryL.SETRANGE("Purchase Doc. No.",PurchaseLineV."Document No.");
      ParcelStatusHistoryL.SETRANGE("Purchase Doc. Line No.",PurchaseLineV."Line No.");
      ParcelStatusHistoryL.SETRANGE("Purchase Doc. Type",PurchaseLineV."Document Type");
      ParcelStatusHistoryL.SETFILTER("Tracking Code",'<>%1','');
      IF ParcelNoV > ParcelStatusHistoryL.COUNT THEN
        EXIT;

      IF NOT ParcelStatusHistoryL.ISEMPTY THEN BEGIN
        ParcelStatusHistoryL.FINDFIRST;
        IF (ParcelNoV <> 1) AND (ParcelNoV <> 0) THEN BEGIN
          IF ParcelStatusHistoryL.NEXT(ParcelNoV-1) > 0 THEN
            IdentCodeClearTextR := ParcelStatusHistoryL."Tracking Code"
        END ELSE
          IdentCodeClearTextR := ParcelStatusHistoryL."Tracking Code";
      END;
      //E,P1277
      //H1677 05.11.14 DMA -------------------------------------------------------------------
    END;

    BEGIN
    {
      +--------------------------------------------------+
      |                   ∏  Copyright                   |
      |       GOB Software & Systeme GmbH & Co. KG       |
      +--------------------------------------------------+
      |                      home24                      |
      +--------------------------------------------------+

      Project   Date        Consultant  Comment
      ------------------------------------------------------
      P1243     25.04.2014    gob-rste    Object Created
      P1245     12.06.2014  gob-dst     Anpassung fÅr DPD IFTMIN
      P1243     25.04.14    gob-rste    Object Created
      P1277     15.09.14    gob-rhg     - Get current tracking number if label is printed for the second time
      P1277     17.09.2014  gob-Fuchs   Anpassung zur Berechnung des TotalWeights
                18.09.2014  gob-Fuchs   Find Existing Tracking Codes per Parcel
      P1283     24.09.2014  gob-Fuchs   CUParcelStatusHistoryMgmt.FillHistoryByDocLine, Used Interface Parameter auf DPD umgestellt
      P1285     01.10.2014  gob-Fuchs   Delete empty spaces from Post Codes
      P1285     10.10.2014  gob-rhg     Comment out of adding a zero for post codes with less then five digits at the function "GetRoute"

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________
      H1677       05.11.14  DMA       BUG:Dropshipment  DPD Label for three (3) colli Item, CODECHANGE, CODECLEANUP
    }
    END.
  }
}

