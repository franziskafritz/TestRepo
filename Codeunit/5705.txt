OBJECT Codeunit 5705 TransferOrder-Post Receipt
{
  OBJECT-PROPERTIES
  {
    Date=25.06.15;
    Time=09:17:57;
    Modified=Yes;
    Version List=NAVW16.00.01,HME4370,T0100;
  }
  PROPERTIES
  {
    TableNo=5740;
    Permissions=TableData 6507=i;
    OnRun=VAR
            Item@1000 : Record 27;
            SourceCodeSetup@1001 : Record 242;
            InvtSetup@1002 : Record 313;
            InventoryPostingSetup@1011 : Record 5813;
            ValueEntry@1007 : Record 5802;
            ItemLedgEntry@1008 : Record 32;
            ItemApplnEntry@1009 : Record 339;
            ItemReg@1010 : Record 46;
            NoSeriesMgt@1003 : Codeunit 396;
            UpdateAnalysisView@1004 : Codeunit 410;
            UpdateItemAnalysisView@1012 : Codeunit 7150;
            ReservMgt@1013 : Codeunit 99000845;
            Window@1006 : Dialog;
            LineCount@1005 : Integer;
            "**** HME *********************"@1100409000 : Integer;
            ParcelStatusHistoryMgmt@1000000000 : Codeunit 80011;
            TransferHeaderL@1100409007 : Record 5740;
            TransferLineL@1100409008 : Record 5741;
            TransferLineTmpL@1100409001 : TEMPORARY Record 5741;
            NoSeriesMgmtL@1100409004 : Codeunit 396;
            ReleaseTransferL@1100409003 : Codeunit 5708;
            LineNoL@1100409002 : Integer;
            SalesReceivableSetupL@1100409006 : Record 311;
            WareHouseSetupL@1100409005 : Record 5769;
            PurchaseLineL@1102707001 : Record 39;
          BEGIN
            //H4370 23.06.15 TST +++++++++++++++++++++++++++++
            CheckSingleInstance();
            //H4370 26.06.15 TST -----------------------------
            IF Status = Status::Open THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"Release Transfer Document",Rec);
              Status := Status::Open;
              MODIFY;
              COMMIT;
              Status := Status::Released;
            END;
            TransHeader := Rec;
            TransHeader.SetHideValidationDialog(HideValidationDialog);

            WITH TransHeader DO BEGIN
              //H4370 23.06.15 TST +++++++++++++++++++++++++++++
              IF NOT CheckTransferHeader() THEN
                EXIT;
              //H4370 26.06.15 TST -----------------------------
              TESTFIELD("Transfer-from Code");
              TESTFIELD("Transfer-to Code");
              IF ("Transfer-from Code" <> '') AND
                 ("Transfer-from Code" = "Transfer-to Code")
              THEN
                //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                IF IsInternational THEN BEGIN
                  SingleInstanceCU.SetError(TRUE,
                    STRSUBSTNO(Text000, "No.",FIELDCAPTION("Transfer-from Code"),FIELDCAPTION("Transfer-to Code")));
                  EXIT;
                END ELSE
                //H4370 26.06.15 TST -----------------------------
                  ERROR
                    (Text000,
                    "No.",FIELDCAPTION("Transfer-from Code"),FIELDCAPTION("Transfer-to Code"));
              TESTFIELD("In-Transit Code");
              TESTFIELD(Status,Status::Released);
              TESTFIELD("Posting Date");

              WhseReference := "Posting from Whse. Ref.";
              "Posting from Whse. Ref." := 0;

              CopyAndCheckDocDimToTempDocDim;

              TransLine.RESET;
              TransLine.SETRANGE("Document No.","No.");
              TransLine.SETRANGE("Derived From Line No.",0);
              TransLine.SETFILTER(Quantity,'<>0');
              TransLine.SETFILTER("Qty. to Receive",'<>0');
              IF NOT TransLine.FIND('-') THEN
                //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                IF IsInternational THEN BEGIN
                  SingleInstanceCU.SetError(TRUE,Text000);
                  EXIT;
                END ELSE
                //H4370 26.06.15 TST -----------------------------
                  ERROR(Text001);

              WhseReceive := TempWhseRcptHeader.FIND('-');
              InvtPickPutaway := WhseReference <> 0;
              IF NOT (WhseReceive OR InvtPickPutaway) THEN
                CheckWarehouse(TransLine);

              GetLocation("Transfer-to Code");
              IF Location."Bin Mandatory" AND NOT (WhseReceive OR InvtPickPutaway) THEN
                WhsePosting := TRUE;

              //H0550 16.09.13 ARI ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
              IF NOT HideValidationDialog OR GUIALLOWED THEN BEGIN
              //H0550 16.09.13 ARI --------------------------------------------------------
              Window.OPEN(
                '#1#################################\\' +
                Text003);

              Window.UPDATE(1,STRSUBSTNO(Text004,"No."));
              //H0550 16.09.13 ARI ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
              END;
              //H0550 16.09.13 ARI --------------------------------------------------------


              SourceCodeSetup.GET;
              SourceCode := SourceCodeSetup.Transfer;
              InvtSetup.GET;
              InvtSetup.TESTFIELD("Posted Transfer Rcpt. Nos.");
              InventoryPostingSetup.SETRANGE("Location Code","Transfer-from Code");
              InventoryPostingSetup.FIND('-');
              InventoryPostingSetup.SETRANGE("Location Code","Transfer-to Code");
              InventoryPostingSetup.FIND('-');

              IF RECORDLEVELLOCKING THEN BEGIN
                NoSeriesLine.LOCKTABLE;
                IF NoSeriesLine.FIND('+') THEN;
                IF InvtSetup."Automatic Cost Posting" THEN  BEGIN
                  GLEntry.LOCKTABLE;
                  IF GLEntry.FIND('+') THEN;
                END;
              END;

              // Insert receipt header
              IF WhseReceive THEN
                PostedWhseRcptHeader.LOCKTABLE;
              TransRcptHeader.LOCKTABLE;
              TransRcptHeader.INIT;
              TransRcptHeader."Transfer-from Code" := "Transfer-from Code";
              TransRcptHeader."Transfer-from Name" := "Transfer-from Name";
              TransRcptHeader."Transfer-from Name 2" := "Transfer-from Name 2";
              TransRcptHeader."Transfer-from Address" := "Transfer-from Address";
              TransRcptHeader."Transfer-from Address 2" := "Transfer-from Address 2";
              TransRcptHeader."Transfer-from Post Code" := "Transfer-from Post Code";
              TransRcptHeader."Transfer-from City" := "Transfer-from City";
              TransRcptHeader."Transfer-from County" := "Transfer-from County";
              TransRcptHeader."Trsf.-from Country/Region Code" := "Trsf.-from Country/Region Code";
              TransRcptHeader."Transfer-from Contact" := "Transfer-from Contact";
              TransRcptHeader."Transfer-to Code" := "Transfer-to Code";
              TransRcptHeader."Transfer-to Name" := "Transfer-to Name";
              TransRcptHeader."Transfer-to Name 2" := "Transfer-to Name 2";
              TransRcptHeader."Transfer-to Address" := "Transfer-to Address";
              TransRcptHeader."Transfer-to Address 2" := "Transfer-to Address 2";
              TransRcptHeader."Transfer-to Post Code" := "Transfer-to Post Code";
              TransRcptHeader."Transfer-to City" := "Transfer-to City";
              TransRcptHeader."Transfer-to County" := "Transfer-to County";
              TransRcptHeader."Trsf.-to Country/Region Code" := "Trsf.-to Country/Region Code";
              TransRcptHeader."Transfer-to Contact" := "Transfer-to Contact";
              TransRcptHeader."Transfer Order Date" := "Posting Date";
              TransRcptHeader."Posting Date" := "Posting Date";
              TransRcptHeader."Shipment Date" := "Shipment Date";
              TransRcptHeader."Receipt Date" := "Receipt Date";
              TransRcptHeader."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
              TransRcptHeader."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
              TransRcptHeader."Transfer Order No." := "No.";
              TransRcptHeader."External Document No." := "External Document No.";
              TransRcptHeader."In-Transit Code" := "In-Transit Code";
              TransRcptHeader."Shipping Agent Code" := "Shipping Agent Code";
              TransRcptHeader."Shipping Agent Service Code" := "Shipping Agent Service Code";
              TransRcptHeader."Shipment Method Code" := "Shipment Method Code";
              TransRcptHeader."Transaction Type" := "Transaction Type";
              TransRcptHeader."Transport Method" := "Transport Method";
              TransRcptHeader."Entry/Exit Point" := "Entry/Exit Point";
              TransRcptHeader.Area := Area;
              TransRcptHeader."Transaction Specification" := "Transaction Specification";
              //S/FOB
              TransRcptHeader."Buy-from Vendor No" := "Buy-from Vendor No";
              TransRcptHeader."Purchase Order No." := "Purchase Order No.";
              TransRcptHeader."Pay-to Vendor No." := "Pay-to Vendor No.";
              TransRcptHeader."Order Date" := "Order Date";
              //E/FOB
              TransRcptHeader."No. Series" := InvtSetup."Posted Transfer Rcpt. Nos.";
              TransRcptHeader."No." :=
                NoSeriesMgt.GetNextNo(
                  InvtSetup."Posted Transfer Rcpt. Nos.","Posting Date",TRUE);
              TransRcptHeader.INSERT;

              DimMgt.MoveOneDocDimToPostedDocDim(
                TempDocDim,DATABASE::"Transfer Header",TempDocDim."Document Type"::" ","No.",0,
                DATABASE::"Transfer Receipt Header",TransRcptHeader."No.");
              IF InvtSetup."Copy Comments Order to Rcpt." THEN BEGIN
                CopyCommentLines(1,3,"No.",TransRcptHeader."No.");
                TransRcptHeader.COPYLINKS(Rec);
              END;

              IF WhseReceive THEN BEGIN
                WhseRcptHeader.GET(TempWhseRcptHeader."No.");
                WhsePostRcpt.CreatePostedRcptHeader(PostedWhseRcptHeader,WhseRcptHeader,TransRcptHeader."No.","Posting Date");
              END;

              // Insert receipt lines
              LineCount := 0;
              IF WhseReceive THEN
                PostedWhseRcptLine.LOCKTABLE;
              DocDim.LOCKTABLE;
              IF InvtPickPutaway THEN
                WhseRqst.LOCKTABLE;
              TransRcptLine.LOCKTABLE;
              TransLine.SETRANGE(Quantity);
              TransLine.SETRANGE("Qty. to Receive");
              IF TransLine.FIND('-') THEN BEGIN
                //H1200 05.12.14 EHN +++++++++++++++++++++++++++
                TransferLineTmpL.RESET;
                TransferLineTmpL.DELETEALL();
                SalesReceivableSetupL.GET;
                WareHouseSetupL.GET;
                //H1200 05.12.14 EHN ---------------------------
                REPEAT
                  LineCount := LineCount + 1;
                  //H0550 16.09.13 ARI ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                  IF NOT HideValidationDialog OR GUIALLOWED THEN
                  //H0550 16.09.13 ARI --------------------------------------------------------
                  Window.UPDATE(2,LineCount);

                  IF TransLine."Item No." <> '' THEN BEGIN
                    Item.GET(TransLine."Item No.");
                    //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                    IF IsInternational THEN BEGIN
                      IF Item.Blocked THEN BEGIN
                        SingleInstanceCU.SetError(TRUE,STRSUBSTNO(HMEText002,Item."No."));
                        EXIT;
                      END;
                    END ELSE
                      Item.TESTFIELD(Blocked,FALSE);
                    //H4370 26.06.15 TST -----------------------------
                  END;

                  TransRcptLine.INIT;
                  TransRcptLine."Document No." := TransRcptHeader."No.";
                  TransRcptLine."Line No." := TransLine."Line No.";
                  TransRcptLine."Item No." := TransLine."Item No.";
                  TransRcptLine.Description := TransLine.Description;
                  TransRcptLine.Quantity := TransLine."Qty. to Receive";
                  TransRcptLine."Unit of Measure" := TransLine."Unit of Measure";
                  TransRcptLine."Shortcut Dimension 1 Code" := TransLine."Shortcut Dimension 1 Code";
                  TransRcptLine."Shortcut Dimension 2 Code" := TransLine."Shortcut Dimension 2 Code";
                  TransRcptLine."Gen. Prod. Posting Group" := TransLine."Gen. Prod. Posting Group";
                  TransRcptLine."Inventory Posting Group" := TransLine."Inventory Posting Group";
                  TransRcptLine."Quantity (Base)" := TransLine."Qty. to Receive (Base)";
                  TransRcptLine."Qty. per Unit of Measure" := TransLine."Qty. per Unit of Measure";
                  TransRcptLine."Unit of Measure Code" := TransLine."Unit of Measure Code";
                  TransRcptLine."Gross Weight" := TransLine."Gross Weight";
                  TransRcptLine."Net Weight" := TransLine."Net Weight";
                  TransRcptLine."Unit Volume" := TransLine."Unit Volume";
                  TransRcptLine."Variant Code" := TransLine."Variant Code";
                  TransRcptLine."Units per Parcel" := TransLine."Units per Parcel";
                  TransRcptLine."Description 2" := TransLine."Description 2";
                  TransRcptLine."Transfer Order No." := TransLine."Document No.";
                  TransRcptLine."Receipt Date" := TransLine."Receipt Date";
                  TransRcptLine."Shipping Agent Code" := TransLine."Shipping Agent Code";
                  TransRcptLine."Shipping Agent Service Code" := TransLine."Shipping Agent Service Code";
                  TransRcptLine."In-Transit Code" := TransLine."In-Transit Code";
                  TransRcptLine."Transfer-from Code" := TransLine."Transfer-from Code";
                  TransRcptLine."Transfer-to Code" := TransLine."Transfer-to Code";
                  TransRcptLine."Transfer-To Bin Code" := TransLine."Transfer-To Bin Code";
                  TransRcptLine."Shipping Time" := TransLine."Shipping Time";
                  TransRcptLine."Item Category Code" := TransLine."Item Category Code";
                  TransRcptLine."Product Group Code" := TransLine."Product Group Code";
                  //S/FOB/p0282
                  TransRcptLine."Buy-from Vendor No" := TransLine."Buy-from Vendor No";
                  TransRcptLine."Purchase Order No." := TransLine."Purchase Order No.";
                  TransRcptLine."Purchase Order Line No." := TransLine."Purchase Order Line No.";
                  TransRcptLine."Container No." := TransLine."Container No.";
                  TransRcptLine."Bill of Lading No." := TransLine."Bill of Lading No.";
                  //E/FOB/p0282
                  //T0100 26.02.15 RST +++++++++++++++++++++++++++
                  TransRcptLine."Repurchase Price" := TransLine."Repurchase Price";
                  //T0100 26.02.15 RST ---------------------------
                  //H3823 10.03.15 RST +++++++++++++++++++++++++++
                  TransRcptLine."Linked Sales Doc. Type" := TransLine."Linked Sales Doc. Type";
                  TransRcptLine."Linked Sales Doc. No."  := TransLine."Linked Sales Doc. No.";
                  TransRcptLine."Linked Sales Line No."  := TransLine."Linked Sales Line No.";
                  TransRcptLine."Return Receipt No."     := TransLine."Return Receipt No.";
                  TransRcptLine."Return Receipt Line No.":= TransLine."Return Receipt Line No.";
                  //H3823 10.03.15 RST -----------------------------
                  TransRcptLine.INSERT;
                  //H1868 12.12.14 DMA ++++++++++++++++++++++++
                  PurchaseLineL.RESET;
                  PurchaseLineL.SETRANGE("Transfer Order No.",TransLine."Document No.");
                  PurchaseLineL.SETRANGE("Transfer Order Line No.",TransLine."Line No.");
                  IF PurchaseLineL.FINDFIRST THEN BEGIN
                    PurchaseLineL."Transfer Order No." := TransRcptHeader."No.";
                    PurchaseLineL.MODIFY;
                  END;
                  //H1868 12.12.14 DMA ------------------------
                  //H1200 05.12.14 EHN +++++++++++++++++++++++++++
                  IF (TransLine."Transfer-from Code" = SalesReceivableSetupL."Default Sales Return Location") AND
                     (TransLine."Transfer-to Code" = WareHouseSetupL."Int. Return Location Code") AND
                     (TransLine."Qty. to Receive" <> 0) AND
                     (TransLine."Linked Sales Doc. No." <> '') AND
                     (TransLine."Linked Sales Line No." <> 0) THEN BEGIN
                    TransferLineTmpL.INIT;
                    TransferLineTmpL := TransLine;
                    TransferLineTmpL.INSERT;
                  END;
                  //H1200 05.12.14 EHN ---------------------------

                  IF TransLine."Qty. to Receive" > 0 THEN BEGIN
                    OriginalQuantity := TransLine."Qty. to Receive";
                    OriginalQuantityBase := TransLine."Qty. to Receive (Base)";
                    PostItemJnlLine(TransLine,TransRcptHeader,TransRcptLine);
                    TransRcptLine."Item Rcpt. Entry No." := InsertRcptEntryRelation(TransRcptLine);
                    TransRcptLine.MODIFY;
                    SaveTempWhseSplitSpec(TransLine);
                    IF WhseReceive THEN BEGIN
                      WhseRcptLine.SETCURRENTKEY(
                        "No.","Source Type","Source Subtype","Source No.","Source Line No.");
                      WhseRcptLine.SETRANGE("No.",WhseRcptHeader."No.");
                      WhseRcptLine.SETRANGE("Source Type",DATABASE::"Transfer Line");
                      WhseRcptLine.SETRANGE("Source No.",TransLine."Document No.");
                      WhseRcptLine.SETRANGE("Source Line No.",TransLine."Line No.");
                      WhseRcptLine.FIND('-');
                      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                      IF IsInternational THEN BEGIN
                        IF WhseRcptLine."Qty. to Receive"<> TransRcptLine.Quantity THEN BEGIN
                          SingleInstanceCU.SetError(TRUE,STRSUBSTNO(HMEText003, WhseRcptLine.TABLECAPTION,
                           WhseRcptLine.FIELDCAPTION("Qty. to Receive"),TransRcptLine.TABLECAPTION,TransRcptLine.FIELDCAPTION(Quantity)));
                          EXIT;
                        END;
                      END ELSE
                      //H4370 26.06.15 TST -----------------------------
                        WhseRcptLine.TESTFIELD("Qty. to Receive",TransRcptLine.Quantity);
                      WhsePostRcpt.SetItemEntryRelation(PostedWhseRcptHeader,PostedWhseRcptLine,TempItemEntryRelation2);
                      WhsePostRcpt.CreatePostedRcptLine(
                        WhseRcptLine,PostedWhseRcptHeader,PostedWhseRcptLine,TempWhseSplitSpecification);
                    END;
                    IF WhsePosting THEN
                      PostWhseJnlLine(ItemJnlLine,OriginalQuantity,OriginalQuantityBase,TempWhseSplitSpecification);
                  END;

                  DimMgt.MoveOneDocDimToPostedDocDim(
                    DocDim,DATABASE::"Transfer Line",DocDim."Document Type"::" ","No.",TransLine."Line No.",
                    DATABASE::"Transfer Receipt Line",TransRcptLine."Document No.");
                UNTIL TransLine.NEXT = 0;
              END;

              //S/P1282
              ParcelStatusHistoryMgmt.MoveParcelInfosToArchive(
                1,                            // p_Command::All,SalesDoc,PurchDoc,Date,EntryNo
                TransHeader."No.",            // p_DocNo
                0,                            // p_LineNo
                6,                            // p_DocType
                '',                           // p_DateFilterString
                0);                           // p_EntryNo
              // Code gets deactivated when old tbl. "DHL Parcel History" isn't used anymore
              ParcelStatusHistoryMgmt.MarkOldDHLParcelEntriesArchive(
                1,                            // p_Command: 0 = "Is Active Order", 1 = "Is Archived Order"
                TransHeader."No.",            // p_SalesOrderNo,
                6);                           // p_SalesDocType
              //E/P1282

              InvtSetup.GET;
              IF InvtSetup."Automatic Cost Adjustment" <>
                 InvtSetup."Automatic Cost Adjustment"::Never
              THEN BEGIN
                InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
                InvtAdjmt.MakeMultiLevelAdjmt;
              END;

              ValueEntry.LOCKTABLE;
              ItemLedgEntry.LOCKTABLE;
              ItemApplnEntry.LOCKTABLE;
              ItemReg.LOCKTABLE;
              TransLine.LOCKTABLE;
              IF WhsePosting THEN
                WhseEntry.LOCKTABLE;

              TransLine.SETFILTER(Quantity,'<>0');
              TransLine.SETFILTER("Qty. to Receive",'<>0');
              IF TransLine.FIND('-') THEN BEGIN
                REPEAT
                  TransLine.VALIDATE("Quantity Received",TransLine."Quantity Received" + TransLine."Qty. to Receive");
                  TransLine.UpdateWithWarehouseShipReceive;
                  ReservMgt.SetItemJnlLine(ItemJnlLine);
                  ReservMgt.SetItemTrackingHandling(1); // Allow deletion
                  ReservMgt.DeleteReservEntries(TRUE,0);
                  //S/FOB/P0282
                  IF TransLine."Container No." <> '' THEN
                    TransLine."Container No." := '';
                  IF TransLine."Bill of Lading No." <> '' THEN
                    TransLine."Bill of Lading No." := '';
                  //S/FOB/p0282
                  TransLine.MODIFY;
                UNTIL TransLine.NEXT = 0;
              END;

              IF WhseReceive THEN
                WhseRcptLine.LOCKTABLE;
              LOCKTABLE;
              IF WhseReceive THEN BEGIN
                WhsePostRcpt.PostUpdateWhseDocuments(WhseRcptHeader);
                TempWhseRcptHeader.DELETE;
              END;

              "Last Receipt No." := TransRcptHeader."No.";
              MODIFY;

              TransLine.SETRANGE(Quantity);
              TransLine.SETRANGE("Qty. to Receive");
              HeaderDeleted := DeleteOneTransferOrder(TransHeader,TransLine);
              IF NOT HeaderDeleted THEN BEGIN
                WhseTransferRelease.Release(TransHeader);
                ReserveTransLine.UpdateItemTrackingAfterPosting(TransHeader,1);
              END;
              //H1200 05.12.14 EHN +++++++++++++++++++++++++++
              IF TransferLineTmpL.FIND('-') THEN BEGIN
                CLEAR(TransferHeaderL);
                TransferHeaderL.INIT;
                TransferHeaderL.VALIDATE("No. Series",SalesReceivableSetupL."Sal. Ret. Transfer Order Nos.");
                TransferHeaderL.VALIDATE("No.",NoSeriesMgmtL.GetNextNo(
                  SalesReceivableSetupL."Sal. Ret. Transfer Order Nos.",WORKDATE,TRUE));
                TransferHeaderL.INSERT(TRUE);
                TransferHeaderL.VALIDATE("Transfer-from Code", WareHouseSetupL."Int. Return Location Code");
                TransferHeaderL.VALIDATE("Transfer-to Code", WareHouseSetupL."Return Warehouse Location Code");
                TransferHeaderL.VALIDATE("In-Transit Code", WareHouseSetupL."Ret. Transit Location Code");
                TransferHeaderL.VALIDATE("Posting Date",WORKDATE);
                TransferHeaderL.VALIDATE("Shipment Date",WORKDATE);
                TransferHeaderL."Created from Transfer Hdr. No." := TransferLineTmpL."Document No.";
                TransferHeaderL.MODIFY(TRUE);
                REPEAT
                  TransferLineL.RESET;
                  TransferLineL.SETRANGE("Document No.", TransferHeaderL."No.");
                  IF TransferLineL.FINDLAST THEN
                    LineNoL := TransferLineL."Line No." + 10000
                  ELSE
                    LineNoL := 10000;

                  TransferLineL.INIT;
                  TransferLineL.VALIDATE("Document No.", TransferHeaderL."No.");
                  TransferLineL.VALIDATE("Line No.", LineNoL);
                  TransferLineL.VALIDATE("Item No.", TransferLineTmpL."Item No.");
                  TransferLineL.VALIDATE(Quantity, TransferLineTmpL.Quantity);
                  TransferLineL.VALIDATE("Qty. to Ship", TransferLineTmpL.Quantity);
                  TransferLineL."Linked Sales Doc. Type" := TransferLineTmpL."Linked Sales Doc. Type";
                  TransferLineL."Linked Sales Doc. No." := TransferLineTmpL."Linked Sales Doc. No.";
                  TransferLineL."Linked Sales Line No." := TransferLineTmpL."Linked Sales Line No.";
                  //T0100 26.02.15 RST +++++++++++++++++++++++++++
                  TransferLineL."Repurchase Price" := TransferLineTmpL."Repurchase Price";
                  //T0100 26.02.15 RST ---------------------------
                  TransferLineL.INSERT(TRUE);
                UNTIL TransferLineTmpL.NEXT = 0;
                CLEAR(ReleaseTransferL);
                ReleaseTransferL.RUN(TransferHeaderL);
              END;
              //H1200 05.12.14 EHN ---------------------------
              IF NOT InvtPickPutaway THEN
                COMMIT;
              CLEAR(WhsePostRcpt);
              CLEAR(InvtAdjmt);
              //H0550 16.09.13 ARI ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
              IF NOT HideValidationDialog OR GUIALLOWED THEN
              //H0550 16.09.13 ARI --------------------------------------------------------
              Window.CLOSE;
            END;
            UpdateAnalysisView.UpdateAll(0,TRUE);
            UpdateItemAnalysisView.UpdateAll(0,TRUE);
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Der Umlagerungsauftrag %2 kann nicht gebucht werden, da der %3 und der %4 identisch sind.;ENU=Transfer order %2 cannot be posted because %3 and %4 are the same.';
      Text001@1001 : TextConst 'DEU=Es gibt nichts zu buchen.;ENU=There is nothing to post.';
      Text002@1002 : TextConst 'DEU="Lagerdurchlauf ist notwendig fÅr Umlagerungsauftrag = %1, %2 = %3.";ENU="Warehouse handling is required for Transfer order = %1, %2 = %3."';
      Text003@1003 : TextConst 'DEU=Umlagerungszeilen buchen   #2######;ENU=Posting transfer lines     #2######';
      Text004@1004 : TextConst 'DEU=Umlagerungsauftrag %1;ENU=Transfer Order %1';
      Text005@1005 : TextConst 'DEU=Die Kombination der in dem Umlagerungsauftrag %1 verwendeten Dimensionen ist gesperrt. %2;ENU=The combination of dimensions used in transfer order %1 is blocked. %2';
      Text006@1006 : TextConst 'DEU=Die Kombination der in dem Umlagerungsauftrag %1, Zeilennr. %2, verwendeten Dimensionen ist gesperrt. %3;ENU=The combination of dimensions used in transfer order %1, line no. %2 is blocked. %3';
      Text007@1007 : TextConst 'DEU=Die in Umlagerungsauftrag %1, Zeilennr. %2, verwendeten Dimensionen sind ungÅltig. %3;ENU=The dimensions used in transfer order %1, line no. %2 are invalid. %3';
      Text008@1008 : TextConst 'DEU=Menge akt. Lieferung (Basis) muss 0 sein.;ENU=Base Qty. to Receive must be 0.';
      TransRcptHeader@1009 : Record 5746;
      TransRcptLine@1010 : Record 5747;
      TransHeader@1011 : Record 5740;
      TransLine@1012 : Record 5741;
      DocDim@1013 : Record 357;
      TempDocDim@1014 : TEMPORARY Record 357;
      ItemJnlLine@1015 : Record 83;
      Location@1016 : Record 14;
      NewLocation@1040 : Record 14;
      WhseRqst@1039 : Record 5765;
      WhseRcptHeader@1027 : Record 7316;
      TempWhseRcptHeader@1033 : TEMPORARY Record 7316;
      WhseRcptLine@1022 : Record 7317;
      PostedWhseRcptHeader@1029 : Record 7318;
      PostedWhseRcptLine@1034 : Record 7319;
      TempWhseSplitSpecification@1018 : TEMPORARY Record 336;
      WhseEntry@1030 : Record 7312;
      TempItemEntryRelation2@1041 : TEMPORARY Record 6507;
      NoSeriesLine@1035 : Record 309;
      GLEntry@1042 : Record 17;
      ItemJnlPostLine@1020 : Codeunit 22;
      DimMgt@1017 : Codeunit 408;
      WhseTransferRelease@1019 : Codeunit 5773;
      ReserveTransLine@1021 : Codeunit 99000836;
      WhsePostRcpt@1036 : Codeunit 5760;
      InvtAdjmt@1043 : Codeunit 5895;
      SingleInstanceCU@1000000001 : Codeunit 50324;
      SourceCode@1023 : Code[10];
      HideValidationDialog@1024 : Boolean;
      HeaderDeleted@1025 : Boolean;
      WhsePosting@1028 : Boolean;
      WhseReference@1038 : Integer;
      OriginalQuantity@1031 : Decimal;
      OriginalQuantityBase@1032 : Decimal;
      WhseReceive@1026 : Boolean;
      InvtPickPutaway@1037 : Boolean;
      IsInternational@1000000000 : Boolean;
      HMEText002@1000000005 : TextConst 'ENU=Item %1 must no be blocked.';
      HMEText003@1000000004 : TextConst 'ENU=%1 %2 must be equal to %3 %4.';
      HMEText004@1000000003 : TextConst 'ENU=%1 %2 must not be empty.';
      HMEText005@1000000002 : TextConst 'ENU=Status must be released.';

    LOCAL PROCEDURE PostItemJnlLine@2(VAR TransLine3@1000 : Record 5741;TransRcptHeader2@1001 : Record 5746;TransRcptLine2@1002 : Record 5747);
    VAR
      TempJnlLineDim@1003 : TEMPORARY Record 356;
    BEGIN
      ItemJnlLine.INIT;
      ItemJnlLine."Posting Date" := TransRcptHeader2."Posting Date";
      ItemJnlLine."Document Date" := TransRcptHeader2."Posting Date";
      ItemJnlLine."Document No." := TransRcptHeader2."No.";
      ItemJnlLine."Document Type" := ItemJnlLine."Document Type"::"Transfer Receipt";
      ItemJnlLine."Document Line No." := TransRcptLine2."Line No.";
      ItemJnlLine."Transfer Order No." := TransRcptHeader2."Transfer Order No.";
      ItemJnlLine."External Document No." := TransRcptHeader2."External Document No.";
      ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::Transfer;
      ItemJnlLine."Item No." := TransRcptLine2."Item No.";
      ItemJnlLine.Description := TransRcptLine2.Description;
      ItemJnlLine."Shortcut Dimension 1 Code" := TransRcptLine2."Shortcut Dimension 1 Code";
      ItemJnlLine."New Shortcut Dimension 1 Code" := TransRcptLine2."Shortcut Dimension 1 Code";
      ItemJnlLine."Shortcut Dimension 2 Code" := TransRcptLine2."Shortcut Dimension 2 Code";
      ItemJnlLine."New Shortcut Dimension 2 Code" := TransRcptLine2."Shortcut Dimension 2 Code";
      ItemJnlLine."Location Code" := TransHeader."In-Transit Code";
      ItemJnlLine."New Location Code" := TransRcptHeader2."Transfer-to Code";
      ItemJnlLine.Quantity := TransRcptLine2.Quantity;
      ItemJnlLine."Invoiced Quantity" := TransRcptLine2.Quantity;
      ItemJnlLine."Quantity (Base)" := TransRcptLine2."Quantity (Base)";
      ItemJnlLine."Invoiced Qty. (Base)" := TransRcptLine2."Quantity (Base)";
      ItemJnlLine."Source Code" := SourceCode;
      ItemJnlLine."Gen. Prod. Posting Group" := TransRcptLine2."Gen. Prod. Posting Group";
      ItemJnlLine."Inventory Posting Group" := TransRcptLine2."Inventory Posting Group";
      ItemJnlLine."Unit of Measure Code" := TransRcptLine2."Unit of Measure Code";
      ItemJnlLine."Qty. per Unit of Measure" := TransRcptLine2."Qty. per Unit of Measure";
      ItemJnlLine."Variant Code":= TransRcptLine2."Variant Code";
      ItemJnlLine."New Bin Code" := TransLine."Transfer-To Bin Code";
      ItemJnlLine."Product Group Code" := TransLine."Product Group Code";
      ItemJnlLine."Item Category Code" := TransLine."Item Category Code";

      //H1331 070714 EHN ++++++++++++++++++++
      ItemJnlLine."Record Entry No." := TransLine3."Record Entry No.";
      ItemJnlLine."Record Table Id" := TransLine3."Record Table Id";
      //H1331 070714 EHN --------------------

      IF TransHeader."In-Transit Code" <> '' THEN BEGIN
        IF NewLocation.Code <> TransHeader."In-Transit Code" THEN
          NewLocation.GET(TransHeader."In-Transit Code");
        ItemJnlLine."Country/Region Code" := NewLocation."Country/Region Code";
      END;
      ItemJnlLine."Transaction Type" :=  TransRcptHeader2."Transaction Type";
      ItemJnlLine."Transport Method" := TransRcptHeader2."Transport Method";
      ItemJnlLine."Entry/Exit Point" := TransRcptHeader2."Entry/Exit Point";
      ItemJnlLine.Area := TransRcptHeader2.Area;
      ItemJnlLine."Transaction Specification" := TransRcptHeader2."Transaction Specification";
      WriteDownDerivedLines(TransLine3);
      ItemJnlPostLine.SetPostponeReservationHandling(TRUE);

      TempDocDim.RESET;
      TempDocDim.SETRANGE("Table ID",DATABASE::"Transfer Line");
      TempDocDim.SETRANGE("Line No.",TransLine3."Line No.");
      DimMgt.CopyDocDimToJnlLineDim(TempDocDim,TempJnlLineDim);
      IF TempJnlLineDim.FIND('-') THEN
        REPEAT
          TempJnlLineDim."New Dimension Value Code" := TempJnlLineDim."Dimension Value Code";
          TempJnlLineDim.MODIFY;
        UNTIL TempJnlLineDim.NEXT = 0;

      ItemJnlPostLine.RunWithCheck(ItemJnlLine,TempJnlLineDim);
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20]);
    VAR
      InvtCommentLine@1004 : Record 5748;
      InvtCommentLine2@1005 : Record 5748;
    BEGIN
      InvtCommentLine.SETRANGE("Document Type",FromDocumentType);
      InvtCommentLine.SETRANGE("No.",FromNumber);
      IF InvtCommentLine.FIND('-') THEN
        REPEAT
          InvtCommentLine2 := InvtCommentLine;
          InvtCommentLine2."Document Type" := ToDocumentType;
          InvtCommentLine2."No." := ToNumber;
          InvtCommentLine2.INSERT;
        UNTIL InvtCommentLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyAndCheckDocDimToTempDocDim@34();
    VAR
      CurrLineNo@1000 : Integer;
    BEGIN
      TempDocDim.RESET;
      TempDocDim.DELETEALL;
      DocDim.SETFILTER("Table ID",'%1|%2',DATABASE::"Transfer Header",DATABASE::"Transfer Line");
      DocDim.SETRANGE("Document Type",DocDim."Document Type"::" ");
      DocDim.SETRANGE("Document No.",TransHeader."No.");
      IF DocDim.FIND('-') THEN BEGIN
        CurrLineNo := DocDim."Line No.";
        REPEAT
          IF CurrLineNo <> DocDim."Line No." THEN BEGIN
            TempDocDim.SETRANGE("Line No.",CurrLineNo);
            CheckDimComb(CurrLineNo);
            CheckDimValuePosting(CurrLineNo);
            CurrLineNo := DocDim."Line No.";
          END;
          TempDocDim.INIT;
          TempDocDim := DocDim;
          TempDocDim.INSERT;
        UNTIL DocDim.NEXT = 0;
        TempDocDim.SETRANGE("Line No.",CurrLineNo);
        CheckDimComb(CurrLineNo);
        CheckDimValuePosting(CurrLineNo);
      END;
      TempDocDim.RESET;
    END;

    LOCAL PROCEDURE CheckDimComb@30(LineNo@1000 : Integer);
    BEGIN
      IF NOT DimMgt.CheckDocDimComb(TempDocDim) THEN
        IF LineNo = 0 THEN
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text005, TransHeader."No.",DimMgt.GetDimCombErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text005,
              TransHeader."No.",DimMgt.GetDimCombErr)
        ELSE
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text006, TransHeader."No.",DimMgt.GetDimCombErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text006,
              TransHeader."No.",LineNo,DimMgt.GetDimCombErr);
    END;

    LOCAL PROCEDURE CheckDimValuePosting@28(LineNo@1000 : Integer);
    VAR
      TheTransLine@1001 : Record 5741;
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
    BEGIN
      IF TheTransLine.GET(TransHeader."No.",LineNo) THEN BEGIN
        TableIDArr[1] := DATABASE::Item;
        NumberArr[1] := TheTransLine."Item No.";
        IF NOT DimMgt.CheckDocDimValuePosting(TempDocDim,TableIDArr,NumberArr) THEN
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text007, TransHeader."No.",LineNo,DimMgt.GetDimValuePostingErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text007,
              TransHeader."No.",LineNo,DimMgt.GetDimValuePostingErr);
      END;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    PROCEDURE WriteDownDerivedLines@1(VAR TransLine3@1000 : Record 5741);
    VAR
      TransLine4@1001 : Record 5741;
      T337@1004 : Record 337;
      TempDerivedSpecification@1006 : TEMPORARY Record 336;
      ItemTrackingMgt@1005 : Codeunit 6500;
      QtyToReceive@1002 : Decimal;
      BaseQtyToReceive@1003 : Decimal;
      TrackingSpecificationExists@1007 : Boolean;
    BEGIN
      TransLine4.SETRANGE("Document No.",TransLine3."Document No.");
      TransLine4.SETRANGE("Derived From Line No.",TransLine3."Line No.");
      IF TransLine4.FIND('-') THEN BEGIN
        QtyToReceive := TransLine3."Qty. to Receive";
        BaseQtyToReceive := TransLine3."Qty. to Receive (Base)";

        T337.SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line");
        T337.SETRANGE("Source ID",TransLine3."Document No.");
        T337.SETRANGE("Source Ref. No.");
        T337.SETRANGE("Source Type",DATABASE::"Transfer Line");
        T337.SETRANGE("Source Subtype",1);
        T337.SETRANGE("Source Batch Name",'');
        T337.SETRANGE("Source Prod. Order Line",TransLine3."Line No.");
        T337.SETFILTER("Qty. to Handle (Base)",'<>0');

        TrackingSpecificationExists :=
          ItemTrackingMgt.SumUpItemTracking(T337,TempDerivedSpecification,TRUE,FALSE);

        REPEAT
          IF TrackingSpecificationExists THEN BEGIN
            TempDerivedSpecification.SETRANGE("Source Ref. No.",TransLine4."Line No.");
            IF TempDerivedSpecification.FIND('-') THEN BEGIN
              TransLine4."Qty. to Receive (Base)" := TempDerivedSpecification."Qty. to Handle (Base)";
              TransLine4."Qty. to Receive" := TempDerivedSpecification."Qty. to Handle";
            END ELSE BEGIN
              TransLine4."Qty. to Receive (Base)" := 0;
              TransLine4."Qty. to Receive" := 0;
            END;
          END;
          IF TransLine4."Qty. to Receive (Base)" <= BaseQtyToReceive THEN BEGIN
            ReserveTransLine.TransferTransferToItemJnlLine(
              TransLine4,ItemJnlLine,TransLine4."Qty. to Receive (Base)",1);
            TransLine4."Quantity (Base)" :=
              TransLine4."Quantity (Base)" - TransLine4."Qty. to Receive (Base)";
            TransLine4.Quantity :=
              TransLine4.Quantity - TransLine4."Qty. to Receive";
            BaseQtyToReceive := BaseQtyToReceive - TransLine4."Qty. to Receive (Base)";
            QtyToReceive := QtyToReceive - TransLine4."Qty. to Receive";
          END ELSE BEGIN
            ReserveTransLine.TransferTransferToItemJnlLine(
              TransLine4,ItemJnlLine,BaseQtyToReceive,1);
            TransLine4.Quantity := TransLine4.Quantity - QtyToReceive;
            TransLine4."Quantity (Base)" := TransLine4."Quantity (Base)" - BaseQtyToReceive;
            BaseQtyToReceive := 0;
            QtyToReceive := 0;
          END;
          IF TransLine4."Quantity (Base)" = 0 THEN
            TransLine4.DELETE
          ELSE BEGIN
            TransLine4."Qty. to Ship" := TransLine4.Quantity;
            TransLine4."Qty. to Ship (Base)" := TransLine4."Quantity (Base)";
            TransLine4."Qty. to Receive" := TransLine4.Quantity;
            TransLine4."Qty. to Receive (Base)" := TransLine4."Quantity (Base)";
            TransLine4."Quantity Shipped" := 0;
            TransLine4."Qty. Shipped (Base)" := 0;
            TransLine4."Quantity Received" := 0;
            TransLine4."Qty. Received (Base)" := 0;
            TransLine4."Qty. in Transit" := 0;
            TransLine4."Qty. in Transit (Base)" := 0;
            TransLine4."Outstanding Quantity" := TransLine4.Quantity;
            TransLine4."Outstanding Qty. (Base)" := TransLine4."Quantity (Base)";
            TransLine4.MODIFY;
          END;
        UNTIL (TransLine4.NEXT = 0) OR (BaseQtyToReceive = 0);
      END;

      IF BaseQtyToReceive <> 0 THEN
        ERROR(Text008);
    END;

    LOCAL PROCEDURE InsertRcptEntryRelation@38(VAR TransRcptLine@1002 : Record 5747) : Integer;
    VAR
      ItemEntryRelation@1001 : Record 6507;
      TempItemEntryRelation@1000 : TEMPORARY Record 6507;
    BEGIN
      TempItemEntryRelation2.RESET;
      TempItemEntryRelation2.DELETEALL;

      IF ItemJnlPostLine.CollectItemEntryRelation(TempItemEntryRelation) THEN BEGIN
        IF TempItemEntryRelation.FIND('-') THEN BEGIN
          REPEAT
            ItemEntryRelation := TempItemEntryRelation;
            ItemEntryRelation.TransferFieldsTransRcptLine(TransRcptLine);
            ItemEntryRelation.INSERT;
            TempItemEntryRelation2 := TempItemEntryRelation;
            TempItemEntryRelation2.INSERT;
          UNTIL TempItemEntryRelation.NEXT = 0;
          EXIT(0);
        END;
      END ELSE
        EXIT(ItemJnlLine."Item Shpt. Entry No.");
    END;

    LOCAL PROCEDURE CheckWarehouse@7301(VAR TransLine@1000 : Record 5741);
    VAR
      TransLine2@1001 : Record 5741;
      WhseValidateSourceLine@1003 : Codeunit 5777;
      ShowError@1002 : Boolean;
    BEGIN
      TransLine2.COPY(TransLine);
      IF TransLine2.FIND('-') THEN
        REPEAT
          GetLocation(TransLine2."Transfer-to Code");
          IF Location."Require Receive" OR Location."Require Put-away" THEN BEGIN
            IF Location."Bin Mandatory" THEN
              ShowError := TRUE
            ELSE
              IF WhseValidateSourceLine.WhseLinesExist(
                   DATABASE::"Transfer Line",
                   1, // In
                   TransLine2."Document No.",
                   TransLine2."Line No.",
                   0,
                   TransLine2.Quantity)
              THEN
                ShowError := TRUE;

            IF ShowError THEN
              ERROR(
                Text002,
                TransLine2."Document No.",
                TransLine2.FIELDCAPTION("Line No."),
                TransLine2."Line No.");
          END;
        UNTIL TransLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveTempWhseSplitSpec@5(TransLine@1001 : Record 5741);
    VAR
      TempHandlingSpecification@1000 : TEMPORARY Record 336;
    BEGIN
      TempWhseSplitSpecification.RESET;
      TempWhseSplitSpecification.DELETEALL;
      IF ItemJnlPostLine.CollectTrackingSpecification(TempHandlingSpecification) THEN
        IF TempHandlingSpecification.FIND('-') THEN
          REPEAT
            TempWhseSplitSpecification := TempHandlingSpecification;
            TempWhseSplitSpecification."Entry No." := TempHandlingSpecification."Transfer Item Entry No.";
            TempWhseSplitSpecification."Source Type" := DATABASE::"Transfer Line";
            TempWhseSplitSpecification."Source Subtype" := 1;
            TempWhseSplitSpecification."Source ID" := TransLine."Document No.";
            TempWhseSplitSpecification."Source Ref. No." := TransLine."Line No.";
            TempWhseSplitSpecification.INSERT;
          UNTIL TempHandlingSpecification.NEXT = 0;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        Location.GetLocationSetup(LocationCode,Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE PostWhseJnlLine@4(ItemJnlLine@1000 : Record 83;OriginalQuantity@1001 : Decimal;OriginalQuantityBase@1006 : Decimal;VAR TempHandlingSpecification@1003 : TEMPORARY Record 336);
    VAR
      WhseJnlLine@1002 : Record 7311;
      TempWhseJnlLine2@1005 : TEMPORARY Record 7311;
      ItemTrackingMgt@1004 : Codeunit 6500;
      WMSMgmt@1007 : Codeunit 7302;
      WhseJnlPostLine@1008 : Codeunit 7301;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        Quantity := OriginalQuantity;
        "Quantity (Base)" := OriginalQuantityBase;
        GetLocation("New Location Code");
        IF Location."Bin Mandatory" THEN
          IF WMSMgmt.CreateWhseJnlLine(ItemJnlLine,1,WhseJnlLine,TRUE,FALSE) THEN BEGIN
            WMSMgmt.SetTransferLine(TransLine,WhseJnlLine,1,TransRcptHeader."No.");
            ItemTrackingMgt.SplitWhseJnlLine(WhseJnlLine,TempWhseJnlLine2,TempHandlingSpecification,TRUE);
            IF TempWhseJnlLine2.FIND('-') THEN
              REPEAT
                WMSMgmt.CheckWhseJnlLine(TempWhseJnlLine2,1,0,TRUE);
                WhseJnlPostLine.RUN(TempWhseJnlLine2);
              UNTIL TempWhseJnlLine2.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE SetWhseRcptHeader@3(VAR WhseRcptHeader2@1000 : Record 7316);
    BEGIN
      WhseRcptHeader := WhseRcptHeader2;
      TempWhseRcptHeader := WhseRcptHeader;
      TempWhseRcptHeader.INSERT;
    END;

    PROCEDURE CheckSingleInstance@1000000000();
    VAR
      ObjectTypeL@1000000000 : ' ,Table,Form,Report,Dataport,XMLPort,Codeunit,MenuSuite,Page';
      ObjectIDL@1000000001 : Integer;
    BEGIN
      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
      ObjectTypeL := 0;
      ObjectIDL := 0;
      SingleInstanceCU.GetObject(ObjectTypeL,ObjectIDL);

      IsInternational := (ObjectTypeL = 3) AND (ObjectIDL = 50196);
      //H4370 26.06.15 TST -----------------------------
    END;

    PROCEDURE CheckTransferHeader@1000000002() : Boolean;
    VAR
      InvtSetupL@1000000000 : Record 313;
    BEGIN
      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
      IF IsInternational THEN BEGIN
        IF TransHeader."Transfer-from Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("Transfer-from Code")));
          EXIT(FALSE);
        END;
        IF TransHeader."Transfer-to Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("Transfer-to Code")));
          EXIT(FALSE);
        END;
        IF TransHeader."In-Transit Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("In-Transit Code")));
          EXIT(FALSE);
        END;
        IF TransHeader.Status <> TransHeader.Status::Released THEN BEGIN
          SingleInstanceCU.SetError(TRUE,HMEText005);
          EXIT(FALSE);
        END;
        InvtSetupL.GET;
        IF InvtSetupL."Posted Transfer Rcpt. Nos." = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, InvtSetupL.TABLECAPTION,
              InvtSetupL.FIELDCAPTION("Posted Transfer Rcpt. Nos.")));
          EXIT(FALSE);
        END;
      END;

      EXIT(TRUE);
      //H4370 26.06.15 TST -----------------------------
    END;

    BEGIN
    {
      +-----------------------------------------------------+
      |               GOB Software & Systeme                |
      +-----------------------------------------------------+
      |                                                     |
      -------------------------------------------------------

      Version       Comment
      -------------------------------------------------------
      p0282   gob-mab    FOB           OnRun
      P1282  gob-ael  20141021  Archive "Parcel Status History" after posting



      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      H0550       16.09.13 ARI       Don't show Dialog when executing on Application Server
      H1331       07.07.14 EHN       Fill values into fields : "Record Entry No.", "Record Table Id"
      H1200       05.12.14 EHN       Ship Sales Returns via Return Transit location
      H1868       12.12.14 DMA       BUG-->Modification of Transfer Header,CODECHANGE
      H4370       23.06.15 TST       Stop in Case of Errors if started by Report 50196, new functions

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |     Cooperation Tectura & Home24 NAV Team  |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________

      T0100       26.02.15 CC-RST     Fill Repurchase Price
      H3823       10.03.15 CC-RST     Fill Linked Fields in Line
    }
    END.
  }
}

