OBJECT Codeunit 90102 Import Deadlock Graph
{
  OBJECT-PROPERTIES
  {
    Date=16.12.11;
    Time=12:00:00;
    Version List=SSI/PTB#DLG1.05;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      XMLDOM_Doc@1000000004 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      XMLDOM_Element@1000000005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      XMLDOM_Node@1000000006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      XMLDOM_Node2@1000000015 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      XMLDOM_Node3@1000000017 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      XMLDOM_Node4@1000000023 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      XMLDOM_NodeMap@1000000011 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF83-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNamedNodeMap";
      XMLDOM_NodeMap2@1000000024 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF83-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNamedNodeMap";
      XMLDOM_Attribute@1000000012 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF85-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMAttribute";
      DLGraph@1000000001 : Record 90101;
      DLGraphProc@1000000002 : Record 90102;
      DLGraphExecStack@1000000022 : Record 90105;
      DLGraphRes@1000000003 : Record 90103;
      DLGraphNo@1000000000 : Integer;
      Victim@1000000013 : Text[30];
      InputBuffer@1000000020 : Text[1024];
      Frame@1000000021 : Text[1024];
      FrameTmp@1000000027 : BigText;
      InStrm@1000000028 : InStream;
      OutStrm@1000000029 : OutStream;
      n@1000000016 : Integer;
      m@1000000018 : Integer;
      o@1000000025 : Integer;
      i@1000000014 : Integer;
      j@1000000019 : Integer;
      k@1000000026 : Integer;
      TimeStamp@1000000009 : Text[30];
      WithRename@1000000008 : Boolean;
      WithSave@1000000010 : Boolean;
      Window@1000000007 : Dialog;

    PROCEDURE ImportSingleFile@1000000000(SourceFile@1000000000 : Text[1024]);
    BEGIN
      IF DLGraphNo = 0 THEN
        GetStartNo();

      IF ISCLEAR(XMLDOM_Doc) THEN
        CREATE(XMLDOM_Doc);
      XMLDOM_Doc.load(SourceFile);
      XMLDOM_Element := XMLDOM_Doc.documentElement;
      XMLDOM_Node := XMLDOM_Element.firstChild;
      IF ISCLEAR(XMLDOM_Node) THEN BEGIN
        CLEAR(XMLDOM_Doc);
        CLEAR(XMLDOM_Element);
        CLEAR(XMLDOM_Node);
        IF WithRename THEN
          RENAME(SourceFile, SourceFile + '_' + TimeStamp);
        EXIT;
      END;

      // Import Deadlocks
      XMLDOM_Node := XMLDOM_Node.selectSingleNode('//deadlock-list');
      m := XMLDOM_Node.childNodes.length - 1;
      FOR i := 0 TO m DO BEGIN

        DLGraphNo := DLGraphNo + 1;
        DLGraph.INIT;
        DLGraph."Entry No." := DLGraphNo;

        XMLDOM_Node2 := XMLDOM_Node.childNodes.item(i);
        XMLDOM_NodeMap := XMLDOM_Node2.attributes;
        XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('victim');
        Victim := XMLDOM_Attribute.text;

        // Import Processes
        XMLDOM_Node2 := XMLDOM_Node2.selectSingleNode('process-list');
        n := XMLDOM_Node2.childNodes.length - 1;
        FOR j := 0 TO n DO BEGIN

          XMLDOM_Node3 := XMLDOM_Node2.childNodes.item(j);
          XMLDOM_NodeMap := XMLDOM_Node3.attributes;

          DLGraphProc.INIT;
          DLGraphProc."Deadlock Graph Entry No." := DLGraph."Entry No.";

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('id');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Process ID" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('spid');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraphProc.SPID, XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('loginname');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Login Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('hostname');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Host Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('clientapp');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Application Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('currentdb');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraph."Database ID", XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('waitresource');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc.Waitresource := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('waittime');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraphProc.Waittime, XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('transactionname');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Transaction Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('lasttranstarted');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Last Transaction started" := EvaluateDateTime(XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('lockMode');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Lock Mode" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('lastbatchstarted');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Last Batch started" := EvaluateDateTime(XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('lastbatchcompleted');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Last Batch completed" := EvaluateDateTime(XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('isolationlevel');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphProc."Isolation Level" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('lockTimeout');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraphProc."Lock Timeout", XMLDOM_Attribute.text);

          DLGraphProc.Victim := (DLGraphProc."Process ID" = Victim);

          IF  DLGraphProc.Victim THEN
            DLGraph."Deadlock Timestamp" := DLGraphProc."Last Transaction started";

          InputBuffer := '';
          XMLDOM_Node3 := XMLDOM_Node3.selectSingleNode('inputbuf');
          IF NOT ISCLEAR(XMLDOM_Node3) THEN BEGIN
            EVALUATE(InputBuffer, COPYSTR(XMLDOM_Node3.text, 1, 1024));
            DLGraphProc."Input Buffer 1" := COPYSTR(InputBuffer, 1, 250);
            IF STRLEN(InputBuffer) > 250 THEN
              DLGraphProc."Input Buffer 2" := COPYSTR(InputBuffer, 251, 250);
            IF STRLEN(InputBuffer) > 500 THEN
              DLGraphProc."Input Buffer 3" := COPYSTR(InputBuffer, 501, 250);
            IF STRLEN(InputBuffer) > 750 THEN
              DLGraphProc."Input Buffer 4" := COPYSTR(InputBuffer, 751, 250);
          END;

          IF NOT DLGraphProc.INSERT THEN;

          XMLDOM_Node3 := XMLDOM_Node3.parentNode.selectSingleNode('executionStack');
          o := XMLDOM_Node3.childNodes.length - 1;
          FOR k := 0 TO o DO BEGIN
            XMLDOM_Node4 := XMLDOM_Node3.childNodes.item(k);
            XMLDOM_NodeMap2 := XMLDOM_Node4.attributes;

            DLGraphExecStack.INIT;
            DLGraphExecStack."Deadlock Graph Entry No." := DLGraph."Entry No.";
            DLGraphExecStack."Process ID" := DLGraphProc."Process ID";
            DLGraphExecStack."Frame No." := k;

            XMLDOM_Attribute := XMLDOM_NodeMap2.getNamedItem('procname');
            IF NOT ISCLEAR(XMLDOM_Attribute) THEN
              EVALUATE(DLGraphExecStack."Procedure Name", XMLDOM_Attribute.text);

            XMLDOM_Attribute := XMLDOM_NodeMap2.getNamedItem('line');
            IF NOT ISCLEAR(XMLDOM_Attribute) THEN
              EVALUATE(DLGraphExecStack."Line No.", XMLDOM_Attribute.text);

            XMLDOM_Attribute := XMLDOM_NodeMap2.getNamedItem('sqlhandle');
            IF NOT ISCLEAR(XMLDOM_Attribute) THEN
              EVALUATE(DLGraphExecStack."SQL Handle", XMLDOM_Attribute.text);

            Frame := '';
            DLGraphExecStack."Frame BLOB".CREATEOUTSTREAM(OutStrm);
            OutStrm.WRITE(XMLDOM_Node4.text);
            DLGraphExecStack."Frame BLOB".CREATEINSTREAM(InStrm);
            FrameTmp.READ(InStrm);
            FrameTmp.GETSUBTEXT(Frame, 1, 1024);

            DLGraphExecStack."Frame 1" := COPYSTR(Frame, 1, 250);
            IF STRLEN(Frame) > 250 THEN
              DLGraphExecStack."Frame 2" := COPYSTR(Frame, 251, 250);
            IF STRLEN(Frame) > 500 THEN
              DLGraphExecStack."Frame 3" := COPYSTR(Frame, 501, 250);
            IF STRLEN(Frame) > 750 THEN
              DLGraphExecStack."Frame 4" := COPYSTR(Frame, 751, 250);

            IF NOT DLGraphExecStack.INSERT THEN;
          END;
        END;

        // Import Resources
        XMLDOM_Node2 := XMLDOM_Node2.parentNode();
        XMLDOM_Node2 := XMLDOM_Node2.selectSingleNode('resource-list');
        n := XMLDOM_Node2.childNodes.length - 1;
        FOR j := 0 TO n DO BEGIN

          XMLDOM_Node3 := XMLDOM_Node2.childNodes.item(j);
          XMLDOM_NodeMap := XMLDOM_Node3.attributes;

          DLGraphRes.INIT;
          DLGraphRes."Deadlock Graph Entry No." := DLGraph."Entry No.";

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('id');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraphRes."Resource ID", XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('dbid');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            EVALUATE(DLGraphRes."Database ID", XMLDOM_Attribute.text);

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('objectname');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphRes."Object Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('indexname');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphRes."Index Name" := XMLDOM_Attribute.text;

          XMLDOM_Attribute := XMLDOM_NodeMap.getNamedItem('mode');
          IF NOT ISCLEAR(XMLDOM_Attribute) THEN
            DLGraphRes.Mode := XMLDOM_Attribute.text;


          XMLDOM_Node4 := XMLDOM_Node3.selectSingleNode('owner-list');
          XMLDOM_Node4 := XMLDOM_Node4.childNodes.item(0);
          IF NOT ISCLEAR(XMLDOM_Node4) THEN BEGIN
            XMLDOM_NodeMap2 := XMLDOM_Node4.attributes;
            XMLDOM_Attribute := XMLDOM_NodeMap2.getNamedItem('id');
            IF NOT ISCLEAR(XMLDOM_Attribute) THEN
              DLGraphRes."Process ID" := XMLDOM_Attribute.text;
          END;

          IF NOT DLGraphRes.INSERT THEN;

        END;

        IF WithSave THEN
          DLGraph."Deadlock Graph".IMPORT(SourceFile, FALSE);
        DLGraph."Import DateTime" := CURRENTDATETIME;
        DLGraph."Import Filename" := COPYSTR(SourceFile, 1, 250);
        DLGraph."Import User-ID" := USERID;
        IF NOT DLGraph.INSERT THEN;
      END;

      CLEAR(XMLDOM_Doc);
      CLEAR(XMLDOM_Element);
      CLEAR(XMLDOM_Node);
      CLEAR(XMLDOM_Node2);
      CLEAR(XMLDOM_Node3);
      CLEAR(XMLDOM_Node4);
      CLEAR(XMLDOM_NodeMap);
      CLEAR(XMLDOM_NodeMap2);
      CLEAR(XMLDOM_Attribute);

      IF WithRename THEN
        RENAME(SourceFile, SourceFile + '_' + TimeStamp);

      COMMIT;
      YIELD;
    END;

    PROCEDURE ImportDirectory@1000000001(SourceDirectory@1000000000 : Text[1024]);
    VAR
      FileSys@1000000001 : Record 2000000022;
    BEGIN
      FileSys.RESET;
      FileSys.SETRANGE(Path, SourceDirectory);
      FileSys.SETRANGE("Is a file", TRUE);
      FileSys.SETFILTER(Name, '*.xdl');
      IF FileSys.FINDSET(FALSE, FALSE) THEN BEGIN
        Window.OPEN('Importing Deadlock Graph ... \' +
                    '#1############################');
        GetStartNo();
        REPEAT
          Window.UPDATE(1, FileSys.Name);
          ImportSingleFile(FileSys.Path + FileSys.Name);
        UNTIL FileSys.NEXT = 0;
        Window.CLOSE;
      END;
    END;

    PROCEDURE SetRename@1000000003(InWithRename@1000000000 : Boolean);
    BEGIN
      WithRename := InWithRename;
    END;

    PROCEDURE SetSave@1000000004(InWithSave@1000000000 : Boolean);
    BEGIN
      WithSave := InWithSave;
    END;

    LOCAL PROCEDURE GetStartNo@1000000002();
    BEGIN
      IF NOT DLGraph.FINDLAST THEN
        DLGraphNo := 0
      ELSE
        DLGraphNo := DLGraph."Entry No.";

      TimeStamp := FORMAT(CURRENTDATETIME, 0, '<Year4><Month,2><Day,2>_<Hours,2><Minutes,2><Seconds,2>');
    END;

    LOCAL PROCEDURE EvaluateDateTime@1000000005(DateTime_TXT@1000000000 : Text[30]) DateTime_DT : DateTime;
    VAR
      Y@1000000001 : Integer;
      M@1000000002 : Integer;
      D@1000000003 : Integer;
      TI@1000000004 : Time;
    BEGIN
      // 2007-05-30T10:06:31.903

      DateTime_TXT := CONVERTSTR(DateTime_TXT, 'T', ' ');

      IF NOT EVALUATE(Y, COPYSTR(DateTime_TXT, 1, 4)) THEN
        Y := 1753;
      IF NOT EVALUATE(M, COPYSTR(DateTime_TXT, 6, 2)) THEN
        M := 1;
      IF NOT EVALUATE(D, COPYSTR(DateTime_TXT, 9, 2)) THEN
        D := 1;
      IF NOT EVALUATE(TI, COPYSTR(DateTime_TXT, 12, 12)) THEN
        TI := 0T;

      DateTime_DT := CREATEDATETIME(DMY2DATE(D, M, Y), TI);
      EXIT(DateTime_DT)
    END;

    PROCEDURE QuickImport@1000000006();
    VAR
      PTBSetup@1000000000 : Record 90100;
      FileSys@1000000002 : Record 2000000022;
      ShellCmd@1000000001 : Text[1024];
      ExecSQLCMD@1000000003 : Codeunit 90101;
      ReturnCode@1000000004 : Integer;
      SourceFile@1000000005 : Text[1024];
      ResultFile@1000000006 : Text[1024];
    BEGIN
      PTBSetup.GET;
      PTBSetup.TESTFIELD("Deadlock Trace Path");
      PTBSetup.TESTFIELD("Deadlock Graph Path");
      PTBSetup.TESTFIELD("XDL Exctractor");
      IF NOT EXISTS(PTBSetup."XDL Exctractor") THEN
        ERROR('"%1" not found.', PTBSetup."XDL Exctractor");

      FileSys.RESET;
      FileSys.SETRANGE(Path, PTBSetup."Deadlock Trace Path");
      FileSys.SETRANGE("Is a file", TRUE);
      FileSys.SETFILTER(Name, 'ssi_Deadlock_Trace_*.trc');
      IF FileSys.FINDFIRST THEN BEGIN
        Window.OPEN('Extracting Deadlock Graph ...');

        TimeStamp :=  FORMAT(CURRENTDATETIME, 0, '<Year4><Month,2><Day,2>_<Hours,2><Minutes,2><Seconds,2>');
        SourceFile := FileSys.Path + FileSys.Name;
        ResultFile := PTBSetup."Deadlock Graph Path" + 'DLG_' + TimeStamp + '.xdl';

        ShellCmd := STRSUBSTNO('"%1" "%2" "%3"', PTBSetup."XDL Exctractor", SourceFile, ResultFile);

        ReturnCode := ExecSQLCMD.RunCommand(ShellCmd, 0, TRUE);
        YIELD;
        IF ReturnCode <> 0 THEN
          ERROR('XDL Extraction failed.');

        RENAME(SourceFile, SourceFile + '_ok');
        YIELD;
        Window.CLOSE;

        Window.OPEN('Importing Deadlock Graph ...');
        WithRename := TRUE;
        ImportSingleFile(ResultFile);
        Window.CLOSE;
      END ELSE
        MESSAGE('No valid TRC File found.');
    END;

    EVENT XMLDOM_Doc@1000000004::ondataavailable@198();
    BEGIN
    END;

    EVENT XMLDOM_Doc@1000000004::onreadystatechange@-609();
    BEGIN
    END;

    BEGIN
    {
      *********************************************************
      ***              STRYK System Improvement             ***
      ***    Performance Optimization & Troubleshooting     ***
      ***  (c) 2007, STRYK System Improvement, J”rg Stryk   ***
      ***               Contact: www.stryk.info             ***
      *********************************************************
    }
    END.
  }
}

