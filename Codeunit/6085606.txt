OBJECT Codeunit 6085606 Version Upgrade
{
  OBJECT-PROPERTIES
  {
    Date=13.09.13;
    Time=01:00:00;
    Version List=DCW13.70.00.3.50;
  }
  PROPERTIES
  {
    OnRun=VAR
            ClientAddIn@1160040000 : Codeunit 6085660;
          BEGIN
            Window.OPEN(Text000);

            UpgradeCategory;
            UpgradeTemplate;
            UpgradeTemplateField;
            UpgradeDocument;
            UpgradeValue;
            CreateDragAndDropCat;

            ClientAddIn.CreateClientAddInEntries;

            Window.CLOSE;

            MESSAGE(Text002);
          END;

  }
  CODE
  {
    VAR
      Text000@1160040007 : TextConst 'DEU=Upgrade von Document Capture\\#1#########################;ENU=Upgrading Document Capture\\#1#########################';
      Text001@1160040000 : TextConst 'DEU=Update von %1;ENU=Updating %1';
      Text002@1160040006 : TextConst 'DEU=Upgrade Document Capture abgeschlossen.;ENU=Upgrading Document Capture complete.';
      Window@1160040010 : Dialog;
      Text003@1160040004 : TextConst 'DEU=%1 %2 werden vom Update nicht untersttzt.;ENU=%1 %2 not supported by the upgrade.';

    PROCEDURE UpgradeCategory@1160040001();
    VAR
      Category@1160040000 : Record 6085575;
      Template@161024012 : Record 6085579;
    BEGIN
      Window.UPDATE(1,STRSUBSTNO(Text001,Category.TABLECAPTION));

      IF Category.FINDSET THEN
        REPEAT
          IF Category."Source Type" > Category."Source Type"::Employee THEN
            ERROR(Text003,Category.FIELDCAPTION("Source Type"),FORMAT(Category."Source Type"));

          IF Category."Source Type" > Category."Source Type"::" " THEN
            CASE Category."Source Type" OF
              Category."Source Type"::Vendor:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Vendor);
                  Category.VALIDATE("Source Field No. (Name)",2);
                  Category.VALIDATE("Source Search Field No.",3);
                  Category.VALIDATE("Destination Header Table No.",DATABASE::"Purchase Header");
                  Category.VALIDATE("Destination Line Table No.",DATABASE::"Purchase Line");
                END;

              Category."Source Type"::Contact:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Contact);
                  Category.VALIDATE("Source Field No. (Name)",2);
                  Category.VALIDATE("Source Search Field No.",3);
                END;

              Category."Source Type"::Customer:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Customer);
                  Category.VALIDATE("Source Field No. (Name)",2);
                  Category.VALIDATE("Source Search Field No.",3);
                END;

              Category."Source Type"::Job:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Job);
                  Category.VALIDATE("Source Field No. (Name)",3);
                  Category.VALIDATE("Source Search Field No.",2);
                END;

              Category."Source Type"::Item:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Item);
                  Category.VALIDATE("Source Field No. (Name)",3);
                  Category.VALIDATE("Source Search Field No.",4);
                END;

              Category."Source Type"::"Fixed Asset":
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::"Fixed Asset");
                  Category.VALIDATE("Source Field No. (Name)",2);
                  Category.VALIDATE("Source Search Field No.",3);
                END;

              Category."Source Type"::Employee:
                BEGIN
                  Category.VALIDATE("Source Table No.",DATABASE::Employee);
                  Category.VALIDATE("Source Field No. (Name)",5);
                  Category.VALIDATE("Source Search Field No.",7);
                END;
            END;

          Template.SETRANGE("Category Code",Category.Code);
          Template.SETRANGE("Codeunit ID: Register",CODEUNIT::"Purch. Doc. - Register");
          IF NOT Template.ISEMPTY THEN BEGIN
            Category."Codeunit ID: Reopen" := CODEUNIT::"Purch. Doc. - Reopen";
            CreateIdentFields(Category);
          END;
          Category."Source Type" := Category."Source Type"::" ";
          Category.MODIFY;
        UNTIL Category.NEXT = 0;
    END;

    PROCEDURE UpgradeTemplate@1160040002();
    VAR
      Template@1160040000 : Record 6085579;
      DocCat@1160040001 : Record 6085575;
    BEGIN
      Window.UPDATE(1,STRSUBSTNO(Text001,Template.TABLECAPTION));

      IF Template.FINDSET THEN
        REPEAT
          IF Template."Source Type" > Template."Source Type"::Employee THEN
            ERROR(Text003,Template.FIELDCAPTION("Source Type"),FORMAT(Template."Source Type"));

          IF Template."Source Type" > Template."Source Type"::System THEN BEGIN
            IF Template."Codeunit ID: Line Capture" = CODEUNIT::"Purch. Doc. - Line Capture" THEN
              UpdateUnitCost(Template."No.");
            DocCat.GET(Template."Category Code");
            Template."Source Record ID Tree ID" := CreateRecIDTreeID(DocCat."Source Table No.",DocCat."Source Field No.",
              Template."Source No.");
            IF Template."Source No." <> '' THEN
              Template.TESTFIELD("Source Record ID Tree ID");
            Template."Source Type" := Template."Source Type"::System;
            Template."Source No." := '';
            Template.MODIFY;
          END;
        UNTIL Template.NEXT = 0;
    END;

    PROCEDURE UpgradeTemplateField@1160040003();
    VAR
      Field@1160040000 : Record 6085580;
      FixedValue@1160040001 : Text[250];
    BEGIN
      Window.UPDATE(1,STRSUBSTNO(Text001,Field.TABLECAPTION));

      IF Field.FINDSET THEN
        REPEAT
          IF Field."Data Type" = Field."Data Type"::Lookup THEN
            FixedValue := Field."Fixed Value (Lookup)"
          ELSE
            FixedValue := Field.GetFixedValue;
          IF Field.Code = 'CURRCODE' THEN BEGIN
            Field."Codeunit ID: Is OK" := 0;
            Field."Codeunit ID: Lookup" := 0;
            Field."Data Type" := Field."Data Type"::Lookup;
            Field.VALIDATE("Source Table No.",DATABASE::Currency);
          END ELSE BEGIN
            IF  Field."Lookup Table" >  Field."Lookup Table"::"Dimension Value" THEN
              ERROR(Text003,Field.FIELDCAPTION("Lookup Table"),FORMAT(Field."Lookup Table"));

            IF  Field."Lookup Table" >  Field."Lookup Table"::" " THEN
              IF Field."Data Type" = Field."Data Type"::Lookup THEN
                CASE Field."Lookup Table" OF
                  Field."Lookup Table"::Vendor:
                    Field.VALIDATE("Source Table No.",DATABASE::Vendor);

                  Field."Lookup Table"::Contact:
                    Field.VALIDATE("Source Table No.",DATABASE::Contact);

                  Field."Lookup Table"::Customer:
                    Field.VALIDATE("Source Table No.",DATABASE::Customer);

                  Field."Lookup Table"::Job:
                    Field.VALIDATE("Source Table No.",DATABASE::Job);

                  Field."Lookup Table"::Item:
                    Field.VALIDATE("Source Table No.",DATABASE::Item);

                  Field."Lookup Table"::"Fixed Asset":
                    Field.VALIDATE("Source Table No.",DATABASE::"Fixed Asset");

                  Field."Lookup Table"::Employee:
                    Field.VALIDATE("Source Table No.",DATABASE::Employee);

                  Field."Lookup Table"::"Dimension Value":
                    BEGIN
                      Field.VALIDATE("Source Table No.",DATABASE::"Dimension Value");
                      Field."Source Field No." := 2;
                      CreateTableFilter(349,1,Field."Source Table Filter GUID",Field."Lookup Rel. Code");
                    END;
                END
            ELSE
              Field.TESTFIELD("Lookup Table",0);
          END;
          IF FixedValue <> '' THEN
            Field.SetFixedValue(FixedValue);
          Field."Lookup Table" := Field."Lookup Table"::" ";
          Field."Lookup Rel. Code" := '';
          Field.MODIFY;
        UNTIL Field.NEXT = 0;
    END;

    PROCEDURE UpgradeDocument@1160040004();
    VAR
      DCSetup@1160040000 : Record 6085573;
      Document@1160040001 : Record 6085590;
      DocCat@161024012 : Record 6085575;
      EmptyGUID@161024014 : GUID;
    BEGIN
      Window.UPDATE(1,STRSUBSTNO(Text001,Document.TABLECAPTION));

      DCSetup.GET;
      DCSetup.TESTFIELD(DCSetup."TIFF File Path");
      DCSetup.TESTFIELD(DCSetup."PDF File Path");

      IF (DCSetup."PDF Storage Type" <> DCSetup."PDF Storage Type"::"Disk File") OR
        (DCSetup."TIFF Storage Type" <> DCSetup."TIFF Storage Type"::"Disk File")
      THEN BEGIN
        DCSetup."PDF Storage Type" := 0;
        DCSetup."TIFF Storage Type" := 0;
        DCSetup.MODIFY;
      END;

      IF Document.FINDSET THEN
        REPEAT
          Document.CALCFIELDS("TIFF Image File","PDF File");

          IF Document."TIFF Image File".HASVALUE THEN BEGIN
            Document."TIFF Image File".EXPORT(Document.GetTIFFileName);
            CLEAR(Document."TIFF Image File");
          END;

          IF Document."PDF File".HASVALUE THEN BEGIN
            Document."PDF File".EXPORT(Document.GetPDFileName);
            CLEAR(Document."PDF File");
          END;

          IF Document."Source Type" > Document."Source Type"::Employee THEN
            ERROR(Text003,Document.FIELDCAPTION("Source Type"),FORMAT(Document."Source Type"));

          IF Document."Source Type" > Document."Source Type"::" " THEN BEGIN
            DocCat.GET(Document."Document Category Code");
            Document."Source Record ID Tree ID" := CreateRecIDTreeID(DocCat."Source Table No.",DocCat."Source Field No.",
              Document."Source No.");
            IF Document."Source No." <> '' THEN
              Document.TESTFIELD("Source Record ID Tree ID");
            Document."Source Type" := Document."Source Type"::" ";
            Document."Source No." := '';
          END;

          Document.MODIFY;
        UNTIL Document.NEXT = 0;
    END;

    PROCEDURE CreateTableFilter@1160040005(TableNo@1160040004 : Integer;FieldNo@1160040003 : Integer;VAR TableGUID@1160040005 : GUID;RelCode@1160040000 : Text[30]);
    VAR
      TableFilterField@1160040001 : Record 6085762;
      EmptyGUID@1160040002 : GUID;
    BEGIN
      IF TableGUID = EmptyGUID THEN
        TableGUID := CREATEGUID;

      TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
      TableFilterField.SETRANGE("Field No.",FieldNo);
      IF RelCode = '' THEN BEGIN
        IF TableFilterField.FINDFIRST THEN
          TableFilterField.DELETE;
        EXIT;
      END;

      IF NOT TableFilterField.FINDFIRST THEN BEGIN
        TableFilterField."Table Filter GUID" := TableGUID;
        TableFilterField."Table No." := TableNo;
        TableFilterField."Field No." := FieldNo;
        TableFilterField."Filter Type" := TableFilterField."Filter Type"::"Fixed Filter";
        TableFilterField.INSERT;
      END;

      TableFilterField.SetValues(RelCode,TableFilterField."Filter Type",'',0);
      TableFilterField.MODIFY;
    END;

    PROCEDURE UpdateUnitCost@1160040006(TemplateNo@1160040001 : Code[20]);
    VAR
      Field@1160040000 : Record 6085580;
      LineNo@1160040002 : Integer;
      TemplFieldRule@1160040003 : Record 6085583;
    BEGIN
      IF Field.GET(TemplateNo,Field.Type::Line,'UNITCOST') THEN BEGIN
        Field.Required := TRUE;
        Field.MODIFY;

        TemplFieldRule.SETRANGE("Template No.",TemplateNo);
        TemplFieldRule.SETRANGE(Type,TemplFieldRule.Type::Line);
        TemplFieldRule.SETRANGE(Code,'UNITCOST');
        TemplFieldRule.SETRANGE(Rule,'>0');
        IF TemplFieldRule.ISEMPTY THEN BEGIN
          LineNo := 10000;
          TemplFieldRule.SETRANGE(Rule);
          IF TemplFieldRule.FINDLAST THEN
            LineNo := TemplFieldRule."Entry No." + 10000;

          TemplFieldRule.INIT;
          TemplFieldRule."Template No." := TemplateNo;
          TemplFieldRule.Type := TemplFieldRule.Type::Line;
          TemplFieldRule.Code := 'UNITCOST';
          TemplFieldRule."Entry No." := LineNo;
          TemplFieldRule.Rule := '>0';
          TemplFieldRule.INSERT;
        END;
      END;
    END;

    PROCEDURE UpgradeValue@1160040010();
    VAR
      Value@1160040000 : Record 6085593;
      Field@1160040001 : Record 6085580;
      RecIDMgt@1160040002 : Codeunit 6085604;
      GLSetup@1160040003 : Record 98;
    BEGIN
      Window.UPDATE(1,STRSUBSTNO(Text001,Value.TABLECAPTION));

      GLSetup.GET;

      IF Value.FINDSET THEN
        REPEAT
          IF Field.GET(Value."Template No.",Value.Type,Value.Code) THEN BEGIN
            IF (Field."Data Type" = Field."Data Type"::Lookup) AND (Value."Value (Lookup)" <> '') AND
              (GLSetup."LCY Code" <> Value."Value (Lookup)") THEN BEGIN
              Value."Value (Record ID Tree ID)" := CreateRecIDTreeID(Field."Source Table No.",Field."Source Field No.",
                Value."Value (Lookup)");
              IF Value."Value (Lookup)" <> '' THEN
                Value.TESTFIELD(Value."Value (Record ID Tree ID)");
            END;
          END;

          IF (Value."Value (Text)" = '') AND (Value."Value (Lookup)" <> '') THEN
            Value."Value (Text)" := Value."Value (Lookup)";

          Value."Lookup Table" := Value."Lookup Table"::" ";
          Value."Value (Lookup)" := '';
          Value."Value (Rel. Code)" := '';
          Value.MODIFY;
        UNTIL Value.NEXT = 0;
    END;

    PROCEDURE CreateIdentFields@1160040008(Category@1160040003 : Record 6085575);
    VAR
      Vendor@161024012 : Record 23;
    BEGIN
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO(Name),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("Name 2"),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO(Address),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("Address 2"),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO(City),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("Phone No."),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("E-Mail"),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("Home Page"),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("Currency Code"),1);
      CreateIdentField(Category.Code,Category."Source Table No.",Vendor.FIELDNO("VAT Registration No."),3);
    END;

    PROCEDURE CreateIdentField@161024017(DocCatCode@161024012 : Code[20];TableNo@161024015 : Integer;FieldNo@161024014 : Integer;Rating@161024016 : Integer);
    VAR
      IdentField@161024013 : Record 6085759;
    BEGIN
      IdentField."Document Category Code" := DocCatCode;
      IdentField."Table No." := TableNo;
      IdentField."Field No." := FieldNo;
      IdentField.Rating := Rating;
      IdentField.INSERT;
    END;

    PROCEDURE CreateRecIDTreeID@1160040007(SourceTableNo@1160040004 : Integer;SourceFieldNo@1160040003 : Integer;SourceValue@1160040005 : Code[20]) : Integer;
    VAR
      RecIDTree@1160040000 : Record 6085763;
    BEGIN
      IF SourceValue = '' THEN
        EXIT;

      RecIDTree.SETCURRENTKEY("Table No.","Parent ID","Value (Text)");
      RecIDTree.SETRANGE("Table No.",SourceTableNo);
      RecIDTree.SETRANGE("Parent ID",0);
      RecIDTree.SETRANGE("Field No.",1);
      RecIDTree.SETRANGE("Key Index",1);
      RecIDTree.SETRANGE("Value (Text)",SourceValue);
      IF NOT RecIDTree.FINDFIRST THEN BEGIN
        RecIDTree."Table No." := SourceTableNo;
        RecIDTree."Parent ID" := 0;
        RecIDTree."Field No." := 1;
        RecIDTree."Key Index" := 1;
        RecIDTree."Value (Text)" := SourceValue;
        RecIDTree.INSERT(TRUE);
      END;
      EXIT(RecIDTree.ID);
    END;

    PROCEDURE CreateDragAndDropCat@1160040000();
    VAR
      DocCat@1160040000 : Record 6085575;
    BEGIN
      DocCat.Code := 'DRAGNDROP';
      DocCat.Description := 'Drag & Drop';
      DocCat."Scanning Allowed" := FALSE;
      DocCat."Allow Drag-and-Drop" := TRUE;
      DocCat.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

