OBJECT Codeunit 6085723 Purch. Alloc.-Reverse
{
  OBJECT-PROPERTIES
  {
    Date=13.03.14;
    Time=01:00:00;
    Version List=DCW13.70.00.3.50.07;
  }
  PROPERTIES
  {
    TableNo=6085730;
    OnRun=VAR
            NewPurchAllocHeader@161024014 : Record 6085730;
            PurchAllocLine@161024013 : Record 6085731;
            NewPurchAllocLine@161024012 : Record 6085731;
            PurchHeader@161024016 : Record 38;
            DocDim@1160040000 : Record 357;
            DimMgt@1160040001 : Codeunit 408;
            PostingDate@161024015 : Date;
          BEGIN
            CLEARALL;

            PurchAllocHeader.COPY(Rec);

            WITH PurchAllocHeader DO BEGIN
              TESTFIELD(Status,Status::Posted);

              IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN BEGIN
                PurchHeader.GET("Document Type","Document No.");
                IF GenJnlCheckLine.DateNotAllowed(PurchHeader."Posting Date") THEN
                  PurchHeader.FIELDERROR("Posting Date",Text003)
                ELSE
                  PostingDate := PurchHeader."Posting Date";
              END ELSE
                PostingDate := "Posting Date";

              NewPurchAllocHeader := PurchAllocHeader;
              NewPurchAllocHeader."No." := '';
              NewPurchAllocHeader."Posting Date" := PostingDate;
              NewPurchAllocHeader.Status := NewPurchAllocHeader.Status::Open;
              NewPurchAllocHeader."Applies-to Doc. No." := PurchAllocHeader."No.";
              NewPurchAllocHeader.SetSkipCopyDim(TRUE);
              NewPurchAllocHeader.INSERT(TRUE);

              DocDim.SETRANGE("Table ID",DATABASE::"Purch. Allocation Header");
              DocDim.SETRANGE("Document Type",0);
              DocDim.SETRANGE("Document No.",PurchAllocLine."Document No.");
              DocDim.SETRANGE("Line No.",0);
              DimMgt.MoveDocDimToDocDim(DocDim,DATABASE::"Purch. Allocation Header",NewPurchAllocHeader."Document No.",0,0);


              PurchAllocLine.SETRANGE("Document No.",PurchAllocHeader."No.");
              IF PurchAllocLine.FINDFIRST THEN
                REPEAT
                  NewPurchAllocLine := PurchAllocLine;
                  NewPurchAllocLine."Document No." := NewPurchAllocHeader."No.";
                  NewPurchAllocLine.VALIDATE(Amount,-PurchAllocLine.Amount);
                  NewPurchAllocLine.VALIDATE("Amount Including VAT",-PurchAllocLine."Amount Including VAT");
                  NewPurchAllocLine.INSERT;

                  DocDim.SETRANGE("Table ID",DATABASE::"Purch. Allocation Line");
                  DocDim.SETRANGE("Document Type",0);
                  DocDim.SETRANGE("Document No.",PurchAllocLine."Document No.");
                  DocDim.SETRANGE("Line No.",PurchAllocLine."Line No.");
                  DimMgt.MoveDocDimToDocDim(DocDim,DATABASE::"Purch. Allocation Line",NewPurchAllocLine."Document No.",0,
                    NewPurchAllocLine."Line No.");
                UNTIL PurchAllocLine.NEXT = 0;

              CODEUNIT.RUN(CODEUNIT::"Purch. Alloc.-Post",NewPurchAllocHeader);
              NewPurchAllocHeader.Status := Status::Reversed;
              NewPurchAllocHeader.MODIFY;

              "Applies-to Doc. No." := NewPurchAllocHeader."No.";
              Status := Status::Reversed;
              MODIFY;
            END;

            Rec := PurchAllocHeader;
          END;

  }
  CODE
  {
    VAR
      PurchAllocHeader@1070000 : Record 6085730;
      Text003@161024012 : TextConst 'DEU=liegt nicht innerhalb des zugelassenen Buchungszeitraums;ENU=is not within your range of allowed posting dates';
      GenJnlCheckLine@161024013 : Codeunit 11;

    BEGIN
    END.
  }
}

