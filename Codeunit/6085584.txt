OBJECT Codeunit 6085584 File System Management
{
  OBJECT-PROPERTIES
  {
    Date=14.03.14;
    Time=01:00:00;
    Version List=DCW13.70.00.3.50.07;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FileSystem@161024012 : Automation "{D1D0B2AB-A006-4BA8-8CC8-6129957BF40A} 1.0:{4FB6FC10-A85C-36C3-AD0B-C4D9DA8B171F}:'Continia Document Capture for Dynamics NAV - Common Library'.FileSystem";

    PROCEDURE GetClientFilename@1160040002(ServerFilename@1160040000 : Text[1024]) : Text[1024];
    BEGIN
      EXIT(ServerFilename);
    END;

    PROCEDURE DirectoryExists@161024013(Path@161024012 : Text[1024]) : Boolean;
    BEGIN
      IF NOT CreateVars THEN
        EXIT(FALSE);

      EXIT(FileSystem.DirectoryExists(Path));
    END;

    PROCEDURE CreateDirectory@161024015(Path@161024012 : Text[1024]) : Boolean;
    BEGIN
      IF NOT CreateVars THEN
        EXIT(FALSE);

      EXIT(FileSystem.CreateDirectory(Path));
    END;

    PROCEDURE IsFileLocked@161024017(Filename@161024012 : Text[1024]) : Boolean;
    BEGIN
      IF NOT CreateVars THEN
        EXIT(FALSE);

      EXIT(FileSystem.IsFileLocked(Filename));
    END;

    PROCEDURE CopyFile@1160040000(FromFile@1160040001 : Text[1024];ToFile@1160040000 : Text[1024]);
    BEGIN
      FILE.COPY(FromFile,ToFile);
    END;

    PROCEDURE MoveFile@161024020(FromFile@161024012 : Text[1024];ToFile@161024013 : Text[1024]) : Boolean;
    BEGIN
      IF NOT CreateVars THEN
        EXIT(FALSE);

      EXIT(FileSystem.Move(FromFile,ToFile));
    END;

    PROCEDURE DeleteFile@1160040001(Filename@1160040000 : Text[1024];SupressError@1160040001 : Boolean) : Boolean;
    BEGIN
      IF SupressError THEN
        EXIT(FILE.ERASE(Filename))
      ELSE
        FILE.ERASE(Filename)
    END;

    LOCAL PROCEDURE CreateVars@161024012() : Boolean;
    BEGIN
      IF ISCLEAR(FileSystem) THEN
        EXIT(CREATE(FileSystem))
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE GetFilePath@161024016(FullFilename@161024012 : Text[250]) : Text[250];
    BEGIN
      EXIT(COPYSTR(FullFilename,1,LastIndexOf(FullFilename,'\',TRUE)));
    END;

    PROCEDURE GetFilename@161024018(FullFilename@161024012 : Text[250]) : Text[250];
    BEGIN
      EXIT(COPYSTR(FullFilename,LastIndexOf(FullFilename,'\',TRUE)+1));
    END;

    PROCEDURE GetFilenameWithoutExt@161024019(FullFilename@161024012 : Text[250]) : Text[250];
    BEGIN
      FullFilename := COPYSTR(FullFilename,LastIndexOf(FullFilename,'\',TRUE)+1);
      IF STRPOS(FullFilename,'.') <> 0 THEN
        FullFilename := COPYSTR(FullFilename,1,LastIndexOf(FullFilename,'.',TRUE)-1);
      EXIT(FullFilename);
    END;

    PROCEDURE GetFileExtension@161024021(FullFilename@161024012 : Text[250]) : Text[250];
    BEGIN
      EXIT(COPYSTR(FullFilename,LastIndexOf(FullFilename,'.',TRUE)+1));
    END;

    PROCEDURE LocalShell@1160040003(Filename@1160040000 : Text[1024]);
    VAR
      WshShell@1160040001 : Automation "{F935DC20-1CF0-11D0-ADB9-00C04FD58A0B} 1.0:{72C24DD5-D70A-438B-8A42-98424B88AFB8}:'Windows Script Host Object Model'.WshShell";
    BEGIN
      CREATE(WshShell,TRUE);
      WshShell.Run('"' + Filename + '"');
      CLEAR(WshShell);
    END;

    PROCEDURE LastIndexOf@6085573(Text@161024012 : Text[1024];CharToFind@161024013 : Text[1];IgnoreCase@161024014 : Boolean) : Integer;
    VAR
      StartIndex@161024015 : Integer;
    BEGIN
      IF IgnoreCase THEN BEGIN
        Text := UPPERCASE(Text);
        CharToFind := UPPERCASE(CharToFind);
      END;

      StartIndex := STRLEN(Text);
      WHILE StartIndex > 0 DO BEGIN
        IF COPYSTR(Text,StartIndex,1) = CharToFind THEN
          EXIT(StartIndex)
        ELSE
          StartIndex -= 1;
      END;
    END;

    BEGIN
    END.
  }
}

