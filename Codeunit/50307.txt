OBJECT Codeunit 50307 Whse. LF General Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=19.03.15;
    Time=12:19:50;
    Modified=Yes;
    Version List=#HME2157;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      oFile@1000000000 : File;
      GTC_TempFileExtension@1000000001 : TextConst 'DEU=.tmp;ENU=.tmp';
      BigTextLine@1000000002 : BigText;
      "** HME *****"@1000000004 : TextConst;
      HMEText001@1000000003 : TextConst 'ENU=Filename must be specified.';
      HMEText002@1000000005 : TextConst 'ENU=%1 for %2 does not exists.';

    PROCEDURE AppendToString@1119443002(VAR TextParR@1119443000 : Text[1024];TextToAppendParV@1119443001 : Text[1024];ActualStringLengthParV@1119443002 : Integer;PadCharParV@1119443003 : Text[1];PadTypeParV@1119443004 : 'Left,Right') : Boolean;
    VAR
      NoOfNecPadChars@1119443005 : Integer;
      PadLoop@1119443006 : Integer;
    BEGIN
      // This Fct. Depends on passed TextToAppendPar string and the text passed in TextPar and fills in the
      // PadCharPar Character until ActualStringLengthPar. Whether left or right is filled, is controlled
      // by parameter PadTypePar.

      // Hanging text should be cut initially to maximum length
      TextToAppendParV := COPYSTR(TextToAppendParV,1,ActualStringLengthParV);

      NoOfNecPadChars := ActualStringLengthParV - STRLEN(TextToAppendParV);

      FOR PadLoop := 1 TO NoOfNecPadChars DO
        CASE PadTypeParV OF
          PadTypeParV::Left:
            TextToAppendParV := PadCharParV + TextToAppendParV;

          PadTypeParV::Right:
            TextToAppendParV := TextToAppendParV + PadCharParV;

        END;//*** CASE ***

      IF STRLEN(TextParR + TextToAppendParV) > MAXSTRLEN(TextParR) THEN
        EXIT(FALSE);

      TextParR := TextParR + TextToAppendParV;
      EXIT(TRUE);
    END;

    PROCEDURE GetNextEntryNo@1000000006(ObjectIDV@1000000000 : Integer) : Integer;
    VAR
      RecRefL@1000000001 : RecordRef;
      FieldRefL@1000000002 : FieldRef;
      EntryNoL@1000000003 : Integer;
    BEGIN
      RecRefL.OPEN(ObjectIDV);
      IF RecRefL.FINDLAST THEN;
      FieldRefL := RecRefL.FIELD(1);
      EntryNoL := FieldRefL.VALUE;
      EXIT(EntryNoL + 1);
    END;

    PROCEDURE "--- File Functions ---"@1000000000();
    BEGIN
    END;

    PROCEDURE FileCreate@1000000001(FilePathV@1000000000 : Text[250];FileNameV@1000000001 : Text[250]) : Boolean;
    BEGIN
      // Create file.
      // Until the completion of writing into file, the temporary file is created, so that file is not uploaded prematurely.

      EXIT(oFile.CREATE(FilePathV + FileNameV + GTC_TempFileExtension));
    END;

    PROCEDURE FileOpen@1000000002(FilePathV@1000000001 : Text[250];FileNameV@1000000000 : Text[250]) : Boolean;
    BEGIN
      // Datei frs Schreiben ”ffnen.

      oFile.WRITEMODE(TRUE);
      EXIT(oFile.OPEN(FilePathV + FileNameV + GTC_TempFileExtension));
    END;

    PROCEDURE FileAddLine@1000000003(LineV@1000000000 : Text[1000];NewLineAfterV@1000000001 : Boolean);
    VAR
      CR@1000000002 : Char;
      LF@1000000003 : Char;
      FPCManagementL@1000000004 : Codeunit 50003;
    BEGIN
      // Attach text.
      // With NewLineAfterV a line break can be added.

      LineV := FPCManagementL.DeleteCRLF(LineV,FALSE);
      IF NewLineAfterV THEN BEGIN
        CR := 13;
        LF := 10;

        oFile.WRITE(LineV + FORMAT(CR) + FORMAT(LF));
        BigTextLine.ADDTEXT(LineV + FORMAT(CR) + FORMAT(LF));
      END ELSE BEGIN
        oFile.WRITE(LineV);
        BigTextLine.ADDTEXT(LineV);
      END;
    END;

    PROCEDURE FileSave@1000000004();
    BEGIN
      // Close file.

      oFile.CLOSE();
    END;

    PROCEDURE FileConvertToUTF8@1000000007(FilePathV@1000000001 : Text[250];FileNameV@1000000000 : Text[250]) : Boolean;
    VAR
      UTFStreamL@1000000005 : Automation "{2A75196C-D9EB-4129-B803-931327F72D5C} 2.8:{00000566-0000-0010-8000-00AA006D2EA4}:'Microsoft ActiveX Data Objects 2.8 Library'.Stream";
      SubTextL@1000000002 : Text[1024];
      PosL@1000000003 : Integer;
      LengthL@1000000004 : Integer;
    BEGIN
      // Convert file from ANSI to UTF-8

      IF NOT CREATE(UTFStreamL) THEN
        EXIT(FALSE);

      EXISTS(FilePathV + FileNameV + GTC_TempFileExtension);

      UTFStreamL.Open;
      UTFStreamL.Charset('UTF-8');
      UTFStreamL.LineSeparator := 10;

      PosL := 1;
      LengthL := BigTextLine.LENGTH;
      REPEAT
        SubTextL := '';
        BigTextLine.GETSUBTEXT(SubTextL,PosL,1024);
        PosL := PosL + 1024;
        UTFStreamL.WriteText(SubTextL);
      UNTIL PosL >= LengthL;

      UTFStreamL.SaveToFile(FilePathV + FileNameV);
      UTFStreamL.Close;
      ERASE(FilePathV + FileNameV + GTC_TempFileExtension);

      EXIT(TRUE);
    END;

    PROCEDURE FileDeleteAfterError@1000000005(FilePathL@1000000001 : Text[250];FileNameL@1000000000 : Text[250]);
    BEGIN
      // If Error occurs temp-file still exists.
      // Delete temp-file after error:
      ERASE(FilePathL + FileNameL + GTC_TempFileExtension);
    END;

    PROCEDURE GetLastEntryNo@1000000009(ObjectIdV@1000000003 : Integer;FieldNoV@1000000004 : Integer;FilterV@1000000005 : Text[30]) : Integer;
    VAR
      RecRefL@1000000002 : RecordRef;
      FieldRefL@1000000001 : FieldRef;
      EntryNoL@1000000000 : Integer;
    BEGIN
      RecRefL.OPEN(ObjectIdV);
      FieldRefL := RecRefL.FIELD(FieldNoV);
      FieldRefL.SETRANGE(FilterV);
      IF RecRefL.FINDLAST THEN BEGIN
        FieldRefL := RecRefL.FIELD(1);
        EntryNoL := FieldRefL.VALUE;
      END;
      EXIT(EntryNoL);
    END;

    PROCEDURE OpenFileWithEditor@1000000011(RecordIdV@1000000000 : Text[30];FileNameV@1000000001 : Text[250];WhseH24IdentifierV@1000000011 : Code[10]);
    VAR
      WhseLFSetupL@1000000003 : Record 50300;
      FPCInterfacesSetupL@1000000006 : Record 50014;
      WhseH24SubInterfaceL@1000000012 : Record 50506;
      eBayEditorL@1000000007 : Codeunit 5095849;
      FileL@1000000004 : File;
      FilePathL@1000000005 : Text[80];
      TempCharL@1000000002 : Text[1];
      ServerPathL@1000000010 : Text[30];
      SessionL@1000000009 : Record 2000000009;
      ConnectionStrL@1000000008 : Text[150];
    BEGIN
      IF FileNameV = '' THEN
        ERROR(HMEText001);

      WhseLFSetupL.GET;

      //H2157 18.02.15 DMO ++++++++++++++++++++++++++
      WhseH24SubInterfaceL.RESET;
      WhseH24SubInterfaceL.SETRANGE("File Type",RecordIdV);
      WhseH24SubInterfaceL.SETRANGE("Whse. H24 Identifier Code",WhseH24IdentifierV);
      IF WhseH24SubInterfaceL.ISEMPTY THEN
        WhseH24SubInterfaceL.SETRANGE("Whse. H24 Identifier Code");
      WhseH24SubInterfaceL.FINDFIRST;
      FPCInterfacesSetupL.GET(WhseH24SubInterfaceL."FPC Interface Setup Code");
      //H2157 18.02.15 DMO --------------------------

      //H1456 08.09.14 EHN ++++++++++++++++++++++++++++++++++++++++++++++++++++
      ServerPathL := '\\' + WhseLFSetupL."Host Name Application Server" + '\';
      WHILE STRPOS(FileNameV,'\') <> 0 DO
        FileNameV := COPYSTR(FileNameV,STRPOS(FileNameV,'\') + 1);

      FilePathL := FPCInterfacesSetupL."Import Folder";
      IF FilePathL = '' THEN
        FilePathL := FPCInterfacesSetupL."Export Folder";
      FilePathL := DELCHR(FilePathL,'>','\') + '\';

      SessionL.RESET;
      SessionL.SETRANGE("My Session",TRUE);
      SessionL.FINDFIRST;
      IF SessionL."Host Name" <> WhseLFSetupL."Host Name Application Server" THEN
        //H1273 25.11.14 DMA +++++++++++++++++++++++++++++++++++++++++
        IF COPYSTR(FilePathL,1,2) = '\\' THEN
          ConnectionStrL := DELCHR(FilePathL,'=',':') + FileNameV
        ELSE
          ConnectionStrL := ServerPathL + COPYSTR(FilePathL,STRPOS(FilePathL,':')+1,STRLEN(FilePathL)-STRPOS(FilePathL,':')) + FileNameV
        //H1273 25.11.14 DMA -----------------------------------------
      ELSE
        ConnectionStrL := FilePathL + FileNameV;

      IF NOT EXISTS(ConnectionStrL) THEN BEGIN
        FilePathL := FPCInterfacesSetupL."Archive Root Folder";
        FilePathL := DELCHR(FilePathL,'>','\') + '\';
        IF SessionL."Host Name" <> WhseLFSetupL."Host Name Application Server" THEN
          //H1273 25.11.14 DMA +++++++++++++++++++++++++++++++++++++++++
          IF COPYSTR(FilePathL,1,2) = '\\' THEN
            ConnectionStrL := DELCHR(FilePathL,'=',':') + FileNameV
          ELSE
            ConnectionStrL :=
              ServerPathL + COPYSTR(FilePathL,STRPOS(FilePathL,':')+1,STRLEN(FilePathL)-STRPOS(FilePathL,':')) + FileNameV
          //H1273 25.11.14 DMA -----------------------------------------
        ELSE
          ConnectionStrL := FilePathL + FileNameV;
      END;

      eBayEditorL.RunEditor(ConnectionStrL,2,FALSE);

      //H1456 08.09.14 EHN ----------------------------------------------------
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________
      H1408     05.08.14   DMA       Copy RHD Interface, rename and implement as Whs LF Interface
      H1456     08.09.14   EHN       Fix all Whse. LF Forms where we have assist edit button
      H2157     18.02.15   DMO       CODECHANGE: OpenFileWithEditor takes additional parameter;Functions Deleted
    }
    END.
  }
}

