OBJECT Codeunit 6085704 Purch. Doc. - Line Validation
{
  OBJECT-PROPERTIES
  {
    Date=16.10.12;
    Time=01:00:00;
    Version List=DCW13.70.00.3.02;
  }
  PROPERTIES
  {
    TableNo=6085596;
    OnRun=VAR
            Document@1000000000 : Record 6085590;
            Comment@1000000001 : Record 6085594;
            Template@1000000010 : Record 6085579;
            Field@1000000009 : Record 6085580;
            CaptureMgnt@1000000008 : Codeunit 6085576;
            Quantity@1000000003 : Decimal;
            UnitCost@1000000004 : Decimal;
            LineAmount@1000000005 : Decimal;
            DiscAmount@1000000006 : Decimal;
            DiscPct@1000000007 : Decimal;
            LineAmount2@1000000002 : Decimal;
          BEGIN
            Field.SETRANGE("Template No.",Rec."Template No.");
            Field.SETRANGE(Type,Field.Type::Line);

            IF Field.FINDFIRST THEN
              REPEAT
                IF NOT CaptureMgnt.IsValidValue(Field,Rec."Document No.",Rec."Line No.") THEN BEGIN
                  Rec.OK := FALSE;
                  EXIT;
                END;
              UNTIL Field.NEXT = 0;

            Document.GET(Rec."Document No.");
            Template.GET(Rec."Template No.");

            Quantity := PurchDocMgnt.GetLineQuantity(Document,Rec."Line No.");
            UnitCost := PurchDocMgnt.GetLineUnitCost(Document,Rec."Line No.");
            DiscPct := PurchDocMgnt.GetLineDiscPct(Document,Rec."Line No.");
            DiscAmount := PurchDocMgnt.GetLineDiscAmount(Document,Rec."Line No.");
            LineAmount := PurchDocMgnt.GetLineAmount(Document,Rec."Line No.");

            IF (Quantity = 0) AND (UnitCost = 0) AND (LineAmount = 0) AND (DiscPct = 0) AND (DiscAmount = 0) THEN BEGIN
              Rec.OK := TRUE;
              EXIT;
            END;

            LineAmount2 := Quantity * UnitCost;

            IF DiscAmount <> 0 THEN
              LineAmount2 := ROUND(LineAmount2 - DiscAmount)
            ELSE IF DiscPct <> 0 THEN
              LineAmount2 := ROUND(LineAmount2 * ((100-DiscPct) / 100));

            Rec.OK := PurchDocMgnt.AmountsWithinTolerance(Template,LineAmount,LineAmount2);
          END;

  }
  CODE
  {
    VAR
      PurchDocMgnt@1000000001 : Codeunit 6085709;

    BEGIN
    {
      This codeunit validates lines on purchase documents
    }
    END.
  }
}

