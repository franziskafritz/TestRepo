OBJECT Codeunit 50204 Logger
{
  OBJECT-PROPERTIES
  {
    Date=02.09.13;
    Time=14:59:54;
    Version List=HME0513;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
            ClearLog;
          END;

  }
  CODE
  {
    VAR
      Log@1102601000 : TEMPORARY Record 80003;
      InfoCode@1102601001 : TextConst 'ENU=INFO';
      DebugCode@1102601002 : TextConst 'ENU=DEBUG';

    LOCAL PROCEDURE AddLine@1102601008(Level@1102601001 : Code[10];Msg@1102601000 : Text[250]);
    VAR
      NextNo@1102601002 : Integer;
    BEGIN
      // Compute next line number
      IF NOT Log.FINDLAST THEN
        NextNo := 1
      ELSE
        NextNo := Log."No." + 1;

      // Insert line
      Log.INIT;
      Log."No." := NextNo;
      Log.INSERT(TRUE);
      Log.VALIDATE(Level,Level);
      Log.VALIDATE(Line,Msg);
      Log.MODIFY(TRUE);
    END;

    PROCEDURE GetFirstRecord@1102601000(VAR Rec@1102601000 : Record 80003) : Boolean;
    BEGIN
      IF NOT Log.FINDFIRST THEN
        EXIT(FALSE);

      Rec := Log;
      EXIT(TRUE);
    END;

    PROCEDURE GetNextRecord@1102601004(VAR Rec@1102601000 : Record 80003) : Boolean;
    BEGIN
      IF Log.NEXT = 0 THEN
        EXIT(FALSE);

      Rec := Log;
      EXIT(TRUE);
    END;

    PROCEDURE Information@1102601001(Msg@1102601000 : Text[250]);
    BEGIN
      AddLine(InfoCode,Msg);
    END;

    PROCEDURE Debug@1102601002(Msg@1102601000 : Text[250]);
    BEGIN
      AddLine(DebugCode,Msg);
    END;

    PROCEDURE ClearLog@1102601003();
    BEGIN
      Log.DELETEALL(TRUE);
    END;

    BEGIN
    END.
  }
}

