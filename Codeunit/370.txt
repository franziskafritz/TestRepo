OBJECT Codeunit 370 Bank Acc. Reconciliation Post
{
  OBJECT-PROPERTIES
  {
    Date=08.07.14;
    Time=12:00:00;
    Modified=Yes;
    Version List=NAVW15.00.01,AR6.00,eBay1.06.13,GOB1.00,HME0636,HME0757;
  }
  PROPERTIES
  {
    TableNo=273;
    Permissions=TableData 271=rm,
                TableData 272=rm,
                TableData 275=ri,
                TableData 276=ri,
                TableData 287=ri,
                TableData 5001902=rimd;
    OnRun=VAR
            OrigDiff@1105400000 : Decimal;
            "--eBay"@5095800 : Boolean;
            eBayPayment@5095801 : Codeunit 5095807;
            eBayCustomer@5095802 : Codeunit 5095804;
            BankAccLedgEntry2@1000000000 : Record 271;
            PaymLedgEntry2@1000000001 : Record 5001902;
          BEGIN
            Window.OPEN(
              '#1#################################\\' +
              Text000);
            Window.UPDATE(1,STRSUBSTNO('%1 %2',"Bank Account No.","Statement No."));

            TESTFIELD("Statement Date");

            BankAccReconLine2.RESET;
            BankAccReconLine2.SETRANGE("Bank Account No.","Bank Account No.");
            BankAccReconLine2.SETRANGE("Statement No.","Statement No.");
            BankAccReconLine2.CALCSUMS("Statement Amount",Difference);

            IF BankAccReconLine2."Statement Amount" <> "Statement Ending Balance" - "Balance Last Statement" THEN
              ERROR(Text001,FIELDCAPTION("Statement Ending Balance"));

            // Run through lines
            BankAccReconLine.RESET;
            BankAccReconLine.SETRANGE("Bank Account No.","Bank Account No.");
            BankAccReconLine.SETRANGE("Statement No.","Statement No.");
            TotalAmount := 0;
            TotalAppliedAmount := 0;
            TotalDiff := 0;
            ExchrateDiff := 0;  // CLR
            Lines := 0;
            IF BankAccReconLine.FIND('-') THEN BEGIN
              //eBay#001
              eBayPayment.NichtZugeordneteZahlungen(Rec);
              //eBay#001
              //eBay#002
              eBayCustomer.öbernahmeBankverbindungen(Rec);
              //eBay#002

              BankAccLedgEntry.LOCKTABLE;
              CheckLedgEntry.LOCKTABLE;
            // CLR BEGIN
              GLAccSetup.GET;
              PaymLedgEntry.LOCKTABLE;
              ForeignPaymentEntry.LOCKTABLE;

              LicPermission.GET(LicPermission."Object Type"::Table,DATABASE::"Payment Setup");
              IF LicPermission."Read Permission" = LicPermission."Read Permission"::Yes THEN
                PaymentSetup.GET;
            // CLR END
              REPEAT
                Lines := Lines + 1;
                Window.UPDATE(2,Lines);
                AppliedAmount := 0;
                // Adjust entries
                // Test amount and settled amount
                CASE BankAccReconLine.Type OF
                  BankAccReconLine.Type::"Bank Account Ledger Entry":
                    BEGIN
                      // Post GLAccount
                      // CLR +
                      LicPermission.GET(LicPermission."Object Type"::Table,DATABASE::"Payment Setup");
                      IF LicPermission."Read Permission" = LicPermission."Read Permission"::Yes THEN
                        IF "G/L Post"  AND (BankAccReconLine.Difference<>0) THEN BEGIN
                          OrigDiff := BankAccReconLine.Difference;
                          CLEAR(PaymGenJnlPostLine);
                          IF PaymGenJnlPostLine.PostGLAcc(BankAccReconLine) THEN
                            BankAccReconLine2.Difference := BankAccReconLine2.Difference - OrigDiff;
                          OrigDiff := 0;
                        END;
                      // CLR -
                      BankAccLedgEntry.RESET;
                      BankAccLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
                      BankAccLedgEntry.SETRANGE("Bank Account No.",BankAccReconLine."Bank Account No.");
                      BankAccLedgEntry.SETRANGE(Open,TRUE);
                      BankAccLedgEntry.SETRANGE(
                        "Statement Status",BankAccLedgEntry."Statement Status"::"Bank Acc. Entry Applied");
                      BankAccLedgEntry.SETRANGE("Statement No.",BankAccReconLine."Statement No.");
                      BankAccLedgEntry.SETRANGE("Statement Line No.",BankAccReconLine."Statement Line No.");
                      IF BankAccLedgEntry.FIND('-') THEN
                        REPEAT
                          AppliedAmount := AppliedAmount + BankAccLedgEntry."Remaining Amount";
                          BankAccLedgEntry."Remaining Amount" := 0;
                          BankAccLedgEntry.Open := FALSE;
                          BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Closed;
                          // CLR BEGIN
                          Transit := BankAccLedgEntry."Transit Posting";
                          BankAccLedgEntry."Transit Posting" := FALSE;
                          // CLR END;

                          //A/gob-adb/11.11.13
                          //BankAccLedgEntry.MODIFY;
                          BankAccLedgEntry2 := BankAccLedgEntry;
                          BankAccLedgEntry2.MODIFY;
                          //E/gob-adb/11.11.13

                          CheckLedgEntry.RESET;
                          CheckLedgEntry.SETCURRENTKEY("Bank Account Ledger Entry No.");
                          CheckLedgEntry.SETRANGE(
                            "Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
                          CheckLedgEntry.SETRANGE(CheckLedgEntry.Open,TRUE);
                          IF CheckLedgEntry.FIND('-') THEN
                            REPEAT
                              CheckLedgEntry.TESTFIELD(Open,TRUE);
                              CheckLedgEntry.TESTFIELD(
                                "Statement Status",
                                CheckLedgEntry."Statement Status"::"Bank Acc. Entry Applied");
                              CheckLedgEntry.TESTFIELD("Statement No.",'');
                              CheckLedgEntry.TESTFIELD("Statement Line No.",0);
                              CheckLedgEntry.Open := FALSE;
                              CheckLedgEntry."Statement Status" := CheckLedgEntry."Statement Status"::Closed;
                              CheckLedgEntry.MODIFY;
                            UNTIL CheckLedgEntry.NEXT = 0;
                          // CLR BEGIN
                          PaymLedgEntry.RESET;
                          PaymLedgEntry.SETCURRENTKEY("Bank Ledger Entry No.");
                          PaymLedgEntry.SETRANGE(
                            "Bank Ledger Entry No.",BankAccLedgEntry."Entry No.");
                          PaymLedgEntry.SETRANGE(PaymLedgEntry.Open,TRUE);
                          IF PaymLedgEntry.FIND('-') THEN
                            REPEAT
                              PaymLedgEntry.TESTFIELD(Open,TRUE);
                              PaymLedgEntry.TESTFIELD(
                                "Statement Status",
                                PaymLedgEntry."Statement Status"::"Regulation Bank Ledger Entry");
                              PaymLedgEntry.TESTFIELD("Statement No.",'');
                              PaymLedgEntry.TESTFIELD("Statement Line No.",0);
                              PaymLedgEntry.Open := FALSE;
                              PaymLedgEntry."Statement Status" := PaymLedgEntry."Statement Status"::Closed;
                              PaymLedgEntry.MODIFY;
                            UNTIL PaymLedgEntry.NEXT = 0;
                          // CLR END
                        UNTIL BankAccLedgEntry.NEXT = 0;
                        // CLR Begin
                        IF "G/L Post" THEN
                          TotalDiff := TotalDiff + BankAccReconLine.Difference;
                        // CLR END
                    END;
                  BankAccReconLine.Type::"Check Ledger Entry":
                    BEGIN
                      CheckLedgEntry.RESET;
                      CheckLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
                      CheckLedgEntry.SETRANGE("Bank Account No.",BankAccReconLine."Bank Account No.");
                      CheckLedgEntry.SETRANGE(Open,TRUE);
                      CheckLedgEntry.SETRANGE(
                        "Statement Status",CheckLedgEntry."Statement Status"::"Check Entry Applied");
                      CheckLedgEntry.SETRANGE("Statement No.",BankAccReconLine."Statement No.");
                      CheckLedgEntry.SETRANGE("Statement Line No.",BankAccReconLine."Statement Line No.");
                      IF CheckLedgEntry.FIND('-') THEN
                        REPEAT
                          AppliedAmount := AppliedAmount - CheckLedgEntry.Amount;
                          CheckLedgEntry.Open := FALSE;
                          CheckLedgEntry."Statement Status" := CheckLedgEntry."Statement Status"::Closed;
                          CheckLedgEntry.MODIFY;

                          BankAccLedgEntry.GET(CheckLedgEntry."Bank Account Ledger Entry No.");
                          BankAccLedgEntry.TESTFIELD(Open,TRUE);
                          BankAccLedgEntry.TESTFIELD(
                          "Statement Status",BankAccLedgEntry."Statement Status"::"Check Entry Applied");
                          BankAccLedgEntry.TESTFIELD("Statement No.",'');
                          BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
                          BankAccLedgEntry."Remaining Amount" :=
                            BankAccLedgEntry."Remaining Amount" + CheckLedgEntry.Amount;
                          IF BankAccLedgEntry."Remaining Amount" = 0 THEN BEGIN
                            BankAccLedgEntry.Open := FALSE;
                            BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Closed;
                            BankAccLedgEntry."Statement No." := "Statement No.";
                            BankAccLedgEntry."Statement Line No." := CheckLedgEntry."Statement Line No.";
                            // CLR
                            Transit := BankAccLedgEntry."Transit Posting";
                            BankAccLedgEntry."Transit Posting" := FALSE;
                            // CLR
                          END ELSE BEGIN
                            CheckLedgEntry2.RESET;
                            CheckLedgEntry2.SETCURRENTKEY("Bank Account Ledger Entry No.");
                            CheckLedgEntry2.SETRANGE("Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
                            CheckLedgEntry2.SETRANGE(Open,TRUE);
                            CheckLedgEntry2.SETRANGE("Check Type",CheckLedgEntry2."Check Type"::"Partial Check");
                            CheckLedgEntry2.SETRANGE(
                              "Statement Status",CheckLedgEntry2."Statement Status"::"Check Entry Applied");
                            IF NOT CheckLedgEntry2.FIND('-') THEN
                              BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Open;
                          END;
                          BankAccLedgEntry.MODIFY;
                        UNTIL CheckLedgEntry.NEXT = 0;
                        // CLR BEGIN
                        TotalDiff := TotalDiff + BankAccReconLine.Difference;
                        // CLR END
                    END;
                    // CLR BEGIN
                    BankAccReconLine.Type::"Payment Entry":
                    BEGIN
                      PaymAmount := 0;
                      PaymLedgEntry.RESET;
                      PaymLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
                      PaymLedgEntry.SETRANGE("Bank Account No.",BankAccReconLine."Bank Account No.");
                      PaymLedgEntry.SETRANGE(Open,TRUE);
                      PaymLedgEntry.SETRANGE(
                        "Statement Status",PaymLedgEntry."Statement Status"::"Regulation Bank Ledger Entry");
                      PaymLedgEntry.SETRANGE("Statement No.",BankAccReconLine."Statement No.");
                      PaymLedgEntry.SETRANGE("Statement Line No.",BankAccReconLine."Statement Line No.");
                      IF PaymLedgEntry.FIND('-') THEN
                        REPEAT
                          BankAcc.GET(PaymLedgEntry."Bank Account No.");
                          IF PaymLedgEntry."Currency Code"=BankAcc."Currency Code" THEN
                            PaymAmount := PaymLedgEntry.Amount
                          ELSE
                            PaymAmount := PaymLedgEntry."Amount (LCY)";
                          AppliedAmount := AppliedAmount - PaymAmount;
                          PaymLedgEntry.Open := FALSE;
                          PaymLedgEntry."Statement Status" := PaymLedgEntry."Statement Status"::Closed;
                          BankAccLedgEntry.GET(PaymLedgEntry."Bank Ledger Entry No.");
                          PaymLedgEntry."Posting Date" := BankAccLedgEntry."Posting Date";

                          //A/gob-adb/11.11.13
                          //PaymLedgEntry.MODIFY;
                          PaymLedgEntry2 := PaymLedgEntry;
                          PaymLedgEntry2.MODIFY;
                          //E/gob-adb/11.11.13

                          ForeignPaymentEntry.SETCURRENTKEY("Payment No.");
                          ForeignPaymentEntry.SETRANGE("Payment No.",PaymLedgEntry."Payment No.");
                          IF ForeignPaymentEntry.FIND('-') THEN BEGIN
                             ForeignPaymentEntry.Open := FALSE;
                             ForeignPaymentEntry."Statement Status" := ForeignPaymentEntry."Statement Status"::Closed;
                             ForeignPaymentEntry.MODIFY;
                          END;
                          BankAccLedgEntry.TESTFIELD(Open,TRUE);
                          BankAccLedgEntry.TESTFIELD(
                            "Statement Status",BankAccLedgEntry."Statement Status"::Closed);
                          BankAccLedgEntry.TESTFIELD("Statement No.",'');
                          BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
                          BankAccLedgEntry."Remaining Amount" :=
                            BankAccLedgEntry."Remaining Amount" + PaymAmount;
                          IF BankAccLedgEntry."Remaining Amount" = 0 THEN BEGIN
                            BankAccLedgEntry.Open := FALSE;
                            BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Closed;
                            Transit := BankAccLedgEntry."Transit Posting";
                            BankAccLedgEntry."Transit Posting" := FALSE;
                          END;
                          BankAccLedgEntry.MODIFY;
                        TotalDiff := TotalDiff + BankAccReconLine.Difference;
                        UNTIL PaymLedgEntry.NEXT = 0;
                    END;
                  BankAccReconLine.Type::Difference:
                    TotalDiff := TotalDiff + BankAccReconLine."Statement Amount";
                // CLR END
                END;
                // CLR BEGIN
                TransferBankAccounts(BankAccReconLine);
                "Posting Currencyapplication"(BankAccReconLine);
                // CLR  END
                BankAccReconLine.TESTFIELD("Applied Amount",AppliedAmount);
                TotalAmount := TotalAmount + BankAccReconLine."Statement Amount";
                TotalAppliedAmount := TotalAppliedAmount + AppliedAmount;
                ExchrateDiff := ExchrateDiff + BankAccReconLine."Exchange Difference";  // CLR
              UNTIL BankAccReconLine.NEXT = 0;
            END ELSE
              ERROR(Text002);

            // Test amount
            IF TotalAmount <> TotalAppliedAmount + TotalDiff + ExchrateDiff THEN  // CLR: + ExchrateDiff
              ERROR(
                Text003,
                TotalAppliedAmount + TotalDiff,TotalAmount);

            IF BankAccReconLine2.Difference <> TotalDiff THEN
              ERROR(Text004,BankAccReconLine2.Difference,TotalDiff);

            // Get bank
            BankAcc.LOCKTABLE;
            BankAcc.GET("Bank Account No.");
            BankAcc.TESTFIELD(BankAcc.Blocked,FALSE);
            BankAcc."Last Statement No." := "Statement No.";
            BankAcc."Balance Last Statement" := "Balance Last Statement" + TotalAmount;
            BankAcc.MODIFY;

            // Copy and delete statement
            BankAccReconLine.RESET;
            BankAccReconLine.SETRANGE("Bank Account No.","Bank Account No.");
            BankAccReconLine.SETRANGE("Statement No.","Statement No.");
            IF BankAccReconLine.FIND('-') THEN
              REPEAT
                BankAccStmtLine.TRANSFERFIELDS(BankAccReconLine);
                BankAccStmtLine.INSERT;

                // CLR
                AutocreateBankAccount(BankAccReconLine);
                DimMgt.DeleteDocDim(
                  DATABASE::"Bank Acc. Reconciliation Line",0,
                  BankAccStmtLine."Statement No.",BankAccStmtLine."Statement Line No.");
                // CLR

              UNTIL BankAccReconLine.NEXT = 0;
            BankAccReconLine.DELETEALL;

            BankAccStmt.TRANSFERFIELDS(Rec);
            BankAccStmt.INSERT;
            DELETE;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Zeilen buchen              #2######;ENU=Posting lines              #2######';
      Text001@1001 : TextConst 'DEU=%1 stimmt nicht mit dem Totalsaldo Åberein;ENU=%1 is not equal to Total Balance';
      Text002@1002 : TextConst 'DEU=Nichts zu buchen;ENU=Nothing to post';
      Text003@1003 : TextConst 'DEU=Der Ausgleich stimmt nicht. Der gesamte ausgeglichene Betrag ist %1. Er sollte %2 sein.;ENU="The application is not correct. The total amount applied is %1; it should be %2."';
      Text004@1004 : TextConst 'DEU=Die Differenz ist %1. Sie muss %2 sein.;ENU=The total difference is %1. It must be %2.';
      BankAccReconLine@1005 : Record 274;
      BankAccReconLine2@1006 : Record 274;
      BankAcc@1007 : Record 270;
      BankAccLedgEntry@1008 : Record 271;
      CheckLedgEntry@1009 : Record 272;
      CheckLedgEntry2@1010 : Record 272;
      BankAccStmt@1011 : Record 275;
      BankAccStmtLine@1012 : Record 276;
      GLAccSetup@1105400005 : Record 98;
      PaymLedgEntry@1105400006 : Record 5001902;
      ForeignPaymentEntry@1105400007 : Record 5001941;
      BankAccPostingGr@1105400014 : Record 277;
      BankAccRecon@1105400015 : Record 273;
      GenJnlLine@1105400016 : Record 81;
      CurrencyExchRate@1105400021 : Record 330;
      GenJLine@1105400023 : Record 81;
      GenJnlLineDim@1105400024 : TEMPORARY Record 356;
      GenJnlPostLine@1105400017 : Codeunit 12;
      PaymGenJnlPostLine@1105400019 : Codeunit 5001910;
      DimMgt@1105400030 : Codeunit 408;
      Currency@1105400018 : Record 4;
      PaymentSetup@1105400027 : Record 5001901;
      LicPermission@1105400028 : Record 2000000043;
      SourceCodeSetup@1105400010 : Record 242;
      AppliedAmount@1013 : Decimal;
      TotalAmount@1014 : Decimal;
      TotalAppliedAmount@1015 : Decimal;
      TotalDiff@1016 : Decimal;
      ExchrateDiff@1105400004 : Decimal;
      PaymAmount@1105400009 : Decimal;
      Lines@1017 : Integer;
      NextEntryNo@1105400025 : Integer;
      Window@1018 : Dialog;
      Text1105400000@1105400000 : TextConst 'DEU=Transit-Transfer;ENU=Transit-Transfer';
      Text1105400001@1105400001 : TextConst 'DEU=" Buchung Belegnr. %1 kann keine WÑhrungsdifferenz sein ";ENU=" Posting Doc. No. %1 can''t be a currency difference "';
      Text1105400002@1105400002 : TextConst 'DEU=WÑhrungsdiff. aus Zahlungsdatei %1 in %2;ENU=Currency diff. from Paymentfile %1 %2';
      Text1105400003@1105400003 : TextConst 'DEU=WÑhrungsdiff. aus Zahlungsdatei %1 in %2;ENU=Currency diff. from Paymentfile %1 in %2';
      Transit@1105400008 : Boolean;

    PROCEDURE TransferBankAccounts@1105400000(BankAccApplLine@1105400000 : Record 274);
    BEGIN
      IF NOT Transit THEN
        EXIT;
      IF BankAccApplLine.Type=BankAccApplLine.Type::Difference THEN
        EXIT;
      SourceCodeSetup.GET;
      BankAcc.GET(BankAccApplLine."Bank Account No.");
      BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
      BankAccPostingGr.TESTFIELD(BankAccPostingGr."G/L Bank Account No.");
      BankAccPostingGr.TESTFIELD(BankAccPostingGr."G/L Bank Account Transit No.");
      BankAccRecon.GET(BankAccApplLine."Bank Account No.",BankAccApplLine."Statement No.");

      GenJnlLine.INIT;
      GenJnlLine.VALIDATE("Posting Date",BankAccRecon."Statement Date");
      GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
      GenJnlLine.VALIDATE("Account No.",BankAccPostingGr."G/L Bank Account Transit No.");
      GenJnlLine."Document No." := BankAccApplLine."Statement No.";
      GenJnlLine."Document Date" := BankAccApplLine."Transaction Date";

      CASE BankAccApplLine.Type OF
        BankAccApplLine.Type::"Bank Account Ledger Entry" :
          IF BankAccApplLine."Currency Code" <> '' THEN
            GenJnlLine.VALIDATE(Amount,-BankAccApplLine."Original Amount (LCY)")
          ELSE
            GenJnlLine.VALIDATE(Amount,-BankAccApplLine."Statement Amount");
        BankAccApplLine.Type::"Bank Account Ledger Entry",BankAccApplLine.Type::"Check Ledger Entry" :
          GenJnlLine.VALIDATE(Amount,-BankAccApplLine."Statement Amount");
        BankAccApplLine.Type::"Payment Entry" :
          GenJnlLine.VALIDATE(Amount,-BankAccApplLine."Original Amount (LCY)");
      END;
      GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::"G/L Account";
      GenJnlLine.VALIDATE("Bal. Account No.",BankAccPostingGr."G/L Bank Account No.");
      GenJnlLine."System-Created Entry" := TRUE;
      GenJnlLine.Description := STRSUBSTNO(Text1105400000);
      //eBay#003
      GenJnlLine.PayPal := BankAccApplLine."PayPal Import";
      GenJnlLine."Bank Account No. 2" := BankAccApplLine."Bank Account No.";
      GenJnlLine."Statement No." := BankAccApplLine."Statement No.";
      GenJnlLine."Statement Line No." :=BankAccApplLine."Statement Line No.";
      //eBay#003

      //A/H24-EOT/20140214/H0757_fÅrH0636
      GenJnlLine."External Document No." := BankAccApplLine."Order No.";
      IF BankAccApplLine."Amount (LCY)" > 0 THEN
        GenJnlLine."Payment Direction" := GenJnlLine."Payment Direction"::inbound
      ELSE
        GenJnlLine."Payment Direction" := GenJnlLine."Payment Direction"::outbound;
      GenJnlLine."Payment Method Code" := BankAccApplLine."Payment Method Code";
      //E/H24-EOT/20140214/H0757_fÅrH0636

      GenJnlLine."Source Code" := SourceCodeSetup."Bank Acc. Reconciliation";

      IF BankAccApplLine."Global Dimension 1 Code" <> '' THEN BEGIN
        GenJnlLineDim.RESET;
        GenJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
        GenJnlLineDim.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
        GenJnlLineDim.SETRANGE("Dimension Code",BankAccApplLine."Global Dimension 1 Code");
        IF NOT GenJnlLineDim.FIND('-') THEN BEGIN
          GenJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
          GenJnlLineDim."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLineDim."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLineDim."Journal Line No." := GenJnlLine."Line No.";
          GenJnlLineDim."Dimension Code" := BankAccApplLine."Global Dimension 1 Code";
          GenJnlLineDim.INSERT;
        END;
      END;

      IF BankAccApplLine."Global Dimension 2 Code" <> '' THEN BEGIN
        GenJnlLineDim.RESET;
        GenJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
        GenJnlLineDim.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
        GenJnlLineDim.SETRANGE("Dimension Code",BankAccApplLine."Global Dimension 2 Code");
        IF NOT GenJnlLineDim.FIND('-') THEN BEGIN
          GenJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
          GenJnlLineDim."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLineDim."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLineDim."Journal Line No." := GenJnlLine."Line No.";
          GenJnlLineDim."Dimension Code" := BankAccApplLine."Global Dimension 2 Code";
          GenJnlLineDim.INSERT;
        END;
      END;

      CLEAR(GenJnlPostLine);

      GenJnlPostLine.RUN(GenJnlLine);
    END;

    PROCEDURE "Posting Currencyapplication"@1105400001(BankAccApplLine@1105400000 : Record 274);
    VAR
      BankAmount@1105400002 : Decimal;
      "BankAmount (LCY)"@1105400001 : Decimal;
    BEGIN
      IF (BankAccApplLine.Type<>BankAccApplLine.Type::"Payment Entry") THEN
        EXIT;
      IF BankAccApplLine."Exchange Difference"=0 THEN
        EXIT;

      SourceCodeSetup.GET;
      BankAmount := 0;
      "BankAmount (LCY)" := 0;
      BankAcc.GET(BankAccApplLine."Bank Account No.");
      BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");

      IF BankAcc."Currency Code"=BankAccApplLine."Original Curreny Code" THEN
        ERROR(Text1105400001,BankAccApplLine."Document No.");

      Currency.GET(BankAccApplLine."Original Curreny Code");
      Currency.TESTFIELD("Realized Gains Acc.");
      Currency.TESTFIELD("Realized Losses Acc.");
      BankAccRecon.GET(BankAccApplLine."Bank Account No.",BankAccApplLine."Statement No.");

      GenJnlLine.INIT;
      GenJnlLine.VALIDATE("Posting Date",BankAccRecon."Statement Date");
      GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
      IF BankAccApplLine."Exchange Difference" <0 THEN
        GenJnlLine.VALIDATE("Account No.",Currency."Realized Losses Acc.")
      ELSE
        GenJnlLine.VALIDATE("Account No.",Currency."Realized Gains Acc.");
      GenJnlLine."Document No." := BankAccApplLine."Statement No.";
      GenJnlLine."Document Date" := BankAccApplLine."Transaction Date";
      GenJnlLine.VALIDATE("Currency Code",BankAcc."Currency Code");
      GenJnlLine.VALIDATE(Amount,-BankAccApplLine."Exchange Difference");
      GenJnlLine.Description := STRSUBSTNO(Text1105400002,BankAccApplLine."Payment No.",
                                                      BankAccApplLine."Original Curreny Code");
      GenJnlLine."Bal. Account Type" :=GenJnlLine."Account Type"::"G/L Account";
      GenJnlLine.VALIDATE("Bal. Account No.",BankAccPostingGr."G/L Bank Account No.");
      GenJnlLine."System-Created Entry" := TRUE;
      GenJnlLine."Source Code" := SourceCodeSetup."Bank Acc. Reconciliation";

      IF BankAccApplLine."Global Dimension 1 Code" <> '' THEN BEGIN
        GenJnlLineDim.RESET;
        GenJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
        GenJnlLineDim.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
        GenJnlLineDim.SETRANGE("Dimension Code",BankAccApplLine."Global Dimension 1 Code");
        IF NOT GenJnlLineDim.FIND('-') THEN BEGIN
          GenJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
          GenJnlLineDim."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLineDim."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLineDim."Journal Line No." := GenJnlLine."Line No.";
          GenJnlLineDim."Dimension Code" := BankAccApplLine."Global Dimension 1 Code";
          GenJnlLineDim.INSERT;
        END;
      END;

      IF BankAccApplLine."Global Dimension 2 Code" <> '' THEN BEGIN
        GenJnlLineDim.RESET;
        GenJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
        GenJnlLineDim.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
        GenJnlLineDim.SETRANGE("Dimension Code",BankAccApplLine."Global Dimension 2 Code");
        IF NOT GenJnlLineDim.FIND('-') THEN BEGIN
          GenJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
          GenJnlLineDim."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLineDim."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLineDim."Journal Line No." := GenJnlLine."Line No.";
          GenJnlLineDim."Dimension Code" := BankAccApplLine."Global Dimension 2 Code";
          GenJnlLineDim.INSERT;
        END;
      END;
      BankAmount := GenJnlLine.Amount;
      "BankAmount (LCY)" := GenJnlLine."Amount (LCY)";

      CLEAR(GenJnlPostLine);
      GenJnlPostLine.RUN(GenJnlLine);
      BankLedgerPosting(BankAccApplLine,BankAmount,"BankAmount (LCY)");
    END;

    PROCEDURE BankLedgerPosting@1105400003(BankAccApplLine@1105400000 : Record 274;BankAmount@1105400002 : Decimal;"BankAmount (LCY)"@1105400001 : Decimal);
    BEGIN
      SourceCodeSetup.GET;
      BankAccLedgEntry.RESET;
      BankAccLedgEntry.LOCKTABLE;
      //A/gob-adb/11.11.13
      //BankAccLedgEntry.FIND('+');
      BankAccLedgEntry.FINDLAST;
      //E/gob-adb/11.11.13
      NextEntryNo := BankAccLedgEntry."Entry No."+1;
      BankAcc.GET(BankAccApplLine."Bank Account No.");
      BankAcc.TESTFIELD(Blocked,FALSE);
      IF BankAccApplLine."Currency Code" <> '' THEN
        BankAcc.TESTFIELD("Currency Code",BankAccApplLine."Currency Code")
      ELSE BEGIN
        IF BankAccApplLine."Original Curreny Code" = '' THEN
          BankAcc.TESTFIELD("Currency Code",'')
        ELSE
          IF BankAcc."Currency Code" <> '' THEN
            BankAccApplLine.TESTFIELD("Original Curreny Code",BankAcc."Currency Code");
      END;
      BankAcc.TESTFIELD("Bank Acc. Posting Group");
      BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");

      BankAccLedgEntry.INIT;
      BankAccLedgEntry."Bank Account No." := BankAccApplLine."Bank Account No.";
      BankAccLedgEntry."Posting Date" := BankAccApplLine."Transaction Date";
      BankAccLedgEntry."Document Date" := BankAccRecon."Statement Date";
      BankAccLedgEntry."Document Type" := BankAccLedgEntry."Document Type"::Payment;
      BankAccLedgEntry."Document No." :=  BankAccRecon."Statement No.";

      BankAccLedgEntry."External Document No." := TakeBankLedgAppl(BankAccApplLine);

      BankAccLedgEntry."Bank Acc. Posting Group" := BankAcc."Bank Acc. Posting Group";
      BankAccLedgEntry."Global Dimension 1 Code" := BankAccApplLine."Global Dimension 1 Code";
      BankAccLedgEntry."Global Dimension 2 Code" := BankAccApplLine."Global Dimension 2 Code";
      BankAccLedgEntry."Entry No." := NextEntryNo;
      BankAccLedgEntry."Currency Code" := BankAcc."Currency Code";
      BankAccLedgEntry.Amount := BankAmount;
      BankAccLedgEntry."Amount (LCY)" := "BankAmount (LCY)";
      BankAccLedgEntry."User ID" := USERID;
      BankAccLedgEntry.Open := FALSE;
      BankAccLedgEntry."Remaining Amount" := 0;
      BankAccLedgEntry.Positive := BankAccLedgEntry.Amount > 0;

      IF (BankAccLedgEntry.Amount > 0) OR (BankAccLedgEntry."Amount (LCY)" > 0)
      THEN BEGIN
        BankAccLedgEntry."Debit Amount" := BankAccLedgEntry.Amount;
        BankAccLedgEntry."Credit Amount" := 0;
        BankAccLedgEntry."Debit Amount (LCY)" := BankAccLedgEntry."Amount (LCY)";
        BankAccLedgEntry."Credit Amount (LCY)" := 0;
      END ELSE BEGIN
        BankAccLedgEntry."Debit Amount" := 0;
        BankAccLedgEntry."Credit Amount" := -BankAccLedgEntry.Amount;
        BankAccLedgEntry."Debit Amount (LCY)" := 0;
        BankAccLedgEntry."Credit Amount (LCY)" := -BankAccLedgEntry."Amount (LCY)";
      END;
      BankAccLedgEntry.Description := STRSUBSTNO(Text1105400003,BankAccApplLine."Payment No.",
                                                BankAccApplLine."Original Curreny Code");
      BankAccLedgEntry."Source Code" := SourceCodeSetup."Bank Acc. Reconciliation";

      IF ((BankAccLedgEntry.Amount<>0) OR (BankAccLedgEntry."Amount (LCY)"<>0)) THEN
        BankAccLedgEntry.INSERT;
    END;

    PROCEDURE TakeBankLedgAppl@1105400002(BankAccReconLine@1105400000 : Record 274) : Text[20];
    VAR
      BankAccLedgEntry2@1105400001 : Record 271;
      CheckLedgEntry@1105400002 : Record 272;
      PaymLedgEntry@1105400003 : Record 5001902;
    BEGIN
      WITH BankAccReconLine DO BEGIN
        CASE Type OF
          Type::"Bank Account Ledger Entry":
            BEGIN
              BankAccLedgEntry2.RESET;
              BankAccLedgEntry2.SETCURRENTKEY("Bank Account No.",Open);
              BankAccLedgEntry2.SETRANGE("Bank Account No.","Bank Account No.");
              BankAccLedgEntry2.SETRANGE(Open,TRUE);
              BankAccLedgEntry2.SETFILTER(
                "Statement Status",'%1|%2',BankAccLedgEntry2."Statement Status"::Open,
                BankAccLedgEntry2."Statement Status"::"Bank Acc. Entry Applied");
              BankAccLedgEntry2.SETRANGE("Statement No.","Statement No.");
              BankAccLedgEntry2.SETRANGE("Statement Line No.","Statement Line No.");
              IF BankAccLedgEntry2.FIND('-') THEN
                EXIT(BankAccLedgEntry2."Document No.");
            END;
          Type::"Check Ledger Entry":
            BEGIN
              CheckLedgEntry.RESET;
              CheckLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
              CheckLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
              CheckLedgEntry.SETRANGE(Open,TRUE);
              CheckLedgEntry.SETFILTER(
                "Entry Status",'%1|%2',CheckLedgEntry."Entry Status"::Posted,
                CheckLedgEntry."Entry Status"::"Financially Voided");
              CheckLedgEntry.SETFILTER(
                "Statement Status",'%1|%2',CheckLedgEntry."Statement Status"::Open,
                CheckLedgEntry."Statement Status"::"Check Entry Applied");
              CheckLedgEntry.SETRANGE("Statement No.","Statement No.");
              CheckLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
              IF CheckLedgEntry.FIND('-') THEN
                EXIT(CheckLedgEntry."Document No.");
            END;
          Type::"Payment Entry":
            BEGIN
              PaymLedgEntry.RESET;
              PaymLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
              PaymLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
              PaymLedgEntry.SETRANGE(Open,TRUE);
              PaymLedgEntry.SETRANGE("Posting State",PaymLedgEntry."Posting State"::Posted);
              PaymLedgEntry.SETFILTER(
                "Statement Status",'%1|%2',PaymLedgEntry."Statement Status"::Open,
                PaymLedgEntry."Statement Status"::Closed);
              PaymLedgEntry.SETRANGE("Statement No.","Statement No.");
              PaymLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
              EXIT(PaymLedgEntry."Document No.");
            END;
        END;
      END;
      EXIT('');
    END;

    PROCEDURE AutocreateBankAccount@1105400029(BankAccReconLineLoc@1105400001 : Record 274);
    VAR
      CustBankAccount@1105400000 : Record 287;
      BankDocument@1105400002 : Record 5001911;
      BankBranchNo@1105400004 : Record 5001905;
      NoSeriesMgm@1105400003 : Codeunit 396;
    BEGIN
      IF LicPermission."Read Permission" <> LicPermission."Read Permission"::Yes THEN
        EXIT;

      IF NOT PaymentSetup."Autocreate bank account(cust.)" THEN
        EXIT;

      PaymentSetup.TESTFIELD("Auto Bank Account No.");

      IF NOT BankDocument.GET(BankAccReconLineLoc."Import Journal Entry No.") THEN
        EXIT;

      IF BankAccReconLineLoc."Bal. Account Type" <> BankAccReconLineLoc."Bal. Account Type"::Customer THEN
        EXIT;

      IF (BankDocument."Bank Orderer" = '') OR (BankDocument."Account Orderer" = '') THEN
        EXIT;

      CustBankAccount.RESET;
      CustBankAccount.SETRANGE("Customer No.",BankAccReconLineLoc."Bal. Account No.");
      CustBankAccount.SETRANGE("Bank Branch No.",BankDocument."Bank Orderer");
      CustBankAccount.SETRANGE("Bank Account No.",BankDocument."Account Orderer");
      IF CustBankAccount.FIND('-') THEN
        EXIT;

      IF NOT BankBranchNo.GET(BankDocument."Bank Orderer") THEN
        CLEAR(BankBranchNo);
      CustBankAccount.RESET;
      CustBankAccount.INIT;
      CustBankAccount."Customer No." := BankAccReconLineLoc."Bal. Account No.";
      CustBankAccount.Code := NoSeriesMgm.GetNextNo(PaymentSetup."Auto Bank Account No.",TODAY,TRUE);
      CustBankAccount."Bank Branch No." := BankDocument."Bank Orderer";
      CustBankAccount."Bank Account No." := BankDocument."Account Orderer";
      CustBankAccount.Name := BankBranchNo."Bank Name";
      CustBankAccount.City := BankBranchNo.City;
      CustBankAccount.INSERT;
    END;

    BEGIN
    {
      eBay1.03.22 eBay#001
      eBay1.03.27 eBay#002
      eBay1.06.13 eBay#003

      eBay#001 Aufruf eBayPayment.NichtZugeordneteZahlungen() im OnRun
      eBay#002 Aufruf eBayCustomer.öbernahmeBankverbindungen() im OnRun
      eBay#003 öbernahme PayPal & Auszugsinformationen in die Fibu-Buch.-Blattzeile

      +----------------------------------------------+
      | Copyright GOB Software & Systeme             |
      +----------------------------------------------+
      | FP Commerce                                  |
      |                                              |
      +----------------------------------------------+

      Version    Datum       Berater    PSP-Code   Bemerkung
      ---------------------------------------------------------------------------
      GOB1.00    11.11.13    gob-adb    P1143      Performance

      +----------------------------------------------+
      | Home24                                       |
      +----------------------------------------------+
      | Individualanpasungen                         |
      +----------------------------------------------+

      Projekt  Datum     KÅrzel   Bemerkung
      ------------------------------------------------------------------------------------------------------------------------------------
      H0757    14.02.14  H24-EOT  "Order No." durchschleifen in "External Document No." in CLE (im Voraus fÅr H0636)
                                  analog: "Payment Method Code" und "Payment Direction" - Achtung FremdwÑhrung: TBD
    }
    END.
  }
}

