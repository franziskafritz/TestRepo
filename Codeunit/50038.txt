OBJECT Codeunit 50038 Import PayPal STL
{
  OBJECT-PROPERTIES
  {
    Date=05.03.13;
    Time=20:19:27;
    Modified=Yes;
    Version List=CC;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            Code;
          END;

  }
  CODE
  {
    VAR
      eBaySetup@1000000011 : Record 5095800;
      InStr@1100409005 : InStream;
      F@1100409006 : File;
      ShowDialog@1000000002 : Boolean;
      FieldSeparator@1000000028 : Code[10];
      FullFileName@1000000021 : Text[1024];
      ShortFileName@1100409009 : Text[1024];
      Window@1000000008 : Dialog;
      TextImportData@1100409003 : TextConst 'DEU=Importiere Daten von   #2#############################\;ENU=Importing data from     ##2############################\';
      TextImporNo@1100409002 : TextConst 'DEU=Importnr.                         #3##################\;ENU=Import No.                       #3##################\';
      TextPayPalData@1100409001 : TextConst 'DEU=PayPalDaten\;ENU=PayPal Data\';
      TextCount@1100409000 : TextConst 'DEU=Anzahl verarbeitet                #4##################\;ENU=Amount processed                #4##################\';
      TextDone@1000000034 : TextConst 'DEU=%1 Datens„tze eingelesen und %2 Bankkontoabstimmungszeilen erstellt.;ENU=%1 records have been imported und %2 Bank Acc. Reconciliation Line created.';
      NoOfLinesRead@1000000033 : Integer;
      TextProgress@1000000019 : TextConst 'DEU=@9@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=@9@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      DateTime@1000000027 : DateTime;
      TextFileNotFound@1000000036 : TextConst 'DEU=Datei "%1" wurde nicht gefunden.;ENU=File "%1" was not found.';
      Number@1000000012 : Code[3];
      TextConfirmImport@1000000005 : TextConst 'DEU=M”chten Sie die PayPal Daten importieren?;ENU=Do you want to import PayPal data?';
      PayPalDir@1100409008 : Text[1024];
      FTPAutomation@1000000000 : Automation "{42A8A505-1CD3-4CA4-A7EA-E6EBCC481DDC} 1.0:{B32A3669-654B-4CEF-980E-C7753F1FA524}:'FTPNav'.FTPNavClass";

    PROCEDURE Code@1100409004();
    BEGIN
      ShowDialog := GUIALLOWED;
      FieldSeparator := ',';

      //IF GUIALLOWED THEN
      //  IF NOT CONFIRM(TextConfirmImport,FALSE) THEN
      //    EXIT;

      //DYN0010 08.08.12  nas use FTP automation >>
      //DownloadFiles();
      DownloadFTP;
      //DYN0010 08.08.12  nas use FTP automation <<

      ImportFiles();
    END;

    PROCEDURE ImportFiles@1100409007();
    VAR
      GPSetup@1100409003 : Record 50000;
      Files@1100409000 : Record 2000000022;
      d1@1100409001 : Date;
      d2@1100409002 : Date;
    BEGIN
      FieldSeparator := ',';
      Files.SETRANGE(Path,PayPalDir);
      Files.SETFILTER(Name,'@STL*.CSV');
      //Files.SETFILTER(Name,'@TRR-20120807.01.008.csv');
      Files.SETRANGE("Is a file",TRUE);
      IF Files.FIND('-') THEN
        REPEAT
          ShortFileName := Files.Name;
          GPSetup.FIND('-');
          d1 := GetDateFromFileName(GPSetup."PayPal Last Imported Filename");
          d2 := GetDateFromFileName(ShortFileName);
          IF (d2 > d1)
          //OR (ShortFileName = 'TRR-20120807.01.008.csv')
          THEN BEGIN // import new files only
            FullFileName := PayPalDir + Files.Name;
            IF ImportSingleFile() THEN BEGIN
              RENAME(FullFileName,PayPalDir + ShortFileName + '.archive');
              GPSetup.FIND('-');
              GPSetup."PayPal Last Imported Filename" := ShortFileName;
              GPSetup.MODIFY;
              COMMIT;
            END;
          END;
        UNTIL Files.NEXT = 0;
    END;

    PROCEDURE ImportSingleFile_External@1100409005(_FullFileName@1100409004 : Text[1024]);
    VAR
      GPSetup@1100409003 : Record 50000;
      Files@1100409000 : Record 2000000022;
      d1@1100409001 : Date;
      d2@1100409002 : Date;
    BEGIN
      ShowDialog := GUIALLOWED;
      FieldSeparator := ',';

      FullFileName := _FullFileName;
      IF ImportSingleFile() THEN
        RENAME(FullFileName,FullFileName + '.archive')
      ELSE
        ERROR('Failed');
    END;

    PROCEDURE ImportSingleFile@1000000006() : Boolean;
    VAR
      ImportPaymentDetails@1100409000 : Codeunit 50039;
      FileLength@1000000002 : Integer;
      bi@1100409001 : BigInteger;
    BEGIN
      DateTime := CURRENTDATETIME();
      Number := '000';

      eBaySetup.LOCKTABLE;
      eBaySetup.FIND('-');
      eBaySetup."PayPal Import/ Document No." := INCSTR(eBaySetup."PayPal Import/ Document No.");
      eBaySetup.MODIFY();

      IF NOT OpenFile() THEN
        EXIT(FALSE);

      NoOfLinesRead := 0;
      FileLength := F.LEN;

      WHILE NOT InStr.EOS DO BEGIN
        ImportCSVLine(InStr);
        NoOfLinesRead += 1;
        IF ShowDialog THEN BEGIN
          bi := F.POS;
          Window.UPDATE(9,bi * 9999 DIV FileLength);
          Window.UPDATE(3,eBaySetup."PayPal Import/ Document No.");
          Window.UPDATE(4,NoOfLinesRead);
        END;
      END;

      IF ShowDialog THEN BEGIN
        Window.UPDATE(9,9999);
        Window.UPDATE(3, '');
      END;

      F.CLOSE();
      IF ShowDialog THEN
        Window.CLOSE();

      ImportPaymentDetails.CreateBankAccReconciliations('PAYPAL',1);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ImportCSVLine@1000000005(inStr@1000000001 : InStream);
    VAR
      eBayImportPayPal@1100409001 : Record 5095913;
      eBayImportPayPal2@1100409002 : Record 5095913;
      dummy@1000000000 : Text[1024];
      Amount@1100409003 : Text[50];
      IsCredit@1100409000 : Boolean;
      i@1100409004 : Integer;
      GenLedgSetup@1000000002 : Record 98;
      InCurrency@1000000003 : Code[20];
    BEGIN
      IF NoOfLinesRead = 0 THEN BEGIN
        FOR i := 1 TO 30 DO
          dummy := ReadText(inStr);
        EXIT;
      END;

      dummy := ReadText(inStr);                                           // Line Type
      IF dummy <> 'SB' THEN
        EXIT;

      CLEAR(eBayImportPayPal);

      eBayImportPayPal."Import/Document No." := eBaySetup."PayPal Import/ Document No.";
      eBayImportPayPal."Line No." := GenerateNumber(Number);
      eBayImportPayPal."Import at" := DateTime;
      eBayImportPayPal."Import by" := COPYSTR(USERID(),1,MAXSTRLEN(eBayImportPayPal."Import by"));
      eBayImportPayPal."Account Code" := 'PAYPAL';
      eBayImportPayPal."Bank PayPal-E-Mail" := GetPayPalEmail(); // paypal_api2.fp-commerce.de

      eBayImportPayPal."Transaction Code" :=
        COPYSTR(ReadText(inStr),1,MAXSTRLEN(eBayImportPayPal."Transaction Code"));             // Transaktionscode
      eBayImportPayPal.InvoiceID := COPYSTR(ReadText(inStr),1,MAXSTRLEN(eBayImportPayPal.InvoiceID));
      // Rechnungsnr.
      eBayImportPayPal."Reference Transaction ID" :=
        COPYSTR(ReadText(inStr),1,MAXSTRLEN(eBayImportPayPal."Reference Transaction ID"));     // Referenznr.
      dummy := ReadText(inStr);                                           // Referencenr. Type
                                                                          // Transaktionsereigniscode -> Name
      eBayImportPayPal.Name := COPYSTR(TranslateEventCode(ReadText(inStr)),1,MAXSTRLEN(eBayImportPayPal.Name));
      eBayImportPayPal.Type := eBayImportPayPal.Name;

      dummy := ReadText(inStr);                                           // Datum/Zeit/Zone

      dummy := ReadText(inStr);                                           // Datum/Zeit/Zone

      EVALUATE(eBayImportPayPal."Payed at",COPYSTR(dummy,9,2) + COPYSTR(dummy,6,2) + COPYSTR(dummy,1,4) + ' ' +
                                           COPYSTR(dummy,12,2) + COPYSTR(dummy,15,2) + COPYSTR(dummy,18,2));


      IsCredit := UPPERCASE(ReadText(inStr)) = 'CR';                      // CR / DR (Credit / Debit)
      Amount := ReadText(inStr);                                          // Brutto
      // chrmu 240912
      InCurrency := ReadText(inStr);
      GenLedgSetup.GET;
      IF GenLedgSetup."LCY Code" = InCurrency THEN InCurrency := '';
      eBayImportPayPal.VALIDATE(Currency,InCurrency);                // W„hrung
      // chrmu 240912
      eBayImportPayPal."Amount (Gross)" := EvalAmount(Amount,eBayImportPayPal.Currency,IsCredit);

      IsCredit := UPPERCASE(ReadText(inStr)) = 'CR';                      // CR / DR (Credit / Debit)
      Amount := ReadText(inStr);                                          // Gebhr
      dummy := ReadText(inStr);                                           // W„hrung
      eBayImportPayPal."Amount (Costs)" := EvalAmount(Amount,dummy,IsCredit);

      IF (dummy = eBayImportPayPal.Currency) OR (eBayImportPayPal."Amount (Gross)" = 0) OR (eBayImportPayPal."Amount (Costs)" = 0) THEN
        eBayImportPayPal."Amount (Net)":= eBayImportPayPal."Amount (Gross)" + eBayImportPayPal."Amount (Costs)";

      dummy := ReadText(inStr);                                           // Benutzerdefiniertes Feld

      eBayImportPayPal."Ebay User-Id" := COPYSTR(ReadText(inStr), 1, MAXSTRLEN(eBayImportPayPal."Ebay User-Id"));

      dummy := ReadText(inStr);                                           // Zahlungsverfolgungs-ID

      eBayImportPayPal2.RESET;
      eBayImportPayPal2.SETRANGE("Transaction Code",eBayImportPayPal."Transaction Code");
      IF NOT eBayImportPayPal2.ISEMPTY THEN
        eBayImportPayPal."Double Import" := TRUE;

      eBayImportPayPal.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ReadText@1000000033(InStr@1000000001 : InStream) value : Text[1024];
    VAR
      GeneralMgt@1100409000 : Codeunit 11501;
      c@1000000000 : Text[2];
    BEGIN
      value := '';
      WHILE NOT InStr.EOS DO BEGIN
        InStr.READTEXT(c,1);

        IF ((c = FieldSeparator) OR (c = '')) THEN BEGIN
          IF STRLEN(value) > 0 THEN BEGIN
            IF COPYSTR(value,1,1) = '"' THEN
              value := COPYSTR(value,2,MAXSTRLEN(value));

            IF COPYSTR(value,STRLEN(value),1) = '"' THEN
              value := COPYSTR(value,1,STRLEN(value) - 1);
          END;

          EXIT(GeneralMgt.Ansi2Ascii(value));

        END ELSE IF STRLEN(value) < 1024 THEN
          value := value + c;
      END;
    END;

    LOCAL PROCEDURE OpenFile@1000000007() : Boolean;
    BEGIN
      IF ShowDialog THEN
        Window.OPEN(TextPayPalData + TextImportData + TextImporNo + TextCount + TextProgress);

      IF NOT F.OPEN(FullFileName) THEN
        EXIT(FALSE);
      F.CREATEINSTREAM(InStr);

      IF ShowDialog THEN
        Window.UPDATE(2,FullFileName);

      EXIT(TRUE);
    END;

    PROCEDURE TranslateEventCode@1100409002(code@1100409000 : Code[20]) : Text[50];
    VAR
      n@1100409001 : Integer;
    BEGIN
      EVALUATE(n,COPYSTR(code,2));
      CASE TRUE OF
        (n >= 0) AND (n <= 14):
          EXIT('User to user payment');
        (n = 15):
          EXIT('Third Party Payout');
        (n = 16):
          EXIT('Third Party Recoupment');
        (n >= 100) AND (n <= 112):
          EXIT('Non payment fees');
        (n >= 200) AND (n <= 202):
          EXIT('Currency conversion');
        (n >= 300) AND (n <= 303):
          EXIT('ACH deposit');
        (n >= 400) AND (n <= 401):
          EXIT('ACH withdrawal');
        (n >= 500) AND (n <= 505):
          EXIT('Debit card');
        (n >= 600) AND (n <= 701):
          EXIT('Credit Card');
        (n >= 800) AND (n <= 812):
          EXIT('Bonus');
        (n >= 900) AND (n <= 904):
          EXIT('Incentive');
        (n = 1000):
          EXIT('Bill Pay');
        (n >= 1100) AND (n <= 1117):
          EXIT('Reversal');
        (n >= 1200) AND (n <= 1208):
          EXIT('Adjustment');
        (n >= 1300) AND (n <= 1302):
          EXIT('Auth');
        (n = 1400):
          EXIT('Dividend');
        (n >= 1500) AND (n <= 1503):
          EXIT('Temp Hold');
        (n = 1600):
          EXIT('Buyer credit');
        (n = 1601):
          EXIT('BML Credit');
        (n >= 1700) AND (n <= 1701):
          EXIT('Non-bank Withdrawal');
        (n = 1800):
          EXIT('Buyer Credit');
        (n = 1801):
          EXIT('BML Withdrawal');
        (n = 1900):
          EXIT('Account correction');
        (n = 2000):
          EXIT('General Intra-Account Transfer');
        (n = 2001):
          EXIT('Consolidation Transfer');
        (n = 2101):
          EXIT('General Hold');
        (n = 2102):
          EXIT('General Hold Release');
        (n = 2103):
          EXIT('Reserve Hold');
        (n = 2104):
          EXIT('Reserve Release');
        (n = 2105):
          EXIT('Payment Review Hold');
        (n = 2106):
          EXIT('Payment Review Release');
        (n = 2107):
          EXIT('eBay Hold');
        (n = 2108):
          EXIT('eBay Hold Release');
        (n = 2201):
          EXIT('Digital Good Purchase');
        (n = 9700):
          EXIT('Accounts Recievable for Shipping');
        TRUE:
          EXIT(code);
      END;
    END;

    PROCEDURE IsReserveTransaction@1100409001(eBayImportPayPal@1100409000 : Record 5095913) : Boolean;
    BEGIN
      EXIT(eBayImportPayPal.Type IN ['Reserve Hold','Reserve Release']);
    END;

    PROCEDURE EvalAmount@1100409017(Amount@1100409000 : Text[50];CurrencyCode@1100409003 : Code[10];Positive@1100409001 : Boolean) Result : Decimal;
    VAR
      GLSetup@1100409004 : Record 98;
      Currency@1100409005 : Record 4;
      GenLedgSetup@1000000000 : Record 98;
    BEGIN
      // chrmu 240912
      GenLedgSetup.GET;
      IF (CurrencyCode = '') OR (CurrencyCode = GenLedgSetup."LCY Code") THEN BEGIN
        GLSetup.GET;
        GLSetup.TESTFIELD("Amount Rounding Precision");
        IF Amount = '' THEN
          EXIT(0);
        EVALUATE(Result,Amount);
        Result := Result * GLSetup."Amount Rounding Precision";
        IF NOT Positive THEN
          Result := Result * (-1);
      END ELSE BEGIN
      Currency.GET(CurrencyCode);
      Currency.TESTFIELD("Amount Rounding Precision");

      IF Amount = '' THEN
        EXIT(0);

      EVALUATE(Result,Amount);
      Result := Result * Currency."Amount Rounding Precision";
      IF NOT Positive THEN
        Result := Result * (-1);
      END;
      // chrmu 240912
    END;

    PROCEDURE GetPayPalEmail@1100409000() : Text[250];
    VAR
      BankAccount@1100409000 : Record 270;
    BEGIN
      IF NOT BankAccount."PayPal Account" THEN BEGIN
        BankAccount.RESET;
        BankAccount.SETRANGE("PayPal Account",TRUE);
        BankAccount.FINDFIRST;
      END;
      EXIT(BankAccount."PayPal-E-Mail");
    END;

    PROCEDURE ShellExec@1140018(Cmd@1140001 : Text[1024];WindowStyle@1140002 : Integer;WaitOnReturn@1140003 : Boolean;CurrentDir@1140006 : Text[1024]) ExitCode : Integer;
    VAR
      WSH_Shell@1140000 : Automation "{F935DC20-1CF0-11D0-ADB9-00C04FD58A0B} 1.0:{72C24DD5-D70A-438B-8A42-98424B88AFB8}:'Windows Script Host Object Model'.WshShell";
    BEGIN
      TestCmdQuotes(Cmd);
      IF ISSERVICETIER THEN
        ERROR('CU "Import PayPal STL" - ENVIRON is not supported by RTC')
      ELSE
        Cmd := STRSUBSTNO('%1 /c "%2"',ENVIRON('COMSPEC'),Cmd);
      IF ISCLEAR(WSH_Shell) THEN
        CREATE(WSH_Shell,FALSE);
      IF CurrentDir <> '' THEN
        WSH_Shell.CurrentDirectory := CurrentDir;
      ExitCode := WSH_Shell.Run(Cmd,WindowStyle,WaitOnReturn);
      CLEAR(WSH_Shell);
    END;

    LOCAL PROCEDURE TestCmdQuotes@1140017(VAR Cmd@1140005 : Text[1024]);
    VAR
      TestCmd@1140006 : Text[1024];
      TmpCmd@1140004 : Text[1024];
      PreQuoteCmd@1140003 : Text[1024];
      AfterQuoteCmd@1140002 : Text[1024];
      Pos1@1140001 : Integer;
      Pos2@1140000 : Integer;
    BEGIN
      TestCmd := Cmd;
      Cmd := '';
      WHILE STRPOS(TestCmd,'"') <> 0 DO BEGIN
        Pos1 := STRPOS(TestCmd,'"');
        PreQuoteCmd := COPYSTR(TestCmd,1,Pos1 - 1);
        TmpCmd := COPYSTR(TestCmd,Pos1 + 1);
        IF STRPOS(TmpCmd,'"') <> 0 THEN BEGIN
          Pos2 := STRPOS(TmpCmd,'"');
          AfterQuoteCmd := COPYSTR(TmpCmd,Pos2 + 1);
          TmpCmd := COPYSTR(TmpCmd,1,Pos2 - 1);
          TestCmd := AfterQuoteCmd;
          IF STRPOS(TmpCmd,' ') <> 0 THEN
            TmpCmd := '"' + TmpCmd + '"';
        END ELSE BEGIN
          TestCmd := TmpCmd;
          TmpCmd :=  '"' + TmpCmd;
          IF STRPOS(TestCmd,'"') = 0 THEN
            TestCmd := '';
        END;
        Cmd += PreQuoteCmd + TmpCmd;
      END;
      Cmd += TestCmd;
    END;

    PROCEDURE GetDateFromFileName@1100409009(FileName@1100409000 : Text[1024]) : Date;
    VAR
      day@1100409003 : Integer;
      month@1100409002 : Integer;
      year@1100409001 : Integer;
    BEGIN
      // File name example:
      // STL-20111228.01.006.CSV
      EVALUATE(day,COPYSTR(FileName,11,2));
      EVALUATE(month,COPYSTR(FileName,9,2));
      EVALUATE(year,COPYSTR(FileName,5,4));
      EXIT(DMY2DATE(day,month,year));
    END;

    PROCEDURE DownloadFiles@1100409003();
    VAR
      GPSetup@1100409005 : Record 50000;
      StartDate@1100409001 : Date;
      EndDate@1100409002 : Date;
      LastImportedFile@1100409003 : Text[250];
      s@1100409007 : Text[250];
      ScriptFileName@1100409000 : Text[1024];
      BatFileName@1100409004 : Text[1024];
    BEGIN
      // ---------------- Get Setup
      GPSetup.FIND('-');
      GPSetup.TESTFIELD("PayPal Local Path"); // Example:    D:\
      GPSetup.TESTFIELD("PayPal Last Imported Filename"); // For example: STL-20111228.01.006.CSV
      GPSetup.TESTFIELD("PayPal SFTP URL"); // reports.paypal.com
      GPSetup.TESTFIELD("PayPal SFTP Login"); // sftp://sftpBg_paypalfp-commerce.de
      GPSetup.TESTFIELD("PayPal SFTP Password"); // Home24_2012
      GPSetup.TESTFIELD("PayPal SFTP Host Key"); // 87:a4:aa:4a:ef:bb:52:07:c2:6d:39:97:42:35:74:fe

      PayPalDir := GPSetup."PayPal Local Path";
      LastImportedFile := GPSetup."PayPal Last Imported Filename";
      StartDate := GetDateFromFileName(LastImportedFile) + 1;
      EndDate := TODAY;

      // ---------------- Create Script File
      ScriptFileName := PayPalDir + 'PayPal.txt';
      F.CREATE(ScriptFileName);
      F.TEXTMODE(TRUE);
      F.WRITEMODE(TRUE);
      F.WRITE('option batch abort');
      F.WRITE('option confirm off');
      F.WRITE('open sftp://' +
        GPSetup."PayPal SFTP Login" +
        ':' + GPSetup."PayPal SFTP Password" + '@' +
        GPSetup."PayPal SFTP URL" +
        ' -hostkey="ssh-rsa 1024 ' +
        GPSetup."PayPal SFTP Host Key" +
        '"');
      F.WRITE('cd ppreports/outgoing');
      F.WRITE('option transfer binary');

      WHILE StartDate <= EndDate DO BEGIN
        s := FORMAT(StartDate,0,'<Year4><Month,2><Day,2>');
        F.WRITE('get STL-' + s + '.01.006.CSV ' + PayPalDir);
        StartDate := StartDate + 1;
      END;

      F.WRITE('close');
      F.WRITE('exit');
      F.CLOSE;
      CLEAR(F);

      // ---------------- Create Batch File
      BatFileName := PayPalDir + 'GetPayPalFiles.bat';
      F.CREATE(BatFileName);
      F.TEXTMODE(TRUE);
      F.WRITEMODE(TRUE);

      F.WRITE('@del WinSCP.log >NUL 2>NUL');
      F.WRITE('@del GetPayPalFiles.log >NUL 2>NUL');
      F.WRITE('@winscp.exe /console /script=PayPal.txt /log=WinSCP.log 2>GetPayPalFiles.log');
      F.CLOSE;

      // ---------------- Execute Batch File
      ShellExec('GetPayPalFiles.bat',0,TRUE,PayPalDir);

      // ---------------- Erase Files
      ERASE(ScriptFileName);
      ERASE(BatFileName);
    END;

    PROCEDURE GenerateNumber@1100409006(VAR n@1100409000 : Code[3]) : Code[3];
    VAR
      c1@1100409001 : Code[1];
      c2@1100409002 : Code[1];
      c3@1100409003 : Code[1];
    BEGIN
      c1 := COPYSTR(n,1,1);
      c2 := COPYSTR(n,2,1);
      c3 := COPYSTR(n,3,1);
      IF NOT IncChar(c3) THEN
        IF NOT IncChar(c2) THEN BEGIN
          IF NOT IncChar(c1) THEN BEGIN
            ERROR('Could not generate number in CU 50038 -> GenerateNumber(): ' + n);
          END ELSE BEGIN
            c2 := '0';
            c3 := '0';
          END;
        END ELSE BEGIN
          c3 := '0';
        END;

      n := c1 + c2 + c3;
      EXIT(n);
    END;

    PROCEDURE IncChar@1100409008(VAR c@1100409000 : Code[1]) Success : Boolean;
    VAR
      ch@1100409001 : Char;
    BEGIN
      IF c = '9' THEN
        c := 'A'
      ELSE IF c = 'Z' THEN
        EXIT(FALSE)
      ELSE BEGIN
        EVALUATE(ch,c);
        ch += 1;
        c := FORMAT(ch);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE DownloadFTP@1000000000();
    VAR
      FTPClient@1000000000 : Automation "{42A8A505-1CD3-4CA4-A7EA-E6EBCC481DDC} 1.0:{B32A3669-654B-4CEF-980E-C7753F1FA524}:'FTPNav'.FTPNavClass";
      GPSetup@1000000001 : Record 50000;
    BEGIN
      //DYN0010 08.08.12  nas use FTP automation >>


      GPSetup.GET;
      GPSetup.TESTFIELD("PayPal Local Path"); // Example:    D:\
      //GPSetup.TESTFIELD("PayPal SFTP URL"); // reports.paypal.com
      //GPSetup.TESTFIELD("PayPal SFTP Login"); // sftp://sftpBg_paypalfp-commerce.de
      //GPSetup.TESTFIELD("PayPal SFTP Password"); // Home24_2012
      PayPalDir := GPSetup."PayPal Local Path";

      EXIT; // now thru general ftp download chrmu 15.11.12
      {
      IF ISCLEAR(FTPClient) THEN CREATE(FTPClient);
      IF NOT FTPClient.DownloadDirectory(
              GPSetup."PayPal SFTP URL",
              GPSetup."PayPal SFTP Login",
              GPSetup."PayPal SFTP Password",
              GPSetup."PayPal SFTP Folder",
              GPSetup."PayPal Local Path",
              TRUE,FALSE)
      THEN
        ERROR(FTPClient.GetLastErrorText);
      //DYN0010 08.08.12  nas use FTP automation <<
      }
    END;

    BEGIN
    {
      CC01  15.02.2012  DEBER.YM  Codeunit created

      // ---------------------------------------------------------------------------------------------------------------------------
      // This CU downloads PayPal STL-Files in CSV format from PayPal SFTP server into a local folder.
      // Afterwards transactions from downloaded files are imported into "eBayImportPayPal" table.
      // After importing transactions from a each single File, "Bank Acc. Reconciliation" is created,
      // and the file is renamed (*.archive)
      // ---------------------------------------------------------------------------------------------------------------------------
      // SFTP Client "WinSCP" must be installed and the path to its executable must be added into system PATH variable.
      // http://winscp.net
      //
      // Table 50000:
      //   PayPal SFTP URL:               reports.paypal.com
      //   PayPal SFTP Login
      //   PayPal SFTP Password
      //   PayPal SFTP Host Key:          87:a4:aa:4a:ef:bb:52:07:c2:6d:39:97:42:35:74:fe
      //   PayPal Last Imported Filename: contains the name of the last imported file, i.e.: STL-20120110.01.006.CSV
      //                                  it must be initialized manually before the first run.
      //   PayPal Local Path:             contains the full address to a local folder for saving downloaded files, i.e.: D:\PayPal\
      //
      // Table "eBaySetup":
      //   PayPal Import/ Document No.:   contains a value that is used in "eBayImportPayPal" table, i.e.: PP11000293
      // ---------------------------------------------------------------------------------------------------------------------------

      //060712  chrmu cut read string to the allowed amount
      DYN0010 08.08.12  nas use FTP automation
      DYN0011 24.09.12  chrmu - fix to import the files manually till ftp component test
      DYN0011 25.11.12  chrmu - disable ftp download
    }
    END.
  }
}

