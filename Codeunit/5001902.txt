OBJECT Codeunit 5001902 Paym Acc. Recon. Apply Entries
{
  OBJECT-PROPERTIES
  {
    Date=07.06.07;
    Time=12:00:00;
    Version List=AR5.00;
  }
  PROPERTIES
  {
    Permissions=TableData 271=rm,
                TableData 5001902=rim;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text1105400000@1105400000 : TextConst 'DEU=%1 muss %2 oder %3 sein.;ENU=%1 must be %2 or %3.';
      BankAccLedgEntry@1105400001 : Record 271;
      Corrected@1105400002 : Boolean;
      BankAccount@1105400003 : Record 270;

    PROCEDURE ToggleReconNo@1105400004(VAR PaymLedgEntry@1105400000 : Record 5001902;VAR BankAccReconLine@1105400001 : Record 274;VAR ChangeAmount@1105400002 : Boolean);
    BEGIN
      BankAccLedgEntry.LOCKTABLE;
      PaymLedgEntry.LOCKTABLE;
      BankAccReconLine.LOCKTABLE;
      BankAccount.GET(BankAccReconLine."Bank Account No.");
      BankAccReconLine.FIND;
      IF PaymLedgEntry."Statement No." = '' THEN BEGIN
        SetReconNo(PaymLedgEntry,BankAccReconLine);
        IF BankAccount."Currency Code"<>PaymLedgEntry."Currency Code" THEN
          BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" - PaymLedgEntry."Amount (LCY)"
        ELSE
          BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" - PaymLedgEntry.Amount;
        BankAccReconLine."Applied Entries" := BankAccReconLine."Applied Entries" + 1;
      END ELSE BEGIN
        RemoveReconNo(PaymLedgEntry,BankAccReconLine,TRUE);
        IF BankAccount."Currency Code"<>PaymLedgEntry."Currency Code" THEN
          BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" + PaymLedgEntry."Amount (LCY)"
        ELSE
          BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" + PaymLedgEntry.Amount;
        BankAccReconLine."Applied Entries" := BankAccReconLine."Applied Entries" - 1;
      END;
      IF ChangeAmount THEN
        BankAccReconLine.VALIDATE("Statement Amount",BankAccReconLine."Applied Amount")
      ELSE
        BankAccReconLine.VALIDATE("Statement Amount");
      BankAccReconLine.MODIFY;
    END;

    PROCEDURE SetReconNo@1105400005(VAR PaymLedgEntry@1105400000 : Record 5001902;VAR BankAccReconLine@1105400001 : Record 274);
    BEGIN
      PaymLedgEntry.TESTFIELD(Open,TRUE);
      IF (PaymLedgEntry."Statement Status" <> PaymLedgEntry."Statement Status"::Open) AND
         (PaymLedgEntry."Statement Status" <>
          PaymLedgEntry."Statement Status"::Closed)
      THEN
        PaymLedgEntry.FIELDERROR(
          "Statement Status",STRSUBSTNO(Text1105400000,
          PaymLedgEntry.FIELDCAPTION("Statement Status"),
          PaymLedgEntry."Statement Status"::Open,
          PaymLedgEntry."Statement Status"::Closed));
          PaymLedgEntry.TESTFIELD("Statement No.",'');
          PaymLedgEntry.TESTFIELD("Statement Line No.",0);
          PaymLedgEntry.TESTFIELD("Posting State",PaymLedgEntry."Posting State"::Posted);
          PaymLedgEntry.TESTFIELD("Bank Account No.",BankAccReconLine."Bank Account No.");
          PaymLedgEntry."Statement Status" := PaymLedgEntry."Statement Status"::"Regulation Bank Ledger Entry";
          PaymLedgEntry."Statement No." := BankAccReconLine."Statement No.";
          PaymLedgEntry."Statement Line No." := BankAccReconLine."Statement Line No.";
          PaymLedgEntry."Posting Date" := BankAccReconLine."Transaction Date";
          PaymLedgEntry.MODIFY;

          BankAccLedgEntry.GET(PaymLedgEntry."Bank Ledger Entry No.");
          BankAccLedgEntry.TESTFIELD(Open,TRUE);
          IF (BankAccLedgEntry."Statement Status" <> BankAccLedgEntry."Statement Status"::Open) AND
            (BankAccLedgEntry."Statement Status" <>
            BankAccLedgEntry."Statement Status"::Closed)
          THEN
            BankAccLedgEntry.FIELDERROR(
            "Statement Status",STRSUBSTNO(Text1105400000,
            BankAccLedgEntry.FIELDCAPTION("Statement Status"),
            BankAccLedgEntry."Statement Status"::Open,
            BankAccLedgEntry."Statement Status"::Closed));
          BankAccLedgEntry.TESTFIELD("Statement No.",'');
          BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
          BankAccLedgEntry."Statement Status" :=
          BankAccLedgEntry."Statement Status"::Closed;
          BankAccLedgEntry."Statement No." := '';
          BankAccLedgEntry."Statement Line No." := 0;
          BankAccLedgEntry.MODIFY;
    END;

    PROCEDURE RemoveReconNo@1105400006(VAR PaymLedgEntry@1105400000 : Record 5001902;VAR BankAccReconLine@1105400001 : Record 274;Test@1105400002 : Boolean);
    VAR
      PaymLedgEntry2@1105400003 : Record 5001902;
    BEGIN
      PaymLedgEntry.TESTFIELD(Open,TRUE);
      IF Test THEN BEGIN
        PaymLedgEntry.TESTFIELD(
          "Statement Status",PaymLedgEntry."Statement Status"::"Regulation Bank Ledger Entry");
        PaymLedgEntry.TESTFIELD("Statement No.",BankAccReconLine."Statement No.");
        PaymLedgEntry.TESTFIELD("Statement Line No.",BankAccReconLine."Statement Line No.");
      END;
      PaymLedgEntry.TESTFIELD("Bank Account No.",BankAccReconLine."Bank Account No.");
      PaymLedgEntry."Statement Status" := PaymLedgEntry."Statement Status"::Open;
      PaymLedgEntry."Statement No." := '';
      PaymLedgEntry."Statement Line No." := 0;
      PaymLedgEntry.MODIFY;

      PaymLedgEntry2.RESET;
      PaymLedgEntry2.SETCURRENTKEY("Bank Ledger Entry No.");
      PaymLedgEntry2.SETRANGE(
        "Bank Ledger Entry No.",PaymLedgEntry."Bank Ledger Entry No.");
      PaymLedgEntry2.SETRANGE(
        "Statement Status",PaymLedgEntry."Statement Status"::"Regulation Bank Ledger Entry");
      IF PaymLedgEntry2.ISEMPTY THEN BEGIN
        BankAccLedgEntry.GET(PaymLedgEntry."Bank Ledger Entry No.");
        BankAccLedgEntry.TESTFIELD(Open,TRUE);
        IF Test THEN BEGIN
          BankAccLedgEntry.TESTFIELD(
            "Statement Status",BankAccLedgEntry."Statement Status"::Closed);
          BankAccLedgEntry.TESTFIELD("Statement No.",'');
          BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
        END;
        BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Open;
        BankAccLedgEntry."Statement No." := '';
        BankAccLedgEntry."Statement Line No." := 0;
        BankAccLedgEntry.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

