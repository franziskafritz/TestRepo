OBJECT Codeunit 5327153 ACFWM Update
{
  OBJECT-PROPERTIES
  {
    Date=04.04.13;
    Time=00:00:00;
    Version List=ACF3.02.03;
  }
  PROPERTIES
  {
    OnRun=VAR
            Core@1000000000 : Codeunit 5327151;
          BEGIN
            IF NOT CONFIRM(Text000,FALSE,Core.""()) THEN EXIT;

            AddTextConstantsAllLanguages();
            SetupUpdates();
            GeneralDataUpdates();
            DecimalFieldAdjustment();
            CreateActionAndRelationCodes();
            AdjustWPEFieldPosition();
            MESSAGE(Text001);
          END;

  }
  CODE
  {
    VAR
      Text000@1000000000 : TextConst 'DEU=Anveo Update\\WICHTIG: Vor dem Starten des Updates nach einem Objektimport erneut an der Datenbank anmelden.\\M”chten Sie das Update auf Version %1 starten?;ENU=Anveo Update\\Important: Before running the update process after an object import, please close and reopen database connection.\\Do you want to start update to version %1?';
      Text001@1000000001 : TextConst 'DEU=Update abgeschlossen.\Bitte Konsistenzprfung starten.;ENU=Update finished.\Please run a consistency check now.';
      Text002@1000000002 : TextConst 'DEU=Warnung!\\M”chten Sie wirklich die gesamten Web Desk Daten l”schen?\Wenn Sie fortfahren, sind in der Datenbank keine Web Desk Daten mehr vorhanden. Erstellen Sie vorher ein Backup.;ENU=Attention!\\Do you really want to delete the complete Web Desk data?\ If you proceed, all information are lost.. Create a backup before.';
      Text003@1000000003 : TextConst 'DEU=Abgeschlossen.;ENU=Finished.';

    PROCEDURE AddTextConstantsAllLanguages@1000000004();
    BEGIN
      // Automatically detect language codes

      // English languages
      AddTextConstantsLanguage(3081,0);  // ENA English (Australian)
      AddTextConstantsLanguage(4105,0);  // ENC English (Canadian)
      AddTextConstantsLanguage(2057,0);  // ENG English (United Kingdom)
      AddTextConstantsLanguage(6153,0);  // ENI English (Ireland)
      AddTextConstantsLanguage(13321,0); // ENP English (Philippines)
      AddTextConstantsLanguage(1033,0);  // ENU English (USA)
      AddTextConstantsLanguage(5129,0);  // ENZ English (New Zealand)

      // German languages
      AddTextConstantsLanguage(3079,1);  // DEA German (Austrian)
      AddTextConstantsLanguage(2055,1);  // DES German (Swiss)
      AddTextConstantsLanguage(1031,1);  // DEU German (German)
    END;

    PROCEDURE AddTextConstantsLanguage@1000000007(WindowsLanguageID@1000000000 : Integer;Lang@1000000002 : 'English,German');
    VAR
      Language@1000000001 : Record 8;
      ModuleMapping@1000000003 : Codeunit 5327179;
    BEGIN
      // Set language filter of main events codeunit
      ModuleMapping.GetLanguageFilter(Language);

      Language.SETRANGE("Windows Language ID",WindowsLanguageID);
      IF NOT Language.FINDFIRST THEN EXIT;

      IF Lang = Lang::English THEN AddTextConstants(Language.Code, '');
      IF Lang = Lang::German THEN AddTextConstants('', Language.Code);
    END;

    LOCAL PROCEDURE AddTextConstants@1000000000(LanguageCodeEN@1000000002 : Code[10];LanguageCodeDE@1000000003 : Code[10]);
    BEGIN

      //Add new text constants
      //2.01.04
      AddMLString('BIGTEXTEDITORINFO',LanguageCodeDE,'%1 Zeichen',TRUE);
      AddMLString('BIGTEXTEDITORINFO',LanguageCodeEN,'%1 Signs',TRUE);
      AddMLString('BIGTEXTEDITORSAVESUCCESS',LanguageCodeDE,'Ihre Žnderung wurde erfolgreich gespeichert.',TRUE);
      AddMLString('BIGTEXTEDITORSAVESUCCESS',LanguageCodeEN,'Data updated.',TRUE);
      AddMLString('WINDOWOPENURL',LanguageCodeDE,'Externe Webseite',TRUE);
      AddMLString('WINDOWOPENURL',LanguageCodeEN,'External Web Site',TRUE);
      AddMLString('WINDOWFILEHANDLING',LanguageCodeDE,'Datei Management',TRUE);
      AddMLString('WINDOWFILEHANDLING',LanguageCodeEN,'File Management',TRUE);
      AddMLString('WINPROGRESSCANCEL',LanguageCodeDE,'Verarbeitung abgebrochen.',FALSE);
      AddMLString('WINPROGRESSCANCEL',LanguageCodeEN,'Processing cancelled.',FALSE);
      AddMLString('FILEMANAGERRESETBUTTON', LanguageCodeDE, 'Zurcksetzen',TRUE);
      AddMLString('FILEMANAGERRESETBUTTON', LanguageCodeEN, 'Reset',TRUE);
      AddMLString('FILEMANAGERTITEL', LanguageCodeDE, 'Datei Manager',TRUE);
      AddMLString('FILEMANAGERTITEL', LanguageCodeEN, 'File Manager',TRUE);
      AddMLString('FILEMANAGERUPLOADBUTTON', LanguageCodeDE, 'Datei hochladen',TRUE);
      AddMLString('FILEMANAGERUPLOADBUTTON', LanguageCodeEN, 'File upload',TRUE);
      AddMLString('FILEMANAGEREMPTYTEXT', LanguageCodeDE, 'Bitte w„hlen Sie eine Datei aus...',TRUE);
      AddMLString('FILEMANAGEREMPTYTEXT', LanguageCodeEN, 'Select file...',TRUE);
      AddMLString('FILEMANAGERLABELTEXT', LanguageCodeDE, 'Datei',TRUE);
      AddMLString('FILEMANAGERLABELTEXT', LanguageCodeEN, 'File',TRUE);
      //2.01.05
      AddMLString('DROPDOWNDEFAULTCAPTION1',LanguageCodeDE,'Code',FALSE);
      AddMLString('DROPDOWNDEFAULTCAPTION1',LanguageCodeEN,'Code',FALSE);
      AddMLString('DROPDOWNDEFAULTCAPTION2',LanguageCodeDE,'Beschreibung',FALSE);
      AddMLString('DROPDOWNDEFAULTCAPTION2',LanguageCodeEN,'Description',FALSE);
      AddMLString('WD_VERSION',LanguageCodeDE,'--',TRUE);
      AddMLString('WD_VERSION',LanguageCodeEN,'--',TRUE);
      //2.01.06
      AddMLString('USERSESSIONSERROR',LanguageCodeDE,
       'Ihre Lizenz l„sst keine weiteren Benutzer zu. Warten Sie, bis ein anderer Benutzer das Programm beendet hat.',FALSE);
      AddMLString('USERSESSIONSERROR',LanguageCodeEN,
       'Your program license does not permit more users to work simultaneously. Wait until another user has stopped using the program.',
       FALSE);
      AddMLString('LOGGINGWRITEERROR',LanguageCodeDE,'Aktionsprotokoll kann nicht in %1 geschrieben werden.',FALSE);
      AddMLString('LOGGINGWRITEERROR',LanguageCodeEN,'Action log cannot be written in %1.',FALSE);
      //2.02
      AddMLString('COMPANYCHANGE',LanguageCodeDE,'™ffnen',TRUE);
      AddMLString('COMPANYCHANGE',LanguageCodeEN,'Open',TRUE);
      AddMLString('COMPANYMANAGER',LanguageCodeDE,'Mandanten Manager',TRUE);
      AddMLString('COMPANYMANAGER',LanguageCodeEN,'Company Manager',TRUE);
      AddMLString('COMPANYMANAGERSHOW',LanguageCodeDE,'true',TRUE);
      AddMLString('COMPANYMANAGERSHOW',LanguageCodeEN,'true',TRUE);
      AddMLString('MAINMENUCOMPANY',LanguageCodeDE,'Mandant',TRUE);
      AddMLString('MAINMENUCOMPANY',LanguageCodeEN,'Company',TRUE);
      //2.02.01
      AddMLString('FILEMANAGERDOWNLOADFIN',LanguageCodeDE,'Download erfolgreich',TRUE);
      AddMLString('FILEMANAGERDOWNLOADFIN',LanguageCodeEN,'Download successful',TRUE);
      //2.04
      AddMLString('UPDATEINSERTACCOUNTERROR',LanguageCodeDE,'Web Benutzer Update/Insert fehlgeschlagen',FALSE);
      AddMLString('UPDATEINSERTACCOUNTERROR',LanguageCodeEN,'Web User Update/Insert failed',FALSE);
      AddMLString('NOACCOUNTTABLEERROR',LanguageCodeDE,'Web Benutzer Update/Insert fehlgeschlagen. ' +
                  'Es mssen alle Benutzer repliziert werden. Bitte starten Sie die Replizierung aus der Web Desk Einrichtung.',FALSE);
      AddMLString('NOACCOUNTTABLEERROR',LanguageCodeEN,'Web User Update/Insert failed. Web User Replication has to be started. ' +
                  'Please start the replication from the Web Desk Setup.',FALSE);
      AddMLString('WEBUSERDOESNOTEXISTERROR',LanguageCodeDE,'Web Benutzer existiert nicht.',FALSE);
      AddMLString('WEBUSERDOESNOTEXISTERROR',LanguageCodeEN,'Web User does not exist.',FALSE);
      AddMLString('ACCOUNTDELETEERROR',LanguageCodeDE,'L”schen des Accounts aus dem Web Store fehlgeschlagen. ' +
                  'Es mssen alle Benutzer repliziert werden. Bitte starten Sie die Replizierung aus der Web Desk Einrichtung.',FALSE);
      AddMLString('ACCOUNTDELETEERROR',LanguageCodeEN,'Deletion from the Web Store failed. Web User Replication has to be started. ' +
                  'Please start the replication from the Web Desk Setup.',FALSE);
      AddMLString('CLOSEANYWAY',LanguageCodeDE,'Datensatz wurde nicht gespeichert. Trotzdem schlieáen?', TRUE);
      AddMLString('CLOSEANYWAY',LanguageCodeEN,'Record was not saved. Close anyway?',TRUE);
      AddMLString('FORMATTIME',LanguageCodeDE,'H:i:s',FALSE);
      AddMLString('FORMATTIME',LanguageCodeEN,'H:i:s',FALSE);
      AddMLString('MOBILELINEMENU',LanguageCodeDE,'%1 Zeilenauswahl',FALSE);
      AddMLString('MOBILELINEMENU',LanguageCodeEN,'%1 Line Selection',FALSE);
      AddMLString('MOBILEMENU',LanguageCodeDE,'%1 Men',FALSE);
      AddMLString('MOBILEMENU',LanguageCodeEN,'%1 Menu',FALSE);
      //2.04.02
      AddMLString('USERREPLICATIONRUNNING',LanguageCodeDE,'Web Benutzer werden repliziert...',FALSE);
      AddMLString('USERREPLICATIONRUNNING',LanguageCodeEN,'Replicating Web Users...',FALSE);
      AddMLString('NOWEBUSERCOMPANYRIGHTS',LanguageCodeDE,'Der Web Benutzer %1 hat keine Rechte fr den Mandanten %2.',FALSE);
      AddMLString('NOWEBUSERCOMPANYRIGHTS',LanguageCodeEN,'The Web User %1 has no rights for company %2.',FALSE);

      //2.06
      AdjustMLString('BIGTEXTEDITORSAVESUCCESS', LanguageCodeEN, 'Update successed', 'Update successful', TRUE);

      //2.09.03
      AddMLString('GMKEYNOTDEFINED',LanguageCodeDE,'Google Maps Key nicht definiert oder fehlerhaft. Funktionalit„t nicht verfgbar.',
      TRUE);
      AddMLString('GMKEYNOTDEFINED',LanguageCodeEN,'Google Maps Key not defined or incorrect. Maps functionality not available.',
      TRUE);

      //3.00
      AddMLString('GRIDFILTERBOOLEANNO',LanguageCodeDE,'Nein',TRUE);
      AddMLString('GRIDFILTERBOOLEANNO',LanguageCodeEN,'No',TRUE);
      AddMLString('GRIDFILTERBOOLEANYES',LanguageCodeDE,'Ja',TRUE);
      AddMLString('GRIDFILTERBOOLEANYES',LanguageCodeEN,'Yes',TRUE);
      AddMLString('REPORTPERMISSION',LanguageCodeDE,'Sie drfen den Report "%1" nicht ausfhren.',FALSE);
      AddMLString('REPORTPERMISSION',LanguageCodeEN,'You are not allowed to run report "%1".',FALSE);
      AddMLString('QUICKSEARCHTITLE',LanguageCodeDE,'Suche',TRUE);
      AddMLString('QUICKSEARCHTITLE',LanguageCodeEN,'Search',TRUE);
      AddMLString('WINDOWCLEARBUTTON',LanguageCodeEN,'Clear',FALSE);
      AddMLString('WINDOWCLEARBUTTON',LanguageCodeDE,'L”schen',FALSE);

      AdjustMLString('MOBILEMENU',LanguageCodeDE,'%1 Zeilenauswahl', ' Men', FALSE);
      AdjustMLString('MOBILEMENU',LanguageCodeEN,'%1 Line Selection', ' Menu', FALSE);
      AdjustMLString('MOBILELINEMENU',LanguageCodeDE,'%1 Men', ' Zeilenauswahl', FALSE);
      AdjustMLString('MOBILELINEMENU',LanguageCodeEN,'%1 Menu', ' Line Selection', FALSE);
      AdjustMLString('MOBILELINEMENU',LanguageCodeDE,'%1 Zeilenauswahl', ' Zeilenauswahl', FALSE);
      AdjustMLString('MOBILELINEMENU',LanguageCodeEN,'%1 Line Selection', ' Line Selection', FALSE);
      AdjustMLString('MOBILEMENU',LanguageCodeDE,'%1 Men', ' Men', FALSE);
      AdjustMLString('MOBILEMENU',LanguageCodeEN,'%1 Menu', ' Menu', FALSE);

      //3.02
      AddMLString('OPTIONVALUENOTVALID',LanguageCodeDE, '''%1'' ist kein gltiger Wert. Die folgenden Werte k”nnen verwendet werden: %2.',
        FALSE);
      AddMLString('OPTIONVALUENOTVALID',LanguageCodeEN, '''%1'' is not a valid value. The following values can be used: %2.', FALSE);

      //3.02.02
      AdjustMLString('ANOTHERUSERMODIFIEDREC',LanguageCodeEN,
        'Another User has modified the record after you retrived it from the database.',
        'Another user has modified the record after you retrieved it from the database.', TRUE);
    END;

    PROCEDURE SetupUpdates@1000000002();
    VAR
      Setup@1000000000 : Record 5327160;
    BEGIN
      Setup.GET;
      IF Setup."Timeout for interactionrequest"=0 THEN BEGIN
        Setup."Timeout for interactionrequest" := 120;
        Setup.MODIFY(TRUE);
      END;
      IF Setup."Web User Replication Batchsize"=0 THEN BEGIN
        Setup."Web User Replication Batchsize" := 3000;
        Setup.MODIFY(TRUE);
      END;
    END;

    PROCEDURE GeneralDataUpdates@1000000008();
    VAR
      Icon@1000000000 : Record 5327163;
      WebPageElementTab@1000000001 : Record 5327102;
      WebPageElementField@1000000002 : Record 5327103;
      WebPageElementMenu@1000000003 : Record 5327104;
    BEGIN
      // 2.07->2.08
      // Renamed Icons: action -> actions, settings -> setup
      Icon.SETRANGE(Description, 'action');
      Icon.DELETEALL;
      Icon.SETRANGE(Description, 'settings');
      Icon.DELETEALL;
      Icon.Description := 'actions';
      IF Icon.INSERT THEN ;
      Icon.Description := 'setup';
      IF Icon.INSERT THEN ;
      // Update existing data
      WebPageElementTab.SETRANGE("Icon Description", 'action');
      WebPageElementTab.MODIFYALL("Icon Description", 'actions');
      WebPageElementTab.SETRANGE("Icon Description", 'settings');
      WebPageElementTab.MODIFYALL("Icon Description", 'setup');
      WebPageElementField.SETRANGE("Button Icon", 'action');
      WebPageElementField.MODIFYALL("Button Icon", 'actions');
      WebPageElementField.SETRANGE("Button Icon", 'settings');
      WebPageElementField.MODIFYALL("Button Icon", 'setup');
      WebPageElementMenu.SETRANGE("Icon Description", 'action');
      WebPageElementMenu.MODIFYALL("Icon Description", 'actions');
      WebPageElementMenu.SETRANGE("Icon Description", 'settings');
      WebPageElementMenu.MODIFYALL("Icon Description", 'setup');

      // 2.10
      AddIcon('document_send_green');
      AddIcon('document_send_red');
      AddIcon('document_receive_green');
      AddIcon('document_receive_red');
    END;

    LOCAL PROCEDURE AddMLString@1000000001(Code@1000000000 : Code[30];LanguageCode@1000000001 : Code[10];Value@1000000002 : Text[250];ExporttoWebDesk@1000000003 : Boolean);
    VAR
      MLString@1000000004 : Record 5327162;
    BEGIN
      // Add Multilanguage String value
      IF LanguageCode = '' THEN EXIT;
      MLString.INIT;
      MLString.Code := Code;
      MLString.Language := LanguageCode;
      MLString.Value := Value;
      MLString."Export to WebDesk" := ExporttoWebDesk;
      IF MLString.INSERT THEN;
    END;

    PROCEDURE AdjustMLString@1000000010(Code@1000000004 : Code[30];LanguageCode@1000000003 : Code[10];Value@1000000002 : Text[250];NewValue@1000000001 : Text[250];ExporttoWebDesk@1000000000 : Boolean);
    VAR
      MLString@1000000005 : Record 5327162;
    BEGIN
      // Adjust Multilanguage String value
      IF LanguageCode = '' THEN EXIT;

      IF NOT MLString.GET(Code, LanguageCode) THEN
        AddMLString(Code, LanguageCode, NewValue, ExporttoWebDesk)
      ELSE BEGIN
        IF MLString.Value = Value THEN
          MLString.Value := NewValue;
        MLString."Export to WebDesk" := ExporttoWebDesk;
        MLString.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE DeleteField@1000000003(TableNo@1000000000 : Integer;FieldNo@1000000001 : Integer;FieldName@1000000004 : Text[30]);
    VAR
      TableInformation@1000000006 : Record 2000000028;
      Field@1000000005 : Record 2000000041;
      RecRef@1000000002 : RecordRef;
      FldRef@1000000003 : FieldRef;
    BEGIN
      //Deletes all values of a field
      //FieldName must be set for security reasons: Do not delete field with same number, but another usage.
      TableInformation.SETRANGE(TableInformation."Table No.", TableNo);
      TableInformation.FINDSET;
      REPEAT
        RecRef.OPEN(TableNo, FALSE, TableInformation."Company Name");
        Field.RESET;
        Field.SETRANGE(TableNo, TableNo);
        IF FieldNo <> 0 THEN Field.SETRANGE("No.", FieldNo);
        IF FieldName <> '' THEN Field.SETRANGE(FieldName, FieldName);
        Field.SETRANGE(Enabled, TRUE);
        IF Field.FINDFIRST THEN BEGIN
          IF RecRef.FINDSET THEN REPEAT
            FldRef := RecRef.FIELD(FieldNo);
            CASE FORMAT(FldRef.TYPE) OF
              'Boolean': FldRef.VALUE := FALSE;
              'Date': FldRef.VALUE := 0D;
              'Time': FldRef.VALUE := 0T;
              'DateTime': FldRef.VALUE := CREATEDATETIME(0D,0T);
              'Text','Code': FldRef.VALUE :=  '';
              'Decimal','Integer': FldRef.VALUE:=0;
            END;
            RecRef.MODIFY;
          UNTIL RecRef.NEXT=0;
        END;
        // else
        //  message('Deletion of field failed. Field %1 %2 not found in Table %3.', fieldno, Fieldname, TableInformation."Table Name");
        RecRef.CLOSE;
      UNTIL TableInformation.NEXT=0;
    END;

    LOCAL PROCEDURE DeleteTable@1000000006(TableNo@1000000000 : Integer);
    VAR
      TableInformation@1000000003 : Record 2000000028;
      RecRef@1000000001 : RecordRef;
    BEGIN
      //Deletes all values of a field
      //FieldName must be set for security reasons: Do not delete field with same number, but another usage.
      TableInformation.SETRANGE(TableInformation."Table No.", TableNo);
      IF TableInformation.FINDSET THEN REPEAT
        RecRef.OPEN(TableNo, FALSE, TableInformation."Company Name");
        RecRef.DELETEALL;
        RecRef.CLOSE;
      UNTIL TableInformation.NEXT=0;
    END;

    LOCAL PROCEDURE DeleteCompleteWebDesk@1000000005();
    BEGIN
      //Delete complete Web Desk data for Update scenario (Data per Company = No)
      IF NOT CONFIRM(Text002,FALSE) THEN EXIT;
      DeleteTable(DATABASE::"ACF Web Page");
      DeleteTable(DATABASE::"ACF Web Page Element");
      DeleteTable(DATABASE::"ACF Web Page Element Tab");
      DeleteTable(DATABASE::"ACF Web Page Element Field");
      DeleteTable(DATABASE::"ACF Web Page Table Field Rel.");
      DeleteTable(DATABASE::"ACF Web Page Element Menu");
      DeleteTable(DATABASE::"ACF Menu");
      DeleteTable(DATABASE::"ACF Web User Protocol");
      DeleteTable(DATABASE::"ACF Consistency Check Line");
      DeleteTable(DATABASE::"ACF Setup");
      DeleteTable(DATABASE::"ACF Web User");
      DeleteTable(DATABASE::"ACF Web User Role");
      DeleteTable(DATABASE::"ACF Rights");
      DeleteTable(DATABASE::"ACF Web Role");
      DeleteTable(DATABASE::"ACF Multilanguage");
      DeleteTable(DATABASE::"ACF Web User Form Definition");
      DeleteTable(DATABASE::"ACF Text Constant");
      DeleteTable(DATABASE::"ACF Icon");
      DeleteTable(DATABASE::"ACF Temp Rec");
      MESSAGE(Text003);
    END;

    PROCEDURE DecimalFieldAdjustment@1000000009();
    VAR
      WebPageElement@1000000000 : Record 5327101;
      WebPageElementField@1000000001 : Record 5327103;
    BEGIN
      IF WebPageElementField.FINDSET THEN REPEAT
        IF WebPageElement.GET(WebPageElementField."Web Page Code", WebPageElementField."Web Page Element Line No.") THEN BEGIN
          IF WebPageElement."Element Type" IN [WebPageElement."Element Type"::Card,
                                               WebPageElement."Element Type"::"Card Mobile"] THEN BEGIN
            WebPageElementField."Decimal Precision View" := 0;
            WebPageElementField.MODIFY(FALSE);
          END;
        END;
      UNTIL WebPageElementField.NEXT = 0;
    END;

    PROCEDURE CreateActionAndRelationCodes@5326062();
    VAR
      WebPageElement@5326064 : Record 5327101;
      WebPageElementField@5326063 : Record 5327103;
      WebPageElementMenu@5326062 : Record 5327104;
      RelationCode@5326065 : Record 5327132;
      FunctionCode@5326066 : Record 5327133;
    BEGIN
      IF WebPageElement.FINDSET THEN REPEAT
        IF WebPageElement."Double Click Function Code" <> '' THEN BEGIN
          FunctionCode.Code := WebPageElement."Double Click Function Code";
          IF FunctionCode.INSERT THEN ;
        END;
        IF WebPageElement."Dbl. Click Cust. Table Rel." <> '' THEN BEGIN
          RelationCode.Code := WebPageElement."Dbl. Click Cust. Table Rel.";
          IF RelationCode.INSERT THEN ;
        END;
        IF WebPageElement."Cust. Table Rel. Subelement" <> '' THEN BEGIN
          RelationCode.Code := WebPageElement."Cust. Table Rel. Subelement";
          IF RelationCode.INSERT THEN ;
        END;
      UNTIL WebPageElement.NEXT = 0;

      IF WebPageElementField.FINDSET THEN REPEAT
        IF WebPageElementField."DropDown Custom Table Relation" <> '' THEN BEGIN
          RelationCode.Code := WebPageElementField."DropDown Custom Table Relation";
          IF RelationCode.INSERT THEN ;
        END;
        IF WebPageElementField."Button Function Code" <> '' THEN BEGIN
          FunctionCode.Code := WebPageElementField."Button Function Code";
          IF FunctionCode.INSERT THEN ;
        END;
        IF WebPageElementField."Button Custom Table Relation" <> '' THEN BEGIN
          RelationCode.Code := WebPageElementField."Button Custom Table Relation";
          IF RelationCode.INSERT THEN ;
        END;
      UNTIL WebPageElementField.NEXT = 0;

      IF WebPageElementMenu.FINDSET THEN REPEAT
        IF WebPageElementMenu."Function Code" <> '' THEN BEGIN
          FunctionCode.Code := WebPageElementMenu."Function Code";
          IF FunctionCode.INSERT THEN ;
        END;
        IF WebPageElementMenu."Custom Table Relation" <> '' THEN BEGIN
          RelationCode.Code := WebPageElementMenu."Custom Table Relation";
          IF RelationCode.INSERT THEN ;
        END;
      UNTIL WebPageElementMenu.NEXT = 0;
    END;

    PROCEDURE AddIcon@5326063(Description@5326062 : Text[30]);
    VAR
      Icon@5326063 : Record 5327163;
    BEGIN
      Icon.INIT;
      Icon.Description := Description;
      IF Icon.INSERT THEN ;
    END;

    PROCEDURE AdjustWPEFieldPosition@5326064();
    VAR
      WebPageElementField@5326062 : Record 5327103;
      WebPageElementField2@5326063 : Record 5327103;
    BEGIN
      WebPageElementField.SETRANGE(Position, 0);
      WebPageElementField.SETFILTER("Field Name", '<>%1', 'PK');

      IF WebPageElementField.FINDSET THEN REPEAT
        WebPageElementField2.SETRANGE("Web Page Code", WebPageElementField."Web Page Code");
        WebPageElementField2.SETRANGE("Web Page Element Line No.", WebPageElementField."Web Page Element Line No.");
        WebPageElementField2.SETRANGE(Code, WebPageElementField.Code);
        IF WebPageElementField2.FINDSET THEN REPEAT
          WebPageElementField2.Position := WebPageElementField2.Position + 1;
          WebPageElementField2.MODIFY(FALSE);
        UNTIL WebPageElementField2.NEXT = 0;
      UNTIL WebPageElementField.NEXT = 0;
    END;

    BEGIN
    {

       The changelog and update process definition is now described in the document "Update process and changelog.pdf"
    }
    END.
  }
}

