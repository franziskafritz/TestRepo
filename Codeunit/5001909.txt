OBJECT Codeunit 5001909 Gen. Jnl.-Apply Payment
{
  OBJECT-PROPERTIES
  {
    Date=15.06.11;
    Time=06:00:00;
    Version List=AR5.00,eBay1.07.02;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);

            WITH GenJnlLine DO BEGIN
              GetCurrency;
              IF "Bal. Account Type" IN
                 ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
              THEN BEGIN
                AccType := "Bal. Account Type";
                AccNo := "Bal. Account No.";
              END ELSE BEGIN
                AccType := "Account Type";
                AccNo := "Account No.";
              END;
              CASE AccType OF
                AccType::Customer:
                  BEGIN
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    IF "Applies-to ID" = '' THEN
                      "Applies-to ID" := "Document No.";
                    IF "Applies-to ID" = '' THEN
                      ERROR(
                        Text1105400000,
                        FIELDCAPTION("Document No."),FIELDCAPTION("Applies-to ID"));
                    ApplyCustEntries.SetGenJnlLine(GenJnlLine,GenJnlLine.FIELDNO("Applies-to ID"));
                    ApplyCustEntries.SETRECORD(CustLedgEntry);
                    ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
                    ApplyCustEntries.LOOKUPMODE(TRUE);
                    OK := ApplyCustEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyCustEntries);
                    IF NOT OK THEN
                      EXIT;
                    CustLedgEntry.RESET;
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF CustLedgEntry.FINDFIRST THEN BEGIN
                      CurrencyCode2 := CustLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                          CustLedgEntry.CALCFIELDS("Remaining Amount");
                          CustLedgEntry."Remaining Amount" :=
                            GenJnlPostLine.ExchAmount(
                              CustLedgEntry."Remaining Amount",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Amount" :=
                            ROUND(CustLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            GenJnlPostLine.ExchAmount(
                              CustLedgEntry."Remaining Pmt. Disc. Possible",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(CustLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");

                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(Rec,CustLedgEntry,0,FALSE)
                          THEN
                            Amount := Amount - (CustLedgEntry."Remaining Amount" - CustLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - CustLedgEntry."Remaining Amount";
                        UNTIL CustLedgEntry.NEXT = 0;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor) THEN
                          Amount := -Amount;
                        VALIDATE(Amount);
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                        UNTIL CustLedgEntry.NEXT = 0;
                      IF "Currency Code" <> CurrencyCode2 THEN
                        IF Amount = 0 THEN BEGIN
                          IF NOT
                             CONFIRM(
                               Text1105400001 +
                               Text1105400002,TRUE,
                               FIELDCAPTION("Currency Code"),TABLECAPTION,"Currency Code",
                               CustLedgEntry."Currency Code")
                          THEN
                            ERROR(Text1105400003);
                          "Currency Code" := CustLedgEntry."Currency Code"
                        END ELSE
                          CheckAgainstApplnCurrency("Currency Code",CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" := '';
                    Rec := GenJnlLine;
                    //Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;
                  END;
                AccType::Vendor:
                  BEGIN
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    IF "Applies-to ID" = '' THEN
                      "Applies-to ID" := "Document No.";
                    IF "Applies-to ID" = '' THEN
                      ERROR(
                        Text1105400000,
                        FIELDCAPTION("Document No."),FIELDCAPTION("Applies-to ID"));
                    ApplyVendEntries.SetGenJnlLine(GenJnlLine,GenJnlLine.FIELDNO("Applies-to ID"));
                    ApplyVendEntries.SETRECORD(VendLedgEntry);
                    ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
                    ApplyVendEntries.LOOKUPMODE(TRUE);
                    OK := ApplyVendEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyVendEntries);
                    IF NOT OK THEN
                      EXIT;
                    VendLedgEntry.RESET;
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF VendLedgEntry.FINDFIRST THEN BEGIN
                      CurrencyCode2 := VendLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                          VendLedgEntry.CALCFIELDS("Remaining Amount");
                          VendLedgEntry."Remaining Amount" :=
                            GenJnlPostLine.ExchAmount(
                              VendLedgEntry."Remaining Amount",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Amount" :=
                            ROUND(VendLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            GenJnlPostLine.ExchAmount(
                              VendLedgEntry."Remaining Pmt. Disc. Possible",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(VendLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");
                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(Rec,VendLedgEntry,0,FALSE)
                          THEN
                            Amount := Amount - (VendLedgEntry."Remaining Amount" - VendLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - VendLedgEntry."Remaining Amount";
                        UNTIL VendLedgEntry.NEXT = 0;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor) THEN
                          Amount := -Amount;
                        VALIDATE(Amount);
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                        UNTIL VendLedgEntry.NEXT = 0;
                      IF "Currency Code" <> CurrencyCode2 THEN
                        IF Amount = 0 THEN BEGIN
                          IF NOT
                             CONFIRM(
                               Text1105400001 +
                               Text1105400002,TRUE,
                               FIELDCAPTION("Currency Code"),TABLECAPTION,"Currency Code",
                               VendLedgEntry."Currency Code")
                          THEN
                            ERROR(Text1105400003);
                          "Currency Code" := VendLedgEntry."Currency Code"
                        END ELSE
                          CheckAgainstApplnCurrency("Currency Code",VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" :='';
                    Rec := GenJnlLine;
                    //Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;
                  END;
                ELSE
                  ERROR(
                    Text1105400005,
                    FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
              END;
            END;
            Rec := GenJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text1105400000@1105400000 : TextConst 'DEU=Sie mÅssen %1 oder %2 angeben.;ENU=You must specify %1 or %2.';
      Text1105400001@1105400001 : TextConst 'DEU=Der %1 in der %2 wird von %3 auf %4 geÑndert.\;ENU=The %1 in the %2 will be changed from %3 to %4.\';
      Text1105400002@1105400002 : TextConst 'DEU=Mîchten Sie fortfahren?;ENU=Do you wish to continue?';
      Text1105400003@1105400003 : TextConst 'DEU=Die Verarbeitung wurde unterbrochen, um die Warnung zu berÅcksichtigen.;ENU=The update has been interrupted to respect the warning.';
      Text1105400005@1105400005 : TextConst 'DEU=%1 oder %2 muss Debitor oder Kreditor sein.;ENU=The %1 or %2 must be Customer or Vendor.';
      Text1105400006@1105400006 : TextConst 'DEU=Alle Posten innerhalb eines Ausgleiches mÅssen in derselben WÑhrung sein.;ENU=All entries in one application must be in the same currency.';
      Text1105400007@1105400007 : TextConst 'DEU="Alle Posten innerhalb eines Ausgleiches mÅssen in derselben WÑhrung oder in einer oder mehreren EWU WÑhrungen sein. ";ENU="All entries in one application must be in the same currency or one or more of the EMU currencies. "';
      GenJnlLine@1008 : Record 81;
      CustLedgEntry@1009 : Record 21;
      VendLedgEntry@1010 : Record 25;
      GLSetup@1011 : Record 98;
      Currency@1012 : Record 4;
      ApplyCustEntries@1013 : Form 232;
      ApplyVendEntries@1014 : Form 233;
      GenJnlPostLine@1015 : Codeunit 12;
      PaymentToleranceMgt@1105400004 : Codeunit 426;
      AccNo@1016 : Code[20];
      CurrencyCode2@1017 : Code[10];
      OK@1018 : Boolean;
      AccType@1019 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';

    PROCEDURE CheckAgainstApplnCurrency@1(ApplnCurrencyCode@1000 : Code[10];CompareCurrencyCode@1001 : Code[10];AccType@1002 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';Message@1003 : Boolean) : Boolean;
    VAR
      Currency@1004 : Record 4;
      Currency2@1005 : Record 4;
      SalesSetup@1006 : Record 311;
      PurchSetup@1007 : Record 312;
      CurrencyAppln@1009 : 'No,EMU,All';
    BEGIN
      IF (ApplnCurrencyCode = CompareCurrencyCode) THEN
        EXIT(TRUE);

      CASE AccType OF
        AccType::Customer:
          BEGIN
            SalesSetup.GET;
            CurrencyAppln := SalesSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text1105400006)
                    ELSE
                      EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text1105400007)
                    ELSE
                      EXIT(FALSE);
                END;
            END;
          END;
        AccType::Vendor:
          BEGIN
            PurchSetup.GET;
            CurrencyAppln := PurchSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text1105400006)
                    ELSE
                      EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text1105400007)
                    ELSE
                      EXIT(FALSE);
                END;
            END;
          END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE GetCurrency@2();
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE BEGIN
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
      END;
    END;

    PROCEDURE ApplyformField@1105400000(VAR RecGenJL@1105400001 : Record 81);
    VAR
      eBayFinancialAccounting@5095800 : Codeunit 5095805;
    BEGIN
      GenJnlLine := RecGenJL;
      WITH GenJnlLine DO BEGIN
      IF "Bal. Account Type" IN
        ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
      THEN BEGIN
        AccNo := "Bal. Account No.";
        AccType := "Bal. Account Type";
      END ELSE BEGIN
        AccNo := "Account No.";
        AccType := "Account Type";
      END;

      CASE AccType OF
        AccType::Customer:
          BEGIN

      //eBay#001
            IF "Applies-to Doc. Type" = "Applies-to Doc. Type"::Order THEN BEGIN
              eBayFinancialAccounting.GenJnlApplyPaymentLookup(GenJnlLine);
              RecGenJL := GenJnlLine;
              EXIT;
            END;
      //eBay#001

            CustLedgEntry.RESET;
            CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive,"Due Date");
            CustLedgEntry.SETRANGE("Customer No.",AccNo);
            CustLedgEntry.SETRANGE(Open,TRUE);
            IF "Applies-to Doc. No." <> '' THEN BEGIN
              CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
              IF CustLedgEntry.FINDFIRST THEN;
              CustLedgEntry.SETRANGE("Document Type");
              CustLedgEntry.SETRANGE("Document No.");
            END ELSE IF "Applies-to ID" <> '' THEN BEGIN
              CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
              IF CustLedgEntry.FINDFIRST THEN;
              CustLedgEntry.SETRANGE("Applies-to ID");
            END ELSE IF "Applies-to Doc. Type" <> 0 THEN BEGIN
              CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              IF CustLedgEntry.FINDFIRST THEN;
              CustLedgEntry.SETRANGE("Document Type");
            END ELSE IF Amount <> 0 THEN BEGIN
              CustLedgEntry.SETRANGE(Positive,Amount < 0);
              IF CustLedgEntry.FINDFIRST THEN;
              CustLedgEntry.SETRANGE(Positive);
            END;
            ApplyCustEntries.SetGenJnlLine(RecGenJL,GenJnlLine.FIELDNO("Applies-to Doc. No."));
            ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
            ApplyCustEntries.SETRECORD(CustLedgEntry);
            ApplyCustEntries.LOOKUPMODE(TRUE);
            IF ApplyCustEntries.RUNMODAL = ACTION::LookupOK THEN BEGIN
              ApplyCustEntries.GETRECORD(CustLedgEntry);
              CLEAR(ApplyCustEntries);
              IF Amount = 0 THEN BEGIN
                CustLedgEntry.CALCFIELDS("Remaining Amount");
                IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(RecGenJL,CustLedgEntry,0,FALSE)
                THEN
                  Amount := -(CustLedgEntry."Remaining Amount" -
                    CustLedgEntry."Remaining Pmt. Disc. Possible")
                ELSE
                  Amount := -CustLedgEntry."Remaining Amount";
                IF "Bal. Account Type" IN
                  ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
                THEN
                  Amount := -Amount;
                "Applies-to Doc. Type" := "Applies-to Doc. Type"::" ";
                "Applies-to Doc. No." := '';
                "Applies-to ID" := '';
                VALIDATE(Amount);
              END;
              "Applies-to Doc. Type" := CustLedgEntry."Document Type";
              "Applies-to Doc. No." := CustLedgEntry."Document No.";
              "Applies-to ID" := '';
              //Check Payment Tolerance
              IF  RecGenJL.Amount <> 0 THEN
                IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                  EXIT;
            END ELSE
              CLEAR(ApplyCustEntries);
          END;
        AccType::Vendor:
          BEGIN
            VendLedgEntry.RESET;
            VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
            VendLedgEntry.SETRANGE("Vendor No.",AccNo);
            VendLedgEntry.SETRANGE(Open,TRUE);
            IF "Applies-to Doc. No." <> '' THEN BEGIN
              VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
              IF VendLedgEntry.FINDFIRST THEN;
              VendLedgEntry.SETRANGE("Document Type");
              VendLedgEntry.SETRANGE("Document No.");
            END ELSE IF "Applies-to ID" <> '' THEN BEGIN
              VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
              IF VendLedgEntry.FINDFIRST THEN;
              VendLedgEntry.SETRANGE("Applies-to ID");
            END ELSE IF "Applies-to Doc. Type" <> 0 THEN BEGIN
              VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              IF VendLedgEntry.FINDFIRST THEN;
              VendLedgEntry.SETRANGE("Document Type");
            END ELSE IF Amount <> 0 THEN BEGIN
              VendLedgEntry.SETRANGE(Positive,Amount < 0);
              IF VendLedgEntry.FINDFIRST THEN;
              VendLedgEntry.SETRANGE(Positive);
            END;
            ApplyVendEntries.SetGenJnlLine(RecGenJL,GenJnlLine.FIELDNO("Applies-to Doc. No."));
            ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
            ApplyVendEntries.SETRECORD(VendLedgEntry);
            ApplyVendEntries.LOOKUPMODE(TRUE);
            IF ApplyVendEntries.RUNMODAL = ACTION::LookupOK THEN BEGIN
              ApplyVendEntries.GETRECORD(VendLedgEntry);
              CLEAR(ApplyVendEntries);
              IF Amount = 0 THEN BEGIN
                VendLedgEntry.CALCFIELDS("Remaining Amount");
                IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(RecGenJL,VendLedgEntry,0,FALSE)
                THEN
                  Amount := -(VendLedgEntry."Remaining Amount" -
                    VendLedgEntry."Remaining Pmt. Disc. Possible")
                ELSE
                  Amount := -VendLedgEntry."Remaining Amount";
                IF "Bal. Account Type" IN
                  ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
                THEN
                  Amount := -Amount;
                "Applies-to Doc. Type" := "Applies-to Doc. Type"::" ";
                "Applies-to Doc. No." := '';
                "Applies-to ID" := '';
                VALIDATE(Amount);
              END;
              "Applies-to Doc. Type" := VendLedgEntry."Document Type";
              "Applies-to Doc. No." := VendLedgEntry."Document No.";
              "Applies-to ID" := '';
              //Check Payment Tolerance
              IF  RecGenJL.Amount <> 0 THEN
                IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                  EXIT;
            END ELSE
              CLEAR(ApplyVendEntries);
          END;
      END;
      END;
      RecGenJL := GenJnlLine;
    END;

    BEGIN
    {
      eBay1.00
      eBay#001 Funktion "ApplyformField" um Order erweitert
    }
    END.
  }
}

