OBJECT Codeunit 6085749 Purch. Doc. - Reopen
{
  OBJECT-PROPERTIES
  {
    Date=19.06.13;
    Time=01:00:00;
    Version List=DCW13.70.00.3.50;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            PurchHeader@1000000001 : Record 38;
            Field@1000000005 : Record 6085580;
            PurchInvHeader@1000000002 : Record 122;
            PurchCrMemoHeader@1000000003 : Record 124;
          BEGIN
            // *********************************************************************************************************************************
            // REOPEN A PURCHASE DOCUMENT. IF THIS IS AN OPEN OR POSTED INVOICE OR CREDIT MEMO IT CAN'T BE REOPEN.
            // *********************************************************************************************************************************

            CASE Status OF
              Status::Open:
                ERROR(Text004,Rec.TABLECAPTION);

              Status::Registered:
                BEGIN
                  IF "Created Doc. Subtype" IN [PurchHeader."Document Type"::Invoice,PurchHeader."Document Type"::"Credit Memo"] THEN BEGIN
                    IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                      ERROR(Text001,PurchHeader."Document Type","Created Doc. ID")
                    ELSE BEGIN
                      IF "Created Doc. Subtype" = PurchHeader."Document Type"::Invoice THEN BEGIN
                        PurchInvHeader.SETCURRENTKEY("Pre-Assigned No.");
                        PurchInvHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                        IF NOT PurchInvHeader.ISEMPTY THEN
                          ERROR(Text002);
                      END ELSE IF "Created Doc. Subtype" = PurchHeader."Document Type"::"Credit Memo" THEN BEGIN
                        PurchCrMemoHeader.SETCURRENTKEY("Pre-Assigned No.");
                        PurchCrMemoHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                        IF NOT PurchCrMemoHeader.ISEMPTY THEN
                          ERROR(Text002);
                      END;
                    END;

                    IF NOT CONFIRM(STRSUBSTNO(Text003),TRUE) THEN
                      ERROR('');

                  END ELSE BEGIN
                    IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                      IF NOT CONFIRM(Text005,FALSE,PurchHeader."Document Type",PurchHeader."No.") THEN
                        ERROR('');
                  END;
                END;

              Status::Rejected:
                IF NOT CONFIRM(STRSUBSTNO(Text003),TRUE) THEN
                  ERROR('');
            END;

            VALIDATE(Status,Status::Open);
            MODIFY(TRUE);
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=Der Beleg ist bereits mit %1 &2 verknÅpft.\\Sie mÅssen den vorhandenen %1 lîschen, bevor der Beleg erneut geîffnet werden kann.;ENU=This document is already linked to %1 %2.\\You must delete the existing %1 before the document can be reopened.';
      Text002@1000000001 : TextConst 'DEU=Der Beleg wurde bereits gebucht und kann nicht erneut geîffent werden.;ENU=This document has already been posted and cannot be reopened.';
      Text003@1000000002 : TextConst 'DEU=Mîchten Sie den Beleg erneut îffnen?;ENU=Do you want to reopen the document?';
      Text004@1000000003 : TextConst 'DEU=%1 ist bereits geîffnet.;ENU=%1 is already open.';
      Text005@161024012 : TextConst 'DEU=Der Beleg ist bereits mit %1 %2 verknÅpft.\\Mîchten Sie den Beleg trotzdem îffnen?;ENU=This document is already linked to %1 %2.\\Do you want to reopen the document anyway?';

    BEGIN
    END.
  }
}

