OBJECT Codeunit 50100 Codeunit OnRun Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=07.10.14;
    Time=12:00:00;
    Modified=Yes;
    Version List=GOB1.00,HME1530;
  }
  PROPERTIES
  {
    OnRun=VAR
            LocText001@1000000000 : TextConst 'DEU=No Function initialized.;ENU=No Function initialized.';
          BEGIN
            CASE GlobalRunFunction OF
              GlobalRunFunction::FTP_Download:
                BEGIN
                  FTPDownload(GlobalInterfaceCode);
                END;
              GlobalRunFunction::FTP_Upload:
                BEGIN
                  FTPUpload(GlobalInterfaceCode);
                END;
              GlobalRunFunction::ReleaseOrder:
                BEGIN
                  ReleaseSalesDoc(GlobalOrderNo);
                END;
              GlobalRunFunction::PostPrepayment:
                BEGIN
                  PostPrepayment(GlobalOrderNo);
                END;
              GlobalRunFunction::TriggerInterface:
                BEGIN
                  TriggerInterface(GlobalEntryNo);
                END;
              //H0587 17.06.14 HCN +++++++++++++++++++++++++++++
              GlobalRunFunction::TriggerInterfaceCancel:
                BEGIN
                  TriggerInterfaceCancel(GlobalEntryNo);
                END;
              //H0587 17.06.14 HCN -----------------------------
              ELSE
                ERROR(LocText001);
            END;
          END;

  }
  CODE
  {
    VAR
      GlobalRunFunction@1000000000 : ' ,DeleteSalesOrder,ArchivSalesOrder,CancelSalesOrder,FTP_Upload,FTP_Download,ReleaseOrder,PostPrepayment,TriggerInterface,TriggerInterfaceCancel';
      GlobalOrderNo@1000000001 : Code[20];
      GlobalCancelationReasonCode@1000000002 : Code[10];
      GlobalInterfaceCode@1000000003 : Code[10];
      GlobalEntryNo@1000000004 : Integer;

    PROCEDURE ClearAllGlobals@1000000001();
    BEGIN
      // Immer Funktion aufrufen und die Globals clearen.
      // Neue Globale Variablen hier hinzufgen
      GlobalRunFunction := GlobalRunFunction::" ";
      GlobalOrderNo := '';
      GlobalCancelationReasonCode := '';
      GlobalInterfaceCode := '';
    END;

    PROCEDURE InitFTPUpload@1000000003(p_InterfaceCode@1000000000 : Code[10]);
    BEGIN
      ClearAllGlobals;
      GlobalRunFunction := GlobalRunFunction::FTP_Upload;
      GlobalInterfaceCode := p_InterfaceCode;
    END;

    PROCEDURE FTPUpload@1000000004(p_InterfaceCode@1000000002 : Code[10]);
    VAR
      Home24FTPFunctions@1000000000 : Codeunit 50029;
      FPCInterfaceSetup@1000000001 : Record 50014;
    BEGIN
      IF FPCInterfaceSetup.GET(p_InterfaceCode) AND FPCInterfaceSetup."FTP Upload Active" THEN BEGIN
        //Testfields
        FPCInterfaceSetup.TESTFIELD("FTP Export Address");
        FPCInterfaceSetup.TESTFIELD("FTP Export User");
        FPCInterfaceSetup.TESTFIELD("FTP Export Password");
        FPCInterfaceSetup.TESTFIELD("FTP Export Folder");
        FPCInterfaceSetup.TESTFIELD("Export Folder");
        //H1530 17.09.14 EHN +++++++++++++++++++++++++++++++++++
        IF FPCInterfaceSetup."FTP Upload Archiv Files" THEN
          FPCInterfaceSetup.TESTFIELD("Archive Root Folder");
        //H1530 17.09.14 EHN -----------------------------------
        //Upload Files
        Home24FTPFunctions.UploadFiles(FPCInterfaceSetup."FTP Export Address",
                                       FPCInterfaceSetup."FTP Export User",
                                       FPCInterfaceSetup."FTP Export Password",
                                       FPCInterfaceSetup."FTP Export Folder",
                                       FPCInterfaceSetup."Export Folder",
                                       FPCInterfaceSetup."Upload File Filter",
                                       FPCInterfaceSetup."FTP Upload Use SFTP",
                                       FPCInterfaceSetup."FTP Upload as tmp and Rename",
                                       FPCInterfaceSetup."FTP UL Delete HDD After UL",
                                       FPCInterfaceSetup."FTP Upload Archiv Files",
                                       FPCInterfaceSetup."Archive Root Folder",
                                       //A/P0547
                                       p_InterfaceCode);
                                       //E/P0547
      END;
    END;

    PROCEDURE InitFTPDownload@1000000005(p_InterfaceCode@1000000000 : Code[10]);
    BEGIN
      ClearAllGlobals;
      GlobalRunFunction := GlobalRunFunction::FTP_Download;
      GlobalInterfaceCode := p_InterfaceCode;
    END;

    PROCEDURE FTPDownload@1000000006(p_InterfaceCode@1000000002 : Code[10]);
    VAR
      Home24FTPFunctions@1000000001 : Codeunit 50029;
      FPCInterfaceSetup@1000000000 : Record 50014;
    BEGIN
      IF FPCInterfaceSetup.GET(p_InterfaceCode) AND FPCInterfaceSetup."FTP Download Active" THEN BEGIN
        // Testfields
        FPCInterfaceSetup.TESTFIELD("FTP Import Address");
        FPCInterfaceSetup.TESTFIELD("FTP Import User");
        FPCInterfaceSetup.TESTFIELD("FTP Import Password");
        FPCInterfaceSetup.TESTFIELD("FTP Import Folder");
        FPCInterfaceSetup.TESTFIELD("Import Folder");
        //Download Files
        Home24FTPFunctions.DownloadFiles(FPCInterfaceSetup."FTP Import Address",
          FPCInterfaceSetup."FTP Import User",
          FPCInterfaceSetup."FTP Import Password",
          FPCInterfaceSetup."FTP Import Folder",
          FPCInterfaceSetup."Import Folder",
          FPCInterfaceSetup."FTP Download File Extension",
          FPCInterfaceSetup."FTP Download Use SFTP",
          FPCInterfaceSetup."FTP Download as tmp and Rename",
          FPCInterfaceSetup."FTP DL Delete FTP After DL",
          FPCInterfaceSetup."Check if File already exists", //P0885
          //A/P0547
          p_InterfaceCode,
          //E/P0547
          //H1530 16.09.14 EHN ++++++++++++++++++++++
          FPCInterfaceSetup."Download File Filter"
          //H1530 16.09.14 EHN ----------------------
          );
      END;
    END;

    PROCEDURE InitReleaseSalesDoc@1000000007(p_OrderNo@1000000000 : Code[20]);
    BEGIN
      ClearAllGlobals;
      GlobalRunFunction := GlobalRunFunction::ReleaseOrder;
      GlobalOrderNo := p_OrderNo;
    END;

    PROCEDURE ReleaseSalesDoc@1000000008(p_OrderNo@1000000000 : Code[20]);
    VAR
      SalesHeader@1000000001 : Record 36;
      ReleaseSalesDocument@1000000002 : Codeunit 414;
    BEGIN
      CLEAR(ReleaseSalesDocument);
      SalesHeader.RESET;
      SalesHeader.GET(SalesHeader."Document Type"::Order,p_OrderNo);
      ReleaseSalesDocument.RUN(SalesHeader);
    END;

    PROCEDURE InitPostPrepayment@1000000009(p_OrderNo@1000000000 : Code[20]);
    BEGIN
      ClearAllGlobals;
      GlobalRunFunction := GlobalRunFunction::PostPrepayment;
      GlobalOrderNo := p_OrderNo;
    END;

    PROCEDURE PostPrepayment@1000000010(p_OrderNo@1000000001 : Code[20]);
    VAR
      SalesHeader@1000000000 : Record 36;
      SalesPostPrepayments@1000000002 : Codeunit 442;
    BEGIN
      CLEAR(SalesPostPrepayments);
      SalesHeader.RESET;
      SalesHeader.GET(SalesHeader."Document Type"::Order,p_OrderNo);
      SalesPostPrepayments.Invoice(SalesHeader);
    END;

    PROCEDURE InitTriggerInterface@1000000002(p_EntryNo@1000000000 : Integer);
    BEGIN
      //S/P0966
      ClearAllGlobals;
      GlobalEntryNo := p_EntryNo;
      GlobalRunFunction := GlobalRunFunction::TriggerInterface;
      //E/P0966
    END;

    PROCEDURE TriggerInterface@1000000011(p_EntryNo@1000000000 : Integer);
    VAR
      InterfaceProcessLog@1000000001 : Record 50087;
      InterfaceProcessMgt@1000000002 : Codeunit 50087;
    BEGIN
      InterfaceProcessLog.GET(p_EntryNo);
      InterfaceProcessMgt.ProcessLogForInterfaceAndOrder(InterfaceProcessLog);
    END;

    PROCEDURE InitTriggerInterfaceCancel@1000000000(p_EntryNo@1000000000 : Integer);
    BEGIN
      // InitTriggerInterfaceCancel
      //H0587 17.06.14 HCN +++++++++++++++++++++++++++++
      ClearAllGlobals;
      GlobalEntryNo := p_EntryNo;
      GlobalRunFunction := GlobalRunFunction::TriggerInterfaceCancel;
      //H0587 17.06.14 HCN -----------------------------
    END;

    PROCEDURE TriggerInterfaceCancel@1000000012(p_EntryNo@1000000000 : Integer);
    VAR
      InterfaceProcessLogL@1000000001 : Record 50087;
      InterfaceProcessMgtL@1000000002 : Codeunit 50087;
    BEGIN
      // TriggerInterfaceCancel
      //H0587 17.06.14 HCN +++++++++++++++++++++++++++++
      InterfaceProcessLogL.GET(p_EntryNo);
      InterfaceProcessMgtL.ProcessLogForInterfaceCancel(InterfaceProcessLogL);
      //H0587 17.06.14 HCN -----------------------------
    END;

    BEGIN
    {
      +-----------------------------------------------------+
      |               GOB Software & Systeme                |
      +-----------------------------------------------------+
      |                      FC Commerce                    |
      +-----------------------------------------------------+

      Version   Date      Consultant  Comment
      -------------------------------------------------------
      P0102     01.10.12  gob-rste    Object Created
      P0547     12.11.12  gob-rste    FTP Up- and Download
      P0771     25.01.13  gob-rste    New Functions Release und PostPrepayment
      P0885     18.04.13  gob-Fuchs   Prfen, ob FTP Datei bereits heruntergeladen wurde
      P0966     19.07.13  gob-rste    Process IPL

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      H0587       17.06.14  hcn       Redesign Cancellation Process
      H1530       15.09.14  EHN       WHSLF: create jobs fot FTP up- and download
    }
    END.
  }
}

