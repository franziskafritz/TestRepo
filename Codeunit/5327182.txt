OBJECT Codeunit 5327182 ACF Events Sales & Marketing
{
  OBJECT-PROPERTIES
  {
    Date=04.04.13;
    Time=00:00:00;
    Version List=ACF3.02.03;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Events@5326063 : Codeunit 5327180;
      Mgt@1000000009 : Codeunit 5327126;
      Text001@1000000026 : TextConst 'DEU=Die individuelle Web Page Tabellenrelation %1 existiert nicht.;ENU=Custom Web Page Table relation %1 does not exist.';
      Text002@1000000002 : TextConst 'DEU=Der Datensatz wurde gespeichert.;ENU=Data saved.';
      Text003@1000000033 : TextConst 'DEU=Der Zieldatensatz wurde in der CustomWebPageRelation nicht ge”ffnet;ENU=Destination Record was not opened in CustomWebPageRelation.';
      Text004@1000000025 : TextConst 'DEU=Der Funktionscode %1 existiert nicht.;ENU=Function Code %1 does not exist.';
      Text005@1000000000 : TextConst 'DEU=Das Passwort muss mindestens eine L„nge von 8 Zeichen haben.;ENU=Password length must be at least 8 signs.';
      Text006@1000000011 : TextConst 'DEU=M”chten Sie den Datensatz %1 %2 l”schen?;ENU=Do you want to delete record %1 %2?';
      Text007@1000000010 : TextConst 'DEU=M”chten Sie den Datensatz "%1" in "%2" umbenennen?;ENU=Do you want to rename the record "%1" in "%2?';
      Text008@1000000008 : TextConst 'DEU=M”chten Sie den Datensatz umbenennen?;ENU=Do you want to rename this record?';
      Text80000@5326094 : TextConst 'DEU=Soll %1 %2 wirklich freigegeben werden?;ENU=Do you want to release %1 %2?';
      Text80001@5326095 : TextConst 'DEU=Wollen Sie %1 %2 wieder ”ffnen?;ENU=Do you want to re-open %1 %2?';
      Text80020@1000000012 : TextConst 'DEU=Dies nimmt die letzte Kriterienaktion zurck.\M”chten Sie fortfahren?;ENU=This will undo the last criteria action.\Do you wish to proceed?';
      Text80030@1000000017 : TextConst 'DEU=Es existiert bereits eine neue Aktivit„t. M”chten Sie diese l”schen und eine neue erstellen?;ENU=A new interaction exists. Do you want to cancel it and create a new one?';
      Text80031@1000000016 : TextConst 'DEU=M”chten Sie die Anlage der Aktivit„t abbrechen?;ENU=Do you want to cancel the creation of this Interaction?';
      Text80033@1000000006 : TextConst 'DEU=M”chten Sie die Aktivit„t anlegen?;ENU=Do you want to create this interaction?';
      Text80034@1000000004 : TextConst 'DEU=M”chten Sie das Erstellen der Aktivit„t %1 abbrechen?;ENU=Do you want to cancel the creation of the interaction %1?';
      Text80040@1000000001 : TextConst 'DEU=Feld %1 darf nicht leer sein. Trotzdem schlieáen?;ENU=Field %1 cannot be empty. Close anyway?';
      Text80050@1000000019 : TextConst 'DEU=%1 %2 ist nicht mit einer anderen Tabelle verknpft.;ENU=The %2 record of the %1 is not linked with any other table.';
      Text80060@1000000020 : TextConst 'DEU=Diese Funktion kann nur verwendet werden, wenn die Interaktionen in den Codeunits 80-82 fr Anveo angepasst wurden. Siehe Dokumentation.;ENU=Interactions of codeunits 80-82 have to be Anveo-enabled before you can use this function. See documentation.';
      Style@5326068 : Codeunit 5327125;
      RecordFilter@5326069 : Codeunit 5327128;
      SecurityMgt@5326071 : Codeunit 5327120;
      WebPageElement@5326088 : Record 5327101;
      WebPageElementField@5326087 : Record 5327103;
      RecRef@5326086 : RecordRef;
      FldRef@5326085 : FieldRef;
      FldNo@5326065 : Integer;
      xRecRef@5326084 : RecordRef;
      xFldRef@5326083 : FieldRef;
      xFldNo@5326064 : Integer;
      WindowRecRef@5326082 : RecordRef;
      DestWebPageElement@5326081 : Record 5327101;
      DestRecRef@5326080 : RecordRef;
      CustomTableRelationRecRef@5326079 : RecordRef;
      FunctionCode@5326078 : Code[20];
      RelationCode@5326077 : Code[20];
      FieldName@5326076 : Text[50];
      TableNumber@5326074 : Integer;
      ClosedByNAV@5326066 : Boolean;
      "Field#1"@5326073 : Integer;
      "Field#2"@5326072 : Integer;
      CSS@5326070 : Text[1024];
      WebUser@5326062 : Record 5327140;
      RecRefOriginal@5326067 : RecordRef;
      Text80061@5326089 : TextConst 'DEU=M”chten Sie einen Auftrag aus dem Rahmenauftrag erstellen?;ENU=Do you want to create a sales order from the blanket sales order?';
      Text80062@5326075 : TextConst 'DEU=Auftrag %1 wurde aus Rahmenauftrag %2 erstellt.;ENU=Sales Order %1 has been created from blanket sales order %2.';
      Text80063@5326090 : TextConst 'DEU=Diese Funktion kann nur verwendet werden, wenn die Interaktionen in der Codeunit 87 fr Anveo angepasst wurden. Siehe Dokumentation.;ENU=Interactions of codeunit 87 have to be Anveo-enabled before you can use this function. See documentation.';
      Text80064@5326091 : TextConst 'DEU=Belege k”nnen nur fr die Belegarten Rechnung und Gutschrift angezeigt werden.;ENU=You can only view documents of document type invoice or credit memo.';
      Text80065@5326092 : TextConst 'DEU=Kein Beleg fr %1 gefunden.;ENU=No document found for %1.';

    PROCEDURE GetModuleName@1000000001() ModuleName : Text[30];
    BEGIN
      EXIT('Sales & Marketing');
    END;

    LOCAL PROCEDURE "-- Events"@5326091();
    BEGIN
    END;

    PROCEDURE OnBeforeLoad@1000000003();
    BEGIN
      // Called before loading data for a Web Page Element
      // This event is synchronious call, Web Mangement messages cannot be used here.
      // Attention: Filter set here get lost on reload on Web Pages of type list. Use CustomWebPageRelation instead.

      // Main Events
      SetValuesToMainEvents();
      Events.OnBeforeLoad();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterGetField@1000000013() ShowValue : Boolean;
    BEGIN
      // This event is called after a field is loaded from (temporary) record.
      // It can be modified here before sending it to the user.
      // Useful for:
      // - Getting data not saved in phys. record but from table functions (eg. "Contact Profile Anweser".Question)
      // - Change of Style (Anveo Web Desk) like colors and other field attributes
      // - Enabling/Disabling fields to change the layout (Anveo Mobile lists)

      // **** Definition of layout
      // ** Anveo Web Desk
      // Define a CSS style for a dynamic definition of the field layout.
      // * STYLE-SYNTAX *
      // 1. Set Condition (boolean)
      //    Style.SetCondition(CONDITION (boolean));
      // 2. Set attribute(s)
      //    If CONDITION is TRUE, first value is taken for attributes, otherwise second:
      //    Style.FontWeight('bold', 'normal');
      //     -> Text is bold, if CONDITION is TRUE; text is normal, if FALSE

      // ** Anveo Mobile
      // Layout of lists can be changed by defining more than one field in Web Page Field definition based on same field in table
      // but with different Web Page Field-attributes (eg. colors)
      // Then set one of the Web Page Fields identified by FieldName invisible by returning ShowValue := TRUE/FALSE
      // The Style object is not used by Anveo Mobile because of performance optimization.

      // Default
      ShowValue := TRUE;

      // Duplicate the RecRef because RecRef.SETTABLE deletes the current key (NAV bug)
      RecRefOriginal := RecRef.DUPLICATE;

      // Table based
      CASE RecRef.NUMBER OF
        DATABASE::Customer:                     ShowValue := OAGetField_CUSTOMER();
        DATABASE::"Cust. Ledger Entry":         ShowValue := OAGetField_CUSTLEDGENTRY();
        DATABASE::"Sales Header":               ShowValue := OAGetField_SALESHEADER();
        DATABASE::"Sales Line":                 ShowValue := OAGetField_SALESLINE();
        DATABASE::"Sales Shipment Header":      ShowValue := OAGetField_SALESSHIPMENTHEADER();
        DATABASE::"Sales Invoice Header":       ShowValue := OAGetField_SALESINVOICEHEADER();
        DATABASE::"Sales Cr.Memo Header":       ShowValue := OAGetField_SALESCRMEMOHEADER();
        DATABASE::Contact:                      ShowValue := OAGetField_CONTACT();
        DATABASE::"Interaction Log Entry":      ShowValue := OAGetField_INTERACTIONLOGENTRY();
        DATABASE::"Profile Questionnaire Line": ShowValue := OAGetField_PROFILEQUESTIONLINE();
        DATABASE::"Segment Criteria Line":      ShowValue := OAGetField_SEGMENTCRITERIALINE();
        DATABASE::"Segment Header":             ShowValue := OAGetField_SEGMENTHEADER();
        DATABASE::"Sales Price":                ShowValue := OAGetField_SALESPRICE();
      END;

      // Web Page based
      CASE WebPageElement."Web Page Code" OF
        //'CP_HOME':             ShowValue := OAGetField_CP_HOME();
        'CP_SALESORDER_CARD':  ShowValue := OAGetField_CP_SALESORDER_CARD();
      END;

      // Set unbound fields by Anveo Virtual Fields
      IF WebPageElementField.Type = WebPageElementField.Type::"Virtual Field" THEN
        OAGetField_VIRTUALFIELDS();

      // Main Events
      SetValuesToMainEvents();
      ShowValue := ShowValue AND Events.OnAfterGetField();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterGetRecord@1000000019();
    BEGIN
      // Record is loaded and is ready for transport to GUI.
      // Changes of records cannot be made here because record is already processed for GUI representation.
      // Use OnAfterGetField instead.

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterGetRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterLoad@1000000014();
    BEGIN
      // Called after loading data for a Web Page Element
      // This event is synchronious call, Web Mangement messages cannot be used here.

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterLoad();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnInit@1000000007();
    VAR
      SalesPrice@1000000002 : Record 7002;
      CommentLine@1000000005 : Record 97;
      SalesCommentLine@1000000007 : Record 44;
    BEGIN
      // OnInit is called if a new record is initialized.
      // Default values can be set here

      CASE RecRef.NUMBER OF
        // Example
        // OnInit SalesPrice set Minimum Quantity to 1
        DATABASE::"Sales Price": BEGIN
          RecRef.SETTABLE(SalesPrice);
          SalesPrice."Minimum Quantity" := 1;
          RecRef.GETTABLE(SalesPrice);
        END;

        // Comment Line, Sales Comment Line: Call NAV-Standard Function SetUpNewLine
        DATABASE::"Comment Line": BEGIN
          RecRef.SETTABLE(CommentLine);
          CommentLine.SetUpNewLine();
          RecRef.GETTABLE(CommentLine);
        END;
        DATABASE::"Sales Comment Line": BEGIN
          RecRef.SETTABLE(SalesCommentLine);
          SalesCommentLine.SetUpNewLine();
          RecRef.GETTABLE(SalesCommentLine);
        END;
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnInit();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnBeforeValidate@1000000010() DoValidation : Boolean;
    VAR
      FldRefPostCode@1000000003 : FieldRef;
      RecPostCode@1000000006 : Record 225;
      PostCode@1000000007 : Code[20];
    BEGIN
      // A value is set to a record, but validation is not done yet. Validation can be cancelled here by return value false.
      // Use input of FlowFields for transportation of input values from GUI to NAV

      // Default
      DoValidation := TRUE;

      IF FORMAT(FldRef.CLASS) = 'FlowField' THEN BEGIN
        //Example:
        Mgt.MESSAGE1('FlowField Input: %1. Do not validate field.',FldRef.VALUE);
        EXIT(FALSE);
      END;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header": BEGIN
          //Enter Post Code in field City and validate Post Code to set Post Code and City.
          //Only first Post Code for City is found: If more than one City for one Post Code exists, only first City is set.
          IF FldRef.NUMBER IN [83,9,17] THEN BEGIN //Sell-to, Bill-to and Ship-to City
            PostCode := FldRef.VALUE;
            RecPostCode.SETRANGE(Code, PostCode);
            IF RecPostCode.FINDFIRST THEN BEGIN
              CASE FldRef.NUMBER OF
                83:  FldRefPostCode := RecRef.FIELD(88); //Sell-to City / Post Code
                9:   FldRefPostCode := RecRef.FIELD(85); //Bill-to City / Post Code
                17:  FldRefPostCode := RecRef.FIELD(91); //Ship-to City / Post Code
              END;
              FldRefPostCode.VALIDATE(RecPostCode.Code);
            END;
          END;
        END;
      END;

      // Save xRec and CurrFieldNo for usage of Mgt.GetxRec in table triggers
      Mgt.SetxRec(xRecRef,FldRef.NUMBER);

      // Main Events
      SetValuesToMainEvents();
      DoValidation := DoValidation AND Events.OnBeforeValidate();
      GetValuesFromMainEvents();

      EXIT(DoValidation);
    END;

    PROCEDURE OnValidate@1000000000();
    VAR
      TmpSegmentLine@5326062 : Record 5077;
      tmpRecRef@5326063 : RecordRef;
      tmpFieldRef@5326064 : FieldRef;
      VirtualData@5326065 : Codeunit 5327127;
    BEGIN
      // OnValidate is called on every input made in the GUI
      // Complete reload of web page is done after validating fields.

      // Example for testing OnValidateTrigger
      //IF FldRef.NUMBER <> 0 THEN BEGIN
      //  Mgt.MESSAGE4(Text008,FldRef.VALUE,RecRef.CAPTION, FldRef.CAPTION, xFldRef.VALUE));
      //END;

      CASE WebPageElement."Web Page Code" OF
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnValidate();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnInsertTrigger@1000000016() CallTableOnInsertTrigger : Boolean;
    BEGIN
      // OnInsert trigger is called when data is saved back to SQL-Server
      // Return value CallTableOnInsertTrigger (boolean) sets whether to call default OnInsert-trigger of table afterwards.
      // Default value is true, always call OnInsert-Trigger of tables
      CallTableOnInsertTrigger:=TRUE;

      // Main Events
      SetValuesToMainEvents();
      CallTableOnInsertTrigger := CallTableOnInsertTrigger AND Events.CallOnInsertTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnModifyTrigger@1000000015() CallTableOnModifyTrigger : Boolean;
    BEGIN
      // OnModify trigger is called when data is saved back to SQL-Server
      // Return value CallTableOnModifyTrigger (boolean) sets whether to call default OnModify-trigger of table afterwards.
      // Default value is true, always call OnModify-Trigger of tables
      CallTableOnModifyTrigger:=TRUE;

      // Set xRec
      Mgt.SetxRec(xRecRef,0);

      // Main Events
      SetValuesToMainEvents();
      CallTableOnModifyTrigger := CallTableOnModifyTrigger AND Events.CallOnModifyTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnDeleteTrigger@1000000033() CallTableOnDeleteTrigger : Boolean;
    BEGIN
      // CallOnDeleteTrigger is called when data is saved back to SQL-Server
      // Return value CallTableOnDeleteTrigger (boolean) sets whether to call default OnDelete-trigger of table afterwards.
      // Default value is true, always call OnDelete-Trigger of tables
      CallTableOnDeleteTrigger:=TRUE;

      // Main Events
      SetValuesToMainEvents();
      CallTableOnDeleteTrigger := CallTableOnDeleteTrigger AND Events.CallOnDeleteTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnInsertRecord@1000000025() DoInsert : Boolean;
    BEGIN
      // A record is inserted. Return value decides if record is inserted.
      // Table OnInsert()-Trigger is called afterwards.
      // On return value FALSE does not insert record.

      // Default
      DoInsert := TRUE;

      CASE RecRef.NUMBER OF

      END;

      // Main Events
      SetValuesToMainEvents();
      DoInsert := DoInsert AND Events.OnInsertRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnModifyRecord@1000000027() DoModify : Boolean;
    VAR
      SalesPrice@1000000000 : Record 7002;
      xSalesPrice@1000000001 : Record 7002;
    BEGIN
      // A record is modified. Return value decides if record is modified.
      // Table OnModify()-Trigger is called afterwards.
      // On return value FALSE does not modify record.

      // Default
      DoModify := TRUE;

      CASE RecRef.NUMBER OF
        // Example
        //DATABASE::"Sales Price": BEGIN
        //  RecRef.SETTABLE(SalesPrice);
        //  xRecRef.SETTABLE(xSalesPrice);
        //  FieldRef := RecRef.FIELD(5);
        //  xFieldRef := xRecRef.FIELD(5);
        //  Mgt.Log('OnModifyRecord', FORMAT(xFieldRef.VALUE) + ' -> ' + FORMAT(FieldRef.VALUE));
        //END;
      END;

      // Main Events
      SetValuesToMainEvents();
      DoModify := DoModify AND Events.OnModifyRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnRenameRecord@1000000011() DoRename : Boolean;
    BEGIN
      // A record is renamed. Return value decides if record is renamed.
      // OnBeforeValidate and OnValidate is called before.
      // Table OnRename()-Trigger is called afterwards.
      // On return value FALSE does not rename record.

      // Default
      DoRename := TRUE;

      // Rename Dialog: Long version with primary key fields
      //EXIT(Mgt.CONFIRM2(Text007,FALSE,xRecRef.GETFILTERS,RecRef.GETFILTERS));
      // Short version
      //EXIT(Mgt.CONFIRM(Text008));

      // Main Events
      SetValuesToMainEvents();
      DoRename := DoRename AND Events.OnRenameRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnDeleteRecord@1000000009() DoDelete : Boolean;
    BEGIN
      // OnDeleteRecord is called if a record is to be deleted.
      // It is not called if a new, unsaved record is deleted.

      // Default
      DoDelete := TRUE;

      // Example: Do not show Delete-Confirm-Message in Sales Lines
      IF RecRef.NUMBER = DATABASE::"Sales Line" THEN EXIT(TRUE);

      // Default message:
      //EXIT(Mgt.CONFIRM2(Text006, FALSE,RecRef.CAPTION, RecRef.GETFILTERS));

      // Main Events
      SetValuesToMainEvents();
      DoDelete := DoDelete AND Events.OnDeleteRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterInsertRecord@1000000028();
    VAR
      ACFWebUser@5326065 : Record 5327140;
      SegmentLine@5326064 : Record 5077;
      SegmentHistoryMgt@5326063 : Codeunit 5061;
      SegCriteriaManagement@5326062 : Codeunit 5062;
    BEGIN
      // Called after a record was successfully inserted.

      CASE RecRef.NUMBER OF
        // Update Segment Criteria Line
        DATABASE::"Segment Line": BEGIN
          RecRef.SETTABLE(SegmentLine);
          WITH SegmentLine DO BEGIN
            IF SegmentLine."Contact No." <> '' THEN BEGIN
              SegCriteriaManagement.InsertContact(SegmentLine."Segment No.",SegmentLine."Contact No.");
              SegmentHistoryMgt.InsertLine(SegmentLine."Segment No.",SegmentLine."Contact No.",SegmentLine."Line No.");
              Mgt.ReloadWebPagesTableNo(DATABASE::"Segment Header",0);
            END;
          END;
        END;
      END;

      // Set default values for new records inserted by users with a set relation code
      ACFWebUser.GET(Mgt.GetACFUser);
      CASE ACFWebUser."Relation Type" OF
        ACFWebUser."Relation Type"::Customer: BEGIN
          CASE RecRef.NUMBER OF
            DATABASE::"Sales Header": BEGIN
              FldRef := RecRef.FIELD(2);
              FldRef.VALIDATE(ACFWebUser."Relation Code");
              RecRef.MODIFY(TRUE);
            END;
          END;
        END;
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterInsertRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterModifyRecord@1000000029();
    VAR
      SegmentLine@5326064 : Record 5077;
      xSegmentLine@5326065 : Record 5077;
      SegmentHistoryMgt@5326063 : Codeunit 5061;
      SegCriteriaManagement@5326062 : Codeunit 5062;
    BEGIN
      // Called after a record was successfully modified.

      CASE RecRef.NUMBER OF
        // Update Segment Criteria Line
        DATABASE::"Segment Line": BEGIN
          RecRef.SETTABLE(SegmentLine);
          xRecRef.SETTABLE(xSegmentLine);
          WITH SegmentLine DO BEGIN
            IF "Contact No." <> xSegmentLine."Contact No." THEN BEGIN
              IF xSegmentLine."Contact No." <> '' THEN BEGIN
                SegCriteriaManagement.DeleteContact("Segment No.",xSegmentLine."Contact No.");
                SegmentHistoryMgt.DeleteLine("Segment No.",xSegmentLine."Contact No.","Line No.");
              END;
              IF "Contact No." <> '' THEN BEGIN
                SegCriteriaManagement.InsertContact("Segment No.","Contact No.");
                SegmentHistoryMgt.InsertLine("Segment No.","Contact No.","Line No.");
              END;
              Mgt.ReloadWebPagesTableNo(DATABASE::"Segment Header",0);
            END;
          END;
        END;
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterModifyRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterRenameRecord@1000000030();
    BEGIN
      //Called after a record was successfully renamed.

      CASE RecRef.NUMBER OF

      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterRenameRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterDeleteRecord@1000000031();
    VAR
      SegmentLine@5326064 : Record 5077;
      SegmentHistoryMgt@5326063 : Codeunit 5061;
      SegCriteriaManagement@5326062 : Codeunit 5062;
    BEGIN
      //Called after a record was successfully deleted.

      CASE RecRef.NUMBER OF
        // Update Segment Criteria Line
        DATABASE::"Segment Line": BEGIN
          RecRef.SETTABLE(SegmentLine);
          WITH SegmentLine DO BEGIN
            IF "Contact No." <> '' THEN BEGIN
              SegCriteriaManagement.DeleteContact("Segment No.","Contact No.");
              SegmentHistoryMgt.DeleteLine("Segment No.","Contact No.","Line No.");
              Mgt.ReloadWebPagesTableNo(DATABASE::"Segment Header",0);
            END;
          END;
        END;
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterDeleteRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAction@1000000026() CloseWindow : Boolean;
    BEGIN
      // OnAction is called when a button in main menu, web page menu or beside a field is pushed.
      // Or in case of a double click event with a function code defined.
      // Default FunctionCodes see Main Events codeunit. Insert them here to overwrite if required.

      IF FunctionCode = '' THEN EXIT;

      // Based on Table
      CASE RecRef.NUMBER OF

      END;

      // Based on Function Code
      CASE FunctionCode OF
        // Customer
        'CUSTOMR:OPEN_HOMEPGE': "OnAction_CUSTOMR:OPEN_HOMEPGE"();

        // Contact
        'CONTACT:CUST/VEND/BA': "OnAction_CONTACT:CUST/VEND/BA"();
        'CONTACT:MAIL':         "OnAction_CONTACT:MAIL"();
        'CONTACT:OPENGM':       "OnAction_CONTACT:OPENGM"();
        'CONTACT:OPEN_HOMEPGE': "OnAction_CONTACT:OPEN_HOMEPGE"();
        'CONTACT:TESTPERSON':   "OnAction_CONTACT:TESTPERSON"();
        'CONTACT:TESTCOMPANY':  "OnAction_CONTACT:TESTCOMPANY"();
        'CREATE_INTERACTION':   OnAction_CREATE_INTERACTION();

        // Customer Ledger Entry
        'CUSTLEDGERENTRY:DOC':  "OnAction_CUSTLEDGERENTRY:DOC"();

        // Reports
        'RPT_SALESQUOTE':       OnAction_RPT_SALESQUOTE();
        'RPT_ORD_CONFIRM':      OnAction_RPT_ORDER_CONFIRM();
        'RPT_SALES_INVOICE':    OnAction_RPT_SALES_INVOICE();
        'RPT_SALES_SHIPMENT':   OnAction_RPT_SALES_SHIPMENT();
        'RPT_CREDIT_MEMO':      OnAction_RPT_CREDIT_MEMO();
        'RPT_CUSTTOP10':        OnAction_RPT_CUSTTOP10();
        'RPT_CUST_TRIAL_BAL':   OnAction_RPT_CUST_TRIAL_BAL();

        //Reports for Segments
        'SEGMENT:ADD_CONTACTS': "OnAction_SEGMENT:ADD_CONTACTS"();
        'SEGMENT:REDUCE_CONT':  "OnAction_SEGMENT:REDUCE_CONT"();

        'SEGMENT:GO_BACK':      "OnAction_SEGMENT:GO_BACK"();

        //** Sales Header Functions
        'SH:RELEASE':           "OnAction_SH:RELEASE"();
        'SH:REOPEN':            "OnAction_SH:REOPEN"();
        'SH:POST':              "OnAction_SH:POST"();
        'SH:POST+PRINT':        "OnAction_SH:POST+PRINT"();
        'SH:MAKEORDER':         "OnAction_SH:MAKEORDER"();

        //** Sales Line Functions
        'ITEM:ADD_TO_SL':       "OnAction_ITEM:ADD_TO_SL"();

        // Customer Portal
        'CP_NEW_SALESORDER':    OnAction_CP_NEW_SALESORDER();

        ELSE BEGIN
          //ERROR(Text004,FunctionCode);
          Mgt.Log('OnAction','No FunctionCode found. Asking Main Events codeunit...');
          SetValuesToMainEvents();
          CloseWindow := CloseWindow OR Events.OnAction();
          GetValuesFromMainEvents();
        END;
      END;
    END;

    PROCEDURE OnBeforeCloseWebPage@1000000004() CloseWindow : Boolean;
    VAR
      SalesHeader@1000000001 : Record 36;
    BEGIN
      // OnCloseWebPage is called before a Web Page is closed.
      // The return value CloseWindow (boolean) decides whether the web page is closed or not.
      // ClosedByNAV indicates whether the user (=FALSE) or Mgt.CloseWebPage() (=TRUE) closed the window.

      // Default
      CloseWindow := TRUE;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header":
          BEGIN
            IF (WebPageElement."Element Type" = WebPageElement."Element Type"::Card)
              AND (NOT WebPageElement.Readonly) THEN BEGIN
              RecRef.SETTABLE(SalesHeader);
              CASE SalesHeader."Document Type" OF
                SalesHeader."Document Type"::Order:
                  BEGIN
                    // Scenario #1
                    // Testfield for External Document No
                    // SalesHeader.TESTFIELD("External Document No.");

                    // Scenario #2
                    // With confirmation dialog
                    FldRef := RecRef.FIELD(100); //"External Document No."
                    IF SalesHeader."External Document No." = '' THEN
                      IF NOT Mgt.CONFIRM1(Text80040,FALSE,FldRef.CAPTION) THEN
                        CloseWindow := FALSE;
                  END;
              END;
            END;
          END;

        DATABASE::"Sales Line": BEGIN
          IF (WebPageElement."Element Type" = WebPageElement."Element Type"::"Card Mobile")
             AND (NOT WebPageElement.Readonly) THEN BEGIN
               Mgt.ReloadWebPagesTableNo(37,2);
          END;
        END;
      END;
      //Mgt.ReloadWebPagesTableNo(RecRef.NUMBER,2); //0 = all cards, 1 = all Lists, 2 all

      // Main Events
      SetValuesToMainEvents();
      CloseWindow := CloseWindow AND Events.OnBeforeCloseWebPage();
      GetValuesFromMainEvents();
    END;

    PROCEDURE IsWriteBack@1000000018() WriteRecord : Boolean;
    VAR
      SalesPrice@1000000000 : Record 7002;
    BEGIN
      // IsWriteBack
      // Usage for return value WriteRecord:
      //  Web Page WriteBehaviour is ...
      //   * Direct      - IsWriteBack is ignored. Data is always written back to SQL-Server
      //   * Delayed     - IsWriteBack is ignored. Data is written back to SQL-Server on close web page/modify other record/logout/...
      //   * Defined     - IsWriteBack is used for decision whether to write data back to SQL-Server.
      //   * Primary Key - IsWriteBack is ignored. Data is written back to SQL-Server if non-primay key field is validated.
      // Complete reload of web page is done after validating fields.

      // Default
      WriteRecord := FALSE;

      IF xFldNo<>0 THEN xFldRef := xRecRef.FIELD(xFldNo);

      CASE xRecRef.NUMBER OF
        // Example: Write Record if other value than Customer No. is set.
        // The automatic WriteBehaviour "Primary Key" gives same behaviour.
        DATABASE::Customer: BEGIN
          EXIT(NOT(xFldNo IN [0,1]));
        END;

        // Example "Sales Price" table: Write Record if Unit Price is entered
        //DATABASE::"Sales Price": BEGIN
        //  xRecRef.SETTABLE(SalesPrice);
        //  WriteRecord := (SalesPrice."Unit Price"<>0);
        //END;
      END;

      // Main Events
      SetValuesToMainEvents();
      WriteRecord := WriteRecord OR Events.IsWriteBack();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CustomWebPageRelation@1000000005();
    BEGIN
      // Sets custom relations between two Web Pages, Web Page Elements or for DropDown-Lists defined by function code

      //IF RelationCode = '' THEN ERROR(Text001, CustomTableRelationRecRef.NAME);

      // Optional:
      // Test source of Custom Table Relation
      //CASE CustomTableRelationRecRef.NUMBER OF
      //  DATABASE::"ACF Web Page Element": ;       //Web Page Element: Call from Web Page to Web Page
      //  DATABASE::"ACF Web Page Element Menu": ;  //Web Page Element Menu: Call from Menu
      //  DATABASE::"ACF Web Page Element Field": ; //Web Page Element Field: Call from Field
      //END;

      CASE RelationCode OF
        // Contact - Sorting
        'CONTACT:SORT_BY_NAME': "CTR_CONTACT:SORT_BY_NAME"();
        'CONTACT:SORT_BY_COMP': "CTR_CONTACT:SORT_BY_COMP"();

        // Customer - Relations
        'CUSTOMER:CONTACT':     "CTR_CUSTOMER:CONTACT"();
        'CUSTOMER:CONTACTS':    "CTR_CUSTOMER:CONTACTS"();

        // Sales Header
        'SH:SELL_TO_CONTACT':   "CTR_SH:SELL_TO_CONTACT"();
        'SH:BILL_TO_CONTACT':   "CTR_SH:BILL_TO_CONTACT"();
        'SH:CITY':              "CTR_SH:CITY"();

        // Sales Line
        'SL:UNIT_OF_MEASURE':   "CTR_SL:UNIT_OF_MEASURE"();

        // Other
        'PROFQUESTLNEDESC':     CTR_PROFQUESTLNEDESC();
        'EXAMPLE_ITEM':         CTR_EXAMPLE_ITEM();

      ELSE BEGIN
          //ERROR(Text001, FldRef.VALUE);
          Mgt.Log('CustomWebPageRelation','No CustomWebPageRelation found. Asking Main Events codeunit...');
          SetValuesToMainEvents();
          Events.CustomWebPageRelation();
          GetValuesFromMainEvents();
        END;
      END;

      // Test, if destination record ref is found.
      IF DestRecRef.NUMBER=0 THEN ERROR(Text003);
      // Log actual filter value
      Mgt.Log('CustomWebPageRelation',DestRecRef.GETVIEW);
    END;

    PROCEDURE DropDownFieldDefinition@1000000002();
    BEGIN
      // DropDown Field Definition
      // Defaults for this module only

      {
      CASE TableNumber OF
        DATABASE::TableNumber: BEGIN
          "Field#1" := Record.FIELDNO(FieldName1);
          "Field#2" := Record.FIELDNO(FieldName2);
          EXIT;
        END;
      END;
      }

      // If not defined by this module, load defaults from Main Events codeunit or definition of Web Page Element Field
      SetValuesToMainEvents();
      Events.DropDownFieldDefinition();
      GetValuesFromMainEvents();
    END;

    LOCAL PROCEDURE "-- OnAction-Functions"@5326063();
    BEGIN
    END;

    PROCEDURE "OnAction_CUSTOMR:OPEN_HOMEPGE"@5326075();
    VAR
      Customer@5326062 : Record 18;
    BEGIN
      // Opens customers home page
      RecRef.SETTABLE(Customer);
      IF Customer."Home Page" <> '' THEN
        Mgt.HYPERLINK(Customer."Home Page");
    END;

    PROCEDURE "OnAction_CONTACT:MAIL"@5326064();
    VAR
      Contact@5326063 : Record 5050;
    BEGIN
      // Opens Mailprogram from Contact's e-mail-address
      RecRef.SETTABLE(Contact);
      Mgt.HYPERLINK('mailto:' + Contact."E-Mail");
    END;

    PROCEDURE "OnAction_CONTACT:CUST/VEND/BA"@5326067();
    VAR
      Contact@5326063 : Record 5050;
      ContBusRel@5326066 : Record 5054;
      Customer@5326064 : Record 18;
      Vendor@5326065 : Record 23;
      DestRecRef@5326067 : RecordRef;
    BEGIN
      // Contact relation to customer, vendor or bank account
      RecRef.SETTABLE(Contact);

      ContBusRel.RESET;
      ContBusRel.SETRANGE("Contact No.",Contact."Company No.");
      ContBusRel.SETFILTER("No.",'<>''''');

      IF ContBusRel.COUNT = 0 THEN ERROR(Text80050,Contact.TABLECAPTION,Contact."No.");
      ContBusRel.FIND('-');

      REPEAT
        CASE ContBusRel."Link to Table" OF
          ContBusRel."Link to Table"::Customer: BEGIN
            Customer.GET(ContBusRel."No.");
            DestRecRef.GETTABLE(Customer);
            Mgt.OpenWebPage('CUSTOMER_CARD',FALSE,DestRecRef,'');
          END;
          ContBusRel."Link to Table"::Vendor: BEGIN
            Vendor.GET(ContBusRel."No.");
            DestRecRef.GETTABLE(Vendor);
            Mgt.OpenWebPage('VENDOR_CARD',FALSE,DestRecRef,'');
          END;
          //ContBusRel."Link to Table"::"Bank Account": BEGIN
          //END;
        END;
      UNTIL ContBusRel.NEXT=0;
    END;

    PROCEDURE "OnAction_CONTACT:OPENGM"@5326096();
    VAR
      Contact@5326062 : Record 5050;
      Country@5326063 : Record 9;
      Address@5326064 : Text[1024];
    BEGIN
      RecRef.SETTABLE(Contact);
      IF Country.GET(Contact."Country/Region Code") THEN ;
      Address := Contact.City + ',' + Contact."Post Code" + ',' + Contact.Address + ',' + Country.Name;
      Mgt.OpenGoogleMaps(Address,
                         Contact.Name,
                         Contact.Name,
                         FALSE,
                         768,
                         768);
    END;

    PROCEDURE "OnAction_CONTACT:OPEN_HOMEPGE"@5326106();
    VAR
      Contact@5326062 : Record 5050;
      Country@5326063 : Record 9;
      Address@5326064 : Text[1024];
    BEGIN
      RecRef.SETTABLE(Contact);
      IF Contact."Home Page" <> '' THEN
        Mgt.HYPERLINK(Contact."Home Page");
    END;

    PROCEDURE "OnAction_CONTACT:TESTPERSON"@5326112();
    VAR
      Contact@5326062 : Record 5050;
    BEGIN
      RecRef.SETTABLE(Contact);
      Contact.TESTFIELD(Type, Contact.Type::Person);
    END;

    PROCEDURE "OnAction_CONTACT:TESTCOMPANY"@5326108();
    VAR
      Contact@5326062 : Record 5050;
    BEGIN
      RecRef.SETTABLE(Contact);
      Contact.TESTFIELD(Type, Contact.Type::Company);
    END;

    PROCEDURE OnAction_CREATE_INTERACTION@5326090();
    VAR
      SegmentLine@5326064 : Record 5077;
      SegManagement@5326062 : Codeunit 5051;
      VirtualData@5326063 : Codeunit 5327127;
    BEGIN
      VirtualData.OPEN('SEGMENT_LINE');

      Mgt.MESSAGE(SegmentLine."Contact No.");
      {
      AttachmentTemp.COPY(AttachmentTmp);
      "Attempt Failed" := NOT "Interaction Successful";
      Subject := Description;
      ProcessPostponedAttachment;
      send := (IsFinish AND ("Correspondence Type" <> "Correspondence Type"::" "));
      SegManagement.LogInteraction(Rec,AttachmentTemp,InterLogEntryCommentLineTmp,send,NOT IsFinish)
      }
    END;

    PROCEDURE "OnAction_CUSTLEDGERENTRY:DOC"@5326101();
    VAR
      CustLedgerEntry@5326062 : Record 21;
      SalesInvoiceHeader@5326063 : Record 112;
      SalesCrMemoHeader@5326064 : Record 114;
    BEGIN
      // Open related documents
      RecRef.SETTABLE(CustLedgerEntry);
      CASE CustLedgerEntry."Document Type" OF
        CustLedgerEntry."Document Type"::Invoice: BEGIN
          IF SalesInvoiceHeader.GET(CustLedgerEntry."Document No.") THEN BEGIN
            Mgt.OPENRecRef(DATABASE::"Sales Invoice Header", RecRef);
            RecRef.GETTABLE(SalesInvoiceHeader);
            Mgt.OpenWebPage('SALES_POST_INV_CARD', FALSE, RecRef, '');
          END ELSE
            Mgt.MESSAGE1(Text80065, CustLedgerEntry."Document No.");
        END;

        CustLedgerEntry."Document Type"::"Credit Memo": BEGIN
          IF SalesCrMemoHeader.GET(CustLedgerEntry."Document No.") THEN BEGIN
            Mgt.OPENRecRef(DATABASE::"Sales Cr.Memo Header", RecRef);
            RecRef.GETTABLE(SalesCrMemoHeader);
            Mgt.OpenWebPage('SALES_POST_CRM_CARD', FALSE, RecRef, '');
          END ELSE
            Mgt.MESSAGE1(Text80065, CustLedgerEntry."Document No.");
        END;
        ELSE
          Mgt.MESSAGE(Text80064);
      END;
    END;

    PROCEDURE "OnAction_SH:RELEASE"@5326073();
    VAR
      SalesHeader@5326063 : Record 36;
      ReleaseSalesDocument@5326064 : Codeunit 414;
    BEGIN
      RecRef.SETTABLE(SalesHeader);
      IF Mgt.CONFIRM2(Text80000,TRUE, SalesHeader."Document Type",SalesHeader."No.") THEN BEGIN
        ReleaseSalesDocument.PerformManualRelease(SalesHeader);
        Mgt.ReloadWebPage();
      END;
    END;

    PROCEDURE "OnAction_SH:REOPEN"@5326076();
    VAR
      SalesHeader@5326063 : Record 36;
      ReleaseSalesDocument@5326064 : Codeunit 414;
    BEGIN
      RecRef.SETTABLE(SalesHeader);
      IF Mgt.CONFIRM2(Text80001,FALSE, SalesHeader."Document Type",SalesHeader."No.") THEN BEGIN
        ReleaseSalesDocument.PerformManualReopen(SalesHeader);
        Mgt.ReloadWebPage();
      END;
    END;

    PROCEDURE "OnAction_SH:POST"@5326084();
    VAR
      SalesHeader@5326063 : Record 36;
      SalesPostYesNo@5326065 : Codeunit 81;
    BEGIN
      // Info message to tell user that posting only works when codeunits 80-82 are adapted
      ERROR(Text80060);

      // Calling Sales-Post Yes/No
      RecRef.SETTABLE(SalesHeader);
      SalesPostYesNo.RUN(SalesHeader);
      IF SalesHeader.GET(SalesHeader."Document Type", SalesHeader."No.") THEN
        Mgt.ReloadWebPage()
      ELSE
        Mgt.CloseWebPage();
    END;

    PROCEDURE "OnAction_SH:POST+PRINT"@5326083();
    VAR
      SalesHeader@5326063 : Record 36;
      SalesPostPrint@5326064 : Codeunit 82;
    BEGIN
      //Info message to tell user that posting only works when codeunits 80-82 are adapted
      ERROR(Text80060);

      //Calling Sales-Post+Print
      RecRef.SETTABLE(SalesHeader);
      SalesPostPrint.RUN(SalesHeader);
      IF SalesHeader.GET(SalesHeader."Document Type", SalesHeader."No.") THEN
        Mgt.ReloadWebPage()
      ELSE
        Mgt.CloseWebPage();
    END;

    PROCEDURE "OnAction_SH:MAKEORDER"@5326111();
    VAR
      SalesHeader@5326062 : Record 36;
      NewSalesHeader@5326063 : Record 36;
      BlanketSalesOrdertoOrder@5326064 : Codeunit 87;
    BEGIN
      //Info message to tell user that posting only works when codeunit 87 is adapted
      ERROR(Text80063);

      RecRef.SETTABLE(SalesHeader);
      IF Mgt.CONFIRM(Text80061,FALSE) THEN BEGIN
        BlanketSalesOrdertoOrder.RUN(SalesHeader);
        BlanketSalesOrdertoOrder.GetSalesOrderHeader(NewSalesHeader);
        Mgt.MESSAGE2(Text80062,NewSalesHeader."No.",SalesHeader."No.");
      END;
    END;

    PROCEDURE "OnAction_ITEM:ADD_TO_SL"@5326118();
    VAR
      Item@5326062 : Record 27;
      SalesHeader@5326064 : Record 36;
      SalesLine@5326063 : Record 37;
      LineNo@5326065 : Integer;
    BEGIN
      RecRef.SETTABLE(Item);
      WindowRecRef.SETTABLE(SalesHeader);

      SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", SalesHeader."No.");
      IF SalesLine.FINDLAST THEN
        LineNo := SalesLine."Line No." + 10000
      ELSE
        LineNo := 10000;

      SalesLine.RESET;

      SalesLine.INIT;
      SalesLine.VALIDATE("Document Type", SalesHeader."Document Type");
      SalesLine.VALIDATE("Document No.", SalesHeader."No.");
      SalesLine.VALIDATE("Line No.", LineNo);
      SalesLine.VALIDATE(Type, SalesLine.Type::Item);
      SalesLine.VALIDATE("No.", Item."No.");
      SalesLine.INSERT(TRUE);

      Mgt.CloseWebPage();
      Mgt.ReloadWebPagesTableNo(37, 2);
    END;

    PROCEDURE OnAction_RPT_SALESQUOTE@5326078();
    VAR
      Record@5326064 : Record 36;
      Report@5326065 : Report 204;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(204);

      // Get Sales Header Filter and apply to Report
      RecordFilter.Get_SalesHeader(RecRef, Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options (No. of Copies etc.)
      ReportOptions.OPEN('REPORT_OPTIONS');
      //Report.SetParameters(ReportOptions.getInteger('COPIES'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_ORDER_CONFIRM@5326098();
    VAR
      Record@5326064 : Record 36;
      Report@5326065 : Report 205;
      ReportOptions@5326069 : Codeunit 5327127;
      RecordFilter@5326067 : Codeunit 5327128;
      SecurityMgt@5326066 : Codeunit 5327120;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(205);

      // Get Sales Header Filter and apply to Report
      RecordFilter.Get_SalesHeader(RecRef, Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options (No. of Copies etc.)
      ReportOptions.OPEN('REPORT_OPTIONS');
      //Report.SetParameters(ReportOptions.getInteger('COPIES'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_SALES_INVOICE@5326102();
    VAR
      Record@5326064 : Record 112;
      Report@5326065 : Report 206;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(206);

      // Get Sales Invoice Header Filter and apply to Report
      RecordFilter.Get_SalesInvoiceHeader(RecRef, Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options (No. of Copies etc.)
      ReportOptions.OPEN('REPORT_OPTIONS');
      //Report.SetParameters(ReportOptions.getInteger('COPIES'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_SALES_SHIPMENT@5326103();
    VAR
      Record@5326064 : Record 110;
      Report@5326065 : Report 208;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(208);

      // Get Sales Shipment Header Filter and apply to Report
      RecordFilter.Get_SalesShipmentHeader(RecRef, Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options (No. of Copies etc.)
      ReportOptions.OPEN('REPORT_OPTIONS');
      //Report.SetParameters(ReportOptions.getInteger('COPIES'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_CREDIT_MEMO@5326122();
    VAR
      Record@5326064 : Record 114;
      Report@5326065 : Report 207;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(207);

      // Get Sales Credit Memo Header Filter and apply to Report
      RecordFilter.Get_SalesCrMemoHeader(RecRef, Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options (No. of Copies etc.)
      ReportOptions.OPEN('REPORT_OPTIONS');
      //Report.SetParameters(ReportOptions.getInteger('COPIES'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_CUSTTOP10@5326124();
    VAR
      Record@5326064 : Record 18;
      Report@5326065 : Report 111;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(111);

      // Get Customer Filter and apply to Report
      RecordFilter.GetVT_Customer('CUSTOMER_FILTER',Record);
      Report.SETTABLEVIEW(Record);

      // Set Report options
      ReportOptions.OPEN('REPORT_OPT_CUSTTOP10');
      // If you want to set the Report Options, please create a function in this report to set parameters before running the report.
      //Report.SetParameters(ReportOptions.getInteger('SHOWTYPE'),
      //  ReportOptions.getInteger('NUMBER'),
      //  ReportOptions.getInteger('CHARTTYPE'));

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE OnAction_RPT_CUST_TRIAL_BAL@5326070();
    VAR
      Record@5326064 : Record 18;
      Report@5326065 : Report 129;
      ReportOptions@5326069 : Codeunit 5327127;
      FileName@5326063 : Text[1024];
    BEGIN
      // Check Report Rights
      SecurityMgt.TestRightReport(129);

      // Get Customer Filter and apply to Report
      RecordFilter.GetVT_Customer('CUSTOMER_FILTER',Record);
      Report.SETTABLEVIEW(Record);

      // Print Report as PDF
      FileName := Mgt.GetTempFileName('pdf');
      Report.SAVEASPDF(FileName);

      // Send Report to User
      IF EXISTS(FileName) THEN Mgt.OpenFileDownloadDialogWin(FileName,TRUE);
    END;

    PROCEDURE "OnAction_SEGMENT:ADD_CONTACTS"@5326072();
    VAR
      SegmentHeader@5326062 : Record 5076;
      Contact@5326064 : Record 5050;
      ContactProfileAnswer@5326067 : Record 5089;
      ContactMailingGroup@5326068 : Record 5056;
      InteractionLogEntry@5326069 : Record 5065;
      ContactJobResponsibility@5326066 : Record 5067;
      ContactIndustryGroup@5326070 : Record 5058;
      ContactBusinessRelation@5326071 : Record 5054;
      ValueEntry@5326072 : Record 5802;
      Report@5326065 : Report 5198;
    BEGIN
      SecurityMgt.TestRightReport(5198);

      // Get Filters on Record
      RecordFilter.SETTABLE_SegmentHeader(RecRef, SegmentHeader);
      SegmentHeader.SETRECFILTER;
      RecordFilter.GetVT_Contact('CONTACT_FILTER', Contact);
      RecordFilter.GetVT_ContactProfileAnswer('CONT_PROF_ANS_FILTER', ContactProfileAnswer);
      RecordFilter.GetVT_ContactMailingGroup('CONT_MAIL_GRP_FILTER', ContactMailingGroup);
      RecordFilter.GetVT_InteractionLogEntry('INTERLOGENTRY_FILTER',InteractionLogEntry);
      RecordFilter.GetVT_ContactJobResponsibility('CONTJOBRESPON_FILTER',ContactJobResponsibility);
      RecordFilter.GetVT_ContactIndustryGroup('CONTINDGROUP_FILTER',ContactIndustryGroup);
      RecordFilter.GetVT_ContactBusinessRelation('CONTBUSRELAT_FILTER',ContactBusinessRelation);
      RecordFilter.GetVT_ValueEntry('VALUEENTRY_FILTER',ValueEntry);

      // Set Filters to Report and run
      Report.SETTABLEVIEW(SegmentHeader);
      Report.SETTABLEVIEW(Contact);
      Report.SETTABLEVIEW(ContactProfileAnswer);
      Report.SETTABLEVIEW(ContactMailingGroup);
      Report.SETTABLEVIEW(InteractionLogEntry);
      Report.SETTABLEVIEW(ContactJobResponsibility);
      Report.SETTABLEVIEW(ContactIndustryGroup);
      Report.SETTABLEVIEW(ContactBusinessRelation);
      Report.SETTABLEVIEW(ValueEntry);
      Report.USEREQUESTFORM(FALSE);
      Report.RUNMODAL();
    END;

    PROCEDURE "OnAction_SEGMENT:REDUCE_CONT"@5326144();
    VAR
      SegmentHeader@5326062 : Record 5076;
      Contact@5326064 : Record 5050;
      ContactProfileAnswer@5326067 : Record 5089;
      ContactMailingGroup@5326068 : Record 5056;
      InteractionLogEntry@5326069 : Record 5065;
      ContactJobResponsibility@5326066 : Record 5067;
      ContactIndustryGroup@5326070 : Record 5058;
      ContactBusinessRelation@5326071 : Record 5054;
      ValueEntry@5326072 : Record 5802;
      Report@5326065 : Report 5197;
    BEGIN
      SecurityMgt.TestRightReport(5197);

      // Get Filters on Record
      RecordFilter.SETTABLE_SegmentHeader(RecRef, SegmentHeader);
      SegmentHeader.SETRECFILTER;
      RecordFilter.GetVT_Contact('CONTACT_FILTER', Contact);
      RecordFilter.GetVT_ContactProfileAnswer('CONT_PROF_ANS_FILTER', ContactProfileAnswer);
      RecordFilter.GetVT_ContactMailingGroup('CONT_MAIL_GRP_FILTER', ContactMailingGroup);
      RecordFilter.GetVT_InteractionLogEntry('INTERLOGENTRY_FILTER',InteractionLogEntry);
      RecordFilter.GetVT_ContactJobResponsibility('CONTJOBRESPON_FILTER',ContactJobResponsibility);
      RecordFilter.GetVT_ContactIndustryGroup('CONTINDGROUP_FILTER',ContactIndustryGroup);
      RecordFilter.GetVT_ContactBusinessRelation('CONTBUSRELAT_FILTER',ContactBusinessRelation);
      RecordFilter.GetVT_ValueEntry('VALUEENTRY_FILTER',ValueEntry);

      // Set Filters to Report and run
      Report.SETTABLEVIEW(SegmentHeader);
      Report.SETTABLEVIEW(Contact);
      Report.SETTABLEVIEW(ContactProfileAnswer);
      Report.SETTABLEVIEW(ContactMailingGroup);
      Report.SETTABLEVIEW(InteractionLogEntry);
      Report.SETTABLEVIEW(ContactJobResponsibility);
      Report.SETTABLEVIEW(ContactIndustryGroup);
      Report.SETTABLEVIEW(ContactBusinessRelation);
      Report.SETTABLEVIEW(ValueEntry);
      Report.USEREQUESTFORM(FALSE);
      Report.RUNMODAL();
    END;

    PROCEDURE "OnAction_SEGMENT:GO_BACK"@5326114();
    VAR
      SegmentLine@5326064 : Record 5077;
      SegmentHeader@5326062 : Record 5076;
      Text012@5326063 : TextConst 'DEU=Dies nimmt die letzte Kriterienaktion zurck.\M”chten Sie fortfahren?;ENU=This will undo the last criteria action.\Do you wish to proceed?';
      SegmentHistoryMgt@5326065 : Codeunit 5061;
    BEGIN
      IF RecRef.NUMBER = DATABASE::"Segment Line" THEN BEGIN
        RecRef.SETTABLE(SegmentLine);
        SegmentHeader.GET(SegmentLine."Segment No.");
      END ELSE
        RecRef.SETTABLE(SegmentHeader);
      SegmentHeader.CALCFIELDS("No. of Criteria Actions");
      IF SegmentHeader."No. of Criteria Actions" > 0 THEN
        IF Mgt.CONFIRM(Text012,FALSE) THEN BEGIN
          SegmentHistoryMgt.GoBack(SegmentHeader."No.");
          Mgt.ReloadWebPagesTableNo(DATABASE::"Segment Header",0);
          Mgt.ReloadWebPagesTableNo(DATABASE::"Segment Line",1);
        END;
    END;

    PROCEDURE OnAction_CP_NEW_SALESORDER@5326087();
    VAR
      SalesHeader@5326062 : Record 36;
      ACFWebUser@5326063 : Record 5327140;
    BEGIN
      ACFWebUser.GET(Mgt.GetACFUser);
      RecRef.OPEN(DATABASE::"Sales Header");
      RecRef.SETTABLE(SalesHeader);
      SalesHeader.INIT;
      SalesHeader.VALIDATE("Document Type",SalesHeader."Document Type"::Order);
      SalesHeader.INSERT(TRUE);
      SalesHeader.GET(SalesHeader."Document Type"::Order,SalesHeader."No.");
      SalesHeader.VALIDATE("Sell-to Customer No.",ACFWebUser."Relation Code");
      SalesHeader.VALIDATE("Order Date",TODAY);
      SalesHeader.MODIFY(TRUE);
      RecRef.GETTABLE(SalesHeader);
      Mgt.OpenWebPage('CP_SALESORDER_CARD',FALSE,RecRef,'');
    END;

    LOCAL PROCEDURE "-- CustomTableRelation-Func."@5326062();
    BEGIN
    END;

    PROCEDURE "CTR_CONTACT:SORT_BY_NAME"@5326065();
    VAR
      Contact@5326064 : Record 5050;
    BEGIN
      // Open contact list and sort by name
      Contact.SETCURRENTKEY(Name);
      Mgt.OPENRecRef(DATABASE::Contact,DestRecRef);
      DestRecRef.GETTABLE(Contact);
    END;

    PROCEDURE "CTR_CONTACT:SORT_BY_COMP"@5326069();
    VAR
      Contact@5326064 : Record 5050;
    BEGIN
      // Open contact list and sort by company
      Contact.SETCURRENTKEY("Company Name");
      Mgt.OPENRecRef(DATABASE::Contact,DestRecRef);
      DestRecRef.GETTABLE(Contact);
    END;

    PROCEDURE CTR_PROFQUESTLNEDESC@5326117();
    VAR
      VirtProfileQuestionnaireLine@5326063 : Codeunit 5327127;
      ProfileQuestionnaireLine@5326062 : Record 5088;
    BEGIN
      // NP Untested.
      // Waiting for bugfix Custom Table Relation for DropDownList

      VirtProfileQuestionnaireLine.OPEN('PROFQUESTLNE_FILTER');
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code", VirtProfileQuestionnaireLine.getCode('PROFILE QUEST. CODE'));
      RecRef.GETTABLE(ProfileQuestionnaireLine);
    END;

    PROCEDURE "CTR_CUSTOMER:CONTACT"@5326077();
    VAR
      Contact@5326064 : Record 5050;
      Customer@5326065 : Record 18;
      ContBusRel@5326066 : Record 5054;
    BEGIN
      // Show related company contact from customer
      // Data Source: Customer
      // Destination: Contact

      // 1. Set table reference
      RecRef.SETTABLE(Customer);
      Mgt.OPENRecRef(DATABASE::Contact,DestRecRef);
      // 2. Filter
      ContBusRel.RESET;
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",Customer."No.");
      IF ContBusRel.FIND('-') THEN
        Contact.GET(ContBusRel."Contact No.");
      Contact.SETRECFILTER;
      // 3. Copy Filters to DestRecRef
      DestRecRef.SETVIEW(Contact.GETVIEW);
    END;

    PROCEDURE "CTR_CUSTOMER:CONTACTS"@5326079();
    VAR
      Contact@5326064 : Record 5050;
      Customer@5326065 : Record 18;
      ContBusRel@5326066 : Record 5054;
    BEGIN
      // Show all related Contacts from Customer
      // Data Source: Customer
      // Destination: Contacts

      // 1. Set table reference
      RecRef.SETTABLE(Customer);
      Mgt.OPENRecRef(DATABASE::Contact,DestRecRef);
      // 2. Filter
      ContBusRel.RESET;
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",Customer."No.");
      ContBusRel.FINDFIRST;
      Contact.SETRANGE("Company No.", ContBusRel."Contact No.");
      // 3. Copy Filters to DestRecRef
      DestRecRef.SETVIEW(Contact.GETVIEW);
    END;

    PROCEDURE "CTR_SH:SELL_TO_CONTACT"@5326115();
    VAR
      Customer@5326065 : Record 18;
      SalesHeader@5326062 : Record 36;
      Contact@5326063 : Record 5050;
      ContBusRel@5326064 : Record 5054;
    BEGIN
      //** DRILL-DOWN Field Relations
      //Show all related Contacts from "Sales Header"
      //Data Source: "Sell-to Contact No."
      //Destination: Contact

      //1. Set table reference of data source
      RecRef.SETTABLE(SalesHeader);
      DestRecRef.SETTABLE(Contact);

      //2. Get Customer
      Customer.GET(SalesHeader."Sell-to Customer No.");

      //3. Filter
      ContBusRel.RESET;
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",Customer."No.");
      IF ContBusRel.FIND('-') THEN
        Contact.SETRANGE("Company No.",ContBusRel."Contact No.");
      //3. Set Filter for output
      DestRecRef.SETVIEW(Contact.GETVIEW);
    END;

    PROCEDURE "CTR_SH:BILL_TO_CONTACT"@5326116();
    VAR
      Customer@5326065 : Record 18;
      SalesHeader@5326064 : Record 36;
      Contact@5326063 : Record 5050;
      ContBusRel@5326062 : Record 5054;
    BEGIN
      //** DRILL-DOWN Field Relations
      //Show all related Contacts from "Sales Header"
      //Data Source: "Bill-to Contact No."
      //Destination: Contact

      //1. Set table reference of data source
      RecRef.SETTABLE(SalesHeader);
      DestRecRef.SETTABLE(Contact);

      //2. Get Customer
      Customer.GET(SalesHeader."Bill-to Customer No.");

      //3. Filter
      ContBusRel.RESET;
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",Customer."No.");
      IF ContBusRel.FIND('-') THEN
        Contact.SETRANGE("Company No.",ContBusRel."Contact No.");
      //3. Set Filter for output
      DestRecRef.SETVIEW(Contact.GETVIEW);
    END;

    PROCEDURE "CTR_SH:CITY"@5326130();
    VAR
      SalesHeader@5326064 : Record 36;
      PostCode@5326062 : Record 225;
    BEGIN
      //Data Source: "Sales Header"."Sell-To City", "Bill-To City", "Ship-To City"
      //Destination: "Post Code"

      //1. Set table reference of data source
      RecRef.SETTABLE(SalesHeader);
      DestRecRef.OPEN(DATABASE::"Post Code");
      DestRecRef.SETTABLE(PostCode);
      //2. Filter
      //no filter
      PostCode.SETCURRENTKEY(City,Code);
      //3. Set Filter for output
      DestRecRef.SETVIEW(PostCode.GETVIEW);
    END;

    PROCEDURE "CTR_SL:UNIT_OF_MEASURE"@5326119();
    VAR
      SalesLine@5326062 : Record 37;
      ItemUnit@5326063 : Record 5404;
      ResourceUnit@5326064 : Record 205;
    BEGIN
      //1. Set table reference of data source
      RecRef.SETTABLE(SalesLine);
      CASE SalesLine.Type OF
        SalesLine.Type::Item:
        BEGIN
          ItemUnit.SETRANGE("Item No.",SalesLine."No.");
          Mgt.OPENRecRef(DATABASE::"Item Unit of Measure",DestRecRef);
          DestRecRef.SETVIEW(ItemUnit.GETVIEW());
        END;
        SalesLine.Type::Resource:
        BEGIN
          ResourceUnit.SETRANGE("Resource No.",SalesLine."No.");
          Mgt.OPENRecRef(DATABASE::"Resource Unit of Measure",DestRecRef);
          DestRecRef.SETVIEW(ResourceUnit.GETVIEW());
        END;
      END;
    END;

    PROCEDURE CTR_EXAMPLE_ITEM@5326123();
    VAR
      Item@5326062 : Record 27;
    BEGIN
      Mgt.Log('CTR_EXAMPLE_ITEM','BEGIN');
      Item.SETFILTER("No.", '1???');
      RecRef.GETTABLE(Item);
      Mgt.Log('CTR_EXAMPLE_ITEM','END');
    END;

    PROCEDURE "--Web Page Element Field-Func."@5326080();
    BEGIN
    END;

    PROCEDURE OAGetField_CUSTLEDGENTRY@5326068() ShowValue : Boolean;
    VAR
      CustLedgerEntry@5326068 : Record 21;
    BEGIN
      ShowValue:=TRUE;
      RecRef.SETTABLE(CustLedgerEntry);

      IF Mgt.IsMobile THEN BEGIN
        // Style Definition for Anveo Mobile
        IF FldRef.NUMBER = CustLedgerEntry.FIELDNO("Document Type") THEN BEGIN
          ShowValue := FALSE;
          CASE CustLedgerEntry."Document Type" OF
            CustLedgerEntry."Document Type"::Invoice:        ShowValue := FieldName = '1000005';
            CustLedgerEntry."Document Type"::"Credit Memo":  ShowValue := FieldName = '2000005';
            CustLedgerEntry."Document Type"::Payment:        ShowValue := FieldName = '3000005';
          ELSE
            ShowValue := FieldName = '5';
          END;
        END;

      END ELSE BEGIN
        // Style Definition for Anveo Web Desk
        Style.InitAndSetCondition(WebPageElement, TRUE);
        IF FldRef.NUMBER IN [CustLedgerEntry.FIELDNO("Remaining Amount"),
                             CustLedgerEntry.FIELDNO("Remaining Amt. (LCY)"),
                             CustLedgerEntry.FIELDNO("Due Date")]  THEN BEGIN
          Style.SetCondition((CustLedgerEntry.Open) AND (CustLedgerEntry."Due Date"<TODAY));
          Style.BackgroundColor('#FFDDDD','');
        END;
        IF FldRef.NUMBER IN [CustLedgerEntry.FIELDNO("Document Type")] THEN BEGIN
          Style.SetCondition(TRUE);

          CASE CustLedgerEntry."Document Type" OF
            CustLedgerEntry."Document Type"::Invoice: Style.Image('document_send_green.png','');
            CustLedgerEntry."Document Type"::"Credit Memo": Style.Image('document_receive_red.png','');
            CustLedgerEntry."Document Type"::Payment: Style.Image('money-note.png','');
          END;
          //Style.ImagePosition('95%','95%');
          //Style.TextIndent('0px','0px');
        END;
        Style.SetCondition(CustLedgerEntry."Posting Date" <= 311211D);
        Style.TextColor('gray','black');
      END;
    END;

    PROCEDURE OAGetField_CUSTOMER@5326110() ShowValue : Boolean;
    VAR
      Customer@5326062 : Record 18;
      SalesHeader@5326063 : Record 36;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(Customer);

      // Anveo Web Desk List
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::List THEN BEGIN
        Style.SetCondition(TRUE);
        IF FldRef.NUMBER = Customer.FIELDNO(Name) THEN Style.FontWeight('bold','');

        Style.SetCondition(Customer.Blocked=0);
        Style.TextColor('black','red');
      END;

      // Anveo Web Desk Card
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::Card THEN BEGIN
        IF FldRef.NUMBER = Customer.FIELDNO(Name) THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
        IF FldRef.NUMBER = Customer.FIELDNO(Contact) THEN BEGIN
          Style.SetCondition(Customer."Primary Contact No."='');
          Style.Enabled;
          Style.BackgroundColor('white','gray');
        END;
      END;

      // Anveo Mobile List
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
        CASE FieldName OF
          '2':       ShowValue := (Customer.Blocked=0);
          '1000002': ShowValue := NOT (Customer.Blocked=0);
        END;
      END;

      // Anveo Virtual Fields
      CASE WebPageElementField."Virtual Table Code" OF
       'CUSTOMER_SALESORDERS': BEGIN
          SalesHeader.SETRANGE("Document Type", SalesHeader."Document Type"::Order);
          SalesHeader.SETRANGE("Sell-to Customer No.", Customer."No.");
          IF STRPOS(WebPageElementField."Virtual Field Code", 'OPEN')<>0 THEN
            SalesHeader.SETRANGE(Status, SalesHeader.Status::Open);
          IF STRPOS(WebPageElementField."Virtual Field Code", 'RELEASED')<>0 THEN
            SalesHeader.SETRANGE(Status, SalesHeader.Status::Released);
          IF STRPOS(WebPageElementField."Virtual Field Code", '1W') <> 0 THEN
            SalesHeader.SETRANGE(SalesHeader."Order Date", CALCDATE('<-1W>',WORKDATE),WORKDATE);
          IF STRPOS(WebPageElementField."Virtual Field Code", '4W') <> 0 THEN
            SalesHeader.SETRANGE(SalesHeader."Order Date", CALCDATE('<-4W>',WORKDATE),WORKDATE);

          IF STRPOS(WebPageElementField."Virtual Field Code", 'CNT') <>0 THEN BEGIN
            FldRef.VALUE := SalesHeader.COUNT;
          END ELSE
            Style.NoBorder();
          IF STRPOS(WebPageElementField."Virtual Field Code", 'ICO') <>0  THEN BEGIN
            Style.SetCondition(TRUE);
            Style.Image(GetIndicator(SalesHeader.COUNT,'blue',1,'blue',5,'blue',10,'blue'),'');
            Style.ImagePosition('center','');
          END;
        END;
        'CUSTOMER_STATISTICS': BEGIN
          CASE WebPageElementField."Virtual Field Code" OF
            'ICO_BALANCE': BEGIN
              Customer.CALCFIELDS("Balance (LCY)");
              Style.SetCondition(TRUE);
              IF Customer."Credit Limit (LCY)"<>0 THEN
                Style.ImageNoBorder(GetIndicator(Customer."Balance (LCY)",
                  'green',1,'green',Customer."Credit Limit (LCY)"*0.8,'orange',Customer."Credit Limit (LCY)",'red'),'')
              ELSE
                Style.ImageNoBorder(GetIndicator(Customer."Balance (LCY)",
                  'green',1,'green',100000,'orange',200000,'red'),'');
              Style.ImagePosition('center','');
            END;
            'ICO_BALANCE_DUE': BEGIN
              Customer.CALCFIELDS("Balance Due (LCY)");
              Style.SetCondition(TRUE);
              Style.ImageNoBorder(GetIndicator(Customer."Balance Due (LCY)",
                'green',1,'green',10000,'orange',20000,'red'),'');
              Style.ImagePosition('center','');
            END;
            'ICO_OUTST_ORDERS': BEGIN
              Customer.CALCFIELDS("Balance Due (LCY)");
              Style.SetCondition(TRUE);
              Style.ImageNoBorder(GetIndicator(Customer."Balance Due (LCY)",
                'blue',1,'blue',10000,'blue',20000,'blue'),'');
              Style.ImagePosition('center','');
            END;

          END;
        END;
      END;

      // Do not show Credit Limit if not defined
      //IF (FldRef.NUMBER = Customer.FIELDNO("Credit Limit (LCY)")) AND (Customer."Credit Limit (LCY)" = 0) THEN ShowValue := FALSE;
    END;

    PROCEDURE OAGetField_CONTACT@5326094() ShowValue : Boolean;
    VAR
      CompanyInformation@5326065 : Record 79;
      Contact@5326062 : Record 5050;
      ContactFilterExists@5326064 : Boolean;
    BEGIN
      Mgt.Log('OAGetField_CONTACT','BEGIN');
      ShowValue := TRUE;
      RecRef.SETTABLE(Contact);
      Mgt.Log('OAGetField_CONTACT','Contact: '+Contact."No.");

      // Anveo Mobile List
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
        CASE FieldName OF
          '2': ShowValue := (Contact.Type = Contact.Type::Company);
          '1000002': ShowValue := NOT (Contact.Type = Contact.Type::Company);
        END;
      END;

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        Mgt.Log('OAGetField_CONTACT','Is Anveo Web Desk');
        Style.SetCondition(TRUE);
        // Country Icons
        IF FldRef.NUMBER = Contact.FIELDNO("Country/Region Code")THEN BEGIN
          Style.SetCondition(TRUE);
          IF Contact."Country/Region Code" <> '' THEN
            Style.Image('flags/'+LOWERCASE(Contact."Country/Region Code")+'.png','')
          ELSE BEGIN
            CompanyInformation.GET;
            IF CompanyInformation."Country/Region Code" <> '' THEN
              Style.Image('flags/'+LOWERCASE(CompanyInformation."Country/Region Code")+'.png','');
          END;
          Style.ImagePosition('95%','95%');
          Style.TextIndent('0px','0px');
        END;

        // Currency Icons
        IF FldRef.NUMBER = Contact.FIELDNO("Currency Code") THEN BEGIN
          Style.SetCondition(TRUE);
          CASE Contact."Currency Code" OF
            'USD': Style.Image('money-dollar.png','');
            'EUR': Style.Image('money-euro.png','');
            ELSE BEGIN
              Style.Image('','');
              Style.SetCondition(FALSE);
            END;
          END;
          Style.ImagePosition('5px','5px');
          Style.TextIndent('16px','0px');
        END;
        // Contact Type Icons
        IF FldRef.NUMBER = Contact.FIELDNO(Type)THEN BEGIN
          Style.SetCondition(TRUE);
          IF Contact.Type = Contact.Type::Company THEN
            Style.Image('home.png','')
          ELSE BEGIN
            IF COPYSTR(Contact."Salutation Code",1,1)='M' THEN
              Style.Image('user.gif','')
            ELSE
              Style.Image('user_female.gif','');
          END;
        END;

        // Name-indentation on CONTACT_LIST only
        IF WebPageElement."Web Page Code" = 'CONTACT_LIST' THEN BEGIN
          IF FldRef.NUMBER = Contact.FIELDNO(Name) THEN BEGIN
            Style.SetCondition(Contact.Type = Contact.Type::Company);
            Style.FontWeight('bold','normal');
            // If sorting is set to a key beginning with "Company Name",
            // "Company No." is not empty and no user or sercurity filters are set, then indent persons.
            RecRefOriginal.FILTERGROUP(103);
            ContactFilterExists := RecRefOriginal.GETFILTERS <> '';
            RecRefOriginal.FILTERGROUP(100);
            ContactFilterExists := ContactFilterExists OR (RecRefOriginal.GETFILTERS <> '');
            Style.SetCondition(
                  (STRPOS(RecRefOriginal.CURRENTKEY, Contact.FIELDCAPTION("Company Name"))=1)
              AND (Contact.Type = Contact.Type::Person)
              AND (Contact."Company No."<>'')
              AND (NOT ContactFilterExists));
            Style.TextIndent('15px', '0px');
          END;
        END ELSE BEGIN
          IF FldRef.NUMBER = Contact.FIELDNO(Name) THEN BEGIN
            Style.SetCondition(TRUE);
            Style.FontWeight('bold','');
          END;
        END;
      END;
      Mgt.Log('OAGetField_CONTACT','END');
    END;

    PROCEDURE OAGetField_INTERACTIONLOGENTRY@5326120() ShowValue : Boolean;
    VAR
      InteractionLogEntry@5326062 : Record 5065;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(InteractionLogEntry);

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        IF FldRef.NUMBER = InteractionLogEntry.FIELDNO("Information Flow") THEN BEGIN
          Style.SetCondition(TRUE);
          CASE InteractionLogEntry."Information Flow" OF
            InteractionLogEntry."Information Flow"::Outbound: BEGIN
              Style.ImageNoBorder('arrow_green_right.png', '');
            END;
            InteractionLogEntry."Information Flow"::Inbound: BEGIN
              Style.ImageNoBorder('arrow_green_left.png', '');
            END;
          END;
        END;

        IF FldRef.NUMBER = InteractionLogEntry.FIELDNO(Description) THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
      END ELSE BEGIN
      // Anveo Mobile
        IF FldRef.NUMBER = InteractionLogEntry.FIELDNO("Information Flow") THEN BEGIN
          ShowValue := FALSE;
          CASE InteractionLogEntry."Information Flow" OF
            InteractionLogEntry."Information Flow"::Outbound: ShowValue := FieldName = '1000006';
            InteractionLogEntry."Information Flow"::Inbound:  ShowValue := FieldName = '2000006';
          ELSE
            ShowValue := FieldName = '6';
          END;
        END;
      END;
    END;

    PROCEDURE OAGetField_PROFILEQUESTIONLINE@5326107() ShowValue : Boolean;
    VAR
      ProfileQuestionnaireLine@5326062 : Record 5088;
      ContactProfileAnswer@5326063 : Record 5089;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(ProfileQuestionnaireLine);

      // Anveo Web Desk
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::List THEN BEGIN
        Style.SetCondition(ProfileQuestionnaireLine.Type = ProfileQuestionnaireLine.Type::Question);
        Style.FontWeight('bold','normal');

        IF FldRef.NUMBER = ProfileQuestionnaireLine.FIELDNO(Description) THEN BEGIN
           Style.SetCondition(ProfileQuestionnaireLine.Type = ProfileQuestionnaireLine.Type::Answer);
           Style.TextIndent('20px','0px');
        END;
      END;

      // Unbound fields
      IF WebPageElementField."Virtual Table Code" = 'PROFQUESTIONLINE' THEN BEGIN
        IF WebPageElementField."Virtual Field Code" = 'SELECTED' THEN
          FldRef.VALUE := FALSE; //ContactProfileAnswer.GET(Cont."No.","Profile Questionnaire Code","Line No.");
      END;
    END;

    PROCEDURE OAGetField_SEGMENTCRITERIALINE@5326100() ShowValue : Boolean;
    VAR
      SegmentCriteriaLine@5326062 : Record 5097;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SegmentCriteriaLine);

      // Anveo Web Desk
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::List THEN BEGIN
        Style.SetCondition(SegmentCriteriaLine.Type = SegmentCriteriaLine.Type::Action);
        Style.FontWeight('bold','normal');
        Style.TextIndent('0px','20px');
      END;

      // Unbound fields
      IF WebPageElementField."Virtual Table Code" = 'SEGMENT_CRITERIA' THEN BEGIN
        IF WebPageElementField."Virtual Field Code" = 'ACTION/TABLE' THEN FldRef.VALUE := SegmentCriteriaLine.ActionTable();
        IF WebPageElementField."Virtual Field Code" = 'FILTER' THEN       FldRef.VALUE := SegmentCriteriaLine.Filter();
      END;
    END;

    PROCEDURE OAGetField_SEGMENTHEADER@5326113() ShowValue : Boolean;
    VAR
      SegmentHeader@5326062 : Record 5076;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SegmentHeader);

      // Anveo Mobile
      IF Mgt.IsMobile THEN BEGIN
        IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
          CASE FieldName OF
            '4': ShowValue := SegmentHeader."Salesperson Code" <>'';
          END;
        END;
      END;
    END;

    PROCEDURE OAGetField_VIRTUALFIELDS@5326095();
    VAR
      ContactProfileAnswer@5326062 : Record 5089;
    BEGIN
      // Contact Profile Answer.QUESTION
      IF (WebPageElementField."Virtual Table Code" = 'CONT_PROF_ANSWER') AND
         (WebPageElementField."Virtual Field Code" = 'QUESTION') THEN BEGIN
        RecRef.SETTABLE(ContactProfileAnswer);
        FldRef.VALUE := ContactProfileAnswer.Question;
      END;
    END;

    PROCEDURE OAGetField_SALESHEADER@5326093() ShowValue : Boolean;
    VAR
      SalesHeader@5326062 : Record 36;
      SalesLine@5326063 : Record 37;
      n@5326064 : Integer;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesHeader);

      // Anveo Web Desk Lists
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::List THEN BEGIN
        Style.SetCondition(SalesHeader.Status = SalesHeader.Status::Released);
        Style.BackgroundColor('#DDFFDD','white');
        IF FldRef.NUMBER = SalesHeader.FIELDNO("Sell-to Customer Name") THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
      END;

      // Anveo Web Desk Cards
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::Card THEN BEGIN
        IF FldRef.NUMBER = SalesHeader.FIELDNO(Status) THEN BEGIN
          Style.SetCondition(SalesHeader.Status = SalesHeader.Status::Released);
          Style.BackgroundColor('#DDFFDD','white');
          Style.Image('yes.png','');
        END;
        IF FldRef.NUMBER = SalesHeader.FIELDNO("External Document No.") THEN BEGIN
          Style.SetCondition(SalesHeader."External Document No."='');
          Style.BackgroundColor('#FFDDDD','white');
        END;
        IF FldRef.NUMBER = SalesHeader.FIELDNO("Sell-to Customer Name") THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
        IF FldRef.NUMBER = SalesHeader.FIELDNO("Location Code") THEN BEGIN
          Style.SetCondition(TRUE);
          CASE SalesHeader."Location Code" OF
            'BLUE':    Style.BackgroundColor('#DDDDFF','');
            'GREEN':   Style.BackgroundColor('#DDFFDD','');
            'RED':     Style.BackgroundColor('#FFDDDD','');
            'SILVER':  Style.BackgroundColor('#D0D0D0','');
            'WHITE':   Style.BackgroundColor('#FFFFFF','');
            'YELLOW':  Style.BackgroundColor('#FFFFDD','');
          END;
        END;
      END;

      // Anveo Mobile Lists
      IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
        CASE FieldName OF
          '79':      ShowValue := NOT (SalesHeader.Status = SalesHeader.Status::Released);
          '1000079': ShowValue := (SalesHeader.Status = SalesHeader.Status::Released);
          '100' :    ShowValue := SalesHeader."External Document No." <> '';
        END;
      END;

      // Anveo Virtual Fields
      IF WebPageElementField."Virtual Table Code" = 'SALESHEADER' THEN BEGIN
        IF WebPageElementField."Virtual Field Code" = 'TOTALQTY' THEN BEGIN
          SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
          SalesLine.SETRANGE("Document No.", SalesHeader."No.");
          IF SalesLine.FINDSET THEN REPEAT
            n:=n+SalesLine.Quantity;
          UNTIL SalesLine.NEXT=0;
          FldRef.VALUE := n;
        END;
      END;
    END;

    PROCEDURE OAGetField_SALESLINE@5326105() ShowValue : Boolean;
    VAR
      Item@5326062 : Record 27;
      SalesLine@5326063 : Record 37;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesLine);

      // Anveo Mobile
      IF Mgt.IsMobile THEN BEGIN
        IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
          CASE FieldName OF
            '22': ShowValue := TRUE;
            '1000022': ShowValue := FALSE;
          END;
          IF SalesLine.Type = SalesLine.Type::Item THEN BEGIN
              IF Item.GET(SalesLine."No.") THEN BEGIN
                CASE FieldName OF
                  '22': ShowValue := NOT (Item."Unit Price" < SalesLine."Unit Price");
                  '1000022': ShowValue := (Item."Unit Price" < SalesLine."Unit Price");
                END;
              END
          END;
        END;
      END ELSE BEGIN
        // Anveo Web Desk Lists
        IF WebPageElement."Element Type" = WebPageElement."Element Type"::List THEN BEGIN
          IF FldRef.NUMBER = SalesLine.FIELDNO("Location Code") THEN BEGIN
            Style.SetCondition(TRUE);
            CASE SalesLine."Location Code" OF
              'BLUE':    Style.BackgroundColor('#DDDDFF','');
              'GREEN':   Style.BackgroundColor('#DDFFDD','');
              'RED':     Style.BackgroundColor('#FFDDDD','');
              'SILVER':  Style.BackgroundColor('#D0D0D0','');
              'WHITE':   Style.BackgroundColor('#FFFFFF','');
              'YELLOW':  Style.BackgroundColor('#FFFFDD','');
            END;
          END;
        END;
      END;

      IF WebPageElementField."Virtual Table Code" = 'ITEM' THEN BEGIN
        IF SalesLine.Type = SalesLine.Type::Item THEN BEGIN
          IF Item.GET(SalesLine."No.") AND (WebPageElementField."Virtual Field Code" = 'REPL_SYSTEM_ICO') THEN BEGIN
            Style.SetCondition(Item."Replenishment System" = Item."Replenishment System"::Purchase);
            Style.ImageNoBorder('truck.png','setup.png');
            Style.ImagePosition('center','center');
          END;
        END;
      END;
    END;

    PROCEDURE OAGetField_SALESPRICE@5326121() ShowValue : Boolean;
    VAR
      SalesPrice@5326062 : Record 7002;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesPrice);

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        Style.SetCondition((SalesPrice."Ending Date" <> 0D) AND (SalesPrice."Ending Date" < TODAY));
        Style.TextColor('gray','');

        // Currency Icons
        IF FldRef.NUMBER = SalesPrice.FIELDNO("Currency Code") THEN BEGIN
          Style.SetCondition(TRUE);
          CASE SalesPrice."Currency Code" OF
            'USD': Style.Image('money-dollar.png','');
            'EUR': Style.Image('money-euro.png','');
            ELSE BEGIN
              Style.Image('','');
              Style.SetCondition(FALSE);
            END;
          END;
          Style.ImagePosition('5px','5px');
          Style.TextIndent('16px','0px');
        END;
      END ELSE BEGIN
      END;
    END;

    PROCEDURE OAGetField_SALESSHIPMENTHEADER@5326097() ShowValue : Boolean;
    VAR
      SalesShipmentHeader@5326062 : Record 110;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesShipmentHeader);

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        IF FldRef.NUMBER = SalesShipmentHeader.FIELDNO("Sell-to Customer Name") THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
      END;
    END;

    PROCEDURE OAGetField_SALESINVOICEHEADER@5326099() ShowValue : Boolean;
    VAR
      SalesInvoiceHeader@5326062 : Record 112;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesInvoiceHeader);

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        IF FldRef.NUMBER = SalesInvoiceHeader.FIELDNO("Sell-to Customer Name") THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
      END ELSE BEGIN
      // Anveo Mobile
        IF WebPageElement."Element Type" = WebPageElement."Element Type"::"List Mobile" THEN BEGIN
          CASE FieldName OF
            '32': ShowValue := SalesInvoiceHeader."Currency Code" <>'';
          END;
        END;
      END;
    END;

    PROCEDURE OAGetField_SALESCRMEMOHEADER@5326104() ShowValue : Boolean;
    VAR
      SalesCrMemoHeader@5326062 : Record 114;
    BEGIN
      ShowValue := TRUE;
      RecRef.SETTABLE(SalesCrMemoHeader);

      // Anveo Web Desk
      IF NOT Mgt.IsMobile THEN BEGIN
        IF FldRef.NUMBER = SalesCrMemoHeader.FIELDNO("Sell-to Customer Name") THEN BEGIN
          Style.SetCondition(TRUE);
          Style.FontWeight('bold','');
        END;
      END;
    END;

    PROCEDURE OAGetField_CP_HOME@5326081() ShowValue : Boolean;
    VAR
      SalesHeader@5326068 : Record 36;
      WebUser@5326069 : Record 5327140;
    BEGIN
      ShowValue := TRUE;

      IF Mgt.IsMobile THEN BEGIN
        // Style Definition for Anveo Mobile

      END ELSE BEGIN
        // Style Definition for Anveo Web Desk
        CASE FieldName OF
          '5000000': BEGIN
            Style.InitAndSetCondition(WebPageElement, TRUE);
            Style.NoBorder;
            Style.BackgroundColor('#FF0000','');
            Style.FontWeight('bold','italic');
            FldRef.VALUE := '2';
          END;
        END;
      END;
    END;

    PROCEDURE OAGetField_CP_SALESORDER_CARD@5326082() ShowValue : Boolean;
    VAR
      SalesHeader@5326068 : Record 36;
      SalesLine@5326069 : Record 37;
      n@5326070 : Integer;
    BEGIN
      ShowValue := TRUE;

      IF Mgt.IsMobile THEN BEGIN
        // Style Definition for Anveo Mobile

      END ELSE BEGIN
        // Style Definition for Anveo Web Desk
        // Sales Header
        IF RecRef.NUMBER = DATABASE::"Sales Header" THEN BEGIN
          CASE FieldName OF
            '2000000': BEGIN
              // Calc Total Quantity
              RecRef.SETTABLE(SalesHeader);
              SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
              SalesLine.SETRANGE("Document No.", SalesHeader."No.");
              IF SalesLine.FINDSET THEN REPEAT
                n:= n+SalesLine.Quantity;
              UNTIL SalesLine.NEXT=0;
              FldRef.VALUE := n;
            END;
          END;
        END;
        // Sales Lines
        IF RecRef.NUMBER = DATABASE::"Sales Line" THEN BEGIN
          // Item lines in black if SalesHeader.Status=OPEN, other in gray
          RecRef.SETTABLE(SalesLine);
          SalesHeader.GET(SalesLine."Document Type", SalesLine."Document No.");
          Style.SetCondition((SalesLine.Type = SalesLine.Type::Item) AND (SalesHeader.Status = SalesHeader.Status::Open));
          Style.Enabled;
          Style.TextColor('black','gray');
        END;
      END;
    END;

    LOCAL PROCEDURE "-- SET/GET FUNCTIONS --"@5326066();
    BEGIN
    END;

    PROCEDURE SETVALUES1@5326088(pWebPageElement@5326081 : Record 5327101;pWebPageElementField@5326080 : Record 5327103;VAR pRecRef@5326079 : RecordRef;pFldRef@5326078 : FieldRef;pFldNo@5326062 : Integer;pxRecRef@5326077 : RecordRef;pxFldRef@5326076 : FieldRef;pxFldNo@5326063 : Integer;pWindowRecRef@5326075 : RecordRef;pDestWebPageElement@5326074 : Record 5327101;pDestRecRef@5326073 : RecordRef;pCustomTableRelationRecRef@5326072 : RecordRef;pFunctionCode@5326071 : Code[20];pRelationCode@5326070 : Code[20];pFieldName@5326069 : Text[50];pTableNumber@5326067 : Integer);
    BEGIN
      // Set global variables
      WebPageElement := pWebPageElement;
      WebPageElementField := pWebPageElementField;
      RecRef := pRecRef;
      FldRef := pFldRef;
      FldNo := pFldNo;
      xRecRef := pxRecRef;
      xFldRef := pxFldRef;
      xFldNo := pxFldNo;
      WindowRecRef := pWindowRecRef;
      DestWebPageElement := pDestWebPageElement;
      DestRecRef := pDestRecRef;
      CustomTableRelationRecRef := pCustomTableRelationRecRef;
      FunctionCode := pFunctionCode;
      RelationCode := pRelationCode;
      FieldName := pFieldName;
      TableNumber := pTableNumber;

      IF pFldNo<>0 THEN FldRef := RecRef.FIELD(pFldNo);
      IF pxFldNo<>0 THEN xFldRef := xRecRef.FIELD(pxFldNo);

      WebUser.GET(Mgt.GetACFUser);
    END;

    PROCEDURE SETVALUES2@5326085(pClosedByNAV@5326062 : Boolean;pInitStyle@5326066 : Boolean);
    BEGIN
      // Set global variables
      ClosedByNAV := pClosedByNAV;
      IF pInitStyle THEN
        Style.Init(WebPageElement);
    END;

    PROCEDURE GETVALUES@5326089(VAR pRecRef@5326079 : RecordRef;VAR pFldRef@5326078 : FieldRef;VAR pDestRecRef@5326073 : RecordRef;VAR "pField#1"@5326065 : Integer;VAR "pField#2"@5326064 : Integer;VAR CSS@5326062 : Text[1024]);
    BEGIN
      // Return global variables
      pRecRef := RecRef;
      pFldRef := FldRef;
      pDestRecRef := DestRecRef;
      "pField#1" := "Field#1";
      "pField#2" := "Field#2";
      IF Style.IsInit THEN
        CSS := Style.GetStyle;
    END;

    PROCEDURE SetValuesToMainEvents@5326086();
    BEGIN
      Events.SETVALUES1(WebPageElement,WebPageElementField,RecRef,FldRef,FldNo, xRecRef,xFldRef,xFldNo,WindowRecRef,
       DestWebPageElement,DestRecRef,CustomTableRelationRecRef,FunctionCode,RelationCode,FieldName,TableNumber);
      Events.SETVALUES2(ClosedByNAV, FALSE);
    END;

    PROCEDURE GetValuesFromMainEvents@5326092();
    BEGIN
      Events.GETVALUES(RecRef,FldRef,DestRecRef,"Field#1","Field#2", CSS);
    END;

    LOCAL PROCEDURE "-- Other Functions"@5326071();
    BEGIN
    END;

    PROCEDURE GetIndicator@5326074(InputValue@5326062 : Variant;Color0LessValue1@5326063 : Text[30];Value1@5326065 : Variant;Color1@5326064 : Text[30];Value2@5326067 : Variant;Color2@5326066 : Text[30];Value3@5326069 : Variant;Color3@5326068 : Text[30]) Image : Text[30];
    VAR
      dInputValue@5326077 : Decimal;
      dValue1@5326075 : Decimal;
      dValue2@5326073 : Decimal;
      dValue3@5326071 : Decimal;
    BEGIN
      // Convert Variant to Decimal
      dInputValue := InputValue;
      dValue1 := Value1;
      dValue2 := Value2;
      dValue3 := Value3;
      // Analyse Indicator
      IF dInputValue < dValue1 THEN Image := '0-'+Color0LessValue1
      ELSE IF dInputValue < dValue2 THEN Image := '1-'+Color1
      ELSE IF dInputValue < dValue3 THEN Image := '2-'+Color2
      ELSE Image := '3-'+Color3;
      Image := 'indicator-'+Image+'.png';
    END;

    BEGIN
    {
      ** Events-Codeunit is the main interface between GUI-events and individual C/AL-Code.
      Online documentation: www.NAVwebclient.com/anveo-help-codeunit-acf-events

      Web Page related event-code can be grouped by a module. See codeunit "ACF Module Mapping" for creating a new module.
      Online documentation for module concept: www.NAVwebclient.com/anveo-help-modules
      Events that can be used in a module are marked with a "M".

      ************************************************************************************************************************************
      *** Module for SALES & MARKETING ***************************************************************************************************
      ************************************************************************************************************************************

      Events-Overview
      ---------------
      * User session
        OnLogin                   Called on user login. Login can be cancelled here.
        OnLogout                  Called on user logout or on browser refresh or close.
        OnLoadEnvironment         Called on browser refresh without new login or after login
        GetLanguageFilter         Defines a filter on Language table for user selection.

      * Security
        ReadSecurityFilter        Sets read filters, called by every Web Page Element and DropDown-Lists
        CheckPassword             Tests password quality. Returns TRUE, if password is ok.
        TestPassword              Uses CheckPassword to test password quality. Calls an ERROR, if password is not ok.

      * Loading data (no asynchronious call, Mgt.-functions like Anveo-MESSAGE etc. is not processed by GUI)
        OnBeforeLoad          (M) Called on loading data once per Web Page Element, Filter to Records is set
        OnAfterGetField       (M) Called once per Web Page Element, Record and Field
        OnAfterGetRecord      (M) Called once per Web Page Element and Record
        OnAfterLoad           (M) Called on loading data once per Web Page Element

      * Data modification
        OnInit                (M) A new record is initialized. Default values can be set here.
        OnBeforeValidate      (M) A value is set to a record, but validation is not done yet. Validation can be cancelled here.
        OnValidate            (M) A value is set to a record and validation is done, if OnBeforeValidate returned true.
        CallOnInsertTrigger   (M) A new record is inserted. Return value decides whether to call INSERT(TRUE) or INSERT(FALSE).
        CallOnModifyTrigger   (M) A record is modified. Return value decides whether to call MODIFY(TRUE) or MODIFY(FALSE).
        CallOnDeleteTrigger   (M) A record is deleted. Return value decides whether to call DELETE(TRUE) or DELETE(FALSE).
        OnInsertRecord        (M) A record is inserted. Return value decides whether record is inserted.
        OnModifyRecord        (M) A record is modified. Return value decides whether record is modified.
        OnRenameRecord        (M) A record is renamed. Return value decides whether record is renamed.
        OnDeleteRecord        (M) A record is deleted. OnAction with Function code "DELETE_RECORD()" is also called.
        OnAfterInsertRecord   (M) Called after a record was successfully inserted.
        OnAfterModifyRecord   (M) Called after a record was successfully modified.
        OnAfterRenameRecord   (M) Called after a record was successfully renamed.
        OnAfterDeleteRecord   (M) Called after a record was successfully deleted.
        IsWriteBack           (M) In case of write behaviour "Defined" IsWriteBack defines if record is physically saved or stays tempor.

      * User events
        OnAction              (M) Button in menu or beside a field is pressed, or double click event on list web page.
                                 "Function Code" identifies C/AL-Code to run.
        OnBeforeCloseWebPage  (M) A Web Page is closed. It can be cancelled with return value false.

      * Relations
        CustomWebPageRelation (M) Defines a custom relation between Web Pages / Web Page Elements

      * Reports
        Create a OnAction-Function for each report. See examples.

      * Other
        DropDownFieldDefinition (M) Defines field Nos in DropDown lists.
    }
    END.
  }
}

