OBJECT Codeunit 5704 TransferOrder-Post Shipment
{
  OBJECT-PROPERTIES
  {
    Date=25.06.15;
    Time=09:17:40;
    Modified=Yes;
    Version List=NAVW16.00.0,HME4370,T0053;
  }
  PROPERTIES
  {
    TableNo=5740;
    Permissions=TableData 6507=i;
    OnRun=VAR
            Item@1000 : Record 27;
            SourceCodeSetup@1001 : Record 242;
            InvtSetup@1002 : Record 313;
            InventoryPostingSetup@1007 : Record 5813;
            NoSeriesMgt@1003 : Codeunit 396;
            UpdateAnalysisView@1004 : Codeunit 410;
            UpdateItemAnalysisView@1009 : Codeunit 7150;
            Window@1006 : Dialog;
            LineCount@1005 : Integer;
            NextLineNo@1008 : Integer;
            "S***FOB1.01"@1000000000 : Integer;
            PurchLine@1000000001 : Record 39;
            "E***FOB1.01"@1000000002 : Integer;
            "*** HME **********************"@1000000004 : Integer;
            WarehouseSetupL@1000000003 : Record 5769;
            SalesAndReceivableSetupL@1000000005 : Record 311;
            ArchvWhseTransferLineL@1000000006 : Record 50802;
            ArchvWhseTransferLine2L@1000000007 : Record 50802;
            "***TEC************************"@1100409000 : Integer;
            MPDocumentMgtL@1100409001 : Codeunit 50500;
            "*** P1282 ***"@1000000009 : Integer;
            ParcelStatusHistoryMgmt@1000000008 : Codeunit 80011;
            "****CC************************"@1000000010 : Integer;
            "****T0060*********************"@1000000011 : Integer;
            MPGeneralSetup@1000000012 : Record 50500;
          BEGIN
            //H4370 23.06.15 TST +++++++++++++++++++++++++++++
            CheckSingleInstance();
            //H4370 26.06.15 TST -----------------------------
            IF Status = Status::Open THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"Release Transfer Document",Rec);
              Status := Status::Open;
              MODIFY;
              COMMIT;
              Status := Status::Released;
            END;
            TransHeader := Rec;
            TransHeader.SetHideValidationDialog(HideValidationDialog);

            WITH TransHeader DO BEGIN
              //H4370 23.06.15 TST +++++++++++++++++++++++++++++
              IF NOT CheckTransferHeader() THEN
                EXIT;
              //H4370 26.06.15 TST -----------------------------
              TESTFIELD("Transfer-from Code");
              TESTFIELD("Transfer-to Code");
              IF ("Transfer-from Code" <> '') AND
                 ("Transfer-from Code" = "Transfer-to Code")
              THEN
                //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                IF IsInternational THEN BEGIN
                  SingleInstanceCU.SetError(TRUE,
                    STRSUBSTNO(Text000, "No.",FIELDCAPTION("Transfer-from Code"),FIELDCAPTION("Transfer-to Code")));
                  EXIT;
                END ELSE
                //H4370 26.06.15 TST -----------------------------
                  ERROR
                    (Text000,
                    "No.",FIELDCAPTION("Transfer-from Code"),FIELDCAPTION("Transfer-to Code"));
              TESTFIELD("In-Transit Code");
              TESTFIELD(Status,Status::Released);
              TESTFIELD("Posting Date");

              //T0053 01.10.14 TEC-GH ++++++++++++++++++++++++++++++++++++++++++++++++++
              MPDocumentMgtL.CheckTransfMPLocAllTransfLines(TransHeader);
              //T0053 01.10.14 TEC-GH --------------------------------------------------

              WhseReference := "Posting from Whse. Ref.";
              "Posting from Whse. Ref." := 0;

              IF ("Shipping Advice" = "Shipping Advice"::Complete) THEN
                IF NOT GetShippingAdvice THEN
                  //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                  IF IsInternational THEN BEGIN
                    SingleInstanceCU.SetError(TRUE,Text008);
                    EXIT;
                  END ELSE
                  //H4370 26.06.15 TST -----------------------------
                    ERROR(Text008);

              CopyAndCheckDocDimToTempDocDim;

              TransLine.RESET;
              TransLine.SETRANGE("Document No.","No.");
              TransLine.SETRANGE("Derived From Line No.",0);
              TransLine.SETFILTER(Quantity,'<>0');
              TransLine.SETFILTER("Qty. to Ship",'<>0');
              IF NOT TransLine.FIND('-') THEN
                //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                IF IsInternational THEN BEGIN
                  SingleInstanceCU.SetError(TRUE,Text001);
                  EXIT;
                END ELSE
                //H4370 26.06.15 TST -----------------------------
                  ERROR(Text001);

              WhseShip := TempWhseShptHeader.FIND('-');
              InvtPickPutaway := WhseReference <> 0;
              IF NOT (WhseShip OR InvtPickPutaway) THEN
                CheckWarehouse(TransLine);

              GetLocation("Transfer-from Code");
              IF Location."Bin Mandatory" AND NOT (WhseShip OR InvtPickPutaway) THEN
                WhsePosting := TRUE;
              //H4202 07.05.15 TST +++++++++++++++++++++++++++++
              IF NOT HideValidationDialog OR GUIALLOWED THEN
              //H4202 07.05.15 TST -----------------------------
                Window.OPEN(
                  '#1#################################\\' +
                  Text003);

              //H4202 07.05.15 TST +++++++++++++++++++++++++++++
              IF NOT HideValidationDialog OR GUIALLOWED THEN
              //H4202 07.05.15 TST -----------------------------
                Window.UPDATE(1,STRSUBSTNO(Text004,"No."));

              SourceCodeSetup.GET;
              SourceCode := SourceCodeSetup.Transfer;
              InvtSetup.GET;
              InvtSetup.TESTFIELD("Posted Transfer Shpt. Nos.");
              InventoryPostingSetup.SETRANGE("Location Code","Transfer-from Code");
              InventoryPostingSetup.FIND('-');
              InventoryPostingSetup.SETRANGE("Location Code","Transfer-to Code");
              InventoryPostingSetup.FIND('-');

              IF RECORDLEVELLOCKING THEN BEGIN
                NoSeriesLine.LOCKTABLE;
                IF NoSeriesLine.FIND('+') THEN;
                IF InvtSetup."Automatic Cost Posting" THEN  BEGIN
                  GLEntry.LOCKTABLE;
                  IF GLEntry.FIND('+') THEN;
                END;
              END;

              // Insert shipment header
              PostedWhseShptHeader.LOCKTABLE;
              TransShptHeader.LOCKTABLE;
              TransShptHeader.INIT;
              TransShptHeader."Transfer-from Code" := "Transfer-from Code";
              TransShptHeader."Transfer-from Name" := "Transfer-from Name";
              TransShptHeader."Transfer-from Name 2" := "Transfer-from Name 2";
              TransShptHeader."Transfer-from Address" := "Transfer-from Address";
              TransShptHeader."Transfer-from Address 2" := "Transfer-from Address 2";
              TransShptHeader."Transfer-from Post Code" := "Transfer-from Post Code";
              TransShptHeader."Transfer-from City" := "Transfer-from City";
              TransShptHeader."Transfer-from County" := "Transfer-from County";
              TransShptHeader."Trsf.-from Country/Region Code" := "Trsf.-from Country/Region Code";
              TransShptHeader."Transfer-from Contact" := "Transfer-from Contact";
              TransShptHeader."Transfer-to Code" := "Transfer-to Code";
              TransShptHeader."Transfer-to Name" := "Transfer-to Name";
              TransShptHeader."Transfer-to Name 2" := "Transfer-to Name 2";
              TransShptHeader."Transfer-to Address" := "Transfer-to Address";
              TransShptHeader."Transfer-to Address 2" := "Transfer-to Address 2";
              TransShptHeader."Transfer-to Post Code" := "Transfer-to Post Code";
              TransShptHeader."Transfer-to City" := "Transfer-to City";
              TransShptHeader."Transfer-to County" := "Transfer-to County";
              TransShptHeader."Trsf.-to Country/Region Code" := "Trsf.-to Country/Region Code";
              TransShptHeader."Transfer-to Contact" := "Transfer-to Contact";
              TransShptHeader."Transfer Order Date" := "Posting Date";
              TransShptHeader."Posting Date" := "Posting Date";
              TransShptHeader."Shipment Date" := "Shipment Date";
              TransShptHeader."Receipt Date" := "Receipt Date";
              TransShptHeader."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
              TransShptHeader."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
              TransShptHeader."Transfer Order No." := "No.";
              TransShptHeader."External Document No." := "External Document No.";
              TransShptHeader."In-Transit Code" := "In-Transit Code";
              TransShptHeader."Shipping Agent Code" := "Shipping Agent Code";
              TransShptHeader."Shipping Agent Service Code" := "Shipping Agent Service Code";
              TransShptHeader."Shipment Method Code" := "Shipment Method Code";
              TransShptHeader."Transaction Type" := "Transaction Type";
              TransShptHeader."Transport Method" := "Transport Method";
              TransShptHeader."Entry/Exit Point" := "Entry/Exit Point";
              TransShptHeader.Area := Area;
              TransShptHeader."Transaction Specification" := "Transaction Specification";
              //S/FOB
              TransShptHeader."Container No." := "Container No.";
              TransShptHeader."Buy-from Vendor No" := "Buy-from Vendor No";
              TransShptHeader."Purchase Order No." := "Purchase Order No.";
              TransShptHeader."Pay-to Vendor No." := "Pay-to Vendor No.";
              TransShptHeader."Order Date" := "Order Date";
              TransShptHeader."Bill of Lading No." := "Bill of Lading No.";
             //E/FOB
              TransShptHeader."No. Series" := InvtSetup."Posted Transfer Shpt. Nos.";
              TransShptHeader."No." :=
                NoSeriesMgt.GetNextNo(
                  InvtSetup."Posted Transfer Shpt. Nos.","Posting Date",TRUE);
              TransShptHeader.INSERT;

              DimMgt.MoveOneDocDimToPostedDocDim(
                TempDocDim,DATABASE::"Transfer Header",TempDocDim."Document Type"::" ","No.",0,
                DATABASE::"Transfer Shipment Header",TransShptHeader."No.");
              IF InvtSetup."Copy Comments Order to Shpt." THEN BEGIN
                CopyCommentLines(1,2,"No.",TransShptHeader."No.");
                TransShptHeader.COPYLINKS(Rec);
              END;

              IF WhseShip THEN BEGIN
                WhseShptHeader.GET(TempWhseShptHeader."No.");
                WhsePostShpt.CreatePostedShptHeader(PostedWhseShptHeader,WhseShptHeader,TransShptHeader."No.","Posting Date");
              END;

              // Insert shipment lines
              LineCount := 0;
              IF WhseShip THEN
                PostedWhseShptLine.LOCKTABLE;
              DocDim.LOCKTABLE;
              IF InvtPickPutaway THEN
                WhseRqst.LOCKTABLE;
              TransShptLine.LOCKTABLE;
              TransLine.SETRANGE(Quantity);
              TransLine.SETRANGE("Qty. to Ship");
              IF TransLine.FIND('-') THEN BEGIN
                REPEAT
                  LineCount := LineCount + 1;
                  //H4202 07.05.15 TST +++++++++++++++++++++++++++++
                  IF NOT HideValidationDialog OR GUIALLOWED THEN
                  //H4202 07.05.15 TST -----------------------------
                    Window.UPDATE(2,LineCount);

                  IF TransLine."Item No." <> '' THEN BEGIN
                    Item.GET(TransLine."Item No.");
                    //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                    IF IsInternational THEN BEGIN
                      IF Item.Blocked THEN BEGIN
                        SingleInstanceCU.SetError(TRUE,STRSUBSTNO(HMEText002,Item."No."));
                        EXIT;
                      END;
                    END ELSE
                      Item.TESTFIELD(Blocked,FALSE);
                    //H4370 26.06.15 TST -----------------------------
                  END;

                  //H0919, H1160, H1200, H1718, T0060 25.02.15 CC-GH +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                  WarehouseSetupL.GET;
                  SalesAndReceivableSetupL.GET;
                  MPGeneralSetup.GET;
                  IF NOT (WarehouseSetupL."Return Warehouse Location Code" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (WarehouseSetupL."Foto Studio Location Code" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (WarehouseSetupL."Int. Return Location Code" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (MPGeneralSetup."MPP Whse. Return Location" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (MPGeneralSetup."MPP Int. Return Location" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (SalesAndReceivableSetupL."Default Sales Return Location" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (MPGeneralSetup."MPP Sales Return Location" IN ["Transfer-to Code", "Transfer-from Code"]) AND
                    NOT (TransLine."Skip Container No. Check") THEN
                  //H0919, H1160, H1200, H1718, T0060 25.02.15 CC-GH -------------------------------------------------------------------
                    //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                    BEGIN
                      IF IsInternational THEN BEGIN
                        IF TransLine."Container No." = '' THEN BEGIN
                          SingleInstanceCU.SetError(TRUE,HMEText001);
                          EXIT;
                        END;
                      END ELSE
                        TransLine.TESTFIELD("Container No.");
                    END;
                    //H4370 26.06.15 TST -----------------------------

                  TransShptLine.INIT;
                  TransShptLine."Document No." := TransShptHeader."No.";
                  TransShptLine."Line No." := TransLine."Line No.";
                  TransShptLine."Item No." := TransLine."Item No.";
                  TransShptLine.Description := TransLine.Description;
                  TransShptLine.Quantity := TransLine."Qty. to Ship";
                  TransShptLine."Unit of Measure" := TransLine."Unit of Measure";
                  TransShptLine."Shortcut Dimension 1 Code" := TransLine."Shortcut Dimension 1 Code";
                  TransShptLine."Shortcut Dimension 2 Code" := TransLine."Shortcut Dimension 2 Code";
                  TransShptLine."Gen. Prod. Posting Group" := TransLine."Gen. Prod. Posting Group";
                  TransShptLine."Inventory Posting Group" := TransLine."Inventory Posting Group";
                  TransShptLine."Quantity (Base)" := TransLine."Qty. to Ship (Base)";
                  TransShptLine."Qty. per Unit of Measure" := TransLine."Qty. per Unit of Measure";
                  TransShptLine."Unit of Measure Code" := TransLine."Unit of Measure Code";
                  TransShptLine."Gross Weight" := TransLine."Gross Weight";
                  TransShptLine."Net Weight" := TransLine."Net Weight";
                  TransShptLine."Unit Volume" := TransLine."Unit Volume";
                  TransShptLine."Variant Code" := TransLine."Variant Code";
                  TransShptLine."Units per Parcel" := TransLine."Units per Parcel";
                  TransShptLine."Description 2" := TransLine."Description 2";
                  TransShptLine."Transfer Order No." := TransLine."Document No.";
                  TransShptLine."Shipment Date" := TransLine."Shipment Date";
                  TransShptLine."Shipping Agent Code" := TransLine."Shipping Agent Code";
                  TransShptLine."Shipping Agent Service Code" := TransLine."Shipping Agent Service Code";
                  TransShptLine."In-Transit Code" := TransLine."In-Transit Code";
                  TransShptLine."Transfer-from Code" := TransLine."Transfer-from Code";
                  TransShptLine."Transfer-to Code" := TransLine."Transfer-to Code";
                  TransShptLine."Transfer-from Bin Code" := TransLine."Transfer-from Bin Code";
                  TransShptLine."Shipping Time" := TransLine."Shipping Time";
                  TransShptLine."Item Category Code" := TransLine."Item Category Code";
                  TransShptLine."Product Group Code" := TransLine."Product Group Code";
                  //S/FOB
                  TransShptLine."Buy-from Vendor No" := TransLine."Buy-from Vendor No";
                  TransShptLine."Purchase Order No." := TransLine."Purchase Order No.";
                  TransShptLine."Container No." := TransLine."Container No.";
                  TransShptLine."Purchase Order Line No." := TransLine."Purchase Order Line No.";
                  TransShptLine."Bill of Lading No." := TransLine."Bill of Lading No.";
                  //E/FOB
                  //H1200 09.09.14 EHN +++++++++++++++++++++++++++++
                  TransShptLine."Linked Sales Doc. Type" := TransLine."Linked Sales Doc. Type";
                  TransShptLine."Linked Sales Doc. No." := TransLine."Linked Sales Doc. No.";
                  TransShptLine."Linked Sales Line No." := TransLine."Linked Sales Line No.";
                  TransShptLine."Return Receipt No." := TransLine."Return Receipt No.";
                  TransShptLine."Return Receipt Line No." := TransLine."Return Receipt Line No.";
                  //H1200 09.09.14 EHN -----------------------------

                  //H1718 24.09.14 EHN +++++++++++++++++++++++++++++
                  ArchvWhseTransferLineL.RESET;
                  ArchvWhseTransferLineL.SETCURRENTKEY("Transfer Order No.");
                  ArchvWhseTransferLineL.SETRANGE("Transfer Order No.",TransLine."Document No.");
                  ArchvWhseTransferLineL.SETRANGE("Transfer Order Line No.",TransLine."Line No.");
                  IF ArchvWhseTransferLineL.FIND('-') THEN
                    REPEAT
                      ArchvWhseTransferLine2L.GET(ArchvWhseTransferLineL."Entry No.");
                      ArchvWhseTransferLine2L."Transfer Shipment No." := TransShptLine."Document No.";
                      ArchvWhseTransferLine2L."Transfer Shipment Line No." := TransShptLine."Line No.";
                      ArchvWhseTransferLine2L."Transfer Order No." := '';
                      ArchvWhseTransferLine2L."Transfer Order Line No." := 0;
                      ArchvWhseTransferLine2L.MODIFY(TRUE);
                    UNTIL ArchvWhseTransferLineL.NEXT = 0;
                  //H1718 24.09.14 EHN -----------------------------

                  IF TransLine."Qty. to Ship" > 0 THEN BEGIN
                    OriginalQuantity := TransLine."Qty. to Ship";
                    OriginalQuantityBase := TransLine."Qty. to Ship (Base)";
                    PostItemJnlLine(TransLine,TransShptHeader,TransShptLine);
                    TransShptLine."Item Shpt. Entry No." := InsertShptEntryRelation(TransShptLine);
                    IF WhseShip THEN BEGIN
                      WhseShptLine.SETCURRENTKEY(
                        "No.","Source Type","Source Subtype","Source No.","Source Line No.");
                      WhseShptLine.SETRANGE("No.",WhseShptHeader."No.");
                      WhseShptLine.SETRANGE("Source Type",DATABASE::"Transfer Line");
                      WhseShptLine.SETRANGE("Source No.",TransLine."Document No.");
                      WhseShptLine.SETRANGE("Source Line No.",TransLine."Line No.");
                      WhseShptLine.FIND('-');
                      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
                      IF IsInternational THEN BEGIN
                        IF WhseShptLine."Qty. to Ship"<> TransShptLine.Quantity THEN BEGIN
                          SingleInstanceCU.SetError(TRUE,STRSUBSTNO(HMEText003, WhseShptLine.TABLECAPTION,
                           WhseShptLine.FIELDCAPTION("Qty. to Ship"),TransShptLine.TABLECAPTION,TransShptLine.FIELDCAPTION(Quantity)));
                          EXIT;
                        END;
                      END ELSE
                      //H4370 26.06.15 TST -----------------------------
                        WhseShptLine.TESTFIELD("Qty. to Ship",TransShptLine.Quantity);
                      WhsePostShpt.CreatePostedShptLine(
                        WhseShptLine,PostedWhseShptHeader,PostedWhseShptLine,TempWhseSplitSpecification);
                    END;
                    IF WhsePosting THEN
                      PostWhseJnlLine(ItemJnlLine,OriginalQuantity,OriginalQuantityBase);
                  END;

                  //S/FOB1.01
                  //S/FOB
                  {****auskommentiert******************
                  IF PurchServ.CheckShip(TransLine."Buy-from Vendor No") THEN BEGIN
                  //A/p0282/21.09.12
                  //E/p0282/21.09.12

                  IF PurchLine.GET(PurchLine."Document Type"::Order,TransLine."Purchase Order No.",TransLine."Purchase Order Line No.") THEN
                    IF (PurchLine."Shipping Type" <> '') THEN BEGIN
                  //E/FOB1.01
                      TransLine3.RESET;
                      TransLine3.SETRANGE("Document No.",TransLine."Document No.");
                      TransLine3.SETRANGE("Line No.",TransLine."Line No.");
                      TransLine3.SETFILTER("Qty. to Ship",'>%1',0);
                      IF TransLine3.FINDFIRST THEN BEGIN
                        TransLine3."Bill of Lading No." := '';
                        TransLine3."Container No." := '';
                        IF TransLine3.MODIFY THEN BEGIN
                          PurchLineGlob.RESET;
                          PurchLineGlob.CALCFIELDS("Transfer Order No.","Transfer Order Line No.");
                          PurchLineGlob.SETRANGE("Transfer Order No.",TransLine3."Document No.");
                          PurchLineGlob.SETRANGE("Transfer Order Line No.",TransLine3."Line No.");
                          IF PurchLineGlob.FINDFIRST THEN BEGIN
                            PurchLineGlob.MODIFY;
                          END;
                        END;
                      END;
                    END;
                  //A/p0282/21.09.12
                  ********************mab**************}
                  //E/p0282/21.09.12

                  //E/FOB
                  TransShptLine.INSERT;

                  DimMgt.MoveOneDocDimToPostedDocDim(
                    DocDim,DATABASE::"Transfer Line",DocDim."Document Type"::" ","No.",TransLine."Line No.",
                    DATABASE::"Transfer Shipment Line",TransShptLine."Document No.");
                UNTIL TransLine.NEXT = 0;
              END;

              //S/P1282
              ParcelStatusHistoryMgmt.MoveParcInfToArchWithoutDelete(
                1,                            // p_Command::All,SalesDoc,PurchDoc,Date,EntryNo
                TransHeader."No.",            // p_DocNo
                0,                            // p_LineNo
                6,                            // p_DocType
                '',                           // p_DateFilterString
                0);                           // p_EntryNo
              //E/P1282

              InvtSetup.GET;
              IF InvtSetup."Automatic Cost Adjustment" <>
                 InvtSetup."Automatic Cost Adjustment"::Never
              THEN BEGIN
                InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
                InvtAdjmt.MakeMultiLevelAdjmt;
              END;

              IF WhseShip THEN
                WhseShptLine.LOCKTABLE;
              TransLine.LOCKTABLE;
              TransLine.SETFILTER(Quantity,'<>0');
              TransLine.SETFILTER("Qty. to Ship",'<>0');
              IF TransLine.FIND('-') THEN BEGIN
                NextLineNo := AssignLineNo(TransLine."Document No.");
                REPEAT
                  TransLine2.INIT;
                  TransLine2 := TransLine;
                  TransLine2."Transfer-from Code" := TransLine."In-Transit Code";
                  TransLine2."In-Transit Code" := '';
                  TransLine2."Derived From Line No." := TransLine."Line No.";
                  TransLine2."Line No." := NextLineNo;
                  NextLineNo := NextLineNo + 10000;
                  TransLine2.Quantity := TransLine."Qty. to Ship";
                  TransLine2."Quantity (Base)" := TransLine."Qty. to Ship (Base)";
                  TransLine2."Qty. to Ship" := TransLine2.Quantity;
                  TransLine2."Qty. to Ship (Base)" := TransLine2."Quantity (Base)";
                  TransLine2."Qty. to Receive" := TransLine2.Quantity;
                  TransLine2."Qty. to Receive (Base)" := TransLine2."Quantity (Base)";
                  TransLine2."Quantity Shipped" := 0;
                  TransLine2."Qty. Shipped (Base)" := 0;
                  TransLine2."Quantity Received" := 0;
                  TransLine2."Qty. Received (Base)" := 0;
                  TransLine2."Qty. in Transit" := 0;
                  TransLine2."Qty. in Transit (Base)" := 0;
                  TransLine2."Outstanding Quantity" := TransLine2.Quantity;
                  TransLine2."Outstanding Qty. (Base)" := TransLine2."Quantity (Base)";
                  TransLine2.INSERT;

                  TransferTracking(TransLine,TransLine2,TransLine."Qty. to Ship (Base)");

                  TransLine.VALIDATE("Quantity Shipped",TransLine."Quantity Shipped" + TransLine."Qty. to Ship");

                  TransLine.UpdateWithWarehouseShipReceive;

                  TransLine.MODIFY;
                UNTIL TransLine.NEXT = 0;
              END;

              IF WhseShip THEN
                WhseShptLine.LOCKTABLE;
              LOCKTABLE;
              IF WhseShip THEN BEGIN
                WhsePostShpt.PostUpdateWhseDocuments(WhseShptHeader);
                TempWhseShptHeader.DELETE;
              END;

              "Last Shipment No." := TransShptHeader."No.";
              MODIFY;

              TransLine.SETRANGE(Quantity);
              TransLine.SETRANGE("Qty. to Ship");
              HeaderDeleted := DeleteOneTransferOrder(TransHeader,TransLine);
              IF NOT HeaderDeleted THEN BEGIN
                WhseTransferRelease.Release(TransHeader);
                ReserveTransLine.UpdateItemTrackingAfterPosting(TransHeader,0);
              END;

              IF NOT InvtPickPutaway THEN
                COMMIT;
              CLEAR(WhsePostShpt);
              CLEAR(InvtAdjmt);
              //H4202 07.05.15 TST +++++++++++++++++++++++++++++
              IF NOT HideValidationDialog OR GUIALLOWED THEN
              //H4202 07.05.15 TST -----------------------------
                Window.CLOSE;
            END;
            UpdateAnalysisView.UpdateAll(0,TRUE);
            UpdateItemAnalysisView.UpdateAll(0,TRUE);
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Der Umlagerungsauftrag %2 kann nicht gebucht werden, da der %3 und der %4 identisch sind.;ENU=Transfer order %2 cannot be posted because %3 and %4 are the same.';
      Text001@1001 : TextConst 'DEU=Es gibt nichts zu buchen.;ENU=There is nothing to post.';
      Text002@1002 : TextConst 'DEU="Lagerdurchlauf ist notwendig fÅr Umlagerungsauftrag = %1, %2 = %3.";ENU="Warehouse handling is required for Transfer order = %1, %2 = %3."';
      Text003@1003 : TextConst 'DEU=Umlagerungszeilen buchen   #2######;ENU=Posting transfer lines     #2######';
      Text004@1004 : TextConst 'DEU=Umlagerungsauftrag %1;ENU=Transfer Order %1';
      Text005@1005 : TextConst 'DEU=Die Kombination der in dem Umlagerungsauftrag %1 verwendeten Dimensionen ist gesperrt. %2;ENU=The combination of dimensions used in transfer order %1 is blocked. %2';
      Text006@1006 : TextConst 'DEU=Die Kombination der in dem Umlagerungsauftrag %1, Zeilennr. %2, verwendeten Dimensionen ist gesperrt. %3;ENU=The combination of dimensions used in transfer order %1, line no. %2 is blocked. %3';
      Text007@1007 : TextConst 'DEU=Die in Umlagerungsauftrag %1, Zeilennr. %2, verwendeten Dimensionen sind ungÅltig. %3;ENU=The dimensions used in transfer order %1, line no. %2 are invalid. %3';
      TransShptHeader@1008 : Record 5744;
      TransShptLine@1009 : Record 5745;
      TransHeader@1010 : Record 5740;
      TransLine@1011 : Record 5741;
      TransLine2@1012 : Record 5741;
      DocDim@1013 : Record 357;
      TempDocDim@1014 : TEMPORARY Record 357;
      Location@1015 : Record 14;
      ItemJnlLine@1016 : Record 83;
      WhseRqst@1027 : Record 5765;
      WhseShptHeader@1022 : Record 7320;
      TempWhseShptHeader@1030 : TEMPORARY Record 7320;
      WhseShptLine@1028 : Record 7321;
      PostedWhseShptHeader@1034 : Record 7322;
      PostedWhseShptLine@1035 : Record 7323;
      TempWhseSplitSpecification@1037 : TEMPORARY Record 336;
      TempHandlingSpecification@1018 : TEMPORARY Record 336;
      NoSeriesLine@1029 : Record 309;
      GLEntry@1040 : Record 17;
      ItemJnlPostLine@1021 : Codeunit 22;
      DimMgt@1017 : Codeunit 408;
      WhseTransferRelease@1019 : Codeunit 5773;
      ReserveTransLine@1020 : Codeunit 99000836;
      WhsePostShpt@1036 : Codeunit 5763;
      InvtAdjmt@1041 : Codeunit 5895;
      SourceCode@1023 : Code[10];
      HideValidationDialog@1024 : Boolean;
      HeaderDeleted@1025 : Boolean;
      WhseShip@1026 : Boolean;
      WhsePosting@1033 : Boolean;
      InvtPickPutaway@1039 : Boolean;
      WhseReference@1038 : Integer;
      OriginalQuantity@1031 : Decimal;
      OriginalQuantityBase@1032 : Decimal;
      Text008@11042 : TextConst 'DEU=Dieser Auftrag muss eine vollstÑndige Lieferung darstellen.;ENU=This order must be a complete shipment.';
      PurchServ@1108200000 : Codeunit 50090;
      SingleInstanceCU@1000000001 : Codeunit 50324;
      TransLine3@1108200001 : Record 5741;
      PurchLineGlob@1108200002 : Record 39;
      IsInternational@1000000000 : Boolean;
      HMEText001@1000000002 : TextConst 'ENU=Container No. must not be empty.';
      HMEText002@1000000003 : TextConst 'ENU=Item %1 must no be blocked.';
      HMEText003@1000000004 : TextConst 'ENU=%1 %2 must be equal to %3 %4.';
      HMEText004@1000000005 : TextConst 'ENU=%1 %2 must not be empty.';
      HMEText005@1000000006 : TextConst 'ENU=Status must be released.';

    LOCAL PROCEDURE PostItemJnlLine@2(VAR TransLine3@1000 : Record 5741;TransShptHeader2@1001 : Record 5744;TransShptLine2@1002 : Record 5745);
    VAR
      TempJnlLineDim@1003 : TEMPORARY Record 356;
    BEGIN
      ItemJnlLine.INIT;
      ItemJnlLine."Posting Date" := TransShptHeader2."Posting Date";
      ItemJnlLine."Document Date" := TransShptHeader2."Posting Date";
      ItemJnlLine."Document No." := TransShptHeader2."No.";
      ItemJnlLine."Document Type" := ItemJnlLine."Document Type"::"Transfer Shipment";
      ItemJnlLine."Document Line No." := TransShptLine2."Line No.";
      ItemJnlLine."Transfer Order No." := TransShptHeader2."Transfer Order No.";
      ItemJnlLine."External Document No." := TransShptHeader2."External Document No.";
      ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::Transfer;
      ItemJnlLine."Item No." := TransShptLine2."Item No.";
      ItemJnlLine.Description := TransShptLine2.Description;
      ItemJnlLine."Shortcut Dimension 1 Code" := TransShptLine2."Shortcut Dimension 1 Code";
      ItemJnlLine."New Shortcut Dimension 1 Code" := TransShptLine2."Shortcut Dimension 1 Code";
      ItemJnlLine."Shortcut Dimension 2 Code" := TransShptLine2."Shortcut Dimension 2 Code";
      ItemJnlLine."New Shortcut Dimension 2 Code" := TransShptLine2."Shortcut Dimension 2 Code";
      ItemJnlLine."Location Code" := TransShptHeader2."Transfer-from Code";
      ItemJnlLine."New Location Code" := TransHeader."In-Transit Code";
      ItemJnlLine.Quantity := TransShptLine2.Quantity;
      ItemJnlLine."Invoiced Quantity" := TransShptLine2.Quantity;
      ItemJnlLine."Quantity (Base)" := TransShptLine2."Quantity (Base)";
      ItemJnlLine."Invoiced Qty. (Base)" := TransShptLine2."Quantity (Base)";
      ItemJnlLine."Source Code" := SourceCode;
      ItemJnlLine."Gen. Prod. Posting Group" := TransShptLine2."Gen. Prod. Posting Group";
      ItemJnlLine."Inventory Posting Group" := TransShptLine2."Inventory Posting Group";
      ItemJnlLine."Unit of Measure Code" := TransShptLine2."Unit of Measure Code";
      ItemJnlLine."Qty. per Unit of Measure" := TransShptLine2."Qty. per Unit of Measure";
      ItemJnlLine."Variant Code":= TransShptLine2."Variant Code";
      ItemJnlLine."Bin Code" := TransLine."Transfer-from Bin Code";
      ItemJnlLine."Country/Region Code" := TransShptHeader2."Trsf.-from Country/Region Code";
      ItemJnlLine."Transaction Type" :=  TransShptHeader2."Transaction Type";
      ItemJnlLine."Transport Method" := TransShptHeader2."Transport Method";
      ItemJnlLine."Entry/Exit Point" := TransShptHeader2."Entry/Exit Point";
      ItemJnlLine.Area := TransShptHeader2.Area;
      ItemJnlLine."Transaction Specification" := TransShptHeader2."Transaction Specification";
      ItemJnlLine."Product Group Code" := TransLine."Product Group Code";
      ItemJnlLine."Item Category Code" := TransLine."Item Category Code";

      ReserveTransLine.TransferTransferToItemJnlLine(TransLine3,
        ItemJnlLine,ItemJnlLine."Quantity (Base)",0);

      TempDocDim.RESET;
      TempDocDim.SETRANGE("Table ID",DATABASE::"Transfer Line");
      TempDocDim.SETRANGE("Line No.",TransLine3."Line No.");
      DimMgt.CopyDocDimToJnlLineDim(TempDocDim,TempJnlLineDim);
      IF TempJnlLineDim.FIND('-') THEN
        REPEAT
          TempJnlLineDim."New Dimension Value Code" := TempJnlLineDim."Dimension Value Code";
          TempJnlLineDim.MODIFY;
        UNTIL TempJnlLineDim.NEXT = 0;

      ItemJnlPostLine.RunWithCheck(ItemJnlLine,TempJnlLineDim);
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20]);
    VAR
      InvtCommentLine@1004 : Record 5748;
      InvtCommentLine2@1005 : Record 5748;
    BEGIN
      InvtCommentLine.SETRANGE("Document Type",FromDocumentType);
      InvtCommentLine.SETRANGE("No.",FromNumber);
      IF InvtCommentLine.FIND('-') THEN
        REPEAT
          InvtCommentLine2 := InvtCommentLine;
          InvtCommentLine2."Document Type" := ToDocumentType;
          InvtCommentLine2."No." := ToNumber;
          InvtCommentLine2.INSERT;
        UNTIL InvtCommentLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyAndCheckDocDimToTempDocDim@34();
    VAR
      CurrLineNo@1000 : Integer;
    BEGIN
      TempDocDim.RESET;
      TempDocDim.DELETEALL;
      DocDim.SETFILTER("Table ID",'%1|%2',DATABASE::"Transfer Header",DATABASE::"Transfer Line");
      DocDim.SETRANGE("Document Type",DocDim."Document Type"::" ");
      DocDim.SETRANGE("Document No.",TransHeader."No.");
      IF DocDim.FIND('-') THEN BEGIN
        CurrLineNo := DocDim."Line No.";
        REPEAT
          IF CurrLineNo <> DocDim."Line No." THEN BEGIN
            TempDocDim.SETRANGE("Line No.",CurrLineNo);
            CheckDimComb(CurrLineNo);
            CheckDimValuePosting(CurrLineNo);
            CurrLineNo := DocDim."Line No.";
          END;
          TempDocDim.INIT;
          TempDocDim := DocDim;
          TempDocDim.INSERT;
        UNTIL DocDim.NEXT = 0;
        TempDocDim.SETRANGE("Line No.",CurrLineNo);
        CheckDimComb(CurrLineNo);
        CheckDimValuePosting(CurrLineNo);
      END;
      TempDocDim.RESET;
    END;

    LOCAL PROCEDURE CheckDimComb@30(LineNo@1000 : Integer);
    BEGIN
      IF NOT DimMgt.CheckDocDimComb(TempDocDim) THEN
        IF LineNo = 0 THEN
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text005, TransHeader."No.",DimMgt.GetDimCombErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text005,
              TransHeader."No.",DimMgt.GetDimCombErr)
        ELSE
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text006, TransHeader."No.",DimMgt.GetDimCombErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text006,
              TransHeader."No.",LineNo,DimMgt.GetDimCombErr);
    END;

    LOCAL PROCEDURE CheckDimValuePosting@28(LineNo@1000 : Integer);
    VAR
      TheTransLine@1001 : Record 5741;
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
    BEGIN
      IF TheTransLine.GET(TransHeader."No.",LineNo) THEN BEGIN
        TableIDArr[1] := DATABASE::Item;
        NumberArr[1] := TheTransLine."Item No.";
        IF NOT DimMgt.CheckDocDimValuePosting(TempDocDim,TableIDArr,NumberArr) THEN
          //H4370 23.06.15 TST +++++++++++++++++++++++++++++
          IF IsInternational THEN BEGIN
            SingleInstanceCU.SetError(TRUE,STRSUBSTNO(Text007, TransHeader."No.",LineNo,DimMgt.GetDimValuePostingErr));
            ERROR('');
          END ELSE
          //H4370 26.06.15 TST -----------------------------
            ERROR(
              Text007,
              TransHeader."No.",LineNo,DimMgt.GetDimValuePostingErr);
        END;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    LOCAL PROCEDURE AssignLineNo@1(FromDocNo@1000 : Code[20]) : Integer;
    VAR
      TransLine3@1002 : Record 5741;
    BEGIN
      TransLine3.SETRANGE("Document No.",FromDocNo);
      IF TransLine3.FIND('+') THEN
        EXIT(TransLine3."Line No." + 10000);
    END;

    LOCAL PROCEDURE InsertShptEntryRelation@38(VAR TransShptLine@1002 : Record 5745) : Integer;
    VAR
      TempHandlingSpecification2@1000 : TEMPORARY Record 336;
      ItemEntryRelation@1001 : Record 6507;
      ItemTrackingMgt@1005 : Codeunit 6500;
      WhseSNRequired@1003 : Boolean;
      WhseLNRequired@1004 : Boolean;
    BEGIN
      IF WhsePosting THEN BEGIN
        TempWhseSplitSpecification.RESET;
        TempWhseSplitSpecification.DELETEALL;
      END;

      TempHandlingSpecification2.RESET;
      IF ItemJnlPostLine.CollectTrackingSpecification(TempHandlingSpecification2) THEN BEGIN
        TempHandlingSpecification2.SETRANGE("Buffer Status",0);
        IF TempHandlingSpecification2.FIND('-') THEN BEGIN
          REPEAT
            IF WhsePosting OR WhseShip OR InvtPickPutaway THEN BEGIN
              ItemTrackingMgt.CheckWhseItemTrkgSetup(
                TransShptLine."Item No.",WhseSNRequired,WhseLNRequired,FALSE);
              IF WhseSNRequired OR WhseLNRequired THEN BEGIN
                TempWhseSplitSpecification := TempHandlingSpecification2;
                TempWhseSplitSpecification."Source Type" := DATABASE::"Transfer Line";
                TempWhseSplitSpecification."Source ID" := TransLine."Document No.";
                TempWhseSplitSpecification."Source Ref. No." := TransLine."Line No.";
                TempWhseSplitSpecification.INSERT;
              END;
            END;

            ItemEntryRelation.INIT;
            ItemEntryRelation."Item Entry No." := TempHandlingSpecification2."Entry No.";
            ItemEntryRelation."Serial No." := TempHandlingSpecification2."Serial No.";
            ItemEntryRelation."Lot No." := TempHandlingSpecification2."Lot No.";
            ItemEntryRelation.TransferFieldsTransShptLine(TransShptLine);
            ItemEntryRelation.INSERT;
            TempHandlingSpecification := TempHandlingSpecification2;
            TempHandlingSpecification."Source Prod. Order Line" := TransShptLine."Line No.";
            TempHandlingSpecification."Buffer Status" := TempHandlingSpecification."Buffer Status"::MODIFY;
            TempHandlingSpecification.INSERT;
          UNTIL TempHandlingSpecification2.NEXT = 0;
          EXIT(0);
        END;
      END ELSE
        EXIT(ItemJnlLine."Item Shpt. Entry No.");
    END;

    PROCEDURE TransferTracking@3(VAR FromTransLine@1003 : Record 5741;VAR ToTransLine@1004 : Record 5741;TransferQty@1001 : Decimal);
    VAR
      DummySpecification@1005 : Record 336;
    BEGIN
      TempHandlingSpecification.RESET;
      TempHandlingSpecification.SETRANGE("Source Prod. Order Line",ToTransLine."Derived From Line No.");
      IF TempHandlingSpecification.FIND('-') THEN BEGIN
        REPEAT
          ReserveTransLine.TransferTransferToTransfer(
            FromTransLine,ToTransLine,-TempHandlingSpecification."Quantity (Base)",1,TempHandlingSpecification);
          TransferQty += TempHandlingSpecification."Quantity (Base)";
        UNTIL TempHandlingSpecification.NEXT = 0;
        TempHandlingSpecification.DELETEALL;
      END;

      IF TransferQty > 0 THEN
        ReserveTransLine.TransferTransferToTransfer(
          FromTransLine,ToTransLine,TransferQty,1,DummySpecification);
    END;

    LOCAL PROCEDURE CheckWarehouse@7301(VAR TransLine@1000 : Record 5741);
    VAR
      TransLine2@1001 : Record 5741;
      WhseValidateSourceLine@1003 : Codeunit 5777;
      ShowError@1002 : Boolean;
    BEGIN
      TransLine2.COPY(TransLine);
      IF TransLine2.FIND('-') THEN
        REPEAT
          GetLocation(TransLine2."Transfer-from Code");
          IF Location."Require Pick" OR Location."Require Shipment" THEN BEGIN
            IF Location."Bin Mandatory" THEN
              ShowError := TRUE
            ELSE
              IF WhseValidateSourceLine.WhseLinesExist(
                   DATABASE::"Transfer Line",
                   0, // Out
                   TransLine2."Document No.",
                   TransLine2."Line No.",
                   0,
                   TransLine2.Quantity)
              THEN
                ShowError := TRUE;

            IF ShowError THEN
              ERROR(
                Text002,
                TransLine2."Document No.",
                TransLine2.FIELDCAPTION("Line No."),
                TransLine2."Line No.");
          END;
        UNTIL TransLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        Location.GetLocationSetup(LocationCode,Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE PostWhseJnlLine@4(ItemJnlLine@1000 : Record 83;OriginalQuantity@1001 : Decimal;OriginalQuantityBase@1006 : Decimal);
    VAR
      WhseJnlLine@1002 : Record 7311;
      TempWhseJnlLine2@1005 : TEMPORARY Record 7311;
      ItemTrackingMgt@1004 : Codeunit 6500;
      WMSMgmt@1007 : Codeunit 7302;
      WhseJnlPostLine@1008 : Codeunit 7301;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        Quantity := OriginalQuantity;
        "Quantity (Base)" := OriginalQuantityBase;
        GetLocation("Location Code");
        IF Location."Bin Mandatory" THEN
          IF WMSMgmt.CreateWhseJnlLine(ItemJnlLine,1,WhseJnlLine,FALSE,FALSE) THEN BEGIN
            WMSMgmt.SetTransferLine(TransLine,WhseJnlLine,0,TransShptHeader."No.");
            ItemTrackingMgt.SplitWhseJnlLine(
              WhseJnlLine,TempWhseJnlLine2,TempWhseSplitSpecification,TRUE);
            IF TempWhseJnlLine2.FIND('-') THEN
              REPEAT
                WMSMgmt.CheckWhseJnlLine(TempWhseJnlLine2,1,0,TRUE);
                WhseJnlPostLine.RUN(TempWhseJnlLine2);
              UNTIL TempWhseJnlLine2.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE SetWhseShptHeader@5(VAR WhseShptHeader2@1000 : Record 7320);
    BEGIN
      WhseShptHeader := WhseShptHeader2;
      TempWhseShptHeader := WhseShptHeader;
      TempWhseShptHeader.INSERT;
    END;

    LOCAL PROCEDURE GetShippingAdvice@6() : Boolean;
    VAR
      TransLine@1000 : Record 5741;
    BEGIN
      TransLine.SETRANGE("Document No.",TransHeader."No.");
      IF TransLine.FIND('-') THEN
        REPEAT
          IF TransLine."Quantity (Base)" <>
             TransLine."Qty. to Ship (Base)" + TransLine."Qty. Shipped (Base)"
          THEN
            EXIT(FALSE);
        UNTIL TransLine.NEXT = 0;
      EXIT(TRUE);
    END;

    PROCEDURE CheckSingleInstance@1000000000();
    VAR
      ObjectTypeL@1000000000 : ' ,Table,Form,Report,Dataport,XMLPort,Codeunit,MenuSuite,Page';
      ObjectIDL@1000000001 : Integer;
    BEGIN
      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
      ObjectTypeL := 0;
      ObjectIDL := 0;
      SingleInstanceCU.GetObject(ObjectTypeL,ObjectIDL);

      IsInternational := (ObjectTypeL = 3) AND (ObjectIDL = 50196);
      //H4370 26.06.15 TST -----------------------------
    END;

    PROCEDURE CheckTransferHeader@1000000002() : Boolean;
    VAR
      InvtSetupL@1000000000 : Record 313;
    BEGIN
      //H4370 23.06.15 TST +++++++++++++++++++++++++++++
      IF IsInternational THEN BEGIN
        IF TransHeader."Transfer-from Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("Transfer-from Code")));
          EXIT(FALSE);
        END;
        IF TransHeader."Transfer-to Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("Transfer-to Code")));
          EXIT(FALSE);
        END;
        IF TransHeader."In-Transit Code" = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, TransHeader.FIELDCAPTION("In-Transit Code")));
          EXIT(FALSE);
        END;
        IF TransHeader.Status <> TransHeader.Status::Released THEN BEGIN
          SingleInstanceCU.SetError(TRUE,HMEText005);
          EXIT(FALSE);
        END;
        InvtSetupL.GET;
        IF InvtSetupL."Posted Transfer Shpt. Nos." = '' THEN BEGIN
          SingleInstanceCU.SetError(TRUE,
            STRSUBSTNO(HMEText004, InvtSetupL.TABLECAPTION,InvtSetupL.FIELDCAPTION("Posted Transfer Shpt. Nos.")));
          EXIT(FALSE);
        END;
      END;

      EXIT(TRUE);
      //H4370 26.06.15 TST -----------------------------
    END;

    BEGIN
    {
      +-----------------------------------------------------+
      |               GOB Software & Systeme                |
      +-----------------------------------------------------+
      |                                   |
      -------------------------------------------------------

      Version       Comment
      -------------------------------------------------------
      gob-mab p0282   FOB           OnRun
      gob-mab p0282   FOB1.01       OnRun
      gob-mab p0282   21.09.12    Korrektur
      P1282  gob-ael  20141021  Archive "Parcel Status History" after posting


      +--------------------------------------------------------+
      |                                                        |
      |                      Home24                            |
      |       Internal Customizing by NAV-Team                 |
      |                                                        |
      +--------------------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      H0919       12.03.14 DMA       Don't check container no. for orders related to return warehouse
      H1160       16.06.14 DMA       Return Whs: split Incoming Qty in SR and FOTO, CODECHANGE
      H1200       03.07.14 EHN       Ship Sales Returns via Return Transit location, CODECHANGE
      H1718       24.09.14 EHN       Implementation based on Concept "Warehouse Transfer Worksheet"
      H4202       07.05.15 TST       Suppress Dialogs
      H4370       23.06.15 TST       Stop in Case of Errors if started by Report 50196, new functions

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |     Cooperation Tectura & Home24 NAV Team  |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________
      T0053       01.10.14  TEC-GH    Avoid Transfer Header between MPP Location and Not MPP Location
      T0060       25.02.15  CC-GH     Handel MP Return Locations
    }
    END.
  }
}

