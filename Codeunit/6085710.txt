OBJECT Codeunit 6085710 Purch. Doc. - Transl. (Templ.)
{
  OBJECT-PROPERTIES
  {
    Date=11.11.12;
    Time=01:00:00;
    Version List=DCW13.70.00.3.02;
  }
  PROPERTIES
  {
    TableNo=6085579;
    OnRun=VAR
            Field@1000000001 : Record 6085580;
            DataTransl@1000000003 : Record 6085584;
            PurchLineTransl@1000000000 : Form 6085701;
            Selection@1000000002 : Integer;
          BEGIN
            Selection := STRMENU(Text001,1);
            IF Selection = 0 THEN
              EXIT;

            CASE Selection OF
              1:
                BEGIN
                  DataTransl.FILTERGROUP := 2;

                  Field.SETRANGE("Template No.",Rec."No.");
                  Field.SETRANGE(Type,Field.Type::Header);
                  Field.SETRANGE("Data Type",Field."Data Type"::Number);
                  Field.SETFILTER("Transfer Amount to Purch. Doc.",'<>%1',Field."Transfer Amount to Purch. Doc."::" ");
                  IF Field.FINDFIRST THEN
                    REPEAT
                      DataTransl.SETRANGE("Template No.","No.");
                      DataTransl.SETRANGE(Type,Field.Type);
                      DataTransl.SETRANGE("Field Code",Field.Code);

                      IF NOT DataTransl.FINDFIRST THEN BEGIN
                        DataTransl.INIT;
                        DataTransl."Template No." := "No.";
                        DataTransl.Type := Field.Type;
                        DataTransl."Field Code" := Field.Code;
                        DataTransl."Translate From" := '';
                        DataTransl.CALCFIELDS("Field Description");
                        DataTransl.INSERT;
                      END;
                    UNTIL Field.NEXT = 0;
                  COMMIT;

                  DataTransl.SETRANGE("Field Code");
                  DataTransl.SETFILTER("Translate to Type",'<%1',DataTransl."Translate to Type"::"Salesperson/Purchaser");
                  DataTransl.FILTERGROUP := 0;
                  IF DataTransl.FINDFIRST THEN;

                  FORM.RUNMODAL(FORM::"Purch. Header Amt. Posting",DataTransl);

                  DataTransl.SETRANGE("Translate to Type",DataTransl."Translate to Type"::" ");
                  DataTransl.DELETEALL(TRUE);
                END;


              2:
                BEGIN
                  PurchLineTransl.SetTemplate(Rec);
                  Field.GET(Rec."No.",Field.Type::Line,Text002);
                  PurchLineTransl.SetField(Field);
                  PurchLineTransl.RUN;
                END;

              3:
                BEGIN
                  DataTransl.FILTERGROUP := 2;
                  DataTransl.SETRANGE("Template No.",Rec."No.");
                  DataTransl.SETRANGE(Type,Field.Type::Header);
                  DataTransl.SETRANGE("Field Code",Text003);
                  DataTransl.SETRANGE("Translate to Type",DataTransl."Translate to Type"::"Salesperson/Purchaser");
                  DataTransl.FILTERGROUP := 0;
                  FORM.RUN(0,DataTransl);
                END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'DEU=Zeige Konten fr Betr„ge,Zeige Zeilenbersetzung,Zeige Eink„uferbersetzung;ENU=Show Accounts for Amounts,Show Line Translations,Show Purchaser Translations';
      Text002@1000000001 : TextConst 'DEU=NO;ENU=NO';
      Text003@1000000000 : TextConst 'DEU=OURCONTACT;ENU=OURCONTACT';

    BEGIN
    {
      This codeunit performs post-capture actions on purchase documents
    }
    END.
  }
}

