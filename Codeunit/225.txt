OBJECT Codeunit 225 Gen. Jnl.-Apply
{
  OBJECT-PROPERTIES
  {
    Date=15.09.04;
    Time=12:00:00;
    Version List=NAVW14.00,OPP7.02.03;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            // gbedv EA -------------------------------------------------- BEGIN
            SetupOPplus(Rec);
            IF OPplusEnabled THEN
              OPplusApplication.ExtendedApplication(Rec)
            ELSE BEGIN
            // gbedv EA -------------------------------------------------- END
            GenJnlLine.COPY(Rec);

            WITH GenJnlLine DO BEGIN
              GetCurrency;
              IF "Bal. Account Type" IN
                 ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
              THEN BEGIN
                AccType := "Bal. Account Type";
                AccNo := "Bal. Account No.";
              END ELSE BEGIN
                AccType := "Account Type";
                AccNo := "Account No.";
              END;
              CASE AccType OF
                AccType::Customer:
                  BEGIN
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    IF "Applies-to ID" = '' THEN
                      "Applies-to ID" := "Document No.";
                    IF "Applies-to ID" = '' THEN
                      ERROR(
                        Text000,
                        FIELDCAPTION("Document No."),FIELDCAPTION("Applies-to ID"));
                    ApplyCustEntries.SetGenJnlLine(GenJnlLine,GenJnlLine.FIELDNO("Applies-to ID"));
                    ApplyCustEntries.SETRECORD(CustLedgEntry);
                    ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
                    ApplyCustEntries.LOOKUPMODE(TRUE);
                    OK := ApplyCustEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyCustEntries);
                    IF NOT OK THEN
                      EXIT;
                    CustLedgEntry.RESET;
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF CustLedgEntry.FIND('-') THEN BEGIN
                      CurrencyCode2 := CustLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                          CustLedgEntry.CALCFIELDS("Remaining Amount");
                          CustLedgEntry."Remaining Amount" :=
                            GenJnlPostLine.ExchAmount(
                              CustLedgEntry."Remaining Amount",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Amount" :=
                            ROUND(CustLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            GenJnlPostLine.ExchAmount(
                              CustLedgEntry."Remaining Pmt. Disc. Possible",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(CustLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");
                          CustLedgEntry."Amount to Apply" :=
                            GenJnlPostLine.ExchAmount(
                              CustLedgEntry."Amount to Apply",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Amount to Apply" :=
                            ROUND(CustLedgEntry."Amount to Apply",Currency."Amount Rounding Precision");

                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(Rec,CustLedgEntry,0,FALSE) AND
                            (ABS(CustLedgEntry."Amount to Apply") >=
                            ABS(CustLedgEntry."Remaining Amount" - CustLedgEntry."Remaining Pmt. Disc. Possible"))
                          THEN
                            Amount := Amount - (CustLedgEntry."Amount to Apply" - CustLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - CustLedgEntry."Amount to Apply";
                        UNTIL CustLedgEntry.NEXT = 0;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor) THEN
                          Amount := -Amount;
                        VALIDATE(Amount);
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                        UNTIL CustLedgEntry.NEXT = 0;
                      IF "Currency Code" <> CurrencyCode2 THEN
                        IF Amount = 0 THEN BEGIN
                          IF NOT
                             CONFIRM(
                               Text001 +
                               Text002,TRUE,
                               FIELDCAPTION("Currency Code"),TABLECAPTION,"Currency Code",
                               CustLedgEntry."Currency Code")
                          THEN
                            ERROR(Text003);
                          "Currency Code" := CustLedgEntry."Currency Code"
                        END ELSE
                          CheckAgainstApplnCurrency("Currency Code",CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" := '';
                    MODIFY;
                    //Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;

                  END;
                AccType::Vendor:
                  BEGIN
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    IF "Applies-to ID" = '' THEN
                      "Applies-to ID" := "Document No.";
                    IF "Applies-to ID" = '' THEN
                      ERROR(
                        Text000,
                        FIELDCAPTION("Document No."),FIELDCAPTION("Applies-to ID"));
                    ApplyVendEntries.SetGenJnlLine(GenJnlLine,GenJnlLine.FIELDNO("Applies-to ID"));
                    ApplyVendEntries.SETRECORD(VendLedgEntry);
                    ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
                    ApplyVendEntries.LOOKUPMODE(TRUE);
                    OK := ApplyVendEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyVendEntries);
                    IF NOT OK THEN
                      EXIT;
                    VendLedgEntry.RESET;
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF VendLedgEntry.FIND('-') THEN BEGIN
                      CurrencyCode2 := VendLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                          VendLedgEntry.CALCFIELDS("Remaining Amount");
                          VendLedgEntry."Remaining Amount" :=
                            GenJnlPostLine.ExchAmount(
                              VendLedgEntry."Remaining Amount",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Amount" :=
                            ROUND(VendLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            GenJnlPostLine.ExchAmount(
                              VendLedgEntry."Remaining Pmt. Disc. Possible",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(VendLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");
                          VendLedgEntry."Amount to Apply" :=
                            GenJnlPostLine.ExchAmount(
                              VendLedgEntry."Amount to Apply",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Amount to Apply" :=
                            ROUND(VendLedgEntry."Amount to Apply",Currency."Amount Rounding Precision");

                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(Rec,VendLedgEntry,0,FALSE) AND
                            (ABS(VendLedgEntry."Amount to Apply") >=
                            ABS(VendLedgEntry."Remaining Amount" - VendLedgEntry."Remaining Pmt. Disc. Possible"))
                          THEN
                            Amount := Amount - (VendLedgEntry."Amount to Apply" - VendLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - VendLedgEntry."Amount to Apply";

                        UNTIL VendLedgEntry.NEXT = 0;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor) THEN
                          Amount := -Amount;
                        VALIDATE(Amount);
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                        UNTIL VendLedgEntry.NEXT = 0;
                      IF "Currency Code" <> CurrencyCode2 THEN
                        IF Amount = 0 THEN BEGIN
                          IF NOT
                             CONFIRM(
                               Text001 +
                               Text002,TRUE,
                               FIELDCAPTION("Currency Code"),TABLECAPTION,"Currency Code",
                               VendLedgEntry."Currency Code")
                          THEN
                            ERROR(Text003);
                          "Currency Code" := VendLedgEntry."Currency Code"
                        END ELSE
                          CheckAgainstApplnCurrency("Currency Code",VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" :='';
                    MODIFY;
                    //Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;
                  END;
                ELSE
                  // gbedv GLOE -------------------------------------------------- BEGIN
                  BEGIN
                    GLOpenEntries := FALSE;
                    BalAccount := FALSE;
                    GLAcc.INIT;
                    IF "Applied Account No." <> '' THEN BEGIN
                      IF NOT GLAcc.GET("Applied Account No.") THEN
                        GLAcc.INIT;
                      GLOpenEntries := GLAcc."Build Open Entries";
                      BalAccount := "Applied by" = "Applied by"::"Bal. Account"
                    END;
                    IF GLOpenEntries THEN BEGIN
                      AccNo := GLAcc."No.";
                      GenLedgEntry.SETCURRENTKEY("G/L Account No.",Open,"Posting Date");
                      GenLedgEntry.SETRANGE("G/L Account No.",AccNo);
                      GenLedgEntry.SETRANGE(Open,TRUE);
                      IF "Applies-to ID" = '' THEN
                        "Applies-to ID" := "Document No.";
                      IF "Applies-to ID" = '' THEN
                        ERROR(
                         Text000,
                          FIELDCAPTION("Document No."),FIELDCAPTION("Applies-to ID"));
                      ApplyGLEntries.SetGenJnlLine(GenJnlLine,GenJnlLine.FIELDNO("Applies-to ID"));
                      ApplyGLEntries.SETRECORD(GenLedgEntry);
                      ApplyGLEntries.SETTABLEVIEW(GenLedgEntry);
                      ApplyGLEntries.LOOKUPMODE(TRUE);
                      OK := ApplyGLEntries.RUNMODAL = ACTION::LookupOK;
                      CLEAR(ApplyGLEntries);
                      IF NOT OK THEN
                        EXIT;
                      GenLedgEntry.RESET;
                      GenLedgEntry.SETCURRENTKEY("G/L Account No.",Open,"Posting Date");
                      GenLedgEntry.SETRANGE("G/L Account No.",AccNo);
                      GenLedgEntry.SETRANGE(Open,TRUE);
                      GenLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                      IF GenLedgEntry.FINDFIRST THEN BEGIN
                        IF Amount = 0 THEN BEGIN
                          REPEAT
                            GenLedgEntry.CALCFIELDS("Remaining Amount");
                            Amount := Amount - GenLedgEntry."Remaining Amount";
                          UNTIL GenLedgEntry.NEXT = 0;
                          IF BalAccount THEN
                            Amount := -Amount;
                          VALIDATE(Amount);
                        END;
                        "Applies-to Doc. Type" := 0;
                        "Applies-to Doc. No." := '';
                      END ELSE
                        "Applies-to ID" :='';
                      MODIFY;
                    END ELSE
                      ERROR(Text5157852,FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
                  END;
                  // gbedv GLOE -------------------------------------------------- END
              END;
            END;

            // gbedv EA -------------------------------------------------- BEGIN
            IF OPplusEnabled THEN BEGIN
              FinanceTools.SetApplicationStatus(GenJnlLine);
              GenJnlLine.MODIFY;
            END;
            // gbedv EA -------------------------------------------------- END
            Rec := GenJnlLine;
            // gbedv EA -------------------------------------------------- BEGIN
            END;
            // gbedv EA -------------------------------------------------- END
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie mÅssen %1 oder %2 angeben.;ENU=You must specify %1 or %2.';
      Text001@1001 : TextConst 'DEU=Der %1 in der %2 wird von %3 auf %4 geÑndert.\;ENU=The %1 in the %2 will be changed from %3 to %4.\';
      Text002@1002 : TextConst 'DEU=Mîchten Sie fortfahren?;ENU=Do you wish to continue?';
      Text003@1003 : TextConst 'DEU=Die Verarbeitung wurde unterbrochen, um die Warnung zu berÅcksichtigen.;ENU=The update has been interrupted to respect the warning.';
      Text005@1004 : TextConst 'DEU=%1 oder %2 muss Debitor oder Kreditor sein.;ENU=The %1 or %2 must be Customer or Vendor.';
      Text006@1005 : TextConst 'DEU=Alle Posten innerhalb eines Ausgleiches mÅssen in derselben WÑhrung sein.;ENU=All entries in one application must be in the same currency.';
      Text007@1006 : TextConst 'DEU="Alle Posten innerhalb eines Ausgleiches mÅssen in derselben WÑhrung oder in einer oder mehreren EWU-WÑhrungen sein. ";ENU="All entries in one application must be in the same currency or one or more of the EMU currencies. "';
      GenJnlLine@1008 : Record 81;
      CustLedgEntry@1009 : Record 21;
      VendLedgEntry@1010 : Record 25;
      GLSetup@1011 : Record 98;
      Currency@1012 : Record 4;
      ApplyCustEntries@1013 : Form 232;
      ApplyVendEntries@1014 : Form 233;
      GenJnlPostLine@1015 : Codeunit 12;
      PaymentToleranceMgt@1007 : Codeunit 426;
      AccNo@1016 : Code[20];
      CurrencyCode2@1017 : Code[10];
      OK@1018 : Boolean;
      AccType@1019 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      "+++ OPplus +++"@5157813 : Integer;
      OPplusSetup@5157810 : Record 5157802;
      Template@5157809 : Record 80;
      OPPLicenseInfo@5157812 : Codeunit 5158000;
      GenLedgEntry@5157807 : Record 17;
      GLAcc@5157804 : Record 15;
      OPplusApplication@5157808 : Codeunit 5157803;
      FinanceTools@5157806 : Codeunit 5157802;
      ApplyGLEntries@5157805 : Form 5157856;
      BalAccount@5157803 : Boolean;
      GLOpenEntries@5157802 : Boolean;
      Text5157852@5157852 : TextConst 'DEU=%1 oder %2 muss Debitor, Kreditor oder OP fÅhrendes Sachkonto sein.;ENU=The %1 or %2 must be Customer or Vendor or %1 must be OE building G/L Account.';
      OPplusEnabled@5157811 : Boolean;

    PROCEDURE CheckAgainstApplnCurrency@1(ApplnCurrencyCode@1000 : Code[10];CompareCurrencyCode@1001 : Code[10];AccType@1002 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';Message@1003 : Boolean) : Boolean;
    VAR
      Currency@1004 : Record 4;
      Currency2@1005 : Record 4;
      SalesSetup@1006 : Record 311;
      PurchSetup@1007 : Record 312;
      CurrencyAppln@1009 : 'No,EMU,All';
      "+++ OPplus +++"@5157802 : Integer;
      GLOESetup@5157803 : Record 5157853;
    BEGIN
      IF (ApplnCurrencyCode = CompareCurrencyCode) THEN
        EXIT(TRUE);

      CASE AccType OF
        // gbedv GLOE -------------------------------------------------- BEGIN
        AccType::"G/L Account",AccType::"Bank Account":
          BEGIN
            GLOESetup.GET;
            CurrencyAppln := GLOESetup."Appln. between Currencies";
            IF CurrencyAppln = CurrencyAppln::No THEN
              IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                IF Message THEN
                  ERROR(Text006)
                ELSE
                  EXIT(FALSE);
          END;
        // gbedv GLOE -------------------------------------------------- END
        AccType::Customer:
          BEGIN
            SalesSetup.GET;
            CurrencyAppln := SalesSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text006)
                    ELSE
                      EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text007)
                    ELSE
                      EXIT(FALSE);
                END;
            END;
          END;
        AccType::Vendor:
          BEGIN
            PurchSetup.GET;
            CurrencyAppln := PurchSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text006)
                    ELSE
                      EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text007)
                    ELSE
                      EXIT(FALSE);
                END;
            END;
          END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE GetCurrency@2();
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE BEGIN
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
      END;
    END;

    PROCEDURE "++++ OPplus ++++"@5157802();
    BEGIN
    END;

    PROCEDURE SetupOPplus@5157810(VAR GenJnlLine@5157802 : Record 81);
    BEGIN
      // gbedv EA -------------------------------------------------- BEGIN
      OPplusEnabled := FALSE;
      IF OPPLicenseInfo.IsBasics THEN
        IF OPplusSetup.GET THEN BEGIN
          IF Template.GET(GenJnlLine."Journal Template Name") THEN
            OPplusEnabled := Template."Application Method" = Template."Application Method"::Extended;
        END;
      // gbedv EA -------------------------------------------------- END
    END;

    BEGIN
    {
      -----------------------------------------------------
      (c) gbedv, OPplus, All rights reserved

      No.  Date       changed
      -----------------------------------------------------
      EA   01.11.08   Function Extended Application
      GLOE 01.11.08   Gen. Ledger Open Entries
      -----------------------------------------------------
    }
    END.
  }
}

