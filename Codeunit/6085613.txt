OBJECT Codeunit 6085613 Document Modifiers
{
  OBJECT-PROPERTIES
  {
    Date=12.09.13;
    Time=01:00:00;
    Version List=DCW13.70.00.3.50;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CombineDocuments@1160040000(VAR DocumentsToCombine@1160040000 : Record 6085590);
    VAR
      Document@1160040015 : Record 6085590;
      NewDocument@1160040013 : Record 6085590;
      DocPage@1160040012 : Record 6085591;
      NewDocPage@1160040011 : Record 6085591;
      DocWord@1160040010 : Record 6085592;
      NewDocWord@1160040009 : Record 6085592;
      FileSystem@1160040008 : Codeunit 6085584;
      PDFMgt@1160040007 : Codeunit 6085608;
      TIFFMgt@1160040006 : Codeunit 6085609;
      NewTIFFilename@1160040005 : Text[1024];
      NewPDFFilename@1160040004 : Text[1024];
      NewFilePageCount@1160040002 : Integer;
    BEGIN
      DocumentsToCombine.FINDFIRST;
      NewDocument := DocumentsToCombine;
      NewDocument.SetSourceID('');
      NewDocument.CALCFIELDS("No. of Pages");
      NewFilePageCount := NewDocument."No. of Pages";

      NewTIFFilename := NewDocument.GetTIFFileName + '_temp';
      NewPDFFilename := NewDocument.GetPDFileName + '_temp';

      FileSystem.CopyFile(NewDocument.GetTIFFileName,NewTIFFilename);
      FileSystem.CopyFile(NewDocument.GetPDFileName,NewPDFFilename);

      DocumentsToCombine.NEXT;
      REPEAT
        Document := DocumentsToCombine;
        Document.CALCFIELDS("No. of Pages");

        TIFFMgt.Combine(NewTIFFilename,Document.GetTIFFileName,NewTIFFilename + '1');
        PDFMgt.Combine(NewPDFFilename,Document.GetPDFileName,NewPDFFilename + '1');

        FileSystem.DeleteFile(NewTIFFilename,FALSE);
        FileSystem.DeleteFile(NewPDFFilename,FALSE);

        FileSystem.MoveFile(NewTIFFilename + '1',NewTIFFilename);
        FileSystem.MoveFile(NewPDFFilename + '1',NewPDFFilename);


        DocPage.SETRANGE("Document No.",Document."No.");
        IF DocPage.FINDFIRST THEN
          REPEAT
            NewFilePageCount := NewFilePageCount + 1;

            NewDocPage := DocPage;
            NewDocPage."Document No." := NewDocument."No.";
            NewDocPage."Page No." := NewFilePageCount;
            NewDocPage.INSERT;

            DocWord.SETRANGE("Document No.",Document."No.");
            DocWord.SETRANGE("Page No.",DocPage."Page No.");
            IF DocWord.FINDFIRST THEN
              REPEAT
                NewDocWord := DocWord;

                NewDocWord."Document No." := NewDocument."No.";
                NewDocWord."Page No." := NewDocPage."Page No.";
                NewDocWord.INSERT;
              UNTIL DocWord.NEXT = 0;
          UNTIL DocPage.NEXT = 0;

        Document.DELETE(TRUE);
      UNTIL DocumentsToCombine.NEXT = 0;

      FileSystem.DeleteFile(NewDocument.GetTIFFileName,FALSE);
      FileSystem.DeleteFile(NewDocument.GetPDFileName,FALSE);

      FileSystem.MoveFile(NewTIFFilename,NewDocument.GetTIFFileName);
      FileSystem.MoveFile(NewPDFFilename,NewDocument.GetPDFileName);
    END;

    PROCEDURE Split@1160040001(Document@1160040000 : Record 6085590;SplitAtPage@1160040001 : Integer) NewDocNo : Code[20];
    BEGIN
    END;

    BEGIN
    END.
  }
}

