OBJECT Codeunit 5157893 OPplus Clearing
{
  OBJECT-PROPERTIES
  {
    Date=23.10.14;
    Time=13:00:45;
    Modified=Yes;
    Version List=OPP7.02.03 mit AZV,6.0,AR,P1121,HME1454;
  }
  PROPERTIES
  {
    Permissions=TableData 287=rm,
                TableData 288=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@5157851 : Record 98;
      OPplusSetup@5157847 : Record 5157802;
      OPplusPmtSetup@5157815 : Record 5157892;
      PmtProposal@5157816 : Record 5157895;
      PmtHead@5157814 : Record 5157896;
      PmtLine@5157846 : Record 5157897;
      PmtError@5157823 : Record 5157898;
      PaymentType@5157841 : Record 5157893;
      PmtPostingSetup@5157842 : Record 5157899;
      GenJnlLine@5157817 : Record 81;
      GenJnlTemplate@5157822 : Record 80;
      GenJnlBatch@5157828 : Record 232;
      GenJnlLine2@5157829 : Record 81;
      GenJnlLine3@5157858 : Record 81;
      AppLineDetails@5157845 : Record 5157804;
      BankAcc@5157855 : Record 270;
      CheckLedgerEntry@5157870 : Record 272;
      CustEntry@5157875 : Record 21;
      VendEntry@5157876 : Record 25;
      CustBankAccount@5157888 : Record 287;
      CompInfo@5157871 : Record 79;
      OPplusLicenseInfo@5157891 : Codeunit 5158000;
      DimMgt@5157830 : Codeunit 408;
      PmtTools@5157839 : Codeunit 5157892;
      NoSeriesMgt@5157844 : Codeunit 396;
      AdjustBalance@5157859 : Codeunit 407;
      CustEntryEdit@5157874 : Codeunit 103;
      VendEntryEdit@5157873 : Codeunit 113;
      TierMgt@5157880 : Codeunit 419;
      PmtFile@5157872 : File;
      PmtType@5157802 : ',Debit,Credit';
      FileName@5157826 : Text[1024];
      Line@5157805 : Text[128];
      Totals@5157821 : ARRAY [4] OF Decimal;
      PmtAmt@5157808 : Decimal;
      SumAmt@5157878 : Decimal;
      SumAmtLCY@5157879 : Decimal;
      Purposes@5157806 : Integer;
      DummyInt@1000000049 : BigInteger;
      Text001@5157804 : TextConst 'DEU=Kennung Lastschrift/Gutschrift fehlt.;ENU=Pmt. Type Credit/Debit is missing.';
      DummyStr@5157820 : Code[20];
      PmtTypeCode@5157809 : Code[5];
      Text002@5157810 : TextConst 'DEU=UngÅltige %1 in %2.;ENU=Invalid %1 in %2.';
      BankID@5157819 : Code[8];
      BankAccount@5157818 : Code[10];
      PartnerBankID@5157812 : Code[8];
      PartnerBankAccount@5157811 : Code[10];
      TestBank@5157852 : Code[20];
      Type@5157803 : Code[2];
      Text003@5157824 : TextConst 'DEU=Es sind noch Fehler in %1 %2 %3 %4.;ENU=There are Errors in %1 %2 %3 %4.';
      Text004@5157825 : TextConst 'DEU=%1 darf nicht %2 sein in %3 %4 %5 %6.;ENU=%1 must not be %2 in %3 %4 %5 %6.';
      Text005@5157827 : TextConst 'DEU=Die Datei %1 wurde erzeugt.;ENU=The File %1 has been created.';
      Text006@5157807 : TextConst 'DEU=Bei der Verarbeitung der Zahlung wurden Fehler festgestellt.;ENU=Errors have been encountered while processing the payment.';
      Text007@1000000002 : TextConst 'DEU=Beim Erstellen der SEPA-%1datei  ist ein Fehler ist aufgetreten.;ENU=An Error occurred creating the SEPA %1  File.';
      Text009@5157832 : TextConst 'DEU=Wollen Sie die Zahlungen annullieren?;ENU=Do you want to cancel the payments?';
      Text010@5157831 : TextConst 'DEU=Die Zahlungen wurden annulliert.;ENU=The payments have been canceled.';
      Text011@5157834 : TextConst 'DEU=Wollen Sie die Zahlungen erzeugen?;ENU=Do you want to create the payments?';
      Text012@5157833 : TextConst 'DEU=Die Zahlungen wurden erzeugt.;ENU=The payments have been created.';
      Text013@5157836 : TextConst 'DEU=Wollen Sie die Buchungen erzeugen?;ENU=Do you want to create the postings?';
      Text014@5157835 : TextConst 'DEU=Die Buchungen wurden erzeugt.;ENU=The postings have been created.';
      Text015@5157838 : TextConst 'DEU=Wollen Sie die Buchungen annullieren?;ENU=Do you want to cancel the postings?';
      Text016@5157837 : TextConst 'DEU=Die Buchungen wurden annulliert.;ENU=The postings have been canceled.';
      Text017@5157840 : TextConst 'DEU=Der Status muss %1, %2 oder %3 sein.;ENU=The status must be %1, %2 or %3.';
      Text018@5157843 : TextConst 'DEU=FÅr die %1 %2 wurde keine gÅltige %3 gefunden.;ENU=No valid %3 found for %1 %2.';
      Text019@5157848 : TextConst 'DEU=Wollen Sie eine neue SEPA Datei erzeugen?;ENU=Do you want to create a new SEPA file?';
      Text020@5157849 : TextConst 'DEU=Wollen Sie eine neue DTAUS Datei erzeugen?;ENU=Do you want to create a new DTAUS file?';
      Text021@5157850 : TextConst 'DEU=Es sind keine Zahlungen freigegeben.;ENU=There are no payments approved.';
      Text022@5157853 : TextConst 'DEU=In %1 %2 %3 sind verschiedene Auftraggeberbanken angesprochen. Bitte teilen Sie diesen zunÑchst auf.;ENU=In %1 %2 %3 various Order Bank Accounts are used. Please split first.';
      Text023@5157854 : TextConst 'DEU=Offen,Bearbeitet,Freigegeben,Bezahlt,Buchung in Bearbeitung,Gebucht;ENU=Open,Finished,Released,Payment Done,Posting in Process,Posted';
      Text024@5157813 : TextConst 'DEU="Kontonr. ";ENU="Acc. No. "';
      AccNo@5157856 : Text[20];
      Text025@5157857 : TextConst 'DEU=Wollen Sie die Zahlung auf die verschiedenen Banken aufteilen?;ENU=Do you want to split the payment onto the bank accounts?';
      Text026@5157860 : TextConst 'DEU=Zahldatei;ENU=Payment';
      Indent@5157864 : Boolean;
      FirstLine@5157877 : Boolean;
      FileCreated@5157881 : Boolean;
      IX@5157863 : Integer;
      CRLF@5157862 : Text[2];
      Stack@5157861 : ARRAY [30] OF Text[40];
      Text027@5157866 : TextConst 'DEU=Zeilen erstellen  #1#######   #2###### @3@@@@@@@@@@@@@;ENU=Creating lines    #1#######   #2###### @3@@@@@@@@@@@@@';
      Text028@5157865 : TextConst 'DEU=Buchungen erzeugen      #1###### @2@@@@@@@@@@@@@;ENU=Create posting lines         #1###### @2@@@@@@@@@@@@@';
      ActLineNo@5157868 : Integer;
      TotalLines@5157867 : Integer;
      Dlg@5157869 : Dialog;
      LicPermission@5157882 : Record 2000000043;
      AZVClearing@5157883 : Codeunit 5157894;
      Text030@5157884 : TextConst 'DEU=Wollen Sie eine neue %1 Datei erzeugen?;ENU=Create New %1 File?';
      Text031@5157885 : TextConst 'DEU=%1: Betrag ist zu gro·.;ENU=%1: Amount too large.';
      Text032@5157886 : TextConst 'DEU=Soll die Auftraggeberbank auf %1 geÑndert werden?;ENU=Do you want to change the orderer bank to %1?';
      Text033@5157887 : TextConst 'DEU=Sollen nun die Avise gedruckt werden?;ENU=Do you want to print payment advices?';
      Text61500@1000000000 : TextConst 'DEU=Begleitzettel drucken?;ENU=Print Payment Letter?';
      "******"@1000000001 : Integer;
      PmtLetter@1000000003 : Report 5157896;

    PROCEDURE Clearing@5157808(VAR _PmtProposal@5157802 : Record 5157895);
    VAR
      Successful@5157803 : Boolean;
      Avise@5157805 : Boolean;
      PaymentAvis@5157804 : Report 5157893;
      PmtProposal@5157806 : Record 5157895;
    BEGIN
      IF _PmtProposal.Status >= _PmtProposal.Status::"Payment Done" THEN
        ERROR(Text017,
          SELECTSTR(_PmtProposal.Status::Open+1,Text023),
          SELECTSTR(_PmtProposal.Status::Finished+1,Text023),
          SELECTSTR(_PmtProposal.Status::Released+1,Text023));
      IF CONFIRM(Text011) THEN BEGIN
        OPplusPmtSetup.GET;
        OPplusPmtSetup.TESTFIELD("Clearing Sender");
        CompInfo.GET;
        IF OPplusPmtSetup."Update Version" = '' THEN
          OPplusPmtSetup.Demand4Update;

        _PmtProposal.TESTFIELD("Bal. Account No.");
        // Check Errors
        PmtHead.RESET;
        PmtError.RESET;
        PmtHead.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
        PmtHead.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");
        PmtHead.SETFILTER("Payment Type Code",'<>%1',PmtTools.GetBlocked);
        IF _PmtProposal."Execution Date" <> 0D THEN BEGIN
          IF _PmtProposal."Execution Date" < TODAY THEN
            _PmtProposal."Execution Date" := TODAY;
        END;
        Avise := FALSE;
        IF PmtHead.FINDSET(FALSE,FALSE) THEN BEGIN
          TestBank := PmtHead."Bal. Account No.";
          REPEAT
            IF PmtHead."Print Payment Advice" THEN
              Avise := TRUE;
            PmtError.SETRANGE("Journal Template Name",PmtHead."Gen. Journal Template");
            PmtError.SETRANGE("Journal Batch Name",PmtHead."Gen. Journal Batch");
            PmtError.SETRANGE("Journal Line No.",PmtHead."Gen. Journal Line");
            PmtError.SETRANGE("Warning Message",FALSE);
            IF PmtHead."Bal. Account No." <> TestBank THEN
              ERROR(Text022,PmtHead.TABLECAPTION,PmtHead."Gen. Journal Template",PmtHead."Gen. Journal Batch");
            IF NOT PmtError.ISEMPTY THEN
              ERROR(Text003,PmtHead.TABLECAPTION,PmtHead."Gen. Journal Template",
                PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
          UNTIL PmtHead.NEXT = 0;
          IF TestBank <> _PmtProposal."Bal. Account No." THEN
            IF CONFIRM(Text032,FALSE,TestBank) THEN
              _PmtProposal.VALIDATE("Bal. Account No.",TestBank)
            ELSE
              _PmtProposal.VALIDATE("Bal. Account No.");

        END ELSE
          ERROR(Text021);

        FileCreated := FALSE;
        Successful := Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepa);
        IF Successful THEN
          Successful := Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepaDD);
        IF Successful THEN
          Successful := Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepaB2B);
        IF Successful THEN
          Successful := Create_DTAUS(_PmtProposal);
        IF Successful THEN
          Successful := Create_MT10n(_PmtProposal,PmtTools.GetMT101,TRUE);
        IF Successful THEN
          Successful := Create_MT10n(_PmtProposal,PmtTools.GetMT103,TRUE);
        IF Successful THEN
          Successful := Create_MT10n(_PmtProposal,PmtTools.GetMT104,TRUE);

        // gbedv AZV ------------------------------------------------- BEGIN
        IF LicPermission.GET(LicPermission."Object Type"::Table,DATABASE::"OPplus AZV Pmt. Setup") THEN
          IF (LicPermission."Execute Permission" = LicPermission."Execute Permission"::Yes) THEN
            IF Successful THEN
              Successful := AZVClearing.Create_AZV(_PmtProposal);
        // gbedv AZV ------------------------------------------------- END
        IF NOT Successful THEN
          MESSAGE(Text006)
        ELSE BEGIN
          // Schecks etc. erfolgreich setzen
          PmtHead.RESET;
          PmtHead.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
          PmtHead.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");
          IF PmtHead.FIND('-') THEN
            REPEAT
              IF PaymentType.Code <> PmtHead."Payment Type Code" THEN
                PaymentType.GET(PmtHead."Payment Type Code");
              IF NOT PaymentType."Create File" THEN BEGIN
                PmtHead.CALCFIELDS("Payment Amount");
                IF (PmtHead."Payment Amount" <> 0) OR (PaymentType.Code IN [PmtTools.GetApplic1,PmtTools.GetApplic2]) THEN BEGIN
                  PmtHead.VALIDATE("Payment Done",TRUE);
                  PmtHead.MODIFY;
                END;
              END;
            UNTIL PmtHead.NEXT = 0;

          // Zahlungsvorschlag erfolgreich setzen
          _PmtProposal.VALIDATE(Status,_PmtProposal.Status::"Payment Done");
          _PmtProposal.MODIFY(TRUE);

          IF NOT FileCreated THEN
            MESSAGE(Text012);
          COMMIT;
          IF Avise THEN
            IF CONFIRM(Text033,FALSE) THEN BEGIN
              PmtProposal.SETRANGE("Journal Template Name",_PmtProposal."Journal Template Name");
              PmtProposal.SETRANGE("Journal Batch Name",_PmtProposal."Journal Batch Name");
              PmtHead.SETRANGE("Print Payment Advice",TRUE);
              PaymentAvis.SETTABLEVIEW(PmtProposal);
              PaymentAvis.SETTABLEVIEW(PmtHead);
              PaymentAvis.RUNMODAL;
              CLEAR(PaymentAvis);
            END;
        END;
        //A/gob-czi/P1121/151013
        IF CONFIRM(Text61500,TRUE) THEN BEGIN
          PmtProposal.SETRANGE("Journal Template Name",_PmtProposal."Journal Template Name");
          PmtProposal.SETRANGE("Journal Batch Name",_PmtProposal."Journal Batch Name");
          PmtLetter.SETTABLEVIEW(PmtProposal);
          PmtLetter.USEREQUESTFORM := FALSE;
          PmtLetter.RUNMODAL;
          CLEAR(PmtLetter);
        END;
        //E/gob-czi/P1121/151013

      END;
    END;

    PROCEDURE Repeat_SEPA@5157812(VAR _PmtProposal@5157802 : Record 5157895);
    VAR
      Successful@5157803 : Boolean;
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::Posted);
      IF _PmtProposal."Execution Date" <> 0D THEN BEGIN
        IF _PmtProposal."Execution Date" < TODAY THEN
          _PmtProposal."Execution Date" := TODAY;
        _PmtProposal.MODIFY;
      END;
      IF CONFIRM(Text019) THEN BEGIN
        OPplusPmtSetup.GET;
        OPplusPmtSetup.TESTFIELD("Clearing Sender");
        Successful := Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepa);
        IF Successful THEN
          Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepaDD);
        IF Successful THEN
          Export_SEPA_XML_New(_PmtProposal,PmtTools.GetSepaB2B);
        IF NOT Successful THEN
          MESSAGE(Text006);
      END;
    END;

    PROCEDURE Repeat_DTAUS@5157813(VAR _PmtProposal@5157802 : Record 5157895);
    VAR
      Successful@5157803 : Boolean;
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::Posted);
      IF _PmtProposal."Execution Date" <> 0D THEN BEGIN
        IF _PmtProposal."Execution Date" < TODAY THEN
          _PmtProposal."Execution Date" := TODAY;
          _PmtProposal.MODIFY;
      END;
      IF CONFIRM(Text020) THEN BEGIN
        OPplusPmtSetup.GET;
        OPplusPmtSetup.TESTFIELD("Clearing Sender");
        Successful := Create_DTAUS(_PmtProposal);
        IF NOT Successful THEN
          MESSAGE(Text006);
      END;
    END;

    PROCEDURE Create_DTAUS@4(VAR _PmtProposal@1000000001 : Record 5157895) : Boolean;
    VAR
      SilentFileName@5157804 : Text[1024];
    BEGIN
      PmtProposal := _PmtProposal;
      IF (PmtProposal.Total0405 = 0) AND (PmtProposal.Total51 = 0) THEN
        EXIT(TRUE);

      PmtProposal.TESTFIELD("Bal. Account Type",PmtProposal."Bal. Account Type"::"Bank Account");
      BankAcc.GET(PmtProposal."Bal. Account No.");

      IF (PmtProposal.Total0405 <> 0) THEN BEGIN
        // Datei îffnen
        PmtHead.RESET;
        PmtHead.SETRANGE("Gen. Journal Template",PmtProposal."Journal Template Name");
        PmtHead.SETRANGE("Gen. Journal Batch",PmtProposal."Journal Batch Name");
        PmtHead.SETFILTER("Payment Type Code",'%1|%2',PmtTools.GetTX04,PmtTools.GetTX05);
        PmtHead.SETFILTER("Payment Amount",'<>0');

        IF PmtHead.FINDFIRST THEN;
        IF NOT PmtHead.ISEMPTY THEN BEGIN
          PaymentType.GET(PmtHead."Payment Type Code");
          FileName := PmtTools.GetFileName(BankAcc,PaymentType,PmtProposal."Journal Batch Name",'');
          TotalLines := PmtHead.COUNT;
          ActLineNo := 0;
          PmtType := PmtType::Debit;
          IF NOT ISSERVICETIER THEN BEGIN
            PmtFile.QUERYREPLACE(FALSE);
            SilentFileName := FileName;
          END ELSE
            SilentFileName := TierMgt.ServerTempFileName('',DELCHR(PaymentType."File Supplement",'<>','.'));

          Dlg.OPEN(Text027);
          Dlg.UPDATE(1,PmtHead.GETFILTER("Payment Type Code"));
          PmtFile.TEXTMODE(FALSE);
          PmtFile.WRITEMODE(TRUE);
          PmtFile.CREATE(SilentFileName);
          Create_DTAUSFile;
          PmtFile.CLOSE();
          Dlg.CLOSE();
          IF ISSERVICETIER THEN
            TierMgt.DownloadToFile(SilentFileName,FileName);
          MESSAGE(Text005,FileName);
        END;
      END;
      IF (PmtProposal.Total51 <> 0) THEN BEGIN
        PaymentType.GET(PmtTools.GetTX51);
        FileName := PmtTools.GetFileName(BankAcc,PaymentType,PmtProposal."Journal Batch Name",'');

        // Datei îffnen
        PmtHead.SETRANGE("Gen. Journal Template",PmtProposal."Journal Template Name");
        PmtHead.SETRANGE("Gen. Journal Batch",PmtProposal."Journal Batch Name");
        PmtHead.SETRANGE("Payment Type Code",PmtTools.GetTX51());
        PmtHead.SETFILTER("Payment Amount",'<>0');
        IF NOT PmtHead.ISEMPTY THEN BEGIN
          TotalLines := PmtHead.COUNT;
          ActLineNo := 0;
          PmtType := PmtType::Credit;
          IF NOT ISSERVICETIER THEN BEGIN
            PmtFile.QUERYREPLACE(FALSE);
            SilentFileName := FileName;
          END ELSE
            SilentFileName := TierMgt.ServerTempFileName('',DELCHR(PaymentType."File Supplement",'<>','.'));
          Dlg.OPEN(Text027);
          PmtFile.TEXTMODE(FALSE);
          PmtFile.WRITEMODE(TRUE);
          PmtFile.CREATE(SilentFileName);
          Create_DTAUSFile;
          PmtFile.CLOSE();
          Dlg.CLOSE();
          IF ISSERVICETIER THEN
            TierMgt.DownloadToFile(SilentFileName,FileName);
          MESSAGE(Text005,FileName);
        END;
      END;
      FileCreated := TRUE;
      EXIT(TRUE);
    END;

    PROCEDURE Create_DTAUSFile@1();
    BEGIN
      Totals[1] := 0;
      Totals[2] := 0;
      Totals[3] := 0;
      Totals[4] := 0;

      // Dateikopf
      PmtHead.FIND('-');
      TypeA;

      // Datei ZahlungssÑtze
      REPEAT
        WITH PmtHead DO BEGIN
          ActLineNo += 1;
          Dlg.UPDATE(2,ActLineNo);
          Dlg.UPDATE(3,ROUND(ActLineNo / TotalLines * 10000,1));
          TypeC1();
          TypeC2("Reason Row 1","Reason Row 2");
          IF Purposes > 2 THEN
            TypeC3("Reason Row 3","Reason Row 4","Reason Row 5","Reason Row 6");
          IF Purposes > 6 THEN
            TypeC3("Reason Row 7","Reason Row 8","Reason Row 9","Reason Row 10");
          IF Purposes > 10 THEN
            TypeC3("Reason Row 11","Reason Row 12","Reason Row 13",'');
        END;
        PmtHead."Payment Done" := TRUE;
        PmtHead.MODIFY;
      UNTIL PmtHead.NEXT = 0;

      // DateiEnde
      TypeE;
    END;

    PROCEDURE TypeA@5157802();
    VAR
      ExecDate@5157802 : Date;
    BEGIN
      IF PmtType = PmtType::Debit THEN
        Type := 'LK'
      ELSE IF PmtType = PmtType::Credit THEN
        Type := 'GK'
      ELSE
        ERROR(Text001);

      BankID := PmtTools.Clean_Number(PmtProposal."Bank Branch Code");
      IF STRLEN(BankID) <> 8 THEN
        ERROR(Text002,PmtProposal.FIELDCAPTION("Bank Branch Code"),PmtProposal.TABLECAPTION);

      DummyStr := PmtTools.Clean_Number(PmtProposal."Bank Account No.");
      IF STRLEN(DummyStr) > 9 THEN
        BankAccount := CONVERTSTR(FORMAT(DummyStr,10),' ','0')    // 10-stellig fÅhrende Nullen
      ELSE BEGIN
        EVALUATE(DummyInt,DummyStr);
        BankAccount := CONVERTSTR(FORMAT(DummyInt,10),' ','0');  // 10-stellig fÅhrende Nullen
      END;

      IF PmtProposal."Execution Date" <> 0D THEN
        ExecDate := PmtProposal."Execution Date"
      ELSE
        ExecDate := PmtProposal."Posting Date";

      Line :=
        '0128A' +
        Type +
        BankID +
        PADSTR('',8,'0') +
        PmtTools.DTAUS_String(OPplusPmtSetup."Clearing Sender",27) +
        FORMAT(TODAY,6,'<Day,2><Month,2><Year,2>') +
        PADSTR('',4) +
        BankAccount +
        PADSTR('',10,'0') +
        PADSTR('',15,' ') +
        FORMAT(ExecDate,8,'<Day,2><Month,2><Year4,4>') +
        PADSTR('',24) +
        '1';

      WriteLine(Line);
    END;

    PROCEDURE TypeC1@3();
    VAR
      AccountName@5157802 : Text[150];
    BEGIN
      PmtHead.CALCFIELDS("Payment Amount");
      PmtAmt := PmtHead."Payment Amount";

      IF PmtAmt = 0 THEN
        ERROR(Text004,
          PmtHead.FIELDCAPTION("Payment Amount"),
          '= 0',
          PmtHead.TABLECAPTION,
          PmtHead."Gen. Journal Template",
          PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");

      IF (PmtHead."Reason Row 1" = '') AND (PmtHead."Reason Row 2" = '') THEN
        ERROR(Text004,
          PmtHead.FIELDCAPTION("Reason Row 1"),
          '<>',
          PmtHead.TABLECAPTION,
          PmtHead."Gen. Journal Template",
          PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");


      CASE PmtHead."Payment Type Code" OF
        PmtTools.GetTX04, PmtTools.GetTX05:
          BEGIN
            IF PmtAmt < 0 THEN
              ERROR(Text004,
                PmtHead.FIELDCAPTION("Payment Amount"),
                '< 0',
                PmtHead.TABLECAPTION,
                PmtHead."Gen. Journal Template",
                PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
          END;
        PmtTools.GetTX51:
          BEGIN
            IF PmtAmt > 0 THEN
              ERROR(Text004,
                PmtHead.FIELDCAPTION("Payment Amount"),
                '> 0',
                PmtHead.TABLECAPTION,
                PmtHead."Gen. Journal Template",
                PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
          END;
      END;

      // Fehler prÅfen
      PmtError.RESET;
      PmtError.SETRANGE("Journal Template Name",PmtHead."Gen. Journal Template");
      PmtError.SETRANGE("Journal Batch Name",PmtHead."Gen. Journal Batch");
      PmtError.SETRANGE("Journal Line No.",PmtHead."Gen. Journal Line");
      PmtError.SETRANGE("Warning Message",FALSE);
      IF NOT PmtError.ISEMPTY THEN
        ERROR(Text003,PmtHead.TABLECAPTION,PmtHead."Gen. Journal Template",
          PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");

      // Verwendungszwecke zÑhlen
      CountPurpose;

      Totals[1] := Totals[1] + 1;
      Totals[2] := Totals[2] + (100 * ABS(PmtAmt));   // Betrag

      CASE PmtHead."Payment Type Code" OF
        PmtTools.GetTX04: PmtTypeCode := '04000';
        PmtTools.GetTX05: PmtTypeCode := '05000';
        PmtTools.GetTX51: PmtTypeCode := '51000';
      END;

      // Zahlungspflichtige BLZ + Konto
      PartnerBankID := PmtTools.Clean_Number(PmtHead."Bank Branch No.");
      IF STRLEN(PartnerBankID) <> 8 THEN
        ERROR(Text002,PmtHead.FIELDCAPTION("Bank Branch No."),PmtHead.TABLECAPTION);

      EVALUATE(DummyInt,PartnerBankID);
      Totals[4] := Totals[4] + DummyInt;  // Blz

      DummyStr := PmtTools.Clean_Number(PmtHead."Bank Account No.");
      IF STRLEN(DummyStr) > 9 THEN
        PartnerBankAccount := CONVERTSTR(FORMAT(DummyStr,10),' ','0')    // 10-stellig fÅhrende Nullen
      ELSE BEGIN
        EVALUATE(DummyInt,DummyStr);
        PartnerBankAccount := CONVERTSTR(FORMAT(DummyInt,10),' ','0');   // 10-stellig fÅhrende Nullen
      END;
      EVALUATE(DummyInt,PartnerBankAccount);
      Totals[3] := Totals[3] + DummyInt;  // Konto

      AccountName := PmtTools.DTAUS_String(PmtHead.Name + ' ' + PmtHead.City,27);
      IF PmtHead."Bank Account Owner" <> '' THEN
        AccountName := PmtTools.DTAUS_String(PmtHead."Bank Account Owner",27);

      Line :=
        CONVERTSTR(FORMAT(187 + (Purposes * 29 ),4,'<Integer>'),' ','0') +
        'C' +
        PADSTR('',8,'0') +
        PartnerBankID +
        PartnerBankAccount +
        PADSTR('',13,'0') +
        PmtTypeCode +
        PADSTR('',1) +
        PADSTR('',11,'0') +
        BankID +
        BankAccount +
        CONVERTSTR(FORMAT(ABS(PmtAmt)*100,11,'<Integer>'),' ','0') +
        PADSTR('',3) +
        PADSTR(AccountName,27) +
        PADSTR('',8) ;

      WriteLine(Line);
    END;

    PROCEDURE TypeC2@5157806(Info1@5157804 : Text[50];Info2@5157805 : Text[50]);
    BEGIN
      // Info zu Erweiterungsteilen
      AccNo := PmtHead."Account No.";
      IF PmtHead."Account Type" = PmtHead."Account Type"::Vendor THEN
        IF PmtHead."Our Account No." <> '' THEN
          AccNo := PmtHead."Our Account No.";

      Line :=
        PmtTools.DTAUS_String(OPplusPmtSetup."Clearing Sender",27) +
        PmtTools.DTAUS_String(Text024 + AccNo,27) +
        '1' +
        PADSTR('', 2) +
        CONVERTSTR(FORMAT(Purposes,2,'<Integer>'),' ','0');

      IF Purposes > 0 THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info1,27);

      IF Purposes > 1 THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info2,27);

      Line := PADSTR(Line,128);

      WriteLine(Line);
    END;

    PROCEDURE TypeC3@25(Info1@5157802 : Text[50];Info2@5157803 : Text[50];Info3@5157804 : Text[50];Info4@5157805 : Text[50]);
    BEGIN
      Line := '';
      IF Info1 <> '' THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info1,27);
      IF Info2 <> '' THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info2,27);
      IF Info3 <> '' THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info3,27);
      IF Info4 <> '' THEN
        Line := Line + '02' + PmtTools.DTAUS_String(Info4,27);

      Line := PADSTR(Line,128);
      WriteLine(Line);
    END;

    PROCEDURE TypeE@5157803();
    BEGIN
      Line :=
        '0128E' +
        PADSTR('', 5,' ') +
        CONVERTSTR(FORMAT(Totals[1],7,'<Integer>'),' ','0') +  // Anzahl SÑtze C
        '0000000000000' +
        CONVERTSTR(FORMAT(Totals[3],17,'<Integer>'),' ','0') +  // Summe der Kontonr
        CONVERTSTR(FORMAT(Totals[4],17,'<Integer>'),' ','0') +  // Summe der BLZ
        CONVERTSTR(FORMAT(Totals[2],13,'<Integer>'),' ','0') +  // Summe der BetrÑge
        PADSTR('',51,' ');

      WriteLine(Line);
    END;

    PROCEDURE CountPurpose@5157804();
    BEGIN
      Purposes := 13;
      IF PmtHead."Reason Row 13" = '' THEN
        Purposes := 12;
      IF PmtHead."Reason Row 12" = '' THEN
        Purposes := 11;
      IF PmtHead."Reason Row 11" = '' THEN
        Purposes := 10;
      IF PmtHead."Reason Row 10" = '' THEN
        Purposes := 9;
      IF PmtHead."Reason Row 9" = '' THEN
        Purposes := 8;
      IF PmtHead."Reason Row 8" = '' THEN
        Purposes := 7;
      IF PmtHead."Reason Row 7" = '' THEN
        Purposes := 6;
      IF PmtHead."Reason Row 6" = '' THEN
        Purposes := 5;
      IF PmtHead."Reason Row 5" = '' THEN
        Purposes := 4;
      IF PmtHead."Reason Row 4" = '' THEN
        Purposes := 3;
      IF PmtHead."Reason Row 3" = '' THEN
        Purposes := 2;
      IF PmtHead."Reason Row 2" = '' THEN
        Purposes := 1;
    END;

    PROCEDURE WriteLine@5157805(TextLine@5157802 : Text[256]);
    VAR
      i@5157803 : Integer;
      j@5157804 : Integer;
    BEGIN
      j := STRLEN(TextLine);
      FOR i := 1 TO j DO
        PmtFile.WRITE(TextLine[i]);
    END;

    PROCEDURE PostPayment@5157810(VAR _PmtProposal@5157802 : Record 5157895);
    VAR
      LedgEntryDim@5157808 : Record 355;
      JnlLineDim@5157807 : Record 356;
      TempJnlLineDim@5157806 : TEMPORARY Record 356;
      TempJnlLineDim3@5157805 : TEMPORARY Record 356;
      CurrExchRate@5157810 : Record 330;
      BankAccMandate@5157803 : Record 5157913;
      LineNo@5157804 : Integer;
    BEGIN
      IF NOT CONFIRM(Text013,TRUE) THEN
        EXIT;

      GLSetup.GET;
      IF OPplusLicenseInfo.IsBasics THEN
        OPplusSetup.GET;
      OPplusPmtSetup.GET;
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::"Payment Done");
      PmtProposal := _PmtProposal;
      BankAcc.RESET;

      OPplusPmtSetup.TESTFIELD("Pmt. File No. Series");

      PmtHead.RESET;
      PmtHead.SETCURRENTKEY("Gen. Journal Template","Gen. Journal Batch","Payment Type Code");
      PmtHead.SETRANGE("Gen. Journal Template",PmtProposal."Journal Template Name");
      PmtHead.SETRANGE("Gen. Journal Batch",PmtProposal."Journal Batch Name");
      PmtHead.SETRANGE("Payment Type Code",PmtTools.GetBlocked);
      IF NOT PmtHead.ISEMPTY THEN
        PmtHead.DELETEALL(TRUE);

      PmtHead.SETRANGE("Payment Type Code");
      PmtHead.SETRANGE("Payment Done",FALSE);
      IF NOT PmtHead.ISEMPTY THEN
        PmtHead.DELETEALL(TRUE);

      PmtHead.SETRANGE("Payment Done",TRUE);
      IF PmtHead.FINDFIRST THEN BEGIN
        GenJnlTemplate.GET(PmtHead."Gen. Journal Template");
        GenJnlBatch.INIT;
        GenJnlBatch."Journal Template Name" := PmtHead."Gen. Journal Template";
        GenJnlBatch.Name := PmtHead."Gen. Journal Batch";
        GenJnlBatch."Reason Code" := GenJnlTemplate."Reason Code";
        // gbedv CLR -------------------------------------------------- BEGIN
        GenJnlBatch."Transfer Posting" := OPplusPmtSetup."Transfer Posting";
        // gbedv CLR -------------------------------------------------- END
        IF GenJnlBatch.INSERT THEN;
        LineNo := 0;
        GenJnlLine.RESET;
        GenJnlLine.SETRANGE("Journal Template Name",PmtHead."Gen. Journal Template");
        GenJnlLine.SETRANGE("Journal Batch Name",PmtHead."Gen. Journal Batch");
        GenJnlLine.SETRANGE(Amount,0);
        IF NOT GenJnlLine.ISEMPTY THEN
          GenJnlLine.DELETEALL(TRUE);
        GenJnlLine.RESET;
      END;

      IF OPplusLicenseInfo.IsBasics THEN BEGIN
        AppLineDetails.RESET;
        AppLineDetails.SETRANGE("Journal Template Name",_PmtProposal."Journal Template Name");
        AppLineDetails.SETRANGE("Journal Batch Name",_PmtProposal."Journal Batch Name");
        AppLineDetails.DELETEALL;
      END;

      Dlg.OPEN(Text028);
      ActLineNo := 0;
      PmtLine.RESET;
      PmtLine.SETRANGE("Journal Template Name",PmtProposal."Journal Template Name");
      PmtLine.SETRANGE("Journal Batch Name",PmtProposal."Journal Batch Name");
      TotalLines := PmtLine.COUNT;

      PaymentType.RESET;
      IF PaymentType.FIND('-') THEN
        REPEAT
          PmtHead.SETRANGE("Payment Type Code",PaymentType.Code);

          IF PmtHead.FIND('-') THEN BEGIN
      //H1454 23.10.14 ARI +++++++++++++++++++++++++++
            IF NOT PmtPostingSetup.GET(PaymentType.Code,_PmtProposal."Bal. Account No.",PmtHead."Payment Method Code") THEN
      //H1454 23.10.14 ARI ---------------------------
              IF NOT PmtPostingSetup.GET(PaymentType.Code,'') THEN
                ERROR(Text018,PaymentType.TABLECAPTION,PaymentType.Code,PmtPostingSetup.TABLECAPTION);

            IF PmtProposal."Bal. Account Type" = PmtProposal."Bal. Account Type"::"G/L Account" THEN BEGIN
              PmtPostingSetup."Posting Account Type" := PmtProposal."Bal. Account Type";
              PmtPostingSetup."Posting Account No." := PmtProposal."Bal. Account No.";
            END;

            PmtPostingSetup.TESTFIELD("Posting Account No.");

            IF PaymentType.Code IN [PmtTools.GetCheck(),PmtTools.GetEURCheck()] THEN
              PmtPostingSetup."One Bal. Acc. per Pmt. Head" := TRUE;

            IF NOT PmtPostingSetup."One Bal. Acc. per Pmt. Line" AND NOT
                   PmtPostingSetup."One Bal. Acc. per Pmt. Head" AND NOT
                   PaymentType."Separate Currency"
            THEN BEGIN
              GenJnlLine2.INIT;
              GenJnlLine2."Journal Template Name" := PmtHead."Gen. Journal Template";
              GenJnlLine2."Journal Batch Name" := PmtHead."Gen. Journal Batch";
              // gbedv CLR -------------------------------------------------- BEGIN
              GenJnlLine2."Transit Entry" := OPplusPmtSetup."Transfer Posting";
              // gbedv CLR -------------------------------------------------- END

              //H0882 21.02.14 ARI +++++++++++++++++++++++++++++
              GenJnlLine."Payment Direction" := GenJnlLine."Payment Direction"::outbound;
              //H0882 21.02.14 ARI -----------------------------

              GenJnlLine2."Account Type" := _PmtProposal."Bal. Account Type";
              GenJnlLine2."PIP Account Type" := GenJnlLine2."PIP Account Type"::Account;
              GenJnlLine2."Source Code" := GenJnlTemplate."Source Code";
              GenJnlLine2."Reason Code" := GenJnlBatch."Reason Code";
              GenJnlLine2."Document Type" := GenJnlLine2."Document Type"::Payment;
              GenJnlLine2."Posting Date" := _PmtProposal."Posting Date";
              GenJnlLine2.VALIDATE("Currency Code",PmtHead."Pmt. Currency Code");
              GenJnlLine2."Document No." :=
                NoSeriesMgt.GetNextNo(OPplusPmtSetup."Pmt. File No. Series",_PmtProposal."Posting Date",TRUE);
            END;

            REPEAT
              // Eine Belegnr. pro Scheck
              IF PaymentType."Separate Currency" OR
                 PmtPostingSetup."One Bal. Acc. per Pmt. Head"
              THEN BEGIN
                GenJnlLine2.INIT;
                GenJnlLine2."Journal Template Name" := PmtHead."Gen. Journal Template";
                GenJnlLine2."Journal Batch Name" := PmtHead."Gen. Journal Batch";
                // gbedv CLR -------------------------------------------------- BEGIN
                GenJnlLine2."Transit Entry" := OPplusPmtSetup."Transfer Posting";
                // gbedv CLR -------------------------------------------------- END
                //H0882 21.02.14 ARI +++++++++++++++++++++++++++++
                GenJnlLine."Payment Direction" := GenJnlLine."Payment Direction"::outbound;
                //H0882 21.02.14 ARI -----------------------------
                GenJnlLine2."PIP Account Type" := GenJnlLine2."PIP Account Type"::Account;
                GenJnlLine2."Account Type" := _PmtProposal."Bal. Account Type";
                GenJnlLine2."Source Code" := GenJnlTemplate."Source Code";
                GenJnlLine2."Reason Code" := GenJnlBatch."Reason Code";
                GenJnlLine2."Document Type" := GenJnlLine2."Document Type"::Payment;
                GenJnlLine2."Posting Date" := _PmtProposal."Posting Date";
                IF PaymentType.Code IN [PmtTools.GetCheck(), PmtTools.GetEURCheck()] THEN BEGIN
                  IF BankAcc."No." <> _PmtProposal."Bal. Account No." THEN BEGIN
                    BankAcc.GET(_PmtProposal."Bal. Account No.");
                    BankAcc.TESTFIELD(Blocked,FALSE);
                  END;
                  BankAcc.TESTFIELD("Last Check No.");
                  BankAcc."Last Check No." := INCSTR(BankAcc."Last Check No.");
                  BankAcc.MODIFY;
                  GenJnlLine2."Document No." := BankAcc."Last Check No.";
                  IF PmtPostingSetup."Posting Account Type" = PmtPostingSetup."Posting Account Type"::"Bank Account" THEN
                    GenJnlLine2."Bank Payment Type" := GenJnlLine2."Bank Payment Type"::"Computer Check";
                END ELSE BEGIN
                  GenJnlLine2."Document No." :=
                    NoSeriesMgt.GetNextNo(OPplusPmtSetup."Pmt. File No. Series",_PmtProposal."Posting Date",TRUE);
                END;
              END;
              PmtHead.CALCFIELDS("Payment Amount","Pmt. Amount (LCY)");
              PmtHead.Posted := TRUE;
              PmtHead."Document No." := GenJnlLine2."Document No.";
              PmtHead."Posting Date" := _PmtProposal."Posting Date";
              IF (PmtHead."Payment Type Code" IN [PmtTools.GetSepaDD,PmtTools.GetSepaB2B]) AND NOT
                 (PmtHead."Single Payment")
              THEN
                IF PmtHead."Mandate ID" <> '' THEN BEGIN
                  BankAccMandate.GET(PmtHead."Mandate ID");
                  IF (BankAccMandate."Mandate Frequency" = BankAccMandate."Mandate Frequency"::"Recurrent Payment") AND
                     (BankAccMandate."Mandate Status" = BankAccMandate."Mandate Status"::FRST)
                  THEN
                    BankAccMandate."Mandate Status" := BankAccMandate."Mandate Status"::RCUR;
                  BankAccMandate."Mandate Last Used" := _PmtProposal."Execution Date";
                  BankAccMandate.MODIFY(TRUE);
                END;
              PmtHead.MODIFY;
              PmtLine.SETRANGE("Journal Template Name",PmtHead."Gen. Journal Template");
              PmtLine.SETRANGE("Journal Batch Name",PmtHead."Gen. Journal Batch");
              PmtLine.SETRANGE("Journal Line No.",PmtHead."Gen. Journal Line");
              PmtLine.SETRANGE("Posting Applied Amount",0);
              PmtLine.SETRANGE("Posting Payment Discount",0);
              IF NOT PmtLine.ISEMPTY THEN
                PmtLine.DELETEALL;
              PmtLine.SETRANGE("Posting Applied Amount");
              PmtLine.SETRANGE("Posting Payment Discount");
              PmtLine.FINDSET(FALSE,FALSE);
              FirstLine := TRUE;
              SumAmt := 0;
              SumAmtLCY := 0;
              REPEAT
                ActLineNo += 1;
                Dlg.UPDATE(1,ActLineNo);
                Dlg.UPDATE(2,ROUND(ActLineNo / TotalLines * 10000,1));

                IF FirstLine OR NOT _PmtProposal."Summarize Per Pmt. Head" THEN BEGIN
                  LineNo := LineNo + 10000;
                  GenJnlLine.INIT;
                  FirstLine := FALSE;
                END;
                IF _PmtProposal."Summarize Per Pmt. Head" THEN BEGIN
                  PmtLine.TESTFIELD("Account Type",PmtHead."Account Type");
                  PmtLine.TESTFIELD("Account No.",PmtHead."Account No.");
                  PmtLine.TESTFIELD("Pmt. Currency Code",PmtHead."Pmt. Currency Code");
                END;
                GenJnlLine."Journal Template Name" := PmtHead."Gen. Journal Template";
                GenJnlLine."Journal Batch Name" := PmtHead."Gen. Journal Batch";
                // gbedv CLR -------------------------------------------------- BEGIN
                GenJnlLine."Transit Entry" := OPplusPmtSetup."Transfer Posting";
                // gbedv CLR -------------------------------------------------- END
                //H0882 21.02.14 ARI +++++++++++++++++++++++++++++
                GenJnlLine."Payment Direction" := GenJnlLine."Payment Direction"::outbound;
                //H0882 21.02.14 ARI -----------------------------
                GenJnlLine."Line No." := LineNo;
                GenJnlLine."Source Code" := GenJnlTemplate."Source Code";
                GenJnlLine."Reason Code" := GenJnlBatch."Reason Code";
                GenJnlLine."Document Type" := GenJnlLine."Document Type"::Payment;
                IF PaymentType."Separate Currency" THEN
                  GenJnlLine."Document No." := GenJnlLine2."Document No."
                ELSE IF NOT PmtPostingSetup."One Bal. Acc. per Pmt. Line" THEN
                  GenJnlLine."Document No." := GenJnlLine2."Document No."
                ELSE
                  GenJnlLine."Document No." :=
                    NoSeriesMgt.GetNextNo(OPplusPmtSetup."Pmt. File No. Series",_PmtProposal."Posting Date",TRUE);
                GenJnlLine."Account Type" := PmtLine."Account Type";
                GenJnlLine.VALIDATE("Posting Date",_PmtProposal."Posting Date");
                GenJnlLine.VALIDATE("Account No.",PmtLine."Account No.");
                GenJnlLine.VALIDATE("Currency Code",PmtLine."Pmt. Currency Code");
                GenJnlLine.VALIDATE("VAT Prod. Posting Group");
                GenJnlLine.VALIDATE("Payment Terms Code");
                GenJnlLine.VALIDATE("Bal. VAT Prod. Posting Group");
                GenJnlLine."Mandate ID" := PmtHead."Mandate ID";
                GenJnlLine."Payment Bank Code" := PmtHead."Bank Code";
                IF NOT _PmtProposal."Summarize Per Pmt. Head" THEN BEGIN
                  GenJnlLine."Applies-to Doc. Type" := PmtLine."Applies-to Doc. Type";
                  GenJnlLine."Applies-to Doc. No." := PmtLine."Applies-to Doc. No.";
                  GenJnlLine."ID Applied-Entry" := PmtLine."ID Applied-Entry";
                  GenJnlLine.VALIDATE(Amount,-PmtLine."Posting Applied Amount");
                  GenJnlLine.VALIDATE("Amount (LCY)",-PmtLine."Pmt. Amount (LCY)");
                  GenJnlLine."Posting Payment Discount" := -PmtLine."Posting Payment Discount";
                  GenJnlLine."Applies-to ID" := '';
                  GenJnlLine."External Document No." := PmtLine."External Document No.";
                  GenJnlLine."Payment Method Code" := PmtLine."Orig. Payment Method";
                END ELSE BEGIN
                  SumAmt := SumAmt - PmtLine."Posting Applied Amount";
                  SumAmtLCY := SumAmtLCY - PmtLine."Pmt. Amount (LCY)";
                  IF PmtLine."ID Applied-Entry" > 0 THEN BEGIN
                    IF PmtLine."Account Type" = PmtLine."Account Type"::Customer THEN BEGIN
                      CustEntry.GET(PmtLine."ID Applied-Entry");
                      CustEntry."Amount to Apply" := PmtLine."Posting Applied Amount" + PmtLine."Posting Payment Discount";
                      CustEntry."Applies-to ID" := COPYSTR(GenJnlLine."Journal Batch Name" + FORMAT(GenJnlLine."Line No."),1,20);
                      CustEntry."Remaining Pmt. Disc. Possible" := PmtLine."Posting Payment Discount";
                      IF CustEntry."Pmt. Discount Date" < GenJnlLine."Posting Date" THEN
                        CustEntry."Pmt. Discount Date" := GenJnlLine."Posting Date" + 1;
                      IF CustEntry."Remaining Pmt. Disc. Possible" = 0 THEN
                        CustEntry."Pmt. Discount Date" := 0D;
                      CustEntryEdit.RUN(CustEntry);
                    END ELSE IF PmtLine."Account Type" = PmtLine."Account Type"::Vendor THEN BEGIN
                      VendEntry.GET(PmtLine."ID Applied-Entry");
                      VendEntry."Amount to Apply" := PmtLine."Posting Applied Amount" + PmtLine."Posting Payment Discount";
                      VendEntry."Applies-to ID" := COPYSTR(GenJnlLine."Journal Batch Name" + FORMAT(GenJnlLine."Line No."),1,20);
                      VendEntry."Remaining Pmt. Disc. Possible" := PmtLine."Posting Payment Discount";
                      IF VendEntry."Pmt. Discount Date" < GenJnlLine."Posting Date" THEN
                        VendEntry."Pmt. Discount Date" := GenJnlLine."Posting Date" + 1;
                      IF VendEntry."Remaining Pmt. Disc. Possible" = 0 THEN
                        VendEntry."Pmt. Discount Date" := 0D;
                      VendEntryEdit.RUN(VendEntry);
                    END;
                  END;
                END;
                GenJnlLine."Application Status" := GenJnlLine."Application Status"::Finished;
                IF PmtPostingSetup."One Bal. Acc. per Pmt. Line" THEN BEGIN
                  GenJnlLine."Bal. Account Type" := PmtPostingSetup."Posting Account Type";
                  GenJnlLine.VALIDATE("Bal. Account No.",PmtPostingSetup."Posting Account No.");
                  GenJnlLine."PIP Account Type" := GenJnlLine."PIP Account Type"::"Bal. Account";
                END;
                GenJnlLine.Description := PmtLine."Payment Text";
                GenJnlLine.CreateDim(
                  DimMgt.TypeToTableID1(GenJnlLine."Account Type"),GenJnlLine."Account No.",
                  DimMgt.TypeToTableID1(GenJnlLine."Bal. Account Type"),GenJnlLine."Bal. Account No.",
                  DATABASE::Job,GenJnlLine."Job No.",
                  DATABASE::"Salesperson/Purchaser",GenJnlLine."Salespers./Purch. Code",
                  DATABASE::Campaign,GenJnlLine."Campaign No.");
                GenJnlLine."Payment in Process" := TRUE;
                IF NOT _PmtProposal."Summarize Per Pmt. Head" THEN BEGIN
                  IF PmtLine."Shortcut Dimension 1 Code" <> '' THEN
                    GenJnlLine.VALIDATE("Shortcut Dimension 1 Code",PmtLine."Shortcut Dimension 1 Code");
                  IF PmtLine."Shortcut Dimension 2 Code" <> '' THEN
                    GenJnlLine.VALIDATE("Shortcut Dimension 2 Code",PmtLine."Shortcut Dimension 2 Code");
                  GenJnlLine.INSERT(TRUE);
                END;
                IF OPplusLicenseInfo.IsBasics THEN
                  IF (OPplusSetup."Dim. for Apply Line Details" = OPplusSetup."Dim. for Apply Line Details"::"Applied Entry") AND NOT
                     _PmtProposal."Summarize Per Pmt. Head"
                  THEN BEGIN
                    TempJnlLineDim.RESET;
                    TempJnlLineDim.DELETEALL;
                    TempJnlLineDim3.RESET;
                    TempJnlLineDim3.DELETEALL;
                    JnlLineDim.RESET;
                    JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
                    JnlLineDim.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                    JnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                    JnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
                    JnlLineDim.SETRANGE("Allocation Line No.",0);
                    DimMgt.CopyJnlLineDimToJnlLineDim(JnlLineDim,TempJnlLineDim3);
                    DimMgt.DeleteJnlLineDim(
                      DATABASE::"Gen. Journal Line",
                      GenJnlLine."Journal Template Name",
                      GenJnlLine."Journal Batch Name",
                      GenJnlLine."Line No.",
                      0);
                    IF GenJnlLine."Applied Account Type" = GenJnlLine."Applied Account Type"::Customer THEN
                      LedgEntryDim.SETRANGE("Table ID",DATABASE::"Cust. Ledger Entry")
                    ELSE IF GenJnlLine."Applied Account Type" = GenJnlLine."Applied Account Type"::Vendor THEN
                      LedgEntryDim.SETRANGE("Table ID",DATABASE::"Vendor Ledger Entry")
                    ELSE IF GenJnlLine."Applied Account Type" IN
                      [GenJnlLine."Applied Account Type"::"G/L Account", GenJnlLine."Applied Account Type"::"Bank Account"]
                    THEN
                      LedgEntryDim.SETRANGE("Table ID",DATABASE::"G/L Entry");
                    LedgEntryDim.SETRANGE("Entry No.",GenJnlLine."ID Applied-Entry");
                    DimMgt.CopyLedgEntryDimToJnlLineDim(LedgEntryDim,TempJnlLineDim);
                    TempJnlLineDim3.SETFILTER("Dimension Value Code",'<>%1','');
                    IF TempJnlLineDim3.FINDSET(FALSE,FALSE) THEN
                      REPEAT
                        TempJnlLineDim.SETRANGE("Dimension Code",TempJnlLineDim3."Dimension Code");
                        IF NOT TempJnlLineDim.FIND('-') THEN BEGIN
                          TempJnlLineDim3.SETRANGE("Dimension Code",TempJnlLineDim3."Dimension Code");
                          DimMgt.CopyJnlLineDimToJnlLineDim(TempJnlLineDim3,TempJnlLineDim);
                          TempJnlLineDim3.SETRANGE("Dimension Code");
                        END;
                      UNTIL TempJnlLineDim3.NEXT = 0;
                    TempJnlLineDim.RESET;
                    TempJnlLineDim.SETRANGE("Dimension Code",GLSetup."Global Dimension 1 Code");
                    IF TempJnlLineDim.FINDSET(FALSE,FALSE) THEN
                      GenJnlLine."Shortcut Dimension 1 Code" := TempJnlLineDim."Dimension Value Code"
                    ELSE
                      GenJnlLine."Shortcut Dimension 1 Code" := '';
                    TempJnlLineDim.SETRANGE("Dimension Code",GLSetup."Global Dimension 2 Code");
                    IF TempJnlLineDim.FIND('-') THEN
                      GenJnlLine."Shortcut Dimension 2 Code" := TempJnlLineDim."Dimension Value Code"
                    ELSE
                      GenJnlLine."Shortcut Dimension 2 Code" := '';
                    GenJnlLine.MODIFY;
                    TempJnlLineDim.RESET;
                    DimMgt.MoveJnlLineDimToJnlLineDim(TempJnlLineDim,
                      DATABASE::"Gen. Journal Line",
                      GenJnlLine."Journal Template Name",
                      GenJnlLine."Journal Batch Name",
                      GenJnlLine."Line No.");
                  END;
                IF NOT PmtPostingSetup."One Bal. Acc. per Pmt. Line" THEN BEGIN
                  GenJnlLine2.Amount := GenJnlLine2.Amount - GenJnlLine.Amount;
                  GenJnlLine2."Amount (LCY)" := GenJnlLine2."Amount (LCY)" - GenJnlLine."Amount (LCY)";
                END;
              UNTIL PmtLine.NEXT = 0;
              IF _PmtProposal."Summarize Per Pmt. Head" THEN BEGIN
                GenJnlLine.VALIDATE(Amount,SumAmt);
                GenJnlLine.VALIDATE("Amount (LCY)",SumAmtLCY);
                GenJnlLine2.Amount := GenJnlLine2.Amount - GenJnlLine.Amount;
                GenJnlLine2."Amount (LCY)" := GenJnlLine2."Amount (LCY)" - GenJnlLine."Amount (LCY)";
                GenJnlLine."Applies-to ID" := COPYSTR(GenJnlLine."Journal Batch Name" + FORMAT(GenJnlLine."Line No."),1,20);
                GenJnlLine.Description := _PmtProposal.Description;
                GenJnlLine.INSERT(TRUE);
              END;
              IF (PaymentType."Separate Currency" OR PmtPostingSetup."One Bal. Acc. per Pmt. Head") AND NOT
                  PmtPostingSetup."One Bal. Acc. per Pmt. Line"
              THEN BEGIN
                GenJnlLine2."Line No." := GenJnlLine."Line No." + 10000;
                LineNo := LineNo + 10000;
                GenJnlLine2."Account Type" := PmtPostingSetup."Posting Account Type";
                GenJnlLine2.VALIDATE("Posting Date");
                GenJnlLine2.VALIDATE("Account No.",PmtPostingSetup."Posting Account No.");
                GenJnlLine2."PIP Account Type" := GenJnlLine2."PIP Account Type"::Account;
                IF GenJnlLine2."Account Type" = GenJnlLine2."Account Type"::"G/L Account" THEN BEGIN
                  GenJnlLine2.VALIDATE("Currency Code",PmtHead."Pmt. Currency Code");
                  GenJnlLine2.VALIDATE(Amount,PmtHead."Payment Amount");
                  GenJnlLine2.VALIDATE("Amount (LCY)",PmtHead."Pmt. Amount (LCY)");
                END ELSE BEGIN
                  // ZahlungswÑhrung in KontowÑhrung umrechnen
                  GenJnlLine2.VALIDATE(Amount,
                    CurrExchRate.ExchangeAmtFCYToFCY(GenJnlLine2."Posting Date",
                      PmtHead."Pmt. Currency Code",
                      GenJnlLine2."Currency Code",
                      PmtHead."Payment Amount"));
                  // MandantenwÑhrung validieren
                  GenJnlLine2.VALIDATE("Amount (LCY)",PmtHead."Pmt. Amount (LCY)");
                END;

                GenJnlLine2.Description := COPYSTR(PmtHead."Account No." + '/' + PmtHead.Name,1,50);
                GenJnlLine2.VALIDATE("VAT Prod. Posting Group");
                GenJnlLine2.VALIDATE("Payment Terms Code");
                GenJnlLine2.VALIDATE("Bal. VAT Prod. Posting Group");
                GenJnlLine2.CreateDim(
                  DimMgt.TypeToTableID1(GenJnlLine2."Account Type"),GenJnlLine2."Account No.",
                  DimMgt.TypeToTableID1(GenJnlLine2."Bal. Account Type"),GenJnlLine2."Bal. Account No.",
                  DATABASE::Job,GenJnlLine2."Job No.",
                  DATABASE::"Salesperson/Purchaser",GenJnlLine2."Salespers./Purch. Code",
                  DATABASE::Campaign,GenJnlLine2."Campaign No.");
                GenJnlLine2."Payment in Process" := TRUE;
                // gbedv CLR -------------------------------------------------- BEGIN
                GenJnlLine2."Transit Entry" := OPplusPmtSetup."Transfer Posting";
                // gbedv CLR -------------------------------------------------- END
                GenJnlLine2.INSERT(TRUE);
              END;
            UNTIL PmtHead.NEXT = 0;

            IF NOT PmtPostingSetup."One Bal. Acc. per Pmt. Line" AND NOT
                   PmtPostingSetup."One Bal. Acc. per Pmt. Head" AND NOT
                   PaymentType."Separate Currency"
            THEN BEGIN
              GenJnlLine2."Line No." := GenJnlLine."Line No." + 10000;
              GenJnlLine3 := GenJnlLine2;
              LineNo := LineNo + 10000;
              GenJnlLine2."Account Type" := PmtPostingSetup."Posting Account Type";
              GenJnlLine2.VALIDATE("Posting Date");
              GenJnlLine2.VALIDATE("Account No.",PmtPostingSetup."Posting Account No.");
              GenJnlLine2."PIP Account Type" := GenJnlLine2."PIP Account Type"::Account;
              IF GenJnlLine2."Account Type" = GenJnlLine2."Account Type"::"G/L Account" THEN BEGIN
                GenJnlLine2.VALIDATE("Currency Code",GenJnlLine3."Currency Code");
                GenJnlLine2.VALIDATE(Amount,GenJnlLine3.Amount);
                GenJnlLine2.VALIDATE("Amount (LCY)",GenJnlLine3."Amount (LCY)");
              END ELSE BEGIN
                // MandantenwÑhrung in ZahlungswÑhrung umrechnen
                GenJnlLine2.VALIDATE(Amount,
                  CurrExchRate.ExchangeAmtFCYToFCY(GenJnlLine2."Posting Date",
                    GenJnlLine3."Currency Code",
                    GenJnlLine2."Currency Code",
                    GenJnlLine3.Amount));
                GenJnlLine2.VALIDATE("Amount (LCY)",GenJnlLine3."Amount (LCY)");
              END;
              GenJnlLine2.Description := _PmtProposal.Description;
              GenJnlLine2.VALIDATE("VAT Prod. Posting Group");
              GenJnlLine2.VALIDATE("Payment Terms Code");
              GenJnlLine2.VALIDATE("Bal. VAT Prod. Posting Group");
              GenJnlLine2.CreateDim(
                DimMgt.TypeToTableID1(GenJnlLine2."Account Type"),GenJnlLine2."Account No.",
                DimMgt.TypeToTableID1(GenJnlLine2."Bal. Account Type"),GenJnlLine2."Bal. Account No.",
                DATABASE::Job,GenJnlLine2."Job No.",
                DATABASE::"Salesperson/Purchaser",GenJnlLine2."Salespers./Purch. Code",
                DATABASE::Campaign,GenJnlLine2."Campaign No.");
              GenJnlLine2."Payment in Process" := TRUE;
              // gbedv CLR -------------------------------------------------- BEGIN
              GenJnlLine2."Transit Entry" := OPplusPmtSetup."Transfer Posting";
              // gbedv CLR -------------------------------------------------- END
              GenJnlLine2.INSERT(TRUE);
            END;
          END;

        UNTIL PaymentType.NEXT = 0;

      Dlg.CLOSE();
      AdjustBalance.RUN(GenJnlLine2);

      _PmtProposal.Status := _PmtProposal.Status::"Posting in Process";
      _PmtProposal.MODIFY;

      MESSAGE(Text014);
    END;

    PROCEDURE CancelPayments@5157809(VAR _PmtProposal@5157802 : Record 5157895);
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::"Payment Done");
      IF CONFIRM(Text009) THEN BEGIN
        PmtHead.RESET;
        PmtHead.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
        PmtHead.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");
        PmtHead.MODIFYALL("Payment Done",FALSE);
        OPplusPmtSetup.GET;
        IF OPplusPmtSetup."Release needed" THEN
          _PmtProposal.Status := _PmtProposal.Status::Released
        ELSE
          _PmtProposal.Status := _PmtProposal.Status::Open;
        _PmtProposal.MODIFY(TRUE);
        MESSAGE(Text010);
      END;
    END;

    PROCEDURE CancelPostings@5157811(VAR _PmtProposal@5157802 : Record 5157895);
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::"Posting in Process");
      IF CONFIRM(Text015) THEN BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETRANGE("Journal Template Name",_PmtProposal."Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name",_PmtProposal."Journal Batch Name");
        GenJnlLine.MODIFYALL("Payment in Process",FALSE);
        GenJnlLine.MODIFYALL("Check Printed",FALSE);
        IF GenJnlLine.FIND('-') THEN
          REPEAT
            DimMgt.DeleteJnlLineDim(
              DATABASE::"Gen. Journal Line",
                GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name",GenJnlLine."Line No.",0);
            GenJnlLine.DELETE(TRUE);
          UNTIL GenJnlLine.NEXT = 0;
        IF GenJnlBatch.GET(_PmtProposal."Journal Template Name",_PmtProposal."Journal Batch Name") THEN
          GenJnlBatch.DELETE;
        _PmtProposal.Status := _PmtProposal.Status::"Payment Done";
        _PmtProposal.MODIFY(TRUE);
        PmtLine.RESET;
        PmtLine.SETRANGE("Journal Template Name",_PmtProposal."Journal Template Name");
        PmtLine.SETRANGE("Journal Batch Name",_PmtProposal."Journal Batch Name");
        IF PmtLine.FINDSET(FALSE,FALSE) THEN
          REPEAT
            PmtLine.CreateAppLineDetail();
          UNTIL PmtLine.NEXT = 0;
        PmtHead.RESET;
        PmtHead.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
        PmtHead.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");
        PmtHead.SETFILTER("Check Ledger Entry No.",'<>0');
        IF PmtHead.FIND('-') THEN BEGIN
          REPEAT
            IF CheckLedgerEntry.GET(PmtHead."Check Ledger Entry No.") THEN BEGIN
              CheckLedgerEntry."Entry Status" := CheckLedgerEntry."Entry Status"::Voided;
              CheckLedgerEntry.MODIFY;
            END;
          UNTIL PmtHead.NEXT = 0;
          PmtHead.MODIFYALL("Check Ledger Entry No.",0);
          PmtHead.RESET;
        END;

        MESSAGE(Text016);
      END;
    END;

    PROCEDURE SplitPayment@5157814(VAR _PmtProposal@5157803 : Record 5157895);
    VAR
      PmtProposal3@5157807 : Record 5157895;
      PmtHead2@5157802 : Record 5157896;
      PmtHead3@5157808 : Record 5157896;
      PmtLine2@5157810 : Record 5157897;
      PmtLine3@5157809 : Record 5157897;
      AppLineDetail2@5157812 : Record 5157804;
      AppLineDetail3@5157813 : Record 5157804;
      PmtError2@5157814 : Record 5157898;
      PmtError3@5157815 : Record 5157898;
      NoSeriesManagement@5157811 : Codeunit 396;
      FirstBank@5157804 : Code[20];
      SplitBank@5157805 : Code[20];
      SplitWanted@5157806 : Boolean;
    BEGIN
      IF _PmtProposal.Status >= _PmtProposal.Status::"Payment Done" THEN
        ERROR(Text017,
          SELECTSTR(_PmtProposal.Status::Open+1,Text023),
          SELECTSTR(_PmtProposal.Status::Finished+1,Text023),
          SELECTSTR(_PmtProposal.Status::Released+1,Text023));

      IF NOT CONFIRM(Text025,FALSE) THEN
        EXIT;

      OPplusPmtSetup.GET;
      OPplusPmtSetup.TESTFIELD("Pmt. Suggestion No. Series");

      PmtHead.RESET;
      PmtHead.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
      PmtHead.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");

      SplitWanted := TRUE;

      WHILE SplitWanted DO BEGIN
        SplitWanted := FALSE;

        PmtHead.FIND('-');
        FirstBank := PmtHead."Bal. Account No.";
        IF _PmtProposal."Bal. Account No." <> FirstBank THEN BEGIN
          _PmtProposal.VALIDATE("Bal. Account No.",FirstBank);
          _PmtProposal.MODIFY;
        END;
        REPEAT
          IF PmtHead."Bal. Account No." <> FirstBank THEN BEGIN
            SplitWanted := TRUE;
            SplitBank := PmtHead."Bal. Account No.";
          END;
        UNTIL (PmtHead.NEXT = 0) OR SplitWanted;

        IF SplitWanted THEN BEGIN
          // Create New Proposal
          PmtProposal3 := _PmtProposal;
          PmtProposal3."Journal Batch Name" := NoSeriesManagement.GetNextNo(
            OPplusPmtSetup."Pmt. Suggestion No. Series",WORKDATE,TRUE) + 'Z';
          PmtProposal3.VALIDATE("Bal. Account No.",SplitBank);
          PmtProposal3.INSERT;

          PmtHead2.SETRANGE("Gen. Journal Template",_PmtProposal."Journal Template Name");
          PmtHead2.SETRANGE("Gen. Journal Batch",_PmtProposal."Journal Batch Name");
          PmtHead2.SETRANGE("Bal. Account No.",SplitBank);
          IF PmtHead2.FIND('-') THEN
            REPEAT
              // Create New Head
              PmtHead3 := PmtHead2;
              PmtHead3."Gen. Journal Batch" := PmtProposal3."Journal Batch Name";
              PmtHead3.VALIDATE("Bal. Account No.",SplitBank);
              PmtHead3.INSERT;

              // Create New Lines
              PmtLine2.SETRANGE("Journal Template Name",PmtHead2."Gen. Journal Template");
              PmtLine2.SETRANGE("Journal Batch Name",PmtHead2."Gen. Journal Batch");
              PmtLine2.SETRANGE("Journal Line No.",PmtHead2."Gen. Journal Line");
              IF PmtLine2.FIND('-') THEN REPEAT
                PmtLine3 := PmtLine2;
                PmtLine3."Journal Batch Name" := PmtProposal3."Journal Batch Name";
                PmtLine3.INSERT;
                PmtLine2.DELETE;
              UNTIL PmtLine2.NEXT = 0;

              // Create New Error Lines
              PmtError2.SETRANGE("Journal Template Name",PmtHead2."Gen. Journal Template");
              PmtError2.SETRANGE("Journal Batch Name",PmtHead2."Gen. Journal Batch");
              PmtError2.SETRANGE("Journal Line No.",PmtHead2."Gen. Journal Line");
              IF PmtError2.FIND('-') THEN REPEAT
                PmtError3 := PmtError2;
                PmtError3."Journal Batch Name" := PmtProposal3."Journal Batch Name";
                PmtError3.INSERT;
                PmtError2.DELETE;
              UNTIL PmtError2.NEXT = 0;

              // Create New Line Details
              IF OPplusLicenseInfo.IsBasics THEN BEGIN
                AppLineDetail2.SETRANGE("Journal Template Name",PmtHead2."Gen. Journal Template");
                AppLineDetail2.SETRANGE("Journal Batch Name",PmtHead2."Gen. Journal Batch");
                AppLineDetail2.SETRANGE("Line No.",PmtHead2."Gen. Journal Line");
                IF AppLineDetail2.FIND('-') THEN REPEAT
                  AppLineDetail3 := AppLineDetail2;
                  AppLineDetail3."Journal Batch Name" := PmtProposal3."Journal Batch Name";
                  AppLineDetail3.INSERT;
                  AppLineDetail2.DELETE;
                UNTIL AppLineDetail2.NEXT = 0;
              END;

              // Delete Old Head
              PmtHead2.DELETE;
            UNTIL PmtHead2.NEXT = 0;
        END;

      END;
    END;

    PROCEDURE Export_SEPA_XML_New@5157815(_PmtProposal@5157809 : Record 5157895;PmtType@5157802 : Code[10]) : Boolean;
    VAR
      OK@5157808 : Boolean;
      DirDeb@5157803 : Boolean;
      B2B@5157804 : Boolean;
      DestFileName@5157810 : Text[250];
    BEGIN
      IF PmtType = PmtTools.GetSepa THEN BEGIN
        IF _PmtProposal.TotalSepa = 0 THEN
          EXIT(TRUE)
        ELSE BEGIN
          DirDeb := FALSE;
          B2B := FALSE;
        END;
      END;

      IF PmtType = PmtTools.GetSepaDD THEN BEGIN
        IF _PmtProposal.TotalSepaDD = 0 THEN
          EXIT(TRUE)
        ELSE BEGIN
          DirDeb := TRUE;
          B2B := FALSE;
        END;
      END;

      IF PmtType = PmtTools.GetSepaB2B THEN BEGIN
        IF _PmtProposal.TotalSepaB2B = 0 THEN
          EXIT(TRUE)
        ELSE BEGIN
          DirDeb := TRUE;
          B2B := TRUE;
        END;
      END;

      BankAcc.GET(_PmtProposal."Bal. Account No.");
      PaymentType.GET(PmtType);

      OPplusPmtSetup.TESTFIELD("Clearing Sender");
      IF DirDeb THEN
        OPplusPmtSetup.TESTFIELD("Creditor Identifier");

      // damit ein Land nicht das andere Åberschreibt
      DestFileName := PmtTools.GetFileName(BankAcc,PaymentType,_PmtProposal."Journal Batch Name",
        COPYSTR(_PmtProposal."Bank IBAN",1,2) + '_');
      IF PmtType <> PmtTools.GetSepa THEN
        OK := SepaFileDD(_PmtProposal,PmtType,DestFileName)
      ELSE
        OK := SepaFile(_PmtProposal,PmtType,DestFileName);
      IF OK THEN BEGIN
        MESSAGE(Text005,DestFileName);
        FileCreated := TRUE;
        EXIT(OK);
      END ELSE
        MESSAGE(Text007,Text026);
      EXIT(FALSE);
    END;

    PROCEDURE SepaFile@5157842(PaymentProposal_@5157802 : Record 5157895;PmtType@5157803 : Code[10];VAR FileName@5157812 : Text[250]) Result : Boolean;
    VAR
      CompanyInfo@5157819 : Record 79;
      Setup@5157818 : Record 5157892;
      PmtHead@5157817 : Record 5157896;
      PaymentType@5157816 : Record 5157893;
      SEPAXMLScheme@5157835 : Record 5157912;
      Tools@5157815 : Codeunit 5157892;
      Charges@5157807 : Boolean;
      BankCountryCode@5157810 : Code[10];
      Amount@5157805 : Decimal;
      TotalAmount@5157820 : Decimal;
      TransactionCounter@5157814 : Integer;
      SEPA_WIDESTRING@5157826 : Integer;
      SEPA_SHORTSTRING@5157825 : Integer;
      SEPA_STRING@5157824 : Integer;
      xFile@5157811 : File;
      XS@5157809 : OutStream;
      XMLNS@5157836 : Text[100];
      XSI@5157837 : Text[100];
      Location@5157838 : Text[128];
      SenderName@5157823 : Text[100];
      OrdererName@5157822 : Text[100];
      OtherOrdererID@5157821 : Text[100];
      EndToEndID@5157813 : Text[40];
      Text2000@5157829 : TextConst 'DEU=%1 darf nicht %2 sein in %3 %4 %5 %6.;ENU=%1 must not be %2  in %3 %4 %5 %6.';
      Text2001@5157828 : TextConst 'DEU=leer;ENU=empty';
      SepaOld@5157830 : Boolean;
      SilentFilename@5157831 : Text[1024];
      Text2002@5157806 : TextConst 'DEU=Kdnr.;ENU=Custno.';
      Text2003@5157804 : TextConst 'DEU=Unsere Kdnr.;ENU=Our Custno.';
      SepaDeCCT@5157808 : Boolean;
      Pos@5157833 : Integer;
      TempFileName@5157832 : Text[1024];
    BEGIN
      Result := PmtHead.ISEMPTY;
      IF Result THEN
        EXIT;

      PaymentProposal_.TESTFIELD("Bal. Account Type",PaymentProposal_."Bal. Account Type"::"Bank Account");
      PaymentProposal_.TESTFIELD("Bank IBAN");
      BankCountryCode := COPYSTR(PaymentProposal_."Bank IBAN",1,2);

      IF PaymentType.Code <> PmtType THEN
        PaymentType.GET(PmtType);

      CompanyInfo.GET;
      CompanyInfo.TESTFIELD(Name);

      Setup.GET;
      Setup.TESTFIELD("Clearing Sender");

      BankAcc.GET(PaymentProposal_."Bal. Account No.");
      IF OPplusPmtSetup.SepaCCT OR BankAcc.SepaCCT THEN BEGIN
        TempFileName := FileName;
        Pos := 0;
        WHILE STRPOS(FileName,'CCM') > 0 DO BEGIN
          Pos := STRPOS(FileName,'CCM')+3;
          FileName := COPYSTR(FileName,1,STRPOS(FileName,'CCM')-1);
          FileName := FileName + 'CCT' + COPYSTR(TempFileName, Pos);
        END;
      END;
      Indent := TRUE;
      SEPA_WIDESTRING := 140;
      SEPA_STRING := 70;
      SEPA_SHORTSTRING := 35;
      CRLF[1] := 13;
      CRLF[2] := 10;
      CLEAR(TransactionCounter);
      CLEAR(TotalAmount);
      CLEAR(IX);
      CLEAR(Stack);

      SenderName := DELCHR(COPYSTR(Setup."Clearing Sender",1,SEPA_STRING),'<>');
      SenderName := Tools.SEPA_String(SenderName);
      OrdererName := DELCHR(COPYSTR(CompanyInfo.Name,1,SEPA_STRING),'<>');
      OrdererName := Tools.SEPA_String(OrdererName);
      IF BankCountryCode = 'AT' THEN
        OtherOrdererID := DELCHR(PaymentProposal_."Bank IBAN");

      // Erster Durchgang: Testen und Summieren
      PmtHead.SETRANGE("Gen. Journal Template",PaymentProposal_."Journal Template Name");
      PmtHead.SETRANGE("Gen. Journal Batch",PaymentProposal_."Journal Batch Name");
      PmtHead.SETRANGE("Payment Type Code",PmtType);
      PmtHead.FIND('-');
      REPEAT
        IF DELCHR(PmtHead."Reason Row 1" + PmtHead."Reason Row 2" + PmtHead."Reason Row 3" +
          PmtHead."Reason Row 4") =  ''
        THEN
          ERROR(Text2000,
            PmtHead.FIELDCAPTION("Reason Row 1"),
            Text2001,
            PmtHead.TABLECAPTION,
            PmtHead."Gen. Journal Template",
            PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
        PmtHead.CALCFIELDS(PmtHead."Payment Amount");
        IF PmtHead."Payment Amount" >= 0 THEN
          ERROR(Text2000,
            PmtHead.FIELDCAPTION("Payment Amount"),
            '>= 0',
            PmtHead.TABLECAPTION,
            PmtHead."Gen. Journal Template",
            PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
        TotalAmount += ABS(PmtHead."Payment Amount");
        TransactionCounter += 1;
      UNTIL PmtHead.NEXT = 0;

      IF NOT ISSERVICETIER THEN BEGIN
        xFile.QUERYREPLACE(FALSE);
        SilentFilename := FileName;
      END ELSE
        SilentFilename := TierMgt.ServerTempFileName('',DELCHR(PaymentType."File Supplement",'<>','.'));

      xFile.WRITEMODE(TRUE);
      xFile.CREATE(SilentFilename);
      xFile.CREATEOUTSTREAM(XS);

      // Zweiter Durchgang: Ausgabe
      PmtHead.FIND('-');
      XS.WRITETEXT('<?xml version="1.0" encoding="UTF-8"?>' + CRLF);

      CLEAR(XMLNS);
      CLEAR(XSI);
      CLEAR(Location);

      CASE BankCountryCode OF
        'DE':
          BEGIN
            IF OPplusPmtSetup.SepaCCT OR BankAcc.SepaCCT THEN BEGIN
              SepaDeCCT := TRUE;
              Charges := TRUE;
              XMLNS := '"urn:iso:std:iso:20022:tech:xsd:pain.001.002.03"';
              XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
              Location := '"urn:iso:std:iso:20022:tech:xsd:pain.001.002.03 pain.001.002.03.xsd"';
            END ELSE
              IF OPplusPmtSetup.SepaOld THEN BEGIN
                SepaOld := TRUE;
                Charges := TRUE;
                XMLNS := '"urn:sepade:xsd:pain.001.001.02"';
                XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
                Location := '"urn:sepade:xsd:pain.001.001.02 pain.001.001.02.xsd"';
              END ELSE BEGIN
                XMLNS := '"urn:swift:xsd:$pain.001.002.02"';
                XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
                Location := '"urn:swift:xsd:$pain.001.002.02 pain.001.002.02.xsd"';
              END;
          END;
        'AT':
          BEGIN
            XMLNS := '"APC:STUZZA:payments:ISO:pain:001:001:02:austrian:002"';
            XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
          END;
        ELSE
          BEGIN
            XMLNS := '"urn:swift:xsd:$pain.001.001.02"';
            XSI := '"http://www.w3.org/2001/XMLSchemainstance"';
          END;
      END;

      IF SEPAXMLScheme.GET(
        PmtHead."Bal. Account No.",PmtHead."Payment Type Code",PmtHead."Orderer Country/Region Code")
      THEN BEGIN
        XMLNS := SEPAXMLScheme."Document xmlns";
        XSI := SEPAXMLScheme."xmlns:xsi";
        Location := SEPAXMLScheme."xsi:schemaLocation";
      END ELSE
        IF SEPAXMLScheme.GET(
          PmtHead."Bal. Account No.",PmtHead."Payment Type Code",'')
        THEN BEGIN
          XMLNS := SEPAXMLScheme."Document xmlns";
          XSI := SEPAXMLScheme."xmlns:xsi";
          Location := SEPAXMLScheme."xsi:schemaLocation";
        END;

      IF Location <> '' THEN
        XS.WRITETEXT(
          '<Document xmlns=' + XMLNS +
          ' xmlns:xsi=' + XSI +
          ' xsi:schemaLocation=' + Location + '>')
      ELSE
        XS.WRITETEXT(
          '<Document xmlns=' + XMLNS +
          ' xmlns:xsi=' + XSI + '>');

      XS.WRITETEXT(CRLF);
      // sepa cct -------------------------------------------------- begin
      IF SepaDeCCT THEN
        push(XS,'CstmrCdtTrfInitn')
      ELSE
      // sepa cct -------------------------------------------------- end
        push(XS,'pain.001.001.02');

      // Header
      push(XS,'GrpHdr');
        cont(XS,'MsgId',PmtHead."Gen. Journal Batch");
        cont(XS,'CreDtTm',MakeISODateTime(TODAY));
        cont(XS,'NbOfTxs',FORMAT(TransactionCounter));
      // sepa cct -------------------------------------------------- begin
      IF NOT SepaDeCCT THEN BEGIN
      // sepa cct -------------------------------------------------- end
        cont(XS,'CtrlSum',CONVERTSTR(FORMAT(TotalAmount,0,2),',','.'));
        IF SepaOld THEN
          cont(XS,'Grpg','GRPD')
        ELSE IF OPplusPmtSetup.SepaGrouping > 0 THEN
          cont(XS,'Grpg',FORMAT(OPplusPmtSetup.SepaGrouping))
        ELSE
          cont(XS,'Grpg','MIXD');
      // sepa cct -------------------------------------------------- begin
      END;
      // sepa cct -------------------------------------------------- end
        push(XS,'InitgPty');
        IF BankCountryCode <> 'AT' THEN BEGIN
          cont(XS,'Nm',SenderName);
          IF SepaOld THEN
            SepaPstlAdr(XS,TRUE,CompanyInfo);
        END ELSE BEGIN
          push(XS,'Id');
            push(XS,'OrgId');
              cont(XS,'BkPtyId',OtherOrdererID);
            pop(XS);
          pop(XS);
        END;
        pop(XS);
      pop(XS);  // </GrpHdr>

      // Batch
      push(XS,'PmtInf');
      IF NOT SepaOld THEN
        cont(XS,'PmtInfId',PmtHead."Gen. Journal Batch");
      cont(XS,'PmtMtd','TRF');
      // sepa cct -------------------------------------------------- begin
      IF SepaDeCCT THEN BEGIN
        cont(XS,'BtchBookg','true');
        cont(XS,'NbOfTxs',FORMAT(TransactionCounter));
        cont(XS,'CtrlSum',CONVERTSTR(FORMAT(TotalAmount,0,2),',','.'));
      END;
      // sepa cct -------------------------------------------------- end
      push(XS,'PmtTpInf');
        push(XS,'SvcLvl');
          cont(XS,'Cd','SEPA');
        pop(XS);
      pop(XS);

      IF PaymentProposal_."Execution Date" <> 0D THEN
        cont(XS,'ReqdExctnDt',FORMAT(PaymentProposal_."Execution Date",0,'<year4,4>-<month,2>-<day,2>'))
      ELSE
        cont(XS,'ReqdExctnDt',FORMAT(PaymentProposal_."Posting Date",0,'<year4,4>-<month,2>-<day,2>'));

      push(XS,'Dbtr');
        cont(XS,'Nm',OrdererName);
        IF SepaOld THEN
          SepaPstlAdr(XS,TRUE,CompanyInfo);
      pop(XS);

      push(XS,'DbtrAcct');
        push(XS,'Id');
          cont(XS,'IBAN', DELCHR(PmtHead."Orderer Bank IBAN Code"));
        pop(XS);
      pop(XS);

      push(XS,'DbtrAgt');
        push(XS,'FinInstnId');
          cont(XS,'BIC', DELCHR(PmtHead."Orderer Bank BIC Code"));
        pop(XS);
      pop(XS);

      cont(XS,'ChrgBr','SLEV');

      // Transaction
      TransactionCounter := 0;

      REPEAT
        PmtHead.CALCFIELDS("Payment Amount");
        Amount := ABS(PmtHead."Payment Amount");
        EndToEndID := '';

        IF PmtHead."Account Type" = PmtHead."Account Type"::Customer THEN
          EndToEndID := Text2002 + ' ' + PmtHead."Account No."
        ELSE IF PmtHead."Account Type" = PmtHead."Account Type"::Vendor THEN
          IF PmtHead."Our Account No." <> '' THEN
            EndToEndID := Text2003 + ' ' + PmtHead."Our Account No.";
        IF EndToEndID = '' THEN
          IF NOT OPplusPmtSetup.SepaUniqueEndToEndID THEN
            EndToEndID := 'NOTPROVIDED'
          ELSE
            EndToEndID := COPYSTR(Tools.SEPA_String(EndToEndID),1,SEPA_SHORTSTRING);
        // sepa cct -------------------------------------------------- begin
        //  IF OPplusPmtSetup.SepaUniqueEndToEndID THEN BEGIN
        // sepa cct -------------------------------------------------- end
        IF OPplusPmtSetup.SepaUniqueEndToEndID OR SepaDeCCT THEN BEGIN
          TransactionCounter += 1;
          EndToEndID := COPYSTR(FORMAT(TransactionCounter) + '/' + EndToEndID,1,SEPA_SHORTSTRING);
        END;

        push(XS,'CdtTrfTxInf');

          push(XS,'PmtId');
            cont(XS,'EndToEndId',EndToEndID);
          pop(XS);

          push(XS,'Amt');
            contAttrib(XS,'InstdAmt', CONVERTSTR(FORMAT(Amount,0,2),',','.'), 'Ccy','EUR');
          pop(XS);

          push(XS,'CdtrAgt');
            push(XS,'FinInstnId');
              cont(XS,'BIC',DELCHR(PmtHead."Bank BIC Code"));
            pop(XS);
          pop(XS);

          push(XS,'Cdtr');
          IF PmtHead."Bank Account Owner" <> '' THEN
            cont(XS,'Nm',Tools.SEPA_String(PmtHead."Bank Account Owner"))
          ELSE
            cont(XS,'Nm',Tools.SEPA_String(PmtHead.Name));
          IF SepaOld THEN
            SepaPstlAdr(XS,FALSE,CompanyInfo);
          pop(XS);

          push(XS,'CdtrAcct');
            push(XS,'Id');
              cont(XS,'IBAN',DELCHR(PmtHead."Bank IBAN Code"));
            pop(XS);
          pop(XS);

          push(XS,'RmtInf');
          IF NOT Setup."Fill SEPA Purpose" THEN
            cont(XS,'Ustrd',COPYSTR(Tools.SEPA_String(PmtHead."Reason Row 1" + ' ' + PmtHead."Reason Row 2" + ' ' +
              PmtHead."Reason Row 3" + ' ' + PmtHead."Reason Row 4"),1,SEPA_WIDESTRING))
          ELSE
            cont(XS,'Ustrd',COPYSTR(Tools.SEPA_String(PADSTR(PmtHead."Reason Row 1",35,' ') + PADSTR(PmtHead."Reason Row 2",35,' ') +
              PADSTR(PmtHead."Reason Row 3",35,' ') + PADSTR(PmtHead."Reason Row 4",35,' ')),1,SEPA_WIDESTRING));
          pop(XS);

        pop(XS);  // </CdtTrfTxInf>
      UNTIL PmtHead.NEXT=0;

      pop(XS);  // </PmtInf>
      pop(XS);  // </pain. ...> bzw. </CstmrCdtTrfInitn>
      XS.WRITETEXT('</Document>');
      xFile.CLOSE;

      CLEAR(XS);
      CLEAR(xFile);

      IF ISSERVICETIER THEN
        TierMgt.DownloadToFile(SilentFilename,FileName);

      PmtHead.MODIFYALL("Payment Done",TRUE);
      EXIT(TRUE);
    END;

    PROCEDURE SepaPstlAdr@5157807(StreamVar@1000000000 : OutStream;Orderer@1000000001 : Boolean;CompInfo@1000000003 : Record 79);
    VAR
      Tools@1000000002 : Codeunit 5157892;
    BEGIN
      IF Orderer THEN BEGIN
        push(StreamVar,'PstlAdr');
        IF CompInfo.Address <> '' THEN
          cont(StreamVar,'AdrLine',COPYSTR(Tools.SEPA_String(CompInfo.Address),1,70));
        IF CompInfo."Post Code" + ' ' + CompInfo.City <> '' THEN
          cont(StreamVar,'AdrLine',COPYSTR(Tools.SEPA_String(CompInfo."Post Code" + ' ' + CompInfo.City),1,70));
        cont(StreamVar,'Ctry',PmtHead."Country/Region Code");
        pop(StreamVar);
      END ELSE BEGIN
        push(StreamVar,'PstlAdr');
        IF PmtHead.Address <> '' THEN
          cont(StreamVar,'AdrLine',COPYSTR(Tools.SEPA_String(PmtHead.Address),1,70));
        IF PmtHead."Post Code" + ' ' + PmtHead.City <> '' THEN
          cont(StreamVar,'AdrLine',COPYSTR(Tools.SEPA_String(PmtHead."Post Code" + ' ' + PmtHead.City),1,70));
        cont(StreamVar,'Ctry',PmtHead."Country/Region Code");
        pop(StreamVar);
      END;
    END;

    PROCEDURE SepaDirectDebitSegment@5157823(Stream@5157805 : OutStream;CreditorId@5157802 : Text[35];MandateDate@5157803 : Date;MandateId@5157804 : Text[35]);
    BEGIN
      push(Stream,'DrctDbtTx');
        push(Stream,'MndtRltdInf');
          cont(Stream,'MndtId',DELCHR(MandateId));
          cont(Stream,'DtOfSgntr',FORMAT(MandateDate,0,'<year4,4>-<month,2>-<day,2>'));
          cont(Stream,'AmdmntInd','false');
        pop(Stream);
        push(Stream,'CdtrSchmeId');
          push(Stream,'Id');
            push(Stream,'PrvtId');
              push(Stream,'OthrId');
                cont(Stream,'Id',CreditorId);
                cont(Stream,'IdTp','SEPA');
              pop(Stream);
            pop(Stream);
          pop(Stream);
        pop(Stream);
      pop(Stream);
    END;

    PROCEDURE cont@5157822(Stream@5157804 : OutStream;Tag@5157802 : Text[30];Text@5157803 : Text[1024]);
    BEGIN
      IF Indent THEN
        Stream.WRITETEXT(PADSTR('',2*(IX+1)));
      Stream.WRITETEXT('<' + Tag + '>' + Text + '</' + Tag + '>');
      Stream.WRITETEXT(CRLF);
    END;

    PROCEDURE contAttrib@5157821(Stream@5157806 : OutStream;Tag@5157802 : Text[30];Text@5157803 : Text[1024];AttribTag@5157804 : Text[30];AttribValue@5157805 : Text[30]);
    BEGIN
      IF Indent THEN
        Stream.WRITETEXT(PADSTR('',2*(IX+1)));
      Stream.WRITETEXT('<' + Tag + ' ' + AttribTag + '="' + AttribValue +'">' + Text + '</' + Tag + '>');
      Stream.WRITETEXT(CRLF);
    END;

    PROCEDURE push@5157819(Stream@5157803 : OutStream;Tag@5157802 : Text[30]);
    BEGIN
      IX += 1;
      Stack[IX] := Tag;
      IF Indent THEN
        Stream.WRITETEXT(PADSTR('',2*IX));
      Stream.WRITETEXT('<' + Tag + '>');
      Stream.WRITETEXT(CRLF);
    END;

    PROCEDURE pop@5157818(Stream@5157802 : OutStream);
    BEGIN
      IF Indent THEN
        Stream.WRITETEXT(PADSTR('',2*IX));
      Stream.WRITETEXT('</' + Stack[IX] + '>') ;
      IX -= 1;
      Stream.WRITETEXT(CRLF);
    END;

    PROCEDURE MakeISODateTime@5157817(Date@5157802 : Date) : Text[40];
    BEGIN
      // Beispiel: 2008-07-21T08:35:30
      EXIT(CONVERTSTR(FORMAT(Date,0,'<year4,4>-<month,2>-<day,2>') +
       FORMAT(TIME,0,'T<Hours24,2>:<Minutes,2>:<Seconds,2>'),' ','0'));
    END;

    PROCEDURE SepaFileDD@5157816(PaymentProposal@5157804 : Record 5157895;PmtType@5157803 : Code[10];FileName@5157802 : Text[250]) Result : Boolean;
    VAR
      CompanyInfo@5157831 : Record 79;
      Setup@5157830 : Record 5157892;
      PmtHead@5157829 : Record 5157896;
      CustBankAcc@5157827 : Record 287;
      VendBankAccount@5157824 : Record 288;
      SEPAXMLScheme@5157838 : Record 5157912;
      Tools@5157825 : Codeunit 5157892;
      B2B@5157823 : Boolean;
      BankCountryCode@5157821 : Code[10];
      Amount@5157820 : Decimal;
      TotalAmount@5157819 : Decimal;
      TransactionCounter@5157818 : Integer;
      SEPA_WIDESTRING@5157817 : Integer;
      SEPA_SHORTSTRING@5157816 : Integer;
      SEPA_STRING@5157815 : Integer;
      xFile@5157814 : File;
      XS@5157813 : OutStream;
      XMLNS@5157837 : Text[100];
      XSI@5157828 : Text[100];
      Location@5157809 : Text[128];
      SenderName@5157812 : Text[100];
      OrdererName@5157811 : Text[100];
      OtherOrdererID@5157810 : Text[100];
      EndToEndID@5157808 : Text[40];
      NationalCreditorId@5157807 : Text[40];
      SilentFilename@5157805 : Text[1024];
      Text2000@5157833 : TextConst 'DEU=%1 darf nicht %2 sein in %3 %4 %5 %6.;ENU=%1 must not be %2  in %3 %4 %5 %6.';
      Text2001@5157832 : TextConst 'DEU=leer;ENU=empty';
      SequenceType@5157822 : Text[30];
      Text2002@5157835 : TextConst 'DEU=Kdnr.;ENU=Custno.';
      Text2003@5157836 : TextConst 'DEU=Unsere Kdnr.;ENU=Our Custno.';
      SEPADueDays@5157839 : Integer;
      TargetDays@5157841 : Integer;
      BankDueDate@5157840 : Date;
      TargetDueDate@5157842 : Date;
      BankAccMandate@5157806 : Record 5157913;
      BankAccMandateHistory@5157843 : Record 5157915;
      Changed@5157844 : Boolean;
    BEGIN
      Result := PmtHead.ISEMPTY;
      IF Result THEN
        EXIT;

      PaymentProposal.TESTFIELD("Bal. Account Type",PaymentProposal."Bal. Account Type"::"Bank Account");
      PaymentProposal.TESTFIELD("Bank IBAN");
      BankCountryCode := COPYSTR(PaymentProposal."Bank IBAN",1,2);

      IF PaymentType.Code <> PmtType THEN
        PaymentType.GET(PmtType);

      CompanyInfo.GET;
      CompanyInfo.TESTFIELD(Name);

      Setup.GET;
      Setup.TESTFIELD("Clearing Sender");
      Setup.TESTFIELD("Creditor Identifier");
      B2B := PmtType = PmtTools.GetSepaB2B;

      Indent := TRUE;
      SEPA_WIDESTRING := 140;
      SEPA_STRING := 70;
      SEPA_SHORTSTRING := 35;
      CRLF[1] := 13;
      CRLF[2] := 10;
      CLEAR(TransactionCounter);
      CLEAR(TotalAmount);
      CLEAR(IX);
      CLEAR(Stack);

      SenderName := DELCHR(COPYSTR(Setup."Clearing Sender",1,SEPA_STRING),'<>');
      SenderName := Tools.SEPA_String(SenderName);
      OrdererName := DELCHR(COPYSTR(CompanyInfo.Name,1,SEPA_STRING),'<>');
      OrdererName := Tools.SEPA_String(OrdererName);
      IF BankCountryCode = 'AT' THEN
        OtherOrdererID := DELCHR(PaymentProposal."Bank IBAN");
      NationalCreditorId := DELCHR(Setup."Creditor Identifier");

      // Erster Durchgang: Testen und Summieren
      PmtHead.SETRANGE("Gen. Journal Template",PaymentProposal."Journal Template Name");
      PmtHead.SETRANGE("Gen. Journal Batch",PaymentProposal."Journal Batch Name");
      PmtHead.SETRANGE("Payment Type Code",PmtType);
      PmtHead.FIND('-');
      REPEAT
        IF DELCHR(PmtHead."Reason Row 1" + PmtHead."Reason Row 2" + PmtHead."Reason Row 3" +
          PmtHead."Reason Row 4") =  ''
        THEN
          ERROR(Text2000,
            PmtHead.FIELDCAPTION("Reason Row 1"),
            Text2001,
            PmtHead.TABLECAPTION,
            PmtHead."Gen. Journal Template",
            PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");

        PmtHead.CALCFIELDS(PmtHead."Payment Amount");
        IF PmtHead."Payment Amount" <= 0 THEN
          ERROR(Text2000,
            PmtHead.FIELDCAPTION("Payment Amount"),
            '<= 0',
            PmtHead.TABLECAPTION,
            PmtHead."Gen. Journal Template",
            PmtHead."Gen. Journal Batch",PmtHead."Gen. Journal Line");
        TotalAmount += ABS(PmtHead."Payment Amount");
        TransactionCounter += 1;
      UNTIL PmtHead.NEXT = 0;

      IF NOT ISSERVICETIER THEN BEGIN
        xFile.QUERYREPLACE(FALSE);
        SilentFilename := FileName;
      END ELSE
        SilentFilename := TierMgt.ServerTempFileName('',DELCHR(PaymentType."File Supplement",'<>','.'));

      xFile.WRITEMODE(TRUE);
      xFile.CREATE(SilentFilename);
      xFile.CREATEOUTSTREAM(XS);

      CLEAR(XMLNS);
      CLEAR(XSI);
      CLEAR(Location);

      // Zweiter Durchgang: Ausgabe
      PmtHead.FIND('-');
      XS.WRITETEXT('<?xml version="1.0" encoding="UTF-8"?>' + CRLF);

      CASE BankCountryCode OF
        'AT':
          BEGIN
            XMLNS := '"APC:STUZZA:payments:ISO:pain:008:001:01:austrian:002"';
            XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
          END;
      ELSE
        BEGIN
          //  Ñltere Deklaration fÅr DE: bitte zur Reserve stehen lassen
          //  '<Document xmlns="urn:sepade:xsd:pain.008.001.01"' +
          //  ' xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"' +
          //  ' xsi:schemaLocation="urn:sepade:xsd:pain.008.001.01 pain.008.001.01.xsd">');
          XMLNS := '"urn:swift:xsd:$pain.008.002.01"';
          XSI := '"http://www.w3.org/2001/XMLSchema-instance"';
          Location := '"urn:swift:xsd:$pain.008.002.01 pain.008.002.01.xsd"';
        END;
      END;

      IF SEPAXMLScheme.GET(
        PmtHead."Bal. Account No.",PmtHead."Payment Type Code",PmtHead."Orderer Country/Region Code")
      THEN BEGIN
        XMLNS := SEPAXMLScheme."Document xmlns";
        XSI := SEPAXMLScheme."xmlns:xsi";
        Location := SEPAXMLScheme."xsi:schemaLocation";
      END ELSE
        IF SEPAXMLScheme.GET(
          PmtHead."Bal. Account No.",PmtHead."Payment Type Code",'')
        THEN BEGIN
          XMLNS := SEPAXMLScheme."Document xmlns";
          XSI := SEPAXMLScheme."xmlns:xsi";
          Location := SEPAXMLScheme."xsi:schemaLocation";
        END;

      IF Location <> '' THEN
        XS.WRITETEXT(
          '<Document xmlns=' + XMLNS +
          ' xmlns:xsi=' + XSI +
          ' xsi:schemaLocation=' + Location + '>')
      ELSE
        XS.WRITETEXT(
          '<Document xmlns=' + XMLNS +
          ' xmlns:xsi=' + XSI + '>');

      XS.WRITETEXT(CRLF);
      push(XS,'pain.008.001.01');

      // Header
      push(XS,'GrpHdr');
        cont(XS,'MsgId',PmtHead."Gen. Journal Batch");
        cont(XS,'CreDtTm',MakeISODateTime(TODAY));
        cont(XS,'NbOfTxs',FORMAT(TransactionCounter));
        cont(XS,'CtrlSum',CONVERTSTR(FORMAT(TotalAmount,0,2),',','.'));
        cont(XS,'Grpg','MIXD');
        push(XS,'InitgPty');
        IF BankCountryCode <> 'AT' THEN BEGIN
          cont(XS,'Nm',SenderName);
        END ELSE BEGIN
          push(XS,'Id');
            push(XS,'OrgId');
              cont(XS,'BkPtyId',OtherOrdererID);
            pop(XS);
          pop(XS);
        END;
        pop(XS);
      pop(XS);  // </GrpHdr>

      // Batch
      TransactionCounter := 0;
      REPEAT
        PmtHead.CALCFIELDS("Payment Amount");
        Amount := ABS(PmtHead."Payment Amount");


        // Default
        SequenceType := 'OOFF';

        IF PmtHead."Mandate ID" <> '' THEN BEGIN
          BankAccMandate.GET(PmtHead."Mandate ID");
          IF PmtHead."Account Type" = PmtHead."Account Type"::Customer THEN BEGIN
            CustBankAccount.GET(PmtHead."Account No.",PmtHead."Bank Code");

            IF BankAccMandate."Mandate Frequency" > 0 THEN  BEGIN
              CASE BankAccMandate."Mandate Status" OF
                BankAccMandate."Mandate Status"::FRST: SequenceType := 'FRST';
                BankAccMandate."Mandate Status"::RCUR: SequenceType := 'RCUR';
                BankAccMandate."Mandate Status"::FNAL: SequenceType := 'FNAL';
              END;
            END ELSE
              SequenceType := 'OOFF';

            CASE BankAccMandate."Mandate Type" OF
              BankAccMandate."Mandate Type"::B2B :
                SEPADueDays := OPplusPmtSetup."Adj. DD B2B";
             BankAccMandate."Mandate Type"::CORE :
              CASE CustBankAcc."Mandate Frequency" OF
                BankAccMandate."Mandate Frequency"::"One-off Payment" :
                  SEPADueDays := OPplusPmtSetup."Adj. DD CORE one-off payment";
                BankAccMandate."Mandate Frequency"::"Recurrent Payment" :
                  CASE BankAccMandate."Mandate Status" OF
                    BankAccMandate."Mandate Status"::FRST :
                      SEPADueDays := OPplusPmtSetup."Adj. DD CORE first payment";
                    BankAccMandate."Mandate Status"::RCUR,CustBankAccount."Mandate Status"::FNAL :
                      SEPADueDays := OPplusPmtSetup."Adj. DD CORE recurrent payment";
                  END;
              END;
            END;
            Changed := FALSE;
            BankAccMandateHistory.SETRANGE("Mandate ID",PmtHead."Mandate ID");
            IF NOT BankAccMandateHistory.FINDLAST THEN BEGIN
              BankAccMandateHistory.INIT;
              BankAccMandateHistory."Mandate ID" := PmtHead."Mandate ID";
              BankAccMandateHistory."Version No." := 1;
              BankAccMandateHistory."Account No." := BankAccMandate."Account No.";
              BankAccMandateHistory."Account Type" := BankAccMandate."Account Type";
              BankAccMandateHistory.IBAN := BankAccMandate.IBAN;
              BankAccMandateHistory."SWIFT Code" := BankAccMandate."SWIFT Code";
              BankAccMandateHistory."Mandate Date" := BankAccMandate."Mandate Date";
              BankAccMandateHistory."Mandate Type" := BankAccMandate."Mandate Type";
              BankAccMandateHistory."Mandate Frequency" := BankAccMandate."Mandate Frequency";
              BankAccMandateHistory."Mandate Status" := BankAccMandate."Mandate Status";
              IF BankAccMandate."Mandate On Behalf of Owner" THEN
                BankAccMandateHistory.Owner := CustBankAccount.Owner;
              BankAccMandateHistory.Name := PmtHead.Name;
              BankAccMandateHistory.Orderer := OrdererName;
              BankAccMandateHistory.Sender := SenderName;
              BankAccMandateHistory.Identifier := NationalCreditorId;

              BankAccMandateHistory.INSERT;
            END;
            IF BankAccMandate."Mandate On Behalf of Owner" THEN
              Changed := CheckBankAccHistory(BankAccMandateHistory,BankAccMandate,
                PmtHead.Name,OrdererName,SenderName,NationalCreditorId,CustBankAccount.Owner)
            ELSE
              Changed := CheckBankAccHistory(BankAccMandateHistory,BankAccMandate,
                PmtHead.Name,OrdererName,SenderName,NationalCreditorId,'');
          END ELSE IF PmtHead."Account Type" = PmtHead."Account Type"::Vendor THEN BEGIN
            VendBankAccount.GET(PmtHead."Account No.",PmtHead."Bank Code");

            IF BankAccMandate."Mandate Frequency" > 0 THEN  BEGIN
              CASE BankAccMandate."Mandate Status" OF
                BankAccMandate."Mandate Status"::FRST: SequenceType := 'FRST';
                BankAccMandate."Mandate Status"::RCUR: SequenceType := 'RCUR';
                BankAccMandate."Mandate Status"::FNAL: SequenceType := 'FNAL';
              END;
            END ELSE
              SequenceType := 'OOFF';

            CASE BankAccMandate."Mandate Type" OF
              BankAccMandate."Mandate Type"::B2B :
                SEPADueDays := OPplusPmtSetup."Adj. DD B2B";
             BankAccMandate."Mandate Type"::CORE :
              CASE CustBankAcc."Mandate Frequency" OF
                BankAccMandate."Mandate Frequency"::"One-off Payment" :
                  SEPADueDays := OPplusPmtSetup."Adj. DD CORE one-off payment";
                BankAccMandate."Mandate Frequency"::"Recurrent Payment" :
                  CASE BankAccMandate."Mandate Status" OF
                    BankAccMandate."Mandate Status"::FRST :
                      SEPADueDays := OPplusPmtSetup."Adj. DD CORE first payment";
                    BankAccMandate."Mandate Status"::RCUR,VendBankAccount."Mandate Status"::FNAL :
                      SEPADueDays := OPplusPmtSetup."Adj. DD CORE recurrent payment";
                  END;
              END;
            END;
            Changed := FALSE;
            BankAccMandateHistory.SETRANGE("Mandate ID",PmtHead."Mandate ID");
            IF NOT BankAccMandateHistory.FINDLAST THEN BEGIN
              BankAccMandateHistory.INIT;
              BankAccMandateHistory."Mandate ID" := PmtHead."Mandate ID";
              BankAccMandateHistory."Version No." := 1;
              BankAccMandateHistory."Account No." := BankAccMandate."Account No.";
              BankAccMandateHistory."Account Type" := BankAccMandate."Account Type";
              BankAccMandateHistory.IBAN := BankAccMandate.IBAN;
              BankAccMandateHistory."SWIFT Code" := BankAccMandate."SWIFT Code";
              BankAccMandateHistory."Mandate Date" := BankAccMandate."Mandate Date";
              BankAccMandateHistory."Mandate Type" := BankAccMandate."Mandate Type";
              BankAccMandateHistory."Mandate Frequency" := BankAccMandate."Mandate Frequency";
              BankAccMandateHistory."Mandate Status" := BankAccMandate."Mandate Status";
              IF BankAccMandate."Mandate On Behalf of Owner" THEN
                BankAccMandateHistory.Owner := VendBankAccount.Owner;
              BankAccMandateHistory.Name := PmtHead.Name;
              BankAccMandateHistory.Orderer := OrdererName;
              BankAccMandateHistory.Sender := SenderName;
              BankAccMandateHistory.Identifier := NationalCreditorId;

              BankAccMandateHistory.INSERT;
            END;
            IF BankAccMandate."Mandate On Behalf of Owner" THEN
              Changed := CheckBankAccHistory(BankAccMandateHistory,BankAccMandate,
                PmtHead.Name,OrdererName,SenderName,NationalCreditorId,VendBankAccount.Owner)
            ELSE
              Changed := CheckBankAccHistory(BankAccMandateHistory,BankAccMandate,
                PmtHead.Name,OrdererName,SenderName,NationalCreditorId,'');
          END;
        END;

        IF PaymentProposal."Execution Date" <> 0D THEN
          BankDueDate := PaymentProposal."Execution Date"
        ELSE
          BankDueDate := PaymentProposal."Posting Date";

        IF SEPADueDays > 0 THEN
          PmtTools.CalcSEPATargetDueDate(BankDueDate,SEPADueDays,TargetDays,TargetDueDate)
        ELSE
          TargetDueDate := BankDueDate;

        IF PmtHead."Account Type" = PmtHead."Account Type"::Customer THEN
          EndToEndID := Text2002 + ' ' + PmtHead."Account No."
        ELSE
          EndToEndID := Text2003 + ' ' + PmtHead."Our Account No.";

        IF EndToEndID = '' THEN
          IF NOT OPplusPmtSetup.SepaUniqueEndToEndID THEN
            EndToEndID := 'NOTPROVIDED'
        ELSE
          EndToEndID := COPYSTR(Tools.SEPA_String(EndToEndID),1,SEPA_SHORTSTRING);

        IF OPplusPmtSetup.SepaUniqueEndToEndID THEN BEGIN
          TransactionCounter += 1;
          EndToEndID := COPYSTR(FORMAT(TransactionCounter) + '/' + EndToEndID,1,SEPA_SHORTSTRING);
        END;

        push(XS,'PmtInf');
          cont(XS,'PmtInfId',PmtHead."Gen. Journal Batch");
          cont(XS,'PmtMtd','DD');

          push(XS,'PmtTpInf');
            push(XS,'SvcLvl');
              cont(XS,'Cd','SEPA');
            pop(XS);
            push(XS,'LclInstrm');
              IF B2B THEN
                cont(XS,'Cd','B2B')
              ELSE
                cont(XS,'Cd','CORE');
            pop(XS);
            cont(XS,'SeqTp',SequenceType);
          pop(XS);

          cont(XS,'ReqdColltnDt',FORMAT(TargetDueDate,0,'<year4,4>-<month,2>-<day,2>'));

          push(XS,'Cdtr');
            cont(XS,'Nm',OrdererName);
          pop(XS);

          push(XS,'CdtrAcct');
            push(XS,'Id');
              cont(XS,'IBAN', DELCHR(PmtHead."Orderer Bank IBAN Code"));
            pop(XS);
          pop(XS);

          push(XS,'CdtrAgt');
            push(XS,'FinInstnId');
              cont(XS,'BIC', DELCHR(PmtHead."Orderer Bank BIC Code"));
            pop(XS);
          pop(XS);

          cont(XS,'ChrgBr','SLEV');

          // Transaction
          push(XS,'DrctDbtTxInf');
            push(XS,'PmtId');
              cont(XS,'EndToEndId',EndToEndID);
            pop(XS);

            contAttrib(XS,'InstdAmt', CONVERTSTR(FORMAT(Amount,0,2),',','.'), 'Ccy','EUR');

            SepaDirectDebitSegment(XS,NationalCreditorId,PmtHead."Mandate Date",PmtHead."Mandate ID");

            push(XS,'DbtrAgt');
              push(XS,'FinInstnId');
                cont(XS,'BIC',DELCHR(PmtHead."Bank BIC Code"));
              pop(XS);
            pop(XS);

            push(XS,'Dbtr');
            IF PmtHead."Bank Account Owner" <> '' THEN
              cont(XS,'Nm',Tools.SEPA_String(PmtHead."Bank Account Owner"))
            ELSE
              cont(XS,'Nm',Tools.SEPA_String(PmtHead.Name));
            pop(XS);

            push(XS,'DbtrAcct');
              push(XS,'Id');
                cont(XS,'IBAN',DELCHR(PmtHead."Bank IBAN Code"));
              pop(XS);
            pop(XS);

            IF BankAccMandate."Mandate On Behalf of Owner" THEN BEGIN
              push(XS,'UltmtDbtr');
              cont(XS,'Nm',Tools.SEPA_String(CustBankAccount.Owner));
              pop(XS);
            END;

            push(XS,'RmtInf');
            IF NOT Setup."Fill SEPA Purpose" THEN
              cont(XS,'Ustrd',COPYSTR(Tools.SEPA_String(PmtHead."Reason Row 1" + ' ' + PmtHead."Reason Row 2" + ' ' +
                PmtHead."Reason Row 3" + ' ' + PmtHead."Reason Row 4"),1,SEPA_WIDESTRING))
            ELSE
              cont(XS,'Ustrd',COPYSTR(Tools.SEPA_String(PADSTR(PmtHead."Reason Row 1",35,' ') + PADSTR(PmtHead."Reason Row 2",35,' ') +
                PADSTR(PmtHead."Reason Row 3",35,' ') + PADSTR(PmtHead."Reason Row 4",35,' ')),1,SEPA_WIDESTRING));
            pop(XS);

          pop(XS);  // </DrctDbtTxInf>
        pop(XS);  // </PmtInf>
      UNTIL PmtHead.NEXT=0;
      pop(XS);  // </pain. ...>
      XS.WRITETEXT('</Document>');
      xFile.CLOSE;

      CLEAR(XS);
      CLEAR(xFile);

      IF ISSERVICETIER THEN
        TierMgt.DownloadToFile(SilentFilename,FileName);

      PmtHead.MODIFYALL("Payment Done",TRUE);
      EXIT(TRUE);
    END;

    PROCEDURE Create_MT10n@5157827(VAR _PmtProposal@5157802 : Record 5157895;PmtCode@5157806 : Code[10];New@5157804 : Boolean) : Boolean;
    VAR
      Filename@5157803 : Text[1024];
      SilentFilename@5157805 : Text[1024];
    BEGIN
      PmtProposal := _PmtProposal;

      PmtHead.RESET;
      PmtHead.SETRANGE("Gen. Journal Template",PmtProposal."Journal Template Name");
      PmtHead.SETRANGE("Gen. Journal Batch",PmtProposal."Journal Batch Name");
      PmtHead.SETFILTER("Payment Type Code",'%1',PmtCode);
      PmtHead.SETFILTER("Payment Amount",'<>0');

      IF NOT PmtHead.ISEMPTY THEN BEGIN
        PaymentType.GET(PmtCode);
        IF NOT BankAcc.GET(PmtHead."Bal. Account No.") THEN
          BankAcc.INIT;
        PmtHead.FIND('-');
        Filename := PmtTools.GetFileName(BankAcc,PaymentType,PmtProposal."Journal Batch Name",'');
        IF NOT ISSERVICETIER THEN BEGIN
          SilentFilename := Filename;
          PmtFile.QUERYREPLACE := FALSE;
        END ELSE
          SilentFilename := TierMgt.ServerTempFileName('',DELCHR(PaymentType."File Supplement",'<>','.'));
        PmtFile.WRITEMODE(TRUE);
        PmtFile.TEXTMODE(TRUE);
        PmtFile.CREATE(SilentFilename);
        CASE PmtCode OF
          PmtTools.GetMT101: MT101File(PmtFile,New,PmtProposal."Execution Date");
          PmtTools.GetMT103: MT103File(PmtFile,New,PmtProposal."Execution Date");
          PmtTools.GetMT104: MT104File(PmtFile,New,PmtProposal."Execution Date");
        END;
        PmtFile.CLOSE();
        CLEAR(PmtFile);
        IF ISSERVICETIER THEN
          TierMgt.DownloadToFile(SilentFilename,Filename);
        MESSAGE(Text005,Filename);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE Repeat_MT101@5157830(VAR _PmtProposal@5157802 : Record 5157895);
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::Posted);
      IF CONFIRM(Text030,FALSE,PmtTools.GetMT101) THEN
        Create_MT10n(_PmtProposal,PmtTools.GetMT101,FALSE);
    END;

    PROCEDURE Repeat_MT103@5157826(VAR _PmtProposal@5157802 : Record 5157895);
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::Posted);
      IF CONFIRM(Text030,FALSE,PmtTools.GetMT103) THEN
        Create_MT10n(_PmtProposal,PmtTools.GetMT103,FALSE);
    END;

    PROCEDURE Repeat_MT104@5157833(VAR _PmtProposal@5157802 : Record 5157895);
    BEGIN
      _PmtProposal.TESTFIELD(Status,_PmtProposal.Status::Posted);
      IF CONFIRM(Text030,FALSE,PmtTools.GetMT104) THEN
        Create_MT10n(_PmtProposal,PmtTools.GetMT104,FALSE);
    END;

    PROCEDURE MT103File@5157825(VAR Outfile@5157806 : File;New@5157803 : Boolean;ExecDate@5157808 : Date);
    VAR
      Amount@5157805 : Decimal;
      HelpStr@5157804 : Text[100];
    BEGIN
      BankAcc.TESTFIELD("MT103 Address");
      GLSetup.GET;
      IF ExecDate = 0D THEN
        ExecDate := PmtHead."Posting Date";

      REPEAT
        PmtHead.CALCFIELDS("Payment Amount");
        Amount := ABS(PmtHead."Payment Amount");
        IF STRLEN(FORMAT(Amount,0,'<integer><decimal,3>')) > 15 THEN
          ERROR(Text031,PmtTools.GetMT103);

        // Basic Header Block (alle M)
          // '1:' Block Identifier:
          // 'F' = FIN
          // '01': Service Identifier: User to User Message
          // Sender LT Address: (ôsterreich: BIC(1-8) + LT(9) + Branch (10-13)
          // Session Number: 4n
          // Sequence Number: 6n
        HelpStr := '{1:F01' + DELCHR(PmtHead."Orderer Bank BIC Code") + PADSTR('0',10,'0') + '}';

        // Application Header Block
          // '2:' Block Identifier:
          // I/O Identifier: 'I' = Output
          // Message Type 3n: 103, 202, 920, 198
          // Destination Address (ôsterreich: BIC der OENB)
          // Message Priority: 'N' normal 'U' Urgent
        HelpStr += '{2:I103' + DELCHR(BankAcc."MT103 Address") + 'N' + '}'; // 'U' wÑre urgent
        // Rest (Monitoring, Verfallsdatum) optional

        // User Header Block: optional

        // Content Block
        HelpStr += '{4:';
        Outfile.WRITE(HelpStr);

        // :20:  Referenz Absender M 16x
        Outfile.WRITE(':20:' + PmtTools.Left_String(PmtHead."Gen. Journal Batch",16));

        // :23B: Bank Operation Code M 4!c
        Outfile.WRITE(':23B:CRED');

        // :23E: Instruction Code O 4!c [/30x]

        // :26T: Transaction Type Code O 3!c

        // :32A: Valutadatum, WÑhrung, Betrag 6n3a15d

        // :33B: WÑhrung, Betrag 3a15d (nur bei Umrechnung)
        IF PmtHead."Pmt. Currency Code" = '' THEN
          HelpStr := GLSetup."LCY Code"
        ELSE
          HelpStr := PmtHead."Pmt. Currency Code";
        HelpStr += FORMAT(Amount,0,'<integer><decimals,3>');
        HelpStr := CONVERTSTR(HelpStr,'.',',');
        Outfile.WRITE(':32A:' + FORMAT(ExecDate,0,'<year,2><month,2><day,2>') +
          HelpStr);

        // :50x:  Auftragger: Kontonr., Name u. Adresse [/34x] 4 x 35x
        // :52x:  Auftraggerbank: Kontonr. Name u. Adresse [/34x] 4 x 35x (opt.)
        // :53x:  Auftraggeber: Belastungskonto  (opt.)
        IF PmtHead."Orderer Bank IBAN Code" <> '' THEN BEGIN
          Outfile.WRITE(':50K:/' + DELCHR(PmtHead."Orderer Bank IBAN Code"));
          Outfile.WRITE(DELCHR(PmtHead."Orderer Bank BIC Code"));
          Outfile.WRITE(':53D:/' + DELCHR(PmtHead."Orderer Bank IBAN Code"));
        END ELSE BEGIN
          Outfile.WRITE(':50K:/' + DELCHR(PmtHead."Orderer Bank Account No."));
          Outfile.WRITE(DELCHR(PmtHead."Orderer Bank BIC Code"));
          Outfile.WRITE(':53D:/' + DELCHR(PmtHead."Orderer Bank Account No."));
        END;

        // :56x: Zwischenbank EmpfÑnger (opt.)
        HelpStr := GetIntermediaryBank(PmtHead);
        IF HelpStr <> '' THEN
          Outfile.WRITE(':56A:'+HelpStr);

        // :57x: KontofÅhrende Bank EmpfÑnger (o/m), lÑnderspezfisch
        IF PmtHead."Bank IBAN Code" <> '' THEN
          Outfile.WRITE(':57A:/' + DELCHR(PmtHead."Bank IBAN Code"))
        ELSE
           Outfile.WRITE(':57A:/' + DELCHR(PmtHead."Bank Account No."));
        Outfile.WRITE(DELCHR(PmtHead."Bank BIC Code"));

        // :59(x): EmpfÑnger: Kto.nr., Name, Adresse (34x, 4 x 35x)
        IF PmtHead."Bank IBAN Code" <> '' THEN
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank IBAN Code"))
        ELSE
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank Account No."));
        Outfile.WRITE(MT10nText(PmtHead.Name,FALSE));
        Outfile.WRITE(MT10nText(PmtHead.Address,FALSE));
        HelpStr := PmtHead."Country/Region Code";
        IF PmtHead."Post Code" <> '' THEN
          HelpStr += ' ' + PmtHead."Post Code";
        IF PmtHead.City <> '' THEN
          HelpStr += ' ' + PmtHead.City;
        HelpStr := DELCHR(HelpStr,'<>');
        Outfile.WRITE(MT10nText(HelpStr,FALSE));

        // :70:  Remittance Information o 4 x 35x
        IF PmtHead."Reason Row 1" <> '' THEN
          Outfile.WRITE(':70:' + MT10nText(PmtHead."Reason Row 1",TRUE));
        IF PmtHead."Reason Row 2" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 2",TRUE));
        IF PmtHead."Reason Row 3" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 3",TRUE));
        IF PmtHead."Reason Row 4" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 4",TRUE));

        // :71A: GebÅhrenregelung m 3!a
        Outfile.WRITE(':71A:SHA');

        // :71F: SenderÔs Charges m 3!a 15d
        // :71G: ReceiverÔs Charges m 3!a 15d
        // :72:  Sender-EmpfÑnger-Info o 6 x 35x
        // :77B: Regulatory Reporting 0 3 x 35x
        // :Trailer Block
        Outfile.WRITE('-}');

      UNTIL PmtHead.NEXT = 0;

      IF New THEN
        PmtHead.MODIFYALL("Payment Done",TRUE);
    END;

    PROCEDURE MT101File@5157828(VAR Outfile@5157804 : File;New@5157803 : Boolean;ExecDate@5157802 : Date) : Boolean;
    VAR
      SEPAXMLScheme@5157809 : Record 5157912;
      BankAcc2@5157807 : Record 270;
      CustBankAcc@5157810 : Record 287;
      VendBankAcc@5157808 : Record 288;
      Amount@5157806 : Decimal;
      HelpStr@5157805 : Text[100];
      SwiftHeader@5157814 : Boolean;
      VIR2000@5157813 : Boolean;
      Counter@5157812 : Integer;
      Total@5157811 : Decimal;
    BEGIN
      GLSetup.GET;
      IF ExecDate = 0D THEN
        ExecDate := PmtHead."Posting Date";

      SwiftHeader := TRUE;

      IF NOT SEPAXMLScheme.GET(
        PmtHead."Bal. Account No.",PmtHead."Payment Type Code",PmtHead."Orderer Country/Region Code")
      THEN
        IF NOT SEPAXMLScheme.GET(PmtHead."Bal. Account No.",PmtHead."Payment Type Code",'') THEN
          SEPAXMLScheme."Segment 50H:" := SEPAXMLScheme."Segment 50H:"::"Bank Address";

      IF SEPAXMLScheme."SWIFT Header" = SEPAXMLScheme."SWIFT Header"::No THEN
        SwiftHeader := FALSE;

      IF SEPAXMLScheme."Format VIR 2000" = SEPAXMLScheme."Format VIR 2000"::Yes THEN BEGIN
        VIR2000 := TRUE;
        SwiftHeader := FALSE;
      END;
      IF SwiftHeader THEN
        BankAcc.TESTFIELD("MT101 Address");

      // Basic Header, Application Header, User Header (opt.), MUG (Message User Group)
      HelpStr := '{1:F01' + DELCHR(BankAcc."MT101 Address") + PADSTR('0',10,'0') + '}';
      HelpStr += '{2:I101' + DELCHR(PmtHead."Orderer Bank BIC Code") + 'N}'; // ggf. plus: '3005' + '}';
      HelpStr += '{3:{113:XXXX}}';
      HelpStr += '{4:';
      IF SwiftHeader THEN BEGIN
      Outfile.WRITE(HelpStr);
      Outfile.WRITE(DELCHR(BankAcc."MT101 Address"));
      Outfile.WRITE(DELCHR(PmtHead."Orderer Bank BIC Code"));
      Outfile.WRITE ('101');
      END;

      // Sequence A
      IF NOT VIR2000 THEN BEGIN
      Outfile.WRITE(':20:' + PmtTools.Left_String(PmtHead."Gen. Journal Batch",16));
      Outfile.WRITE(':28D:' + '1/1');
      END ELSE BEGIN
        Outfile.WRITE(':20:' + DELCHR(PmtHead."Orderer Bank BIC Code"));
        Outfile.WRITE(':21R:' + PmtTools.Left_String(PmtHead."Gen. Journal Batch",16));
      END;

      IF PmtHead."Bank Account Owner" <> '' THEN BEGIN
        HelpStr := DELCHR(COPYSTR(PmtHead."Bank Account Owner",1,29),'>');
        HelpStr += FORMAT(PmtHead."Posting Date",0,'<year,2><month,2><day,2>');
        HelpStr := MT10nText(HelpStr,FALSE);
        Outfile.WRITE(':50L:' + HelpStr);
      END;
      IF PmtHead."Orderer Bank IBAN Code" <> '' THEN
        Outfile.WRITE(':50H:/' + DELCHR(PmtHead."Orderer Bank IBAN Code"))
      ELSE
        Outfile.WRITE(':50H:/' + DELCHR(PmtHead."Orderer Bank Account No."));

      IF SEPAXMLScheme."Segment 50H:" = SEPAXMLScheme."Segment 50H:"::"Company Address" THEN BEGIN
        Outfile.WRITE(MT10nText(CompInfo.Name,FALSE));
        Outfile.WRITE(MT10nText(CompInfo.Address,FALSE));
        Outfile.WRITE(MT10nText(CompInfo."Post Code" + ' ' + CompInfo.City,FALSE));
      END ELSE BEGIN
        IF BankAcc.Name <> '' THEN
          Outfile.WRITE(MT10nText(BankAcc.Name,FALSE));
        IF BankAcc."Name 2" <> '' THEN
          Outfile.WRITE(MT10nText(BankAcc."Name 2",FALSE));
        IF BankAcc.Address <> '' THEN
          Outfile.WRITE(MT10nText(BankAcc.Address,FALSE));
        IF BankAcc."Address 2" <> '' THEN
          Outfile.WRITE(MT10nText(BankAcc."Address 2",FALSE));

        HelpStr := '';
        IF BankAcc.City <> '' THEN BEGIN
          IF BankAcc."Post Code" <> '' THEN
            HelpStr := BankAcc."Post Code" + ' ';
          HelpStr += BankAcc.City;
          IF HelpStr <> '' THEN
            Outfile.WRITE(MT10nText(HelpStr,FALSE));
        END;
      END;

      IF VIR2000 THEN
        Outfile.WRITE(':52A:' + DELCHR(PmtHead."Orderer Bank BIC Code"));

      Outfile.WRITE(':30:' + FORMAT(ExecDate,0,'<year,2><month,2><day,2>'));

      // Sequence B
      REPEAT
        PmtHead.CALCFIELDS("Payment Amount");
        Amount := ABS(PmtHead."Payment Amount");
        Counter += 1;
        Total += Amount;
        IF STRLEN(FORMAT(Amount,0,'<integer><decimal,3>')) > 15 THEN
          ERROR(Text031);

        IF NOT VIR2000 THEN
        Outfile.WRITE(':21:' + FORMAT(TODAY,0,'<year,2><month,2><day,2>') +
          PADSTR(FORMAT(PmtHead."Gen. Journal Line"),10));

        // gbedv AZV -------------------------------------------------- BEGIN
        // 23E     Instruction Code
        IF LicPermission.GET(LicPermission."Object Type"::Table,DATABASE::"OPplus AZV Pmt. Setup") THEN
          IF (LicPermission."Execute Permission" = LicPermission."Execute Permission"::Yes) THEN
            IF PmtHead."Instruction 1" <> '' THEN
              Outfile.WRITE(PADSTR(PmtHead."Instruction 1",4));
        // gbedv AZV -------------------------------------------------- END

        IF PmtHead."Pmt. Currency Code" = '' THEN
          HelpStr := GLSetup."LCY Code"
        ELSE
          HelpStr := PmtHead."Pmt. Currency Code";
        HelpStr += FORMAT(Amount,0,'<integer><decimals,3>');
        HelpStr := CONVERTSTR(HelpStr,'.',',');
        Outfile.WRITE(':32B:' + HelpStr);

        HelpStr := GetIntermediaryBank(PmtHead);
        IF HelpStr <> '' THEN
          Outfile.WRITE(':56A:' + HelpStr);

        IF PmtHead."Bank BIC Code" <> '' THEN
        Outfile.WRITE(':57A:' + DELCHR(PmtHead."Bank BIC Code"));

        IF PmtHead."Account Type" = PmtHead."Account Type"::Customer THEN BEGIN
          IF CustBankAcc.GET(PmtHead."Account No.",PmtHead."Bank Code") THEN
            IF CustBankAcc."Routing No." <> '' THEN BEGIN
              Outfile.WRITE(':57D://FW' + CustBankAcc."Routing No.");
              IF CustBankAcc.Name <> '' THEN
                Outfile.WRITE(MT10nText(CustBankAcc.Name,FALSE));
              IF CustBankAcc."Name 2" <> '' THEN
                Outfile.WRITE(MT10nText(CustBankAcc."Name 2",FALSE));
              IF CustBankAcc.Address <> '' THEN
                Outfile.WRITE(MT10nText(CustBankAcc.Address,FALSE));
              HelpStr := '';
              IF CustBankAcc.City <> '' THEN BEGIN
                IF CustBankAcc."Post Code" <> '' THEN
                  HelpStr := CustBankAcc."Post Code" + ' ';
                HelpStr += CustBankAcc.City;
                IF HelpStr <> '' THEN
                  Outfile.WRITE(MT10nText(HelpStr,FALSE));
              END;
            END;
        END ELSE IF PmtHead."Account Type" = PmtHead."Account Type"::Vendor THEN BEGIN
          IF VendBankAcc.GET(PmtHead."Account No.",PmtHead."Bank Code") THEN
            IF VendBankAcc."Routing No." <> '' THEN BEGIN
              Outfile.WRITE(':57D://FW' + VendBankAcc."Routing No.");
              IF VendBankAcc.Name <> '' THEN
                Outfile.WRITE(MT10nText(VendBankAcc.Name,FALSE));
              IF VendBankAcc."Name 2" <> '' THEN
                Outfile.WRITE(MT10nText(VendBankAcc."Name 2",FALSE));
              IF VendBankAcc.Address <> '' THEN
                Outfile.WRITE(MT10nText(VendBankAcc.Address,FALSE));
              HelpStr := '';
              IF VendBankAcc.City <> '' THEN BEGIN
                IF VendBankAcc."Post Code" <> '' THEN
                  HelpStr := VendBankAcc."Post Code" + ' ';
                HelpStr += VendBankAcc.City;
                IF HelpStr <> '' THEN
                  Outfile.WRITE(MT10nText(HelpStr,FALSE));
              END;
            END;
        END;

        IF PmtHead."Bank IBAN Code" <> '' THEN
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank IBAN Code"))
        ELSE
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank Account No."));
        Outfile.WRITE(MT10nText(PmtHead.Name,FALSE));
        Outfile.WRITE(MT10nText(PmtHead.Address,FALSE));
        HelpStr := PmtHead."Country/Region Code";
        IF PmtHead."Post Code" <> '' THEN
          HelpStr += ' ' + PmtHead."Post Code";
        IF PmtHead.City <> '' THEN
          HelpStr += ' ' + PmtHead.City;
        HelpStr := DELCHR(HelpStr,'<>');
        Outfile.WRITE(MT10nText(HelpStr,FALSE));

        IF PmtHead."Reason Row 1" <> '' THEN
          Outfile.WRITE(':70:' + MT10nText(PmtHead."Reason Row 1",TRUE));
        IF PmtHead."Reason Row 2" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 2",TRUE));
        IF PmtHead."Reason Row 3" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 3",TRUE));
        IF PmtHead."Reason Row 4" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 4",TRUE));

        CASE PmtHead.Charges OF
          PmtHead.Charges::Beneficiary: HelpStr := 'BEN';
          PmtHead.Charges::Orderer: HelpStr := 'OUR';
          ELSE HelpStr := 'SHA';
        END;
        Outfile.WRITE(':71A:' + HelpStr);

        IF BankAcc."Account To Charge" <> '' THEN BEGIN
          BankAcc2.GET(BankAcc."Account To Charge");
          BankAcc2.IBAN := DELCHR(BankAcc2.IBAN);
          BankAcc2."Bank Account No." := DELCHR(BankAcc2."Bank Account No.");
          IF BankAcc2.IBAN <> '' THEN BEGIN
            HelpStr := DELCHR(BankAcc.IBAN);
            IF (HelpStr <> '') AND (HelpStr <> BankAcc2.IBAN) THEN
              Outfile.WRITE(':25A:/' + BankAcc2.IBAN)
            ELSE IF STRPOS(BankAcc2.IBAN,DELCHR(BankAcc."Bank Account No.")) < 1 THEN
              Outfile.WRITE(':25A:/' + BankAcc2.IBAN);
          END ELSE BEGIN
            HelpStr := DELCHR(BankAcc."Bank Account No.");
            IF (HelpStr <> '') AND (HelpStr <> BankAcc2."Bank Account No.") THEN
              Outfile.WRITE(':25A:/' + BankAcc2."Bank Account No.")
            ELSE IF STRPOS(DELCHR(BankAcc.IBAN),BankAcc2."Bank Account No.") < 1 THEN
              Outfile.WRITE(':25A:/' + BankAcc2."Bank Account No.");
          END;
        END;
      UNTIL PmtHead.NEXT = 0;
      // Trailer

      IF SwiftHeader THEN
      Outfile.WRITE('-}');

      IF VIR2000 THEN BEGIN
        Outfile.WRITE(':19A:' + PmtTools.FillZeroBeforeText(FORMAT(Counter),4));
        Outfile.WRITE(':19:' + FORMAT(Total,0,'<integer><decimals,3>'));
      END;

      IF New THEN
        PmtHead.MODIFYALL("Payment Done",TRUE);
    END;

    PROCEDURE MT104File@5157835(VAR Outfile@5157806 : File;New@5157803 : Boolean;ExecDate@5157808 : Date);
    VAR
      BankAcc2@5157802 : Record 270;
      Amount@5157805 : Decimal;
      HelpStr@5157804 : Text[100];
    BEGIN
      BankAcc.TESTFIELD("MT101 Address");
      GLSetup.GET;
      IF ExecDate = 0D THEN
        ExecDate := PmtHead."Posting Date";

      // Basic Header, Application Header, User Header (opt.), MUG (Message User Group)
      // HelpStr := '{1:F01' + DELCHR(BankAcc."MT101 Address") + PADSTR('0',10,'0') + '}';
      // HelpStr += '{2:I101' + DELCHR(PmtHead."Orderer Bank BIC Code") + 'N}'; // ggf. plus: '3005' + '}';
      // HelpStr += '{3:{113:XXXX}}';
      // HelpStr += '{4:';
      // Outfile.WRITE(HelpStr);
      // Outfile.WRITE(DELCHR(BankAcc."MT101 Address"));
      Outfile.WRITE ('{-');
      Outfile.WRITE(DELCHR(BankAcc."MT101 Address"));
      Outfile.WRITE(DELCHR(PmtHead."Orderer Bank BIC Code"));
      Outfile.WRITE ('104');

      // Sequence A
      Outfile.WRITE(':20:' + PmtTools.Left_String(PmtHead."Gen. Journal Batch",16));
      Outfile.WRITE(':23E:RFDD');
      IF PmtHead."Bank Account Owner" <> '' THEN BEGIN
        HelpStr := DELCHR(COPYSTR(PmtHead."Bank Account Owner",1,29),'>');
        HelpStr += FORMAT(PmtHead."Posting Date",0,'<year,2><month,2><day,2>');
        Outfile.WRITE(':50L:' + HelpStr);
      END;
      Outfile.WRITE(':30:' + FORMAT(ExecDate,0,'<year,2><month,2><day,2>'));

      IF PmtHead."Orderer Bank IBAN Code" <> '' THEN
        Outfile.WRITE(':50K:/' + DELCHR(PmtHead."Orderer Bank IBAN Code"))
      ELSE
        Outfile.WRITE(':50K:/' + DELCHR(PmtHead."Orderer Bank Account No."));

      Outfile.WRITE(MT10nText(CompInfo.Name,FALSE));
      Outfile.WRITE(MT10nText(CompInfo.Address,FALSE));
      Outfile.WRITE(MT10nText(CompInfo."Post Code" + ' ' + CompInfo.City,FALSE));

      Outfile.WRITE(':52A:' + DELCHR(PmtHead."Orderer Bank BIC Code"));


      // Sequence B
      REPEAT
        PmtHead.CALCFIELDS("Payment Amount");
        Amount := ABS(PmtHead."Payment Amount");
        IF STRLEN(FORMAT(Amount,0,'<integer><decimal,3>')) > 15 THEN
          ERROR(Text031);

        Outfile.WRITE(':21:' + FORMAT(TODAY,0,'<year,2><month,2><day,2>') +
          PADSTR(FORMAT(PmtHead."Gen. Journal Line"),10));

        // 23E     Instruction Code
        Outfile.WRITE(':23E:OTHR/TYPE/01');

        Outfile.WRITE(':21D:' + PmtHead."Gen. Journal Batch" + FORMAT(PmtHead."Gen. Journal Line"));

        IF PmtHead."Pmt. Currency Code" = '' THEN
          HelpStr := GLSetup."LCY Code"
        ELSE
          HelpStr := PmtHead."Pmt. Currency Code";
        HelpStr += FORMAT(Amount,0,'<integer><decimals,3>');
        HelpStr := CONVERTSTR(HelpStr,'.',',');
        Outfile.WRITE(':32B:' + HelpStr);

        HelpStr := GetIntermediaryBank(PmtHead);
        IF HelpStr <> '' THEN
          Outfile.WRITE(':56A:' + HelpStr);
        Outfile.WRITE(':57C://' + DELCHR(PmtHead."Country/Region Code") + DELCHR(PmtHead."Bank Branch No."));
        IF PmtHead."Bank IBAN Code" <> '' THEN
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank IBAN Code"))
        ELSE
          Outfile.WRITE(':59:/' + DELCHR(PmtHead."Bank Account No."));
        Outfile.WRITE(MT10nText(PmtHead.Name,FALSE));
        Outfile.WRITE(MT10nText(PmtHead.Address,FALSE));
        HelpStr := PmtHead."Country/Region Code";
        IF PmtHead."Post Code" <> '' THEN
          HelpStr += ' ' + PmtHead."Post Code";
        IF PmtHead.City <> '' THEN
          HelpStr += ' ' + PmtHead.City;
        HelpStr := DELCHR(HelpStr,'<>');
        Outfile.WRITE(MT10nText(HelpStr,FALSE));

        // :70:  Remittance Information o 4 x 35x
        IF PmtHead."Reason Row 1" <> '' THEN
          Outfile.WRITE(':70:' + MT10nText(PmtHead."Reason Row 1",TRUE));
        IF PmtHead."Reason Row 2" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 2",TRUE));
        IF PmtHead."Reason Row 3" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 3",TRUE));
        IF PmtHead."Reason Row 4" <> '' THEN
          Outfile.WRITE(MT10nText(PmtHead."Reason Row 4",TRUE));

        // :71A: GebÅhrenregelung m 3!a
        CASE PmtHead.Charges OF
          PmtHead.Charges::Beneficiary: HelpStr := 'BEN';
          PmtHead.Charges::Orderer: HelpStr := 'OUR';
          ELSE HelpStr := 'SHA';
        END;
        Outfile.WRITE(':71A:' + HelpStr);

        IF BankAcc."Account To Charge" <> '' THEN BEGIN
          BankAcc2.GET(BankAcc."Account To Charge");
          BankAcc2.IBAN := DELCHR(BankAcc2.IBAN);
          BankAcc2."Bank Account No." := DELCHR(BankAcc2."Bank Account No.");
          IF BankAcc2.IBAN <> '' THEN BEGIN
            HelpStr := DELCHR(BankAcc.IBAN);
            IF (HelpStr <> '') AND (HelpStr <> BankAcc2.IBAN) THEN
              Outfile.WRITE(':25A:/' + BankAcc2.IBAN)
            ELSE IF STRPOS(BankAcc2.IBAN,DELCHR(BankAcc."Bank Account No.")) < 1 THEN
              Outfile.WRITE(':25A:/' + BankAcc2.IBAN);
          END ELSE BEGIN
            HelpStr := DELCHR(BankAcc."Bank Account No.");
            IF (HelpStr <> '') AND (HelpStr <> BankAcc2."Bank Account No.") THEN
              Outfile.WRITE(':25A:/' + BankAcc2."Bank Account No.")
            ELSE IF STRPOS(DELCHR(BankAcc.IBAN),BankAcc2."Bank Account No.") < 1 THEN
              Outfile.WRITE(':25A:/' + BankAcc2."Bank Account No.");
          END;
        END;

      UNTIL PmtHead.NEXT = 0;
      // Trailer
      Outfile.WRITE('-}');

      IF New THEN
        PmtHead.MODIFYALL("Payment Done",TRUE);
    END;

    PROCEDURE GetIntermediaryBank@5157841(PaymentHead@5157802 : Record 5157896) : Text[30];
    VAR
      CustBankAcc@5157804 : Record 287;
      VendBankAcc@5157803 : Record 288;
      HelpStr@5157805 : Text[30];
    BEGIN
      WITH PaymentHead DO BEGIN
        IF "Account Type" = "Account Type"::Vendor THEN BEGIN
          IF (VendBankAcc."Vendor No." <> "Account No.") OR (VendBankAcc.Code <> "Bank Code") THEN
            VendBankAcc.GET("Account No.","Bank Code");
          HelpStr := DELCHR(VendBankAcc."Routing No.");
        END ELSE IF "Account Type" = "Account Type"::Customer THEN BEGIN
          IF (CustBankAcc."Customer No." <> "Account No.") OR (CustBankAcc.Code <> "Bank Code") THEN
            CustBankAcc.GET("Account No.","Bank Code");
          HelpStr := DELCHR(CustBankAcc."Routing No.");
        END ELSE
          EXIT('');
      END;
      // q.a.d. Test auf BIC
      IF (STRLEN(HelpStr) IN [8,11]) AND (DELCHR(COPYSTR(HelpStr,1,6),'=','ABCDEFGHIJKLMNOPQRSTUVWXYZ')='') THEN
        EXIT(HelpStr)
      ELSE
        EXIT('');
    END;

    PROCEDURE MT10nText@5157824(TextPara@5157802 : Text[100];Freetext@5157804 : Boolean) : Text[100];
    VAR
      Pos@5157803 : Integer;
    BEGIN
      TextPara := UPPERCASE(PmtTools.SEPA_String(TextPara));
      IF Freetext THEN BEGIN
        TextPara := DELCHR(TextPara,'<','/');
        Pos := STRPOS(TextPara,'//');
        WHILE Pos > 0 DO BEGIN
          TextPara := COPYSTR(TextPara,1,Pos-1) + '++' + COPYSTR(TextPara,Pos+2);
          Pos := STRPOS(TextPara,'//');
        END;
        // SEPA-String hat geschweifte Klammern bereits entfernt, sonst:
        // TextPara := CONVERTSTR(TextPara,'{','(');
        // TextPara := CONVERTSTR(TextPara,'}',')');
      END;
      EXIT(COPYSTR(TextPara,1,35));
    END;

    PROCEDURE CheckBankAccHistory@5157837(BankAccMandateHistory@5157802 : Record 5157915;BankAccMandate@5157803 : Record 5157913;Name@5157805 : Text[50];Orderer@5157806 : Text[50];Sender@5157807 : Text[50];Identifier@5157808 : Text[50];Owner@5157809 : Text[50]) : Boolean;
    VAR
      BankAccHistory2@5157804 : Record 5157915;
    BEGIN
      IF (BankAccMandate."Account No." <> BankAccMandateHistory."Account No.") OR
         (BankAccMandate."Account Type" <> BankAccMandateHistory."Account Type") OR
         (BankAccMandate.IBAN <> BankAccMandateHistory.IBAN) OR
         (BankAccMandate."SWIFT Code" <> BankAccMandateHistory."SWIFT Code") OR
         (BankAccMandate."Mandate Date" <> BankAccMandateHistory."Mandate Date") OR
         (BankAccMandate."Mandate Type" <> BankAccMandateHistory."Mandate Type") OR
         (BankAccMandate."Mandate Frequency" <> BankAccMandateHistory."Mandate Frequency") OR
         (BankAccMandate."Mandate Status" <> BankAccMandateHistory."Mandate Status") OR
         (Owner <> BankAccMandateHistory.Owner) OR
         (Name <> BankAccMandateHistory.Name) OR
         (Orderer <> BankAccMandateHistory.Orderer) OR
         (Sender <> BankAccMandateHistory.Sender) OR
         (Identifier <> BankAccMandateHistory.Identifier)
      THEN BEGIN
        BankAccHistory2 := BankAccMandateHistory;
        BankAccHistory2."Version No." := BankAccMandateHistory."Version No." + 1;
        BankAccHistory2."Account No." := BankAccMandate."Account No.";
        BankAccHistory2."Account Type" := BankAccMandate."Account Type";
        BankAccHistory2.IBAN := BankAccMandate.IBAN;
        BankAccHistory2."SWIFT Code" := BankAccMandate."SWIFT Code";
        BankAccHistory2."Mandate Date" := BankAccMandate."Mandate Date";
        BankAccHistory2."Mandate Type" := BankAccMandate."Mandate Type";
        BankAccHistory2."Mandate Frequency" := BankAccMandate."Mandate Frequency";
        BankAccHistory2."Mandate Status" := BankAccMandate."Mandate Status";
        BankAccHistory2.Owner := Owner;
        BankAccHistory2.Name := Name;
        BankAccHistory2.Orderer := Orderer;
        BankAccHistory2.Sender := Sender;
        BankAccHistory2.Identifier := Identifier;
        BankAccHistory2.INSERT;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    {
      -----------------------------------------------------
      (c) gbedv, OPplus, All rights reserved

      No.  Date       changed
      -----------------------------------------------------
      PMT  01.11.08   OPplus Payment
                      - Object created
      CLR  01.11.08   German AddOn
      AZV  13.01.10   OPplus DTAZV
      -----------------------------------------------------

      +-----------------------------------------------------+
      |               GOB Software & Systeme                |
      +-----------------------------------------------------+
      |                Home24 Anpassungen                   |
      -------------------------------------------------------

      Version                            Bemerkung
      -------------------------------------------------------
      P1121     gob-czi     15.10.13     Print Payment Letter


      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________
      H0636       12.11.13 HCN       Function
      H0882       20.02.14 ARI       Fill Field "Payment Direction
      H1454       23.10.14 ARI       Use clearing Accounts for Refunds
    }
    END.
  }
}

