OBJECT Codeunit 50084 Run NAS WebService
{
  OBJECT-PROPERTIES
  {
    Date=10.07.15;
    Time=12:36:32;
    Modified=Yes;
    Version List=HME4368;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            ObjectL@1000000004 : Record 2000000001;
            CompanyL@1000000003 : Record 2000000006;
            MultipleCompaniesSetupL@1000000005 : Record 50329;
            OtherCompanyL@1000000000 : Text[30];
            ObjectTypeL@1000000002 : Text[30];
            ObjectIDL@1000000001 : Integer;
          BEGIN
            MultipleCompaniesSetupL.GET;

            IF "Parameter String" = '' THEN
              ERROR(TextHME50001);

            OtherCompanyL  := SELECTSTR(1,"Parameter String");
            ObjectTypeL := SELECTSTR(2,"Parameter String");
            EVALUATE(ObjectIDL,SELECTSTR(3,"Parameter String"));
            EVALUATE(ObjectL.Type,ObjectTypeL);

            ObjectL.GET(ObjectL.Type,'',ObjectIDL);
            CompanyL.GET(OtherCompanyL);

            IF ISCLEAR(XmlDoc) THEN
              CREATE(XmlDoc);
            IF ISCLEAR(XmlHttp) THEN
              CREATE(XmlHttp);

            XmlHttp.setProxy(2,' ','*');

            ConnectionLink := STRSUBSTNO(TextHME50004,MultipleCompaniesSetupL."Server Name",MultipleCompaniesSetupL."Web Service Port",
                                         MultipleCompaniesSetupL."Server Instance",OtherCompanyL,MultipleCompaniesSetupL."Service Name");

            XmlHttp.open('POST',ConnectionLink,0);
            XmlHttp.setRequestHeader('Content-Type', 'text/xml; charset=utf-8');
            XmlHttp.setRequestHeader('SOAPAction',MultipleCompaniesSetupL."Service Name");

            XmlHttp.setTimeouts(10000,10000,10000,100000);  //change last parameter to zero to wait till webservice finishes the job

            XmlHttp.send('<?xml version="1.0" encoding="utf-8"?><soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/">' +
            '<soap:Body><RunJob xmlns="urn:microsoft-dynamics-schemas/codeunit/' + MultipleCompaniesSetupL."Service Name" + '">' +
            '<objectTypeV>'+ObjectTypeL+'</objectTypeV><objectIDV>'+FORMAT(ObjectIDL)+'</objectIDV>'+
            '</RunJob></soap:Body></soap:Envelope>');

            IF XmlHttp.status <> 200 THEN
              MESSAGE(TextHME50003,FORMAT(XmlHttp.status),XmlHttp.statusText);

            XmlDoc.async := FALSE;
            XmlDoc.load(XmlHttp.responseBody);

            XmlNode := XmlDoc.selectSingleNode('//Soap:Envelope/Soap:Body/RunJob_Result/return_value/');

            IF NOT ISCLEAR(XmlNode) THEN BEGIN
              IF XmlNode.text <> TextHME_Finished THEN
                ERROR(XmlNode.text)
            END ELSE BEGIN
              XmlDoc.save(MultipleCompaniesSetupL."Failed Request File Path");
              ERROR(TextHME50002,MultipleCompaniesSetupL."Failed Request File Path");
            END;

            CLEAR(XmlDoc);
            CLEAR(XmlHttp)
          END;

  }
  CODE
  {
    VAR
      TextHME_Finished@1000000001 : TextConst 'ENU=NAS JOB FINISHED';
      TextHME50000@1000000003 : TextConst 'ENU=Company %1 does not exists.';
      XmlHttp@1000000008 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A0B-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.ServerXMLHTTP60";
      XmlDoc@1000000005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      XmlNode@1000000002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      myfile@1000000006 : File;
      TextHME50001@1000000000 : TextConst 'ENU=Please Specify a Parameter string e.g.  Cronus,Codeunit,50010.';
      TextHME50002@1000000007 : TextConst 'ENU=Unhandled Error. Please check the xml file in the folder %1.';
      TextHME50003@1000000004 : TextConst 'ENU=Http Error %1: %2.';
      TextHME50004@1000000009 : TextConst 'ENU=http://%1:%2/%3/WS/%4/Codeunit/%5';
      ConnectionLink@1000000010 : Text[250];

    PROCEDURE RunJob@1000000003(ObjectTypeV@1000000000 : Text[30];ObjectIDV@1000000001 : Integer) : Text[1024];
    VAR
      JobQueueEntryL@1000000002 : Record 472;
    BEGIN
      CLEAR(JobQueueEntryL);
      EVALUATE(JobQueueEntryL."Object Type to Run",ObjectTypeV);
      JobQueueEntryL."Object ID to Run" := ObjectIDV;
      JobQueueEntryL."User ID" := USERID;

      IF CODEUNIT.RUN(CODEUNIT::"Job Queue Start Codeunit",JobQueueEntryL) THEN
        EXIT(TextHME_Finished)
      ELSE
        EXIT(GETLASTERRORTEXT);
    END;

    EVENT XmlDoc@1000000005::ondataavailable@198();
    BEGIN
    END;

    EVENT XmlDoc@1000000005::onreadystatechange@-609();
    BEGIN
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      H4368       17.06.15 BGA       Created
    }
    END.
  }
}

