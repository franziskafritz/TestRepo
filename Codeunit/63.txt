OBJECT Codeunit 63 Sales-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=16.08.10;
    Time=06:00:00;
    Version List=NAVW16.00.01,eBay1.06.24;
  }
  PROPERTIES
  {
    TableNo=37;
    OnRun=VAR
            FromDocDim@1000 : Record 357;
            ToDocDim@1001 : Record 357;
            TempDocDim@1002 : TEMPORARY Record 357;
            DimMgt@1003 : Codeunit 408;
            Selection@1004 : Integer;
            "-eBay"@5095800 : Integer;
            eBaySales@5095801 : Codeunit 5095806;
            eBaySetup@5095802 : Record 5095800;
            PriceTotalBOM@5095803 : Decimal;
            PriceTotal@5095804 : Decimal;
            firstLine@5095805 : Boolean;
            lineDiscountPercent@1000000000 : Decimal;
          BEGIN
            //eBay#005
            firstLine := TRUE;
            //eBay#005
            //eBay#002
            eBaySetup.GET();
            //PriceTotal := "Unit Price";
            PriceTotal := "Unit Price" * Quantity;
            //ebay#004
            //eBay#002
            //eBay#007
            lineDiscountPercent := "Line Discount %";
            //eBay#007

            TESTFIELD(Type,Type::Item);
            TESTFIELD("Quantity Shipped",0);
            TESTFIELD("Return Qty. Received",0);
            CALCFIELDS("Reserved Qty. (Base)");
            TESTFIELD("Reserved Qty. (Base)",0);
            IF "Purch. Order Line No." <> 0 THEN
              ERROR(
                Text000,
                "Purchase Order No.");
            IF "Job Contract Entry No." <> 0 THEN BEGIN
              TESTFIELD("Job No.",'');
              TESTFIELD("Job Contract Entry No.",0);
            END;
            SalesHeader.GET("Document Type","Document No.");
            SalesHeader.TESTFIELD(Status,SalesHeader.Status::Open);
            FromBOMComp.SETRANGE("Parent Item No.","No.");
            NoOfBOMComp := FromBOMComp.COUNT;
            IF NoOfBOMComp = 0 THEN
              ERROR(
                Text001,
                "No.");

            //eBay#001
            IF Auswahl<> 0 THEN
              Selection := Auswahl
            ELSE
            //eBay#001

            Selection := STRMENU(Text004,2);
            IF Selection = 0 THEN
              EXIT;

            ToSalesLine.RESET;
            ToSalesLine.SETRANGE("Document Type","Document Type");
            ToSalesLine.SETRANGE("Document No.","Document No.");
            ToSalesLine := Rec;
            IF ToSalesLine.FIND('>') THEN BEGIN
              LineSpacing := (ToSalesLine."Line No." - "Line No.") DIV (1 + NoOfBOMComp);
              IF LineSpacing = 0 THEN
                ERROR(Text003);
            END ELSE
              LineSpacing := 10000;

            IF "Document Type" IN ["Document Type"::Order,"Document Type"::Invoice] THEN BEGIN
              ToSalesLine := Rec;
              FromBOMComp.SETRANGE(Type,FromBOMComp.Type::Item);
              FromBOMComp.SETFILTER("No.",'<>%1','');
              IF FromBOMComp.FINDSET THEN
                REPEAT
                  FromBOMComp.TESTFIELD(Type,FromBOMComp.Type::Item);
                  Item.GET(FromBOMComp."No.");
                  //eBay#003
                  IF NOT NoConfirm THEN  BEGIN
                    ToSalesLine.SetLagerPr’ung(TRUE);
                    ToSalesLine.SetNoConfirm(TRUE);
                    ToSalesLine.SetNoMessage(TRUE);
                  END;
                  //eBay#003

                  ToSalesLine."Line No." := 0;
                  ToSalesLine."No." := FromBOMComp."No.";
                  ToSalesLine."Variant Code" := FromBOMComp."Variant Code";
                  ToSalesLine."Unit of Measure Code" := FromBOMComp."Unit of Measure Code";
                  ToSalesLine."Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,FromBOMComp."Unit of Measure Code");
                  ToSalesLine."Outstanding Quantity" := ROUND("Quantity (Base)" * FromBOMComp."Quantity per",0.00001);
                  IF ToSalesLine."Outstanding Quantity" > 0 THEN
                    //eBay#003
                    IF NOT NoConfirm THEN
                    //eBay#003
                    ItemCheckAvail.SalesLineCheck(ToSalesLine);
                UNTIL FromBOMComp.NEXT = 0;
            END;

            IF "BOM Item No." = '' THEN
              BOMItemNo := "No."
            ELSE
              BOMItemNo := "BOM Item No.";

            ToSalesLine := Rec;
            ToSalesLine.INIT;
            ToSalesLine.Description := Description;
            ToSalesLine."Description 2" := "Description 2";
            ToSalesLine."BOM Item No." := BOMItemNo;
            ToSalesLine.MODIFY;

            FromDocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
            FromDocDim.SETRANGE("Document Type","Document Type");
            FromDocDim.SETRANGE("Document No.","Document No.");
            FromDocDim.SETRANGE("Line No.","Line No.");

            ToDocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
            ToDocDim.SETRANGE("Document Type","Document Type");
            ToDocDim.SETRANGE("Document No.","Document No.");

            DimMgt.CopyDocDimToDocDim(FromDocDim,TempDocDim);
            CLEAR(TempDocDim);
            FromDocDim.DELETEALL;

            FromBOMComp.RESET;
            FromBOMComp.SETRANGE("Parent Item No.","No.");
            FromBOMComp.FINDSET;
            NextLineNo := "Line No.";
            REPEAT
              ToSalesLine.INIT;
              NextLineNo := NextLineNo + LineSpacing;
              ToSalesLine."Line No." := NextLineNo;
              CASE FromBOMComp.Type OF
                FromBOMComp.Type::" ":
                  ToSalesLine.Type := ToSalesLine.Type::" ";
                FromBOMComp.Type::Item:
                  ToSalesLine.Type := ToSalesLine.Type::Item;
                FromBOMComp.Type::Resource:
                  ToSalesLine.Type := ToSalesLine.Type::Resource;
              END;
              IF ToSalesLine.Type <> ToSalesLine.Type::" " THEN BEGIN
                FromBOMComp.TESTFIELD("No.");
                ToSalesLine.VALIDATE("No.",FromBOMComp."No.");
                IF SalesHeader."Location Code" <> "Location Code" THEN
                  ToSalesLine.VALIDATE("Location Code","Location Code");
                IF FromBOMComp."Variant Code" <> '' THEN
                  ToSalesLine.VALIDATE("Variant Code",FromBOMComp."Variant Code");
                IF ToSalesLine.Type = ToSalesLine.Type::Item THEN BEGIN
                  ToSalesLine."Drop Shipment" := "Drop Shipment";
                  Item.GET(FromBOMComp."No.");
                  ToSalesLine.VALIDATE("Unit of Measure Code",FromBOMComp."Unit of Measure Code");
                  ToSalesLine."Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,ToSalesLine."Unit of Measure Code");
                  ToSalesLine.VALIDATE(Quantity,
                    ROUND(
                      "Quantity (Base)" * FromBOMComp."Quantity per" *
                      UOMMgt.GetQtyPerUnitOfMeasure(Item,ToSalesLine."Unit of Measure Code") /
                      ToSalesLine."Qty. per Unit of Measure",
                      0.00001));
                END ELSE
                  ToSalesLine.VALIDATE(Quantity,"Quantity (Base)" * FromBOMComp."Quantity per");

              END;
              IF SalesHeader."Language Code" = '' THEN
                ToSalesLine.Description := FromBOMComp.Description
              ELSE
                IF NOT ItemTranslation.GET(FromBOMComp."No.",FromBOMComp."Variant Code",SalesHeader."Language Code") THEN
                  ToSalesLine.Description := FromBOMComp.Description;

              ToSalesLine."BOM Item No." := BOMItemNo;
              //eBay#002
              PriceTotalBOM := PriceTotalBOM + ToSalesLine."Unit Price" *ToSalesLine.Quantity;
              //#eBay#005
              IF firstLine THEN BEGIN
                ToSalesLine."Processing of order account" := Rec."Processing of order account";
                //#eBay#006
                ToSalesLine."Account Code" := Rec."Account Code";
                ToSalesLine."Amazon Order No." := Rec."Amazon Order No.";
                ToSalesLine."Amazon Order Item No." := Rec."Amazon Order Item No.";
                //#eBay#006
                ToSalesLine."Shipping Charges to be paid" := "Shipping Charges to be paid";
                ToSalesLine."Shipping and Handling Charges" := "Shipping and Handling Charges";
                ToSalesLine."Shipping and Handling Charges" := "Packing Costs";
                firstLine := FALSE;
              END
              ELSE BEGIN
                ToSalesLine."Processing of order account" := '';
                //#eBay#006
                ToSalesLine."Account Code" := '';
                ToSalesLine."Amazon Order No." := '';
                ToSalesLine."Amazon Order Item No." := '';
                //#eBay#006
                ToSalesLine."Shipping Charges to be paid" := FALSE;
                ToSalesLine."Shipping and Handling Charges" := FALSE;
                ToSalesLine."Shipping and Handling Charges" := FALSE;
              END;
              //eBay#005
              ToSalesLine."Price to adjust" := TRUE;
              //eBay#002

              ToSalesLine.INSERT;

              IF (ToSalesLine.Type = ToSalesLine.Type::Item) AND (ToSalesLine.Reserve = ToSalesLine.Reserve::Always) THEN
                ToSalesLine.AutoReserve;

              IF Selection = 1 THEN BEGIN
                ToSalesLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
                ToSalesLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
                ToSalesLine.MODIFY;
                ToDocDim.SETRANGE("Line No.",ToSalesLine."Line No.");
                ToDocDim.DELETEALL;
                IF TempDocDim.FINDSET THEN BEGIN
                  REPEAT
                    ToDocDim.INIT;
                    ToDocDim."Table ID" := DATABASE::"Sales Line";
                    ToDocDim."Document Type" := ToSalesLine."Document Type";
                    ToDocDim."Document No." := ToSalesLine."Document No.";
                    ToDocDim."Line No." := ToSalesLine."Line No.";
                    ToDocDim."Dimension Code" := TempDocDim."Dimension Code";
                    ToDocDim."Dimension Value Code" := TempDocDim."Dimension Value Code";
                    ToDocDim.INSERT;
                  UNTIL TempDocDim.NEXT = 0;
                END;
              END;
            UNTIL FromBOMComp.NEXT = 0;
            //eBay#002
            IF eBaySetup."Allocate BOM Price to Comp." THEN
              eBaySales.PreisKorrektur(Rec, PriceTotalBOM, PriceTotal, lineDiscountPercent);
            //eBay#002
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Die St…kliste kann nicht entfaltet werden, da sie der Bestellung %1 zugeordnet ist.;ENU=The BOM cannot be exploded on the sales lines because it is associated with purchase order %1.';
      Text001@1001 : TextConst 'DEU=Artikel %1 ist keine St…kliste.;ENU=Item %1 is not a bill of materials.';
      Text003@1002 : TextConst 'DEU=Es ist nicht gen“end Platz, um die St…kliste zu entfalten.;ENU=There is not enough space to explode the BOM.';
      Text004@1003 : TextConst 'DEU=Dimensionen von St…kliste &kopieren,Dimensionen von Komponenten &｜ernehmen;ENU=&Copy dimensions from BOM,&Retrieve dimensions from components';
      ToSalesLine@1004 : Record 37;
      FromBOMComp@1005 : Record 90;
      SalesHeader@1006 : Record 36;
      ItemTranslation@1007 : Record 30;
      Item@1008 : Record 27;
      ItemCheckAvail@1009 : Codeunit 311;
      UOMMgt@1010 : Codeunit 5402;
      BOMItemNo@1011 : Code[20];
      LineSpacing@1012 : Integer;
      NextLineNo@1013 : Integer;
      NoOfBOMComp@1014 : Integer;
      "--eBay"@5095800 : Integer;
      Auswahl@5095801 : Integer;
      NoConfirm@5095802 : Boolean;

    PROCEDURE "-eBay"@5095810();
    BEGIN
    END;

    PROCEDURE SetSelection@5095811(Wert@1000000000 : Integer);
    BEGIN
      Auswahl := Wert;
    END;

    PROCEDURE SetNoConfirm@5095812(Wert@1000000000 : Boolean);
    BEGIN
      NoConfirm := Wert;
    END;

    BEGIN
    {
      eBay1.00 Neue Funktion "SetSelection", Globale Variable "Auswahl"
      eBay1.00.05 eBay#002
      eBay1.02.03 eBay#003
      eBay1.06.02 eBay#006
      eBay1.06.22 eBay#007
      eBay1.06.24 eBay#007

      eBay#001 Auswertung "Auswahl" in "OnRun"
      eBay#002 St…klistenpreis auf Kompomenten verteilen
      eBay#003 Ausschalten Confirm
      ebay#004 Ber…ksichtigung Menge St…klistenartikel <>1
      eBay#005  "Shipping Charges to be paid", ToSalesLine."Shipping and Handling Charges" &
                "Shipping and Handling Charges" nur einmal pro St…kliste
      eBay#006  ToSalesLine."Account Code" := rec."Account Code";
                ToSalesLine."Amazon Order No." := rec."Amazon Order No.";
                ToSalesLine."Amazon Order Item No." := rec."Amazon Order Item No.";
      eBay#007 "Line Discount %"
    }
    END.
  }
}

