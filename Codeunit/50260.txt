OBJECT Codeunit 50260 PayPal Import
{
  OBJECT-PROPERTIES
  {
    Date=27.05.14;
    Time=16:04:04;
    Version List=HME0581;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            Code;
          END;

  }
  CODE
  {
    VAR
      InterFace@1000000000 : Codeunit 5157804;
      GeneralPaymentSetup@1000000001 : Record 50000;
      OPplusCSVPort@1000000002 : Record 5157805;
      PayPalDir@1000000003 : Text[1024];
      ShowDialog@1000000006 : Boolean;
      FullFileName@1000000005 : Text[1024];
      ShortFileName@1000000004 : Text[1024];

    PROCEDURE Code@1000000000();
    BEGIN
      //1.Get Download Path
      GeneralPaymentSetup.GET;
      GeneralPaymentSetup.TESTFIELD("PayPal Local Path");
      GeneralPaymentSetup.TESTFIELD("OPplus CSV Port");
      PayPalDir := GeneralPaymentSetup."PayPal Local Path";

      ImportFiles;
    END;

    PROCEDURE ImportFiles@1100409007();
    VAR
      GPSetup@1100409003 : Record 50000;
      Files@1100409000 : Record 2000000022;
      d1@1100409001 : Date;
      d2@1100409002 : Date;
    BEGIN
      Files.SETRANGE(Path,PayPalDir);
      Files.SETFILTER(Name,'@STL*.CSV');
      Files.SETRANGE("Is a file",TRUE);
      IF Files.FIND('-') THEN
        REPEAT
          ShortFileName := Files.Name;
          GeneralPaymentSetup.FIND('-');
          d1 := GetDateFromFileName(GeneralPaymentSetup."PayPal Last Imported Filename");
          d2 := GetDateFromFileName(ShortFileName);
          IF (d2 > d1) THEN BEGIN
            FullFileName := PayPalDir + Files.Name;
            //2.Change File Source in CSV Port for PayPal
            OPplusCSVPort.GET(GeneralPaymentSetup."OPplus CSV Port");
            OPplusCSVPort."File Name" := FullFileName;
            OPplusCSVPort.MODIFY;

            //3.Start CSV Port
            IF InterFace.Import(GeneralPaymentSetup."OPplus CSV Port",'',FALSE) = 0 THEN BEGIN
              RENAME(FullFileName,PayPalDir + ShortFileName + '.archive');
              GeneralPaymentSetup.FIND('-');
              GeneralPaymentSetup."PayPal Last Imported Filename" := ShortFileName;
              GeneralPaymentSetup.MODIFY;
              COMMIT;
            END;
          END;
        UNTIL Files.NEXT = 0;
    END;

    PROCEDURE GetDateFromFileName@1100409009(FileName@1100409000 : Text[1024]) : Date;
    VAR
      day@1100409003 : Integer;
      month@1100409002 : Integer;
      year@1100409001 : Integer;
    BEGIN
      EVALUATE(day,COPYSTR(FileName,11,2));
      EVALUATE(month,COPYSTR(FileName,9,2));
      EVALUATE(year,COPYSTR(FileName,5,4));
      EXIT(DMY2DATE(day,month,year));
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      H0581       07.05.14 ARI       Created
    }
    END.
  }
}

