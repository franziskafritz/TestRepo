OBJECT Codeunit 50130 SII Update Field Values
{
  OBJECT-PROPERTIES
  {
    Date=27.08.13;
    Time=12:00:00;
    Modified=Yes;
    Version List=SII,HME0532;
  }
  PROPERTIES
  {
    OnRun=VAR
            LocText001@1000000000 : TextConst 'ENU=Codeunit 50130 not initialized';
          BEGIN
            CASE GlobalRunType OF
              GlobalRunType::ProcessUpdateRec :
                BEGIN
                  ProcessUpdateRec(GlobalEntryNo);
                END;
              ELSE
                ERROR(LocText001);
            END;
          END;

  }
  CODE
  {
    VAR
      GlobalRunType@1000000001 : ' ,ProcessUpdateRec';
      GlobalEntryNo@1000000000 : Integer;

    PROCEDURE InsertUpdateRec@1000000000(p_RecRef@1000000000 : RecordRef;p_FieldNo@1000000001 : Integer;p_FieldValueAsText@1000000002 : Text[250];p_WithValidation@1000000004 : Boolean;p_LockTableOnModify@1000000005 : Boolean);
    VAR
      SIIUpdateFieldValues@1000000003 : Record 50130;
    BEGIN
      SIIUpdateFieldValues.RESET;
      SIIUpdateFieldValues."Entry No." := SIIUpdateFieldValues.GetNextEntryNo(TRUE);
      SIIUpdateFieldValues.INSERT(TRUE);
      SIIUpdateFieldValues."To Table No." := p_RecRef.NUMBER;
      SIIUpdateFieldValues."To RecordID" := p_RecRef.RECORDID;
      SIIUpdateFieldValues."To Field No." := p_FieldNo;
      SIIUpdateFieldValues."Field Value" := p_FieldValueAsText;
      SIIUpdateFieldValues."With Validation" := p_WithValidation;
      SIIUpdateFieldValues."Locktable On Modify" := p_LockTableOnModify;
      SIIUpdateFieldValues.Status := SIIUpdateFieldValues.Status::Unprocessed;
      SIIUpdateFieldValues."Insert Timestamp" := CURRENTDATETIME;
      SIIUpdateFieldValues."Insert User" := USERID;
      SIIUpdateFieldValues.MODIFY(TRUE);
    END;

    PROCEDURE InitProcessUpdateRec@1000000006(p_EntryNo@1000000000 : Integer);
    BEGIN
      GlobalEntryNo := 0;
      GlobalRunType := GlobalRunType::" ";
      GlobalEntryNo := p_EntryNo;
      GlobalRunType := GlobalRunType::ProcessUpdateRec;
    END;

    PROCEDURE ProcessUpdateRec@1000000010(p_EntryNo@1000000000 : Integer);
    VAR
      SIIUpdateFieldValues@1000000001 : Record 50130;
      RecRef@1000000002 : RecordRef;
      FieldRef@1000000003 : FieldRef;
      LocText001@1000000004 : TextConst 'ENU=No Record Found.';
      ErrorText@1000000005 : Text[1024];
      xRecRef@1000000006 : RecordRef;
      ChangeLogManagement@1000000007 : Codeunit 423;
      FieldRec@1000000008 : Record 2000000041;
      BooleanVar@1000000009 : Boolean;
      OptionVar@1000000010 : Option;
      IntegerVar@1000000011 : Integer;
      DecimalVar@1000000012 : Decimal;
      BigIntegerVar@1000000013 : BigInteger;
      DateVar@1000000014 : Date;
      TimeVar@1000000015 : Time;
      DateTimeVar@1000000016 : DateTime;
      DurationVar@1000000017 : Duration;
    BEGIN
      IF SIIUpdateFieldValues.GET(p_EntryNo) AND (SIIUpdateFieldValues.Status = SIIUpdateFieldValues.Status::Unprocessed) THEN BEGIN
        CLEAR(RecRef);
        IF SIIUpdateFieldValues."Locktable On Modify" THEN
          RecRef.LOCKTABLE;
        RecRef.OPEN(SIIUpdateFieldValues."To Table No.");
        IF RecRef.GET(SIIUpdateFieldValues."To RecordID") THEN BEGIN
          CLEAR(xRecRef);
          xRecRef.GET(SIIUpdateFieldValues."To RecordID");
          FieldRef := RecRef.FIELD(SIIUpdateFieldValues."To Field No.");
          SIIUpdateFieldValues."Old Field Value" := FORMAT(FieldRef.VALUE);
          //S/P1021
          FieldRec.GET(SIIUpdateFieldValues."To Table No.",SIIUpdateFieldValues."To Field No.");
          CASE FieldRec.Type OF
            FieldRec.Type::Boolean :
              BEGIN
                EVALUATE(BooleanVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(BooleanVar)
                ELSE
                  FieldRef.VALUE := BooleanVar;
              END;
            FieldRec.Type::Integer :
              BEGIN
                EVALUATE(IntegerVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(IntegerVar)
                ELSE
                  FieldRef.VALUE := IntegerVar;
              END;
            FieldRec.Type::Decimal :
              BEGIN
                EVALUATE(DecimalVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(DecimalVar)
                ELSE
                  FieldRef.VALUE := DecimalVar;
              END;
            FieldRec.Type::BigInteger :
              BEGIN
                EVALUATE(BigIntegerVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(BigIntegerVar)
                ELSE
                  FieldRef.VALUE := BigIntegerVar;
              END;
            FieldRec.Type::Date :
              BEGIN
                EVALUATE(DateVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(DateVar)
                ELSE
                  FieldRef.VALUE := DateVar;
              END;
            FieldRec.Type::Time :
              BEGIN
                EVALUATE(TimeVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(TimeVar)
                ELSE
                  FieldRef.VALUE := TimeVar;
              END;
            FieldRec.Type::Duration :
              BEGIN
                EVALUATE(DurationVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(DurationVar)
                ELSE
                  FieldRef.VALUE := DurationVar;
              END;
            FieldRec.Type::DateTime :
              BEGIN
                EVALUATE(DateTimeVar,SIIUpdateFieldValues."Field Value");
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(DateTimeVar)
                ELSE
                  FieldRef.VALUE := DateTimeVar;
              END;
            FieldRec.Type::Option :
              BEGIN
                EVALUATE(OptionVar,FORMAT(SIIUpdateFieldValues."Field Value",0,9));
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(OptionVar)
                ELSE
                  FieldRef.VALUE := OptionVar;
              END;
            //H0532  27.08.13  ABR  +++++++++++++++++++++++++++++++++++++++++
            FieldRec.Type::Text :
              BEGIN
                IF SIIUpdateFieldValues."With Validation" THEN
                  FieldRef.VALIDATE(SIIUpdateFieldValues."Field Value")
                ELSE
                  FieldRef.VALUE := SIIUpdateFieldValues."Field Value";
              END;
            //H0532  27.08.13  ABR  -----------------------------------------

          END;
          //IF SIIUpdateFieldValues."With Validation" THEN
            //FieldRef.VALIDATE(SIIUpdateFieldValues."Field Value")
          //ELSE
            //FieldRef.VALUE := SIIUpdateFieldValues."Field Value";
          //E/P1021
          RecRef.MODIFY;
          ChangeLogManagement.LogModification(RecRef,xRecRef);
          SIIUpdateFieldValues.Status := SIIUpdateFieldValues.Status::Processed;
          SIIUpdateFieldValues."Process Timestamp" := CURRENTDATETIME;
          SIIUpdateFieldValues."Process User" := USERID;
          SIIUpdateFieldValues.MODIFY;
        END ELSE BEGIN
          SIIUpdateFieldValues.Status := SIIUpdateFieldValues.Status::Error;
          SIIUpdateFieldValues."Process Timestamp" := CURRENTDATETIME;
          SIIUpdateFieldValues."Process User" := USERID;
          SIIUpdateFieldValues."Error Text 1" := LocText001;
          SIIUpdateFieldValues.MODIFY;
        END;
      END;
    END;

    PROCEDURE UpdateStatus@1000000001(p_SIIUpdateFieldValues@1000000001 : Record 50130;p_NewStatus@1000000000 : 'Unprocessed,Processed,Error,Canceled');
    BEGIN
      IF p_SIIUpdateFieldValues.FINDSET THEN BEGIN
        REPEAT
          p_SIIUpdateFieldValues.Status := p_NewStatus;
          p_SIIUpdateFieldValues."Error Text 1" := '';
          p_SIIUpdateFieldValues."Error Text 2" := '';
          p_SIIUpdateFieldValues."Error Text 3" := '';
          p_SIIUpdateFieldValues."Error Text 4" := '';
          p_SIIUpdateFieldValues.MODIFY;
        UNTIL p_SIIUpdateFieldValues.NEXT = 0;
      END;
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |     Cooperation GOB & Home24 NAV Team      |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________

      *** Module: SII Shop Interaction Interface ***
      P0982       19.06.13  gob-rste  Object Created

      *** Module: FTTC Face to the Customer ***

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date     Shortcut  Description
      _____________________________________________

      *** Module: FTTC Shipping Confirmation***
      H0532       27.08.13 ABR       Process Text Fields
    }
    END.
  }
}

