OBJECT Codeunit 5327197 ACF Events Admin/Examples
{
  OBJECT-PROPERTIES
  {
    Date=04.04.13;
    Time=00:00:00;
    Version List=ACF3.02.03;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Events@5326063 : Codeunit 5327180;
      Mgt@1000000009 : Codeunit 5327126;
      Text001@1000000026 : TextConst 'DEU=Die individuelle Web Page Tabellenrelation %1 existiert nicht.;ENU=Custom Web Page Table relation %1 does not exist.';
      Text002@1000000002 : TextConst 'DEU=Der Datensatz wurde gespeichert.;ENU=Data saved.';
      Text003@1000000033 : TextConst 'DEU=Der Zieldatensatz wurde in der CustomWebPageRelation nicht ge”ffnet;ENU=Destination Record was not opened in CustomWebPageRelation.';
      Text004@1000000025 : TextConst 'DEU=Der Funktionscode %1 existiert nicht.;ENU=Function Code %1 does not exist.';
      Text006@1000000011 : TextConst 'DEU=M”chten Sie den Datensatz %1 %2 l”schen?;ENU=Do you want to delete record %1 %2?';
      Text007@1000000010 : TextConst 'DEU=M”chten Sie den Datensatz "%1" in "%2" umbenennen?;ENU=Do you want to rename the record "%1" in "%2?';
      Text008@1000000008 : TextConst 'DEU=M”chten Sie den Datensatz umbenennen?;ENU=Do you want to rename this record?';
      Style@5326068 : Codeunit 5327125;
      RecordFilter@5326069 : Codeunit 5327128;
      SecurityMgt@5326071 : Codeunit 5327120;
      WebPageElement@5326088 : Record 5327101;
      WebPageElementField@5326087 : Record 5327103;
      RecRef@5326086 : RecordRef;
      FldRef@5326085 : FieldRef;
      FldNo@5326065 : Integer;
      xRecRef@5326084 : RecordRef;
      xFldRef@5326083 : FieldRef;
      xFldNo@5326064 : Integer;
      WindowRecRef@5326082 : RecordRef;
      DestWebPageElement@5326081 : Record 5327101;
      DestRecRef@5326080 : RecordRef;
      CustomTableRelationRecRef@5326079 : RecordRef;
      FunctionCode@5326078 : Code[20];
      RelationCode@5326077 : Code[20];
      FieldName@5326076 : Text[50];
      TableNumber@5326074 : Integer;
      ClosedByNAV@5326066 : Boolean;
      "Field#1"@5326073 : Integer;
      "Field#2"@5326072 : Integer;
      CSS@5326070 : Text[1024];
      WebUser@5326062 : Record 5327140;
      RecRefOriginal@5326067 : RecordRef;
      Text009@5326099 : TextConst 'DEU=Der eingegebene Wert ist "%1".;ENU=The entered value is "%1".';
      Text80001@5326098 : TextConst 'DEU=Dies ist ein Symbol, welches Sie in Anveo Web Desk und Anveo Mobile fr Mens und Schaltfl„chen verwenden k”nnen.;ENU=You can use this icon for menus and buttons in Anveo Web Desk and Anveo Mobile.';
      Text80002@5326097 : TextConst 'DEU=Nachricht\\Dieses ist eine Mitteilung.;ENU=Message\\This is a message.';
      Text80003@5326096 : TextConst 'DEU=Bitte w„hlen Sie ja oder nein:;ENU=Please select yes or no:';
      Text80004@5326095 : TextConst 'DEU=Sie haben %1 ausgew„hlt.;ENU=You selected %1.';
      Text80005@5326094 : TextConst 'DEU=Auswahl 1,Auswahl 2,Auswahl 3;ENU=Option 1,Option 2,Option 3';
      Text80006@5326093 : TextConst 'DEU=Bitte w„hlen:;ENU=Please select:';
      Text80007@5326092 : TextConst 'DEU=Keine Datei ausgew„hlt.;ENU=No file selected.';
      Text80008@5326091 : TextConst 'DEU=Datei konnte nicht hochgeladen werden.;ENU=Error on file upload.';
      Text80009@5326090 : TextConst 'DEU=Der Upload fr die Datei %1 ist erfolgreich. Sie wird nun angezeigt.;ENU=Upload for file %1 is successful. File is shown afterwards.';
      Text80014@5326089 : TextConst 'DEU=Text;ENU=Description';
      Text80015@5326075 : TextConst 'DEU=Der Demodatensatz existiert bereits. M”chten Sie diesen ersetzen?;ENU=The demo record already exists. Do you want to replace it?';

    PROCEDURE GetModuleName@1000000001() ModuleName : Text[30];
    BEGIN
      EXIT('Admin / Examples');
    END;

    LOCAL PROCEDURE "-- Events"@5326091();
    BEGIN
    END;

    PROCEDURE OnBeforeLoad@1000000003();
    BEGIN
      // Called before loading data for a Web Page Element
      // This event is synchronious call, Web Mangement messages cannot be used here.
      // Attention: Filter set here get lost on reload on Web Pages of type list. Use CustomWebPageRelation instead.

      // Main Events
      SetValuesToMainEvents();
      Events.OnBeforeLoad();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterGetField@1000000013() ShowValue : Boolean;
    BEGIN
      // This event is called after a field is loaded from (temporary) record.
      // It can be modified here before sending it to the user.
      // Useful for:
      // - Getting data not saved in phys. record but from table functions (eg. "Contact Profile Anweser".Question)
      // - Change of Style (Anveo Web Desk) like colors and other field attributes
      // - Enabling/Disabling fields to change the layout (Anveo Mobile lists)

      // **** Definition of layout
      // ** Anveo Web Desk
      // Define a CSS style for a dynamic definition of the field layout.
      // * STYLE-SYNTAX *
      // 1. Set Condition (boolean)
      //    Style.SetCondition(CONDITION (boolean));
      // 2. Set attribute(s)
      //    If CONDITION is TRUE, first value is taken for attributes, otherwise second:
      //    Style.FontWeight('bold', 'normal');
      //     -> Text is bold, if CONDITION is TRUE; text is normal, if FALSE

      // ** Anveo Mobile
      // Layout of lists can be changed by defining more than one field in Web Page Field definition based on same field in table
      // but with different Web Page Field-attributes (eg. colors)
      // Then set one of the Web Page Fields identified by FieldName invisible by returning ShowValue := TRUE/FALSE
      // The Style object is not used by Anveo Mobile because of performance optimization.

      // Default
      ShowValue := TRUE;

      // Duplicate the RecRef because RecRef.SETTABLE deletes the current key (NAV bug)
      RecRefOriginal := RecRef.DUPLICATE;

      // Table based
      CASE RecRef.NUMBER OF
        DATABASE::"ACF Example":                ShowValue := OAGetField_EXAMPLE();
      END;

      // Web Page based
      CASE WebPageElement."Web Page Code" OF
        //'YOURWEBPAGE':                           ShowValue := OAGetField_YOURFUNCTION();
      END;

      // Main Events
      SetValuesToMainEvents();
      ShowValue := ShowValue AND Events.OnAfterGetField();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterGetRecord@1000000019();
    BEGIN
      // Record is loaded and is ready for transport to GUI.
      // Changes of records cannot be made here because record is already processed for GUI representation.
      // Use OnAfterGetField instead.

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterGetRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterLoad@1000000014();
    BEGIN
      // Called after loading data for a Web Page Element
      // This event is synchronious call, Web Mangement messages cannot be used here.

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterLoad();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnInit@1000000007();
    BEGIN
      // OnInit is called if a new record is initialized.
      // Default values can be set here

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnInit();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnBeforeValidate@1000000010() DoValidation : Boolean;
    BEGIN
      // A value is set to a record, but validation is not done yet. Validation can be cancelled here by return value false.
      // Use input of FlowFields for transportation of input values from GUI to NAV

      // Default
      DoValidation := TRUE;

      IF FORMAT(FldRef.CLASS) = 'FlowField' THEN BEGIN
        //Example:
        Mgt.MESSAGE1('FlowField Input: %1. Do not validate field.',FldRef.VALUE);
        EXIT(FALSE);
      END;

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Save xRec and CurrFieldNo for usage of Mgt.GetxRec in table triggers
      Mgt.SetxRec(xRecRef,FldRef.NUMBER);

      // Main Events
      SetValuesToMainEvents();
      DoValidation := DoValidation AND Events.OnBeforeValidate();
      GetValuesFromMainEvents();

      EXIT(DoValidation);
    END;

    PROCEDURE OnValidate@1000000000();
    BEGIN
      // OnValidate is called on every input made in the GUI
      // Complete reload of web page is done after validating fields.

      // Example for testing OnValidateTrigger
      //IF FldRef.NUMBER <> 0 THEN BEGIN
      //  Mgt.MESSAGE4(Text008,FldRef.VALUE,RecRef.CAPTION, FldRef.CAPTION, xFldRef.VALUE));
      //END;

      IF (WebPageElementField."Web Page Code" = 'EXAMPLE_CARD') AND (WebPageElementField."Field Name" = '12000000') THEN BEGIN
        Mgt.MESSAGE1(Text009, FldRef.VALUE);
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnValidate();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnInsertTrigger@1000000016() CallTableOnInsertTrigger : Boolean;
    BEGIN
      // OnInsert trigger is called when data is saved back to SQL-Server
      // Return value CallTableOnInsertTrigger (boolean) sets whether to call default OnInsert-trigger of table afterwards.
      // Default value is true, always call OnInsert-Trigger of tables
      CallTableOnInsertTrigger:=TRUE;

      // Main Events
      SetValuesToMainEvents();
      CallTableOnInsertTrigger := CallTableOnInsertTrigger AND Events.CallOnInsertTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnModifyTrigger@1000000015() CallTableOnModifyTrigger : Boolean;
    BEGIN
      // OnModify trigger is called when data is saved back to SQL-Server
      // Return value CallTableOnModifyTrigger (boolean) sets whether to call default OnModify-trigger of table afterwards.
      // Default value is true, always call OnModify-Trigger of tables
      CallTableOnModifyTrigger:=TRUE;

      // Set xRec
      Mgt.SetxRec(xRecRef,0);

      // Main Events
      SetValuesToMainEvents();
      CallTableOnModifyTrigger := CallTableOnModifyTrigger AND Events.CallOnModifyTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CallOnDeleteTrigger@1000000033() CallTableOnDeleteTrigger : Boolean;
    BEGIN
      // CallOnDeleteTrigger is called when data is saved back to SQL-Server
      // Return value CallTableOnDeleteTrigger (boolean) sets whether to call default OnDelete-trigger of table afterwards.
      // Default value is true, always call OnDelete-Trigger of tables
      CallTableOnDeleteTrigger:=TRUE;

      // Main Events
      SetValuesToMainEvents();
      CallTableOnDeleteTrigger := CallTableOnDeleteTrigger AND Events.CallOnDeleteTrigger();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnInsertRecord@1000000025() DoInsert : Boolean;
    BEGIN
      // A record is inserted. Return value decides if record is inserted.
      // Table OnInsert()-Trigger is called afterwards.
      // On return value FALSE does not insert record.

      // Default
      DoInsert := TRUE;

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      DoInsert := DoInsert AND Events.OnInsertRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnModifyRecord@1000000027() DoModify : Boolean;
    BEGIN
      // A record is modified. Return value decides if record is modified.
      // Table OnModify()-Trigger is called afterwards.
      // On return value FALSE does not modify record.

      // Default
      DoModify := TRUE;

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      DoModify := DoModify AND Events.OnModifyRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnRenameRecord@1000000011() DoRename : Boolean;
    BEGIN
      // A record is renamed. Return value decides if record is renamed.
      // OnBeforeValidate and OnValidate is called before.
      // Table OnRename()-Trigger is called afterwards.
      // On return value FALSE does not rename record.

      // Default
      DoRename := TRUE;

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Rename Dialog: Long version with primary key fields
      //EXIT(Mgt.CONFIRM2(Text007,FALSE,xRecRef.GETFILTERS,RecRef.GETFILTERS));
      // Short version
      //EXIT(Mgt.CONFIRM(Text008));

      // Main Events
      SetValuesToMainEvents();
      DoRename := DoRename AND Events.OnRenameRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnDeleteRecord@1000000009() DoDelete : Boolean;
    BEGIN
      // OnDeleteRecord is called if a record is to be deleted.
      // It is not called if a new, unsaved record is deleted.

      // Default
      DoDelete := TRUE;

      CASE RecRef.NUMBER OF
        // Example: Do not show Delete-Confirm-Message
        //DATABASE::<TableName> THEN EXIT(TRUE);
      END;

      // Default message:
      //EXIT(Mgt.CONFIRM2(Text006, FALSE,RecRef.CAPTION, RecRef.GETFILTERS));

      // Main Events
      SetValuesToMainEvents();
      DoDelete := DoDelete AND Events.OnDeleteRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterInsertRecord@1000000028();
    BEGIN
      // Called after a record was successfully inserted.

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterInsertRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterModifyRecord@1000000029();
    BEGIN
      // Called after a record was successfully modified.

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterModifyRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterRenameRecord@1000000030();
    BEGIN
      //Called after a record was successfully renamed.

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterRenameRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAfterDeleteRecord@1000000031();
    BEGIN
      //Called after a record was successfully deleted.

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;

      // Main Events
      SetValuesToMainEvents();
      Events.OnAfterDeleteRecord();
      GetValuesFromMainEvents();
    END;

    PROCEDURE OnAction@1000000026() CloseWindow : Boolean;
    BEGIN
      // OnAction is called when a button in main menu, web page menu or beside a field is pushed.
      // Or in case of a double click event with a function code defined.
      // Default FunctionCodes see Main Events codeunit. Insert them here to overwrite if required.

      IF FunctionCode = '' THEN EXIT;

      // Based on Table
      CASE RecRef.NUMBER OF
        DATABASE::"ACF Example": BEGIN
          OnAction_EXAMPLE();
          EXIT;
        END;
      END;

      // Based on Function Code
      CASE FunctionCode OF
        'OPENALLWEBPAGES':      OnAction_OPENALLWEBPAGES();

        ELSE BEGIN
          Mgt.Log('OnAction','No FunctionCode found. Asking Main Events codeunit...');
          SetValuesToMainEvents();
          CloseWindow := CloseWindow OR Events.OnAction();
          GetValuesFromMainEvents();
        END;
      END;
    END;

    PROCEDURE OnBeforeCloseWebPage@1000000004() CloseWindow : Boolean;
    BEGIN
      // OnCloseWebPage is called before a Web Page is closed.
      // The return value CloseWindow (boolean) decides whether the web page is closed or not.
      // ClosedByNAV indicates whether the user (=FALSE) or Mgt.CloseWebPage() (=TRUE) closed the window.

      // Default
      CloseWindow := TRUE;

      CASE RecRef.NUMBER OF
        //DATABASE::TableName: <YourCode>
      END;
      //Mgt.ReloadWebPagesTableNo(RecRef.NUMBER,2); //0 = all cards, 1 = all Lists, 2 all

      // Main Events
      SetValuesToMainEvents();
      CloseWindow := CloseWindow AND Events.OnBeforeCloseWebPage();
      GetValuesFromMainEvents();
    END;

    PROCEDURE IsWriteBack@1000000018() WriteRecord : Boolean;
    VAR
      xFldRef@1000000010 : FieldRef;
    BEGIN
      // IsWriteBack
      // Usage for return value WriteRecord:
      //  Web Page WriteBehaviour is ...
      //   * Direct      - IsWriteBack is ignored. Data is always written back to SQL-Server
      //   * Delayed     - IsWriteBack is ignored. Data is written back to SQL-Server on close web page/modify other record/logout/...
      //   * Defined     - IsWriteBack is used for decision whether to write data back to SQL-Server.
      //   * Primary Key - IsWriteBack is ignored. Data is written back to SQL-Server if non-primay key field is validated.
      // Complete reload of web page is done after validating fields.

      // Default
      WriteRecord := FALSE;

      IF xFldNo<>0 THEN xFldRef := xRecRef.FIELD(xFldNo);

      CASE xRecRef.NUMBER OF
        // Example: Write Record if other value than Customer No. is set.
        // The automatic WriteBehaviour "Primary Key" gives same behaviour.
        //DATABASE::Customer: BEGIN
        //  EXIT(NOT(xFldNo IN [0,1]));
        //END;
      END;

      // Main Events
      SetValuesToMainEvents();
      WriteRecord := WriteRecord OR Events.IsWriteBack();
      GetValuesFromMainEvents();
    END;

    PROCEDURE CustomWebPageRelation@1000000005();
    BEGIN
      // Sets custom relations between two Web Pages, Web Page Elements or for DropDown-Lists defined by function code

      // Optional:
      // Test source of Custom Table Relation
      //CASE CustomTableRelationRecRef.NUMBER OF
      //  DATABASE::"ACF Web Page Element": ;       //Web Page Element: Call from Web Page to Web Page
      //  DATABASE::"ACF Web Page Element Menu": ;  //Web Page Element Menu: Call from Menu
      //  DATABASE::"ACF Web Page Element Field": ; //Web Page Element Field: Call from Field
      //END;

      CASE RelationCode OF
        //'YOURRELATIONCODE':          CTR_YOURRELATION();

      ELSE BEGIN
          Mgt.Log('CustomWebPageRelation','No CustomWebPageRelation found. Asking Main Events codeunit...');
          SetValuesToMainEvents();
          Events.CustomWebPageRelation();
          GetValuesFromMainEvents();
        END;
      END;

      // Test, if destination record ref is found.
      IF DestRecRef.NUMBER=0 THEN ERROR(Text003);
      // Log actual filter value
      Mgt.Log('CustomWebPageRelation',DestRecRef.GETVIEW);
    END;

    PROCEDURE DropDownFieldDefinition@1000000002();
    BEGIN
      // DropDown Field Definition
      // Defaults for this module only

      {
      CASE TableNumber OF
        DATABASE::TableNumber: BEGIN
          "Field#1" := Record.FIELDNO(FieldName1);
          "Field#2" := Record.FIELDNO(FieldName2);
          EXIT;
        END;
      END;
      }

      // If not defined by this module, load defaults from Main Events codeunit or definition of Web Page Element Field
      SetValuesToMainEvents();
      Events.DropDownFieldDefinition();
      GetValuesFromMainEvents();
    END;

    LOCAL PROCEDURE "-- OnAction-Functions"@5326063();
    BEGIN
    END;

    PROCEDURE OnAction_EXAMPLE@5326090();
    VAR
      ACFExample@5326062 : Record 5327194;
      UploadedFileName@5326063 : Text[1024];
      ProgressWinI@5326064 : Integer;
      Text001@5326068 : TextConst 'DEU=Fortschritt@1@@@@@@@@\Wert#2###############;ENU=Percentage@1@@@@@@@@\Value#2###############';
      Text002@5326067 : TextConst 'DEU=Fertig.;ENU=Finished.';
      Text003@5326066 : TextConst 'DEU=Fortfahren?;ENU=Do you want to proceed?';
      Text004@5326065 : TextConst 'DEU=Aktion wird abgebrochen.;ENU=You pressed no.';
      Help001@5326077 : TextConst 'DEU=Testen Sie das Umbenennen eines Datensatzes.;ENU=Test renaming the current record.';
      Help002@5326076 : TextConst 'DEU=Testen Sie einen CONFIRM-Dialog durch Žndern der Beschreibung.;ENU=Test a confirmation dialog here by changing the description field.';
      Help003@5326075 : TextConst 'DEU=Dieses Feld zeigt die vorherige Beschreibung und demonstriert die xRec-Funktionalit„t.;ENU=This field shows the previous value of field desciption and shows the xRec functionality.';
      Help004@5326074 : TextConst 'DEU=Dieses Feld demonstriert die Erkennung von CurrFieldNo in den Tabellen.;ENU=This field demonstrates the availability of CurrFieldNo in tables.';
      Help005@5326073 : TextConst 'DEU=Geben Sie eine URL ein und klicken Sie auf "™ffnen".;ENU=Input an URL and click on "Open".';
      Help006@5326072 : TextConst 'DEU=Das Volumen wird automatisch aus den Feldern L„nge, Breite und H”he berechnet.;ENU=See how the volume is caluculated automatically by the fields length, width and height.';
      Help007@5326071 : TextConst 'DEU="Das FlowField wird automatisch berechnet (Lookup auf Beschreibung). ";ENU=This FlowFiels is calculated automatically (Lookup on field Description).';
      Help008@5326070 : TextConst 'DEU=Das Eingabe-FlowField nimmt Daten entgegen, die nicht physikalisch in der Tabelle gespeichert werden. Diese FlowFields k”nnen als Variablen/entkoppelte Felder verwendet werden.;ENU=The Input-FlowFlield transports data to NAV without saving it into the table. These FlowFields can be used as variables/unbound fields.';
      Help009@5326069 : TextConst 'DEU=Das Ausgabe-FlowField bertrag Daten an die Oberfl„che, die nicht physikalisch in der Tabelle gespeichert werden. Diese FlowFields k”nnen als Variablen/entkoppelte Felder verwendet werden.;ENU=The Output-FlowFlield transports data from NAV to the user interface without saving it into the table. These FlowFields can be used as variables/unbound fields.';
    BEGIN
      // Technical Examples on Web Pages EXAMPLE_LIST and EXAMPLE_CARD
      CASE FunctionCode OF
        'EXAMPLE:ICON':         Mgt.MESSAGE(Text80001);
        'EXAMPLE:HELP_RENAME':  Mgt.MESSAGE(Help001);
        'EXAMPLE:HELP_CONFIRM': Mgt.MESSAGE(Help002);
        'EXAMPLE:HELP_XREC':    Mgt.MESSAGE(Help003);
        'EXAMPLE:HELP_CURRFN':  Mgt.MESSAGE(Help004);
        'EXAMPLE:HELP_URL':     Mgt.MESSAGE(Help005);
        'EXAMPLE:HELP_VOLUME':  Mgt.MESSAGE(Help006);
        'EXAMPLE:HELP_FF':      Mgt.MESSAGE(Help007);
        'EXAMPLE:HELP_FF_IN':   Mgt.MESSAGE(Help008);
        'EXAMPLE:HELP_FF_OUT':  Mgt.MESSAGE(Help009);

        'EXAMPLE:MESSAGE': Mgt.MESSAGE(Text80002);

        'EXAMPLE:CONFIRM': Mgt.MESSAGE1(Text80004,Mgt.CONFIRM(Text80003,TRUE));
        'EXAMPLE:STRMENU': Mgt.MESSAGE1(Text80004,Mgt.STRMENU(Text80005,1,Text80006));
        'EXAMPLE:FILEUPLOAD':
          //Fileupload ReturnValue: 0 = cancel, 1 successfull, 2 = upload has a exception
          BEGIN
            //Show Upload Dialog window and wait for an response
            CASE Mgt.OpenFileUploadDialogWin(UploadedFileName) OF
              //No file selected
              0:  Mgt.MESSAGE(Text80007);
              //File selected and upload successful
              1:  BEGIN
                Mgt.MESSAGE1(Text80009,UploadedFileName);
                Mgt.OpenFileDownloadDialogWin(UploadedFileName,TRUE);
              END;
              //File selected, but upload has an error.
              2:  ERROR(Text80008);
            END;
          END;
        'EXAMPLE:DOUBLE': BEGIN
          RecRef.SETTABLE(ACFExample);
          ACFExample.VALIDATE(Length, ACFExample.Length + ACFExample.Length);
          ACFExample.MODIFY(TRUE);
          Mgt.ReloadWebPage();
        END;

        'EXAMPLE:CREATE_DEMOR': BEGIN
          ACFExample.SETFILTER("No.", '%1|%2', 'DEMO-OFF', 'DEMO-ON');
          IF NOT ACFExample.ISEMPTY THEN
            IF NOT Mgt.CONFIRM(Text80015, FALSE) THEN EXIT;
          ACFExample.DELETEALL;
          ACFExample.INIT;
          ACFExample."No." := 'DEMO-OFF';
          ACFExample."Activate Style Demo" := FALSE;
          ACFExample.Description := Text80014;
          ACFExample."Search Name" := Text80014;
          ACFExample.URL := 'www.NAVwebclient.com';
          ACFExample.Length := 10;
          ACFExample.Width := 20;
          ACFExample.VALIDATE(Height,2);
          ACFExample."Demo Integer" := 123;
          ACFExample."Demo Decimal" := 123.45;
          ACFExample."Demo Code" := 'CODE';
          ACFExample."Demo Text" := 'Text';
          ACFExample."Demo Date" := 311212D;
          ACFExample."Demo Time" := 011510T;
          ACFExample."Demo DateTime" := CREATEDATETIME(ACFExample.Date, ACFExample.Time);
          ACFExample.INSERT;
          ACFExample."No." := 'DEMO-ON';
          ACFExample."Activate Style Demo" := TRUE;
          ACFExample.INSERT;
          Mgt.ReloadWebPage();
        END;

        //Progess window
        'EXAMPLE:PROGRESSW': BEGIN
          Mgt.DIALOGOPEN(Text001);
          FOR ProgressWinI := 1 TO 100 DO BEGIN
            Mgt.DIALOGUPDATE(1,ProgressWinI*100);
            Mgt.DIALOGUPDATE(2,FORMAT(ProgressWinI));
            //Ask user for continuing the progress bar...
            IF ProgressWinI = 10 THEN
              IF NOT Mgt.CONFIRM(Text003,TRUE) THEN BEGIN
                //User selects cancellation
                Mgt.MESSAGE(Text004);
                ProgressWinI := 100;
              END;
          END;
          Mgt.DIALOGCLOSE();
          Mgt.MESSAGE(Text002); //Finished
        END;

        'EXAMPLE:OPENURL_FIELD':
          IF FORMAT(RecRef.FIELD(12).VALUE) = '' THEN
            Mgt.MESSAGE(Help005)
          ELSE
            Mgt.HYPERLINK(FORMAT(RecRef.FIELD(12).VALUE));
        'EXAMPLE:OPENURL' : Mgt.OpenURL('www.navwebclient.com','Anveo NAVwebclient.com',FALSE,1024,768);
        'EXAMPLE:OPENURLH': Mgt.HYPERLINK('www.navwebclient.com');
        'EXAMPLE:OPENURL1' : Mgt.OpenURLExtended('www.navwebclient.com','','Anveo NAVwebclient.com',FALSE,1024,768,1);
        'EXAMPLE:OPENURL2' : Mgt.OpenURLExtended('www.navwebclient.com','','Anveo NAVwebclient.com',FALSE,1024,768,2);
        'EXAMPLE:OPENURL3' : Mgt.OpenURLExtended('www.navwebclient.com','','Anveo NAVwebclient.com',FALSE,1024,768,3);
        'EXAMPLE:OPENURL4' : Mgt.OpenURLExtended('www.navwebclient.com','','Anveo NAVwebclient.com',FALSE,1024,768,4);
      END;
    END;

    PROCEDURE OnAction_OPENALLWEBPAGES@5326067();
    VAR
      WebPage@5326062 : Record 5327100;
      WebPageElement2@5326063 : Record 5327101;
    BEGIN
      // Caching Example - open all published web pages
      WebPage.SETRANGE(Published, TRUE);
      WebPage.FINDSET;
      REPEAT
        IF Mgt.IsMobile THEN
          WebPageElement2.SETFILTER(WebPageElement2."Element Type", '%1|%2',
            WebPageElement2."Element Type"::"List Mobile", WebPageElement2."Element Type"::"Card Mobile")
        ELSE
          WebPageElement2.SETFILTER(WebPageElement2."Element Type", '%1|%2',
            WebPageElement2."Element Type"::List, WebPageElement2."Element Type"::Card);
        WebPageElement2.SETRANGE("Web Page Code", WebPage.Code);
        IF WebPageElement2.FINDFIRST THEN BEGIN
          DestRecRef.OPEN(WebPageElement2."Table No.");
          IF DestRecRef.FINDFIRST THEN ;
          Mgt.OpenWebPage(WebPage.Code,FALSE,DestRecRef,'');
          DestRecRef.CLOSE;
          //SLEEP(2000);
          //Mgt.CloseWebPage();

        END;
      UNTIL WebPage.NEXT=0;
    END;

    LOCAL PROCEDURE "-- CustomTableRelation-Func."@5326062();
    BEGIN
    END;

    PROCEDURE CTR_YOURRELATION@5326065();
    BEGIN
      //Mgt.LOG('OnAction_YOURRELATION', 'BEGIN');

      // 1. Set table reference
      //RecRef.SETTABLE(Record);

      // 2. Filter
      //DestRecord.SETFILTER(Field,Record.Field);

      // 3. Copy Filters to DestRecRef
      //DestRecRef.SETVIEW(DestRecord.GETVIEW);

      //Mgt.LOG('OnAction_YOURRELATION', 'END');
    END;

    LOCAL PROCEDURE "--Web Page Element Field-Func."@5326080();
    BEGIN
    END;

    PROCEDURE OAGetField_EXAMPLE@5326066() ShowValue : Boolean;
    VAR
      ACFExample@5326062 : Record 5327194;
    BEGIN
      // ** STYLE-SYNTAX Anveo Web Desk **
      // 1. Set condition on Style-Object  (boolean)
      //    Style.SetCondition( CONDITION (boolean));
      // 2. Set attribute(s)
      //    If CONDITION is TRUE, first value is taken for attributes, otherwise second:
      //    Style.FontWeight('bold', 'normal');
      //     -> Text is bold, if CONDITION is TRUE; text is normal, if FALSE

      ShowValue := TRUE;

      // ** Style Definitions
      // 1. Set condition to ACFExample."Activate Style Demo" (true/false)
      RecRef.SETTABLE(ACFExample);
      Style.SetCondition(ACFExample."Activate Style Demo");

      // 2. Set attributes
      IF FieldName = '2000010' THEN Style.BackgroundColor('#AAFFAA','red');  //AAFFAA = green
      IF FieldName = '3000010' THEN Style.FontWeight('bold','normal');
      IF FieldName = '4000010' THEN BEGIN
        Style.TextColor('black', 'gray');
        Style.Enabled;
      END;
      IF FieldName = '5000010' THEN Style.Visible; // On cards only
      IF FieldName = '6000010' THEN Style.TextAlign('right','left'); //left, right, center
      IF FieldName = '7000010' THEN Style.FontSize('80%','100%');
      IF FieldName = '8000010' THEN Style.ImageNoBorder('yes.png','no.png'); //Icon in Customer_css folder of WebDesk on IIS-Server
      IF FieldName = '1000032' THEN BEGIN
        // Icon on the web
        Style.ImageNoBorder('flags/us.png','flags/de.png');
        // Image Position: First value is horizontal position, second is vertical position
        Style.ImagePosition('center','center');
      END;
      IF FieldName = '2000032' THEN BEGIN
        // Image on the web
        Style.ImageNoBorder('https://demo.navwebclient.com/themes/anveo/images/AnveoLogo.jpg', '');
        Style.Height('30px','16px');
      END;
      IF FieldName = '3000032' THEN BEGIN
        // Image with alignment
        Style.ImageNoBorder('arrow_green_right.png', 'arrow_green_left.png');
        // Image Position: First value is horizontal position, second is vertical position (left, center, right)
        Style.ImagePosition('right center','left center');
      END;
      IF FieldName = '9000010' THEN Style.BorderColor('red','gray');

      // Anveo Virtual Fields
      IF (WebPageElementField."Virtual Table Code" = 'EXAMPLE') AND
         (WebPageElementField."Virtual Field Code" = 'VIRTUALFIELD_OUT') THEN BEGIN
        FldRef.VALUE := ACFExample.GetVolumeText();
      END;
    END;

    LOCAL PROCEDURE "-- SET/GET FUNCTIONS --"@5326072();
    BEGIN
    END;

    PROCEDURE SETVALUES1@5326088(pWebPageElement@5326081 : Record 5327101;pWebPageElementField@5326080 : Record 5327103;VAR pRecRef@5326079 : RecordRef;pFldRef@5326078 : FieldRef;pFldNo@5326062 : Integer;pxRecRef@5326077 : RecordRef;pxFldRef@5326076 : FieldRef;pxFldNo@5326063 : Integer;pWindowRecRef@5326075 : RecordRef;pDestWebPageElement@5326074 : Record 5327101;pDestRecRef@5326073 : RecordRef;pCustomTableRelationRecRef@5326072 : RecordRef;pFunctionCode@5326071 : Code[20];pRelationCode@5326070 : Code[20];pFieldName@5326069 : Text[50];pTableNumber@5326067 : Integer);
    BEGIN
      // Set global variables
      WebPageElement := pWebPageElement;
      WebPageElementField := pWebPageElementField;
      RecRef := pRecRef;
      FldRef := pFldRef;
      FldNo := pFldNo;
      xRecRef := pxRecRef;
      xFldRef := pxFldRef;
      xFldNo := pxFldNo;
      WindowRecRef := pWindowRecRef;
      DestWebPageElement := pDestWebPageElement;
      DestRecRef := pDestRecRef;
      CustomTableRelationRecRef := pCustomTableRelationRecRef;
      FunctionCode := pFunctionCode;
      RelationCode := pRelationCode;
      FieldName := pFieldName;
      TableNumber := pTableNumber;

      IF pFldNo<>0 THEN FldRef := RecRef.FIELD(pFldNo);
      IF pxFldNo<>0 THEN xFldRef := xRecRef.FIELD(pxFldNo);

      WebUser.GET(Mgt.GetACFUser);
    END;

    PROCEDURE SETVALUES2@5326085(pClosedByNAV@5326062 : Boolean;pInitStyle@5326066 : Boolean);
    BEGIN
      // Set global variables
      ClosedByNAV := pClosedByNAV;
      IF pInitStyle THEN
        Style.Init(WebPageElement);
    END;

    PROCEDURE GETVALUES@5326089(VAR pRecRef@5326079 : RecordRef;VAR pFldRef@5326078 : FieldRef;VAR pDestRecRef@5326073 : RecordRef;VAR "pField#1"@5326065 : Integer;VAR "pField#2"@5326064 : Integer;VAR CSS@5326062 : Text[1024]);
    BEGIN
      // Return global variables
      pRecRef := RecRef;
      pFldRef := FldRef;
      pDestRecRef := DestRecRef;
      "pField#1" := "Field#1";
      "pField#2" := "Field#2";
      IF Style.IsInit THEN
        CSS := Style.GetStyle;
    END;

    PROCEDURE SetValuesToMainEvents@5326086();
    BEGIN
      Events.SETVALUES1(WebPageElement,WebPageElementField,RecRef,FldRef,FldNo, xRecRef,xFldRef,xFldNo,WindowRecRef,
       DestWebPageElement,DestRecRef,CustomTableRelationRecRef,FunctionCode,RelationCode,FieldName,TableNumber);
      Events.SETVALUES2(ClosedByNAV, FALSE);
    END;

    PROCEDURE GetValuesFromMainEvents@5326092();
    BEGIN
      Events.GETVALUES(RecRef,FldRef,DestRecRef,"Field#1","Field#2", CSS);
    END;

    LOCAL PROCEDURE "-- Other Functions"@5326071();
    BEGIN
    END;

    BEGIN
    {
      ** Events-Codeunit is the main interface between GUI-events and individual C/AL-Code.
      Online documentation: www.NAVwebclient.com/anveo-help-codeunit-acf-events

      Web Page related event-code can be grouped by a module. See codeunit "ACF Module Mapping" for creating a new module.
      Online documentation for module concept: www.NAVwebclient.com/anveo-help-modules
      Events that can be used in a module are marked with a "M".

      ************************************************************************************************************************************
      *** Module Admin / Examples ********************************************************************************************************
      ************************************************************************************************************************************

      Events-Overview
      ---------------
      * User session
        OnLogin                   Called on user login. Login can be cancelled here.
        OnLogout                  Called on user logout or on browser refresh or close.
        OnLoadEnvironment         Called on browser refresh without new login or after login
        GetLanguageFilter         Defines a filter on Language table for user selection.

      * Security
        ReadSecurityFilter        Sets read filters, called by every Web Page Element and DropDown-Lists
        CheckPassword             Tests password quality. Returns TRUE, if password is ok.
        TestPassword              Uses CheckPassword to test password quality. Calls an ERROR, if password is not ok.

      * Loading data (no asynchronious call, Mgt.-functions like Anveo-MESSAGE etc. is not processed by GUI)
        OnBeforeLoad          (M) Called on loading data once per Web Page Element, Filter to Records is set
        OnAfterGetField       (M) Called once per Web Page Element, Record and Field
        OnAfterGetRecord      (M) Called once per Web Page Element and Record
        OnAfterLoad           (M) Called on loading data once per Web Page Element

      * Data modification
        OnInit                (M) A new record is initialized. Default values can be set here.
        OnBeforeValidate      (M) A value is set to a record, but validation is not done yet. Validation can be cancelled here.
        OnValidate            (M) A value is set to a record and validation is done, if OnBeforeValidate returned true.
        CallOnInsertTrigger   (M) A new record is inserted. Return value decides whether to call INSERT(TRUE) or INSERT(FALSE).
        CallOnModifyTrigger   (M) A record is modified. Return value decides whether to call MODIFY(TRUE) or MODIFY(FALSE).
        CallOnDeleteTrigger   (M) A record is deleted. Return value decides whether to call DELETE(TRUE) or DELETE(FALSE).
        OnInsertRecord        (M) A record is inserted. Return value decides whether record is inserted.
        OnModifyRecord        (M) A record is modified. Return value decides whether record is modified.
        OnRenameRecord        (M) A record is renamed. Return value decides whether record is renamed.
        OnDeleteRecord        (M) A record is deleted. OnAction with Function code "DELETE_RECORD()" is also called.
        OnAfterInsertRecord   (M) Called after a record was successfully inserted.
        OnAfterModifyRecord   (M) Called after a record was successfully modified.
        OnAfterRenameRecord   (M) Called after a record was successfully renamed.
        OnAfterDeleteRecord   (M) Called after a record was successfully deleted.
        IsWriteBack           (M) In case of write behaviour "Defined" IsWriteBack defines if record is physically saved or stays tempor.

      * User events
        OnAction              (M) Button in menu or beside a field is pressed, or double click event on list web page.
                                 "Function Code" identifies C/AL-Code to run.
        OnBeforeCloseWebPage  (M) A Web Page is closed. It can be cancelled with return value false.

      * Relations
        CustomWebPageRelation (M) Defines a custom relation between Web Pages / Web Page Elements

      * Reports
        Create a OnAction-Function for each report. See examples.

      * Other
        DropDownFieldDefinition (M) Defines field Nos in DropDown lists.
    }
    END.
  }
}

