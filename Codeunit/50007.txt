OBJECT Codeunit 50007 General Management Connector
{
  OBJECT-PROPERTIES
  {
    Date=06.02.12;
    Time=14:11:15;
    Version List=DN(1);
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SalesHeader@1000000000 : Record 36;
      SalesLine@1000000001 : Record 37;
      Item@1000000002 : Record 27;

    PROCEDURE ChangeOrderWithGroupon@1000000000(_SalesHeader@1000000000 : Record 36);
    VAR
      L_GrouponAmt@1000000001 : Decimal;
      L_TotalAmt@1000000002 : Decimal;
      L_Text001@1000000003 : TextConst 'DEU=Ein Auftrag mit Groupon muá 0 sein.;ENU=Order with groupon must be zero.';
      L_LineNo@1000000004 : Integer;
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type",_SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",_SalesHeader."No.");
      SalesLine.SETRANGE(Type,SalesLine.Type::Resource);
      SalesLine.SETRANGE("Resource Type",SalesLine."Resource Type"::Groupon,SalesLine."Resource Type"::"Daily Deal");
      IF SalesLine.ISEMPTY THEN
        EXIT
      ELSE BEGIN
        SalesLine.SETRANGE(Type);
        SalesLine.SETRANGE("Resource Type");
        SalesLine.FINDSET;
        REPEAT
          IF (SalesLine.Type = SalesLine.Type::Resource) AND
             ((SalesLine."Resource Type" = SalesLine."Resource Type"::Groupon) OR
             (SalesLine."Resource Type" = SalesLine."Resource Type"::"Daily Deal")) THEN
            L_GrouponAmt := L_GrouponAmt + SalesLine."Line Amount"
          ELSE
            L_TotalAmt := L_TotalAmt + SalesLine."Line Amount";
        UNTIL SalesLine.NEXT = 0;
        IF L_TotalAmt + L_GrouponAmt <> 0 THEN
          ERROR(L_Text001);
        SalesLine.FINDSET;
        L_TotalAmt := 0;
        REPEAT
          IF (SalesLine.Type = SalesLine.Type::Resource) AND
             ((SalesLine."Resource Type" = SalesLine."Resource Type"::Groupon) OR
             (SalesLine."Resource Type" = SalesLine."Resource Type"::"Daily Deal")) THEN BEGIN
            SalesLine.DELETE(TRUE);
          END ELSE BEGIN
            IF SalesLine.Type = SalesLine.Type::Item THEN BEGIN
              Item.GET(SalesLine."No.");
              SalesLine.VALIDATE("Unit Price",Item."Unit Price");
              SalesLine.MODIFY;
            END;
            L_TotalAmt := L_TotalAmt + SalesLine."Line Amount";
          END;
        UNTIL SalesLine.NEXT = 0;
        SalesLine.FINDLAST;
        L_LineNo := SalesLine."Line No." + 10000;
        SalesLine.INIT;
        SalesLine."Document Type" := _SalesHeader."Document Type";
        SalesLine."Document No." := _SalesHeader."No.";
        SalesLine."Line No." := L_LineNo;
        SalesLine.INSERT(TRUE);
        SalesLine.VALIDATE(Type,SalesLine.Type::Resource);
        SalesLine.VALIDATE("No.",'ZRABATT');
        SalesLine.VALIDATE(Quantity,1);
        SalesLine.VALIDATE("Unit Price",-L_TotalAmt -L_GrouponAmt);
        SalesLine.MODIFY;
        SalesHeader.GET(_SalesHeader."Document Type",_SalesHeader."No.");
        SalesHeader.VALIDATE("Payment Method Code",'GROUPON');
        IF SalesHeader."Prepayment %" <> 100 THEN
          SalesHeader.VALIDATE("Prepayment %",100);
        SalesHeader.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

