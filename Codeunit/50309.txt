OBJECT Codeunit 50309 SII Incoming Request Handler
{
  OBJECT-PROPERTIES
  {
    Date=14.01.15;
    Time=11:26:18;
    Modified=Yes;
    Version List=T0084,HME1996;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100409000 : TextConst 'ENU=Error while processing / loading the XML data.';
      SIIRequestXMLLogG@1100409003 : Record 50324;
      RequestTextG@1100409004 : BigText;
      ProcessType@1100409002 : 'SaveXML,ProcessXML,ProcessData,AddToBatchPost';
      Text002@1100409005 : TextConst 'ENU=Couldn''t find order %1. Processing cancelled.';
      showErrors@1000000000 : Boolean;

    PROCEDURE GetFilenameByDialog@1000000005(fileExtensionV@1000000000 : Text[30]) : Text[250];
    VAR
      SIIIncomingRequestXMLLogL@1000000001 : Record 50324;
      FilePathL@1000000002 : Text[250];
    BEGIN
      // T0084 24.11.14 tec-cs ++++++++++++++++++++++++
      IF NOT ISSERVICETIER THEN BEGIN
        FilePathL := SIIIncomingRequestXMLLogL."XML File (Request)".IMPORT(fileExtensionV, TRUE);
      END;
      EXIT(FilePathL);
      // T0084 24.11.14 tec-cs ------------------------
    END;

    PROCEDURE ImportXMLRequestFromFile@1000000001(filenameV@1000000003 : Text[250]) : Boolean;
    VAR
      lFileL@1000000002 : File;
      tempTextL@1000000001 : BigText;
      InStreamL@1000000000 : InStream;
    BEGIN
      // T0084 24.11.14 tec-cs ++++++++++++++++++++++++
      IF filenameV = '' THEN BEGIN
        filenameV := GetFilenameByDialog('*.xml');
        IF filenameV = '' THEN BEGIN
          EXIT(FALSE);
        END;
      END;

      showErrors := TRUE;
      CLEARLASTERROR;

      lFileL.OPEN(filenameV);

      lFileL.CREATEINSTREAM(InStreamL);
      tempTextL.READ(InStreamL);
      lFileL.CLOSE;

      HandleCustomerRequest(tempTextL);
      // T0084 24.11.14 tec-cs ------------------------
    END;

    PROCEDURE HandleCustomerRequest@1000000000(VAR requestText@1000000001 : BigText) responseText : Text[250];
    VAR
      SIIRequestXMLLogL@1100409000 : Record 50324;
      SIIRequestFunctionsL@1000000000 : Codeunit 50310;
    BEGIN
      SIIRequestXMLLogL.RESET;
      SIIRequestXMLLogL.INSERT(TRUE);
      COMMIT;

      // Save the received XML first
      SIIRequestFunctionsL.SetParam(ProcessType::SaveXML, SIIRequestXMLLogL, requestText);
      IF NOT SIIRequestFunctionsL.RUN THEN BEGIN
        AddError(SIIRequestXMLLogL);
        EXIT;
      END;
      CLEAR(requestText);
      COMMIT;
      // Get the updated entry
      SIIRequestFunctionsL.GetEntry(SIIRequestXMLLogL);
      // T0051 24.11.14 tec-cs ++++++++++++++++++++++++++
      IF NOT SIIRequestFunctionsL.ProcessCustomerRequest(SIIRequestXMLLogL) THEN BEGIN
        EXIT(SIIRequestXMLLogL."Last Error Text 1");
      END;
      // T0051 24.11.14 tec-cs --------------------------
    END;

    LOCAL PROCEDURE AddError@1100409003(VAR SIIRequestXMLLogV@1100409000 : Record 50324);
    BEGIN
      SIIRequestXMLLogV.Status := SIIRequestXMLLogV.Status::Error;

      SIIRequestXMLLogV."Last Error Text 1" := COPYSTR(GETLASTERRORTEXT, 1, 250);
      SIIRequestXMLLogV."Last Error Text 2" := COPYSTR(GETLASTERRORTEXT, 251, 250);
      SIIRequestXMLLogV."Last Error Text 3" := COPYSTR(GETLASTERRORTEXT, 501, 250);
      SIIRequestXMLLogV."Last Error Text 4" := COPYSTR(GETLASTERRORTEXT, 751);
      SIIRequestXMLLogV.MODIFY;
      IF showErrors THEN BEGIN
        COMMIT;
        ERROR(GETLASTERRORTEXT);
      END;
    END;

    BEGIN
    {
      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |     Cooperation Tectura & Home24 NAV Team  |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________
      T0050       11.09.14  tec-sf    Created
      T0084       24.11.14  tec-cs    import/export of XML-Requests
      T0051       17.12.14  tec-cs    changes for cleaner code

      +--------------------------------------------+
      |                                            |
      |                  Home24                    |
      |       Internal Customizing by NAV-Team     |
      |                                            |
      +--------------------------------------------+

      Project No. Date      Shortcut  Description
      _____________________________________________
      H1996       27.03.14  MIK       BUG: Hotfix for Deployed Projects T0051 and T0052. Redeploy of this codeunit.
    }
    END.
  }
}

